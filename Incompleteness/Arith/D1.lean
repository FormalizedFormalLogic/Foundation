import Incompleteness.Arith.Theory

noncomputable section

open Classical

namespace LO.FirstOrder

open LO.Arith FirstOrder.Arith

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

variable {L : Language} [(k : â„•) â†’ Encodable (L.Func k)] [(k : â„•) â†’ Encodable (L.Rel k)] [DefinableLanguage L]

variable (V)

namespace Derivation2

def cast (d : T âŠ¢â‚ƒ Î“) (h : Î“ = Î”) : T âŠ¢â‚ƒ Î” := h â–¸ d

def Sequent.codeIn (Î“ : Finset (SyntacticFormula L)) : V := âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V)

instance : GoedelQuote (Finset (SyntacticFormula L)) V := âŸ¨Sequent.codeIn VâŸ©

lemma Sequent.codeIn_def (Î“ : Finset (SyntacticFormula L)) : âŒœÎ“âŒ = âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V) := rfl

variable {V}

open Classical

@[simp] lemma Sequent.codeIn_empty : (âŒœ(âˆ… : Finset (SyntacticFormula L))âŒ : V) = âˆ… := by
  simp [Sequent.codeIn_def, emptyset_def]

lemma Sequent.mem_codeIn_iff {Î“ : Finset (SyntacticFormula L)} {p} : âŒœpâŒ âˆˆ (âŒœÎ“âŒ : V) â†” p âˆˆ Î“ := by
  induction Î“ using Finset.induction generalizing p
  case empty => simp [Sequent.codeIn_def]
  case insert a Î“ ha ih =>
    have : exp âŒœaâŒ + âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V) = insert (âŒœaâŒ : V) (âŒœÎ“âŒ : V) := by
      simp [insert, bitInsert, (not_iff_not.mpr ih.symm).mp ha, add_comm]
      rw [Sequent.codeIn_def]
    simp [ha, Sequent.codeIn_def]
    rw [this]
    simp [â†ih]

lemma Sequent.quote_inj {Î“ Î” : Finset (SyntacticFormula L)} : (âŒœÎ“âŒ : V) = âŒœÎ”âŒ â†’ Î“ = Î” := fun h â†¦ by
  ext p; simp [â†Sequent.mem_codeIn_iff (V := V), h]

lemma Sequent.subset_of_quote_subset_quote {Î“ Î” : Finset (SyntacticFormula L)} :
    (âŒœÎ“âŒ : V) âŠ† âŒœÎ”âŒ â†’ Î“ âŠ† Î” := fun h _ hp â†¦
  Sequent.mem_codeIn_iff.mp <| h <| Sequent.mem_codeIn_iff.mpr hp

@[simp] lemma Sequent.codeIn_singleton (p : SyntacticFormula L) :
    (âŒœ({p} : Finset (SyntacticFormula L))âŒ : V) = {âŒœpâŒ} := by simp [Sequent.codeIn_def]; rfl

@[simp] lemma Sequent.codeIn_insert (Î“ : Finset (SyntacticFormula L)) (p) : (âŒœ(insert p Î“)âŒ : V) = insert âŒœpâŒ âŒœÎ“âŒ := by
  by_cases hp : p âˆˆ Î“
  Â· simp [Sequent.mem_codeIn_iff, hp, insert_eq_self_of_mem]
  Â· have : (âŒœinsert p Î“âŒ : V) = exp âŒœpâŒ + âŒœÎ“âŒ := by simp [Sequent.codeIn_def, hp]
    simp [Sequent.mem_codeIn_iff, this, insert_eq, bitInsert, hp, add_comm]

lemma Sequent.mem_codeIn {Î“ : Finset (SyntacticFormula L)} (hx : x âˆˆ (âŒœÎ“âŒ : V)) : âˆƒ p âˆˆ Î“, x = âŒœpâŒ := by
  induction Î“ using Finset.induction
  case empty => simp at hx
  case insert a Î“ _ ih =>
    have : x = âŒœaâŒ âˆ¨ x âˆˆ (âŒœÎ“âŒ : V) := by simpa using hx
    rcases this with (rfl | hx)
    Â· exact âŸ¨a, by simpâŸ©
    Â· rcases ih hx with âŸ¨p, hx, rflâŸ©
      exact âŸ¨p, by simp [*]âŸ©

lemma Sequent.mem_codeIn_iff' {Î“ : Finset (SyntacticFormula L)} : x âˆˆ (âŒœÎ“âŒ : V) â†” (âˆƒ p âˆˆ Î“, x = âŒœpâŒ) := by
  constructor
  Â· intro h; exact Sequent.mem_codeIn h
  Â· rintro âŸ¨p, hp, rflâŸ©; simp [Sequent.mem_codeIn_iff, hp]

lemma setShift_quote (Î“ : Finset (SyntacticFormula L)) : (L.codeIn V).setShift âŒœÎ“âŒ = âŒœFinset.image (Rew.hom Rew.shift) Î“âŒ := by
  apply mem_ext
  intro x; simp [mem_setShift_iff]
  constructor
  Â· rintro âŸ¨x, hx, rflâŸ©
    rcases Sequent.mem_codeIn hx with âŸ¨p, _, rflâŸ©
    rw [â†quote_shift, Sequent.mem_codeIn_iff]
    simp
    exact âŸ¨p, by simpa [Sequent.mem_codeIn_iff] using hx, rflâŸ©
  Â· intro hx
    rcases Sequent.mem_codeIn hx with âŸ¨p', hp', rflâŸ©
    rcases by simpa using hp' with âŸ¨p, hp, rflâŸ©
    exact âŸ¨âŒœpâŒ, by simpa [Sequent.mem_codeIn_iff] using hp, by simpâŸ©

variable (V)

variable {T : Theory L} [T.Delta1Definable]

def codeIn : {Î“ : Finset (SyntacticFormula L)} â†’ T âŠ¢â‚ƒ Î“ â†’ V
  | _, closed Î” p _ _                         => Arith.axL âŒœÎ”âŒ âŒœpâŒ
  | _, root (Î” := Î”) p _ _                    => Arith.root âŒœÎ”âŒ âŒœpâŒ
  | _, verum (Î” := Î”) _                       => Arith.verumIntro âŒœÎ”âŒ
  | _, and (Î” := Î”) _ (p := p) (q := q) bp bq => Arith.andIntro âŒœÎ”âŒ âŒœpâŒ âŒœqâŒ bp.codeIn bq.codeIn
  | _, or (Î” := Î”) (p := p) (q := q) _ d      => Arith.orIntro âŒœÎ”âŒ âŒœpâŒ âŒœqâŒ d.codeIn
  | _, all (Î” := Î”) (p := p) _ d              => Arith.allIntro âŒœÎ”âŒ âŒœpâŒ d.codeIn
  | _, ex (Î” := Î”) (p := p) _ t d             => Arith.exIntro âŒœÎ”âŒ âŒœpâŒ âŒœtâŒ d.codeIn
  | _, wk (Î“ := Î“) d _                        => Arith.wkRule âŒœÎ“âŒ d.codeIn
  | _, shift (Î” := Î”) d                       => Arith.shiftRule âŒœÎ”.image Rew.shift.homâŒ d.codeIn
  | _, cut (Î” := Î”) (p := p) d dn             => Arith.cutRule âŒœÎ”âŒ âŒœpâŒ d.codeIn dn.codeIn

instance (Î“ : Finset (SyntacticFormula L)) : GoedelQuote (T âŠ¢â‚ƒ Î“) V := âŸ¨codeIn VâŸ©

lemma quote_derivation_def {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (âŒœdâŒ : V) = d.codeIn V := rfl

@[simp] lemma fstidx_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : fstIdx (âŒœdâŒ : V) = âŒœÎ“âŒ := by
  induction d <;> simp [quote_derivation_def, codeIn]

end Derivation2

end LO.FirstOrder

namespace LO.Arith

open FirstOrder FirstOrder.Arith FirstOrder.Semiformula

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

variable {L : Language} [(k : â„•) â†’ Encodable (L.Func k)] [(k : â„•) â†’ Encodable (L.Rel k)] [DefinableLanguage L]

variable {T : Theory L} [T.Delta1Definable]

open Classical

@[simp] lemma formulaSet_codeIn_finset (Î“ : Finset (SyntacticFormula L)) : (L.codeIn V).IsFormulaSet âŒœÎ“âŒ := by
  intro x hx
  rcases Derivation2.Sequent.mem_codeIn hx with âŸ¨p, _, rflâŸ©;
  apply semiformula_quote (n := 0)

open Derivation2

lemma quote_image_shift (Î“ : Finset (SyntacticFormula L)) : (L.codeIn V).setShift (âŒœÎ“âŒ : V) = âŒœÎ“.image Rew.shift.homâŒ := by
  induction Î“ using Finset.induction
  case empty => simp
  case insert p Î“ _ ih => simp [ih]

@[simp] lemma derivation_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).Derivation âŒœdâŒ := by
  induction d
  case closed p hp hn =>
    exact Language.Theory.Derivation.axL (by simp)
      (by simp [Sequent.mem_codeIn_iff, hp])
      (by rw [â†quote_neg, Sequent.mem_codeIn_iff]; simp [hn])
  case root Î” p hT hp =>
    apply Language.Theory.Derivation.root (by simp)
      (by simp [Sequent.mem_codeIn_iff, hp])
      (mem_coded_theory_iff.mpr hT)
  case verum Î” h =>
    exact Language.Theory.Derivation.verumIntro (by simp)
      (by simpa [quote_verum] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
  case and Î” p q hpq dp dq ihp ihq =>
    apply Language.Theory.Derivation.andIntro
      (by simpa [quote_and] using (Sequent.mem_codeIn_iff (V := V)).mpr hpq)
      âŸ¨by simp [fstidx_quote], ihpâŸ©
      âŸ¨by simp [fstidx_quote], ihqâŸ©
  case or Î” p q hpq d ih =>
    apply Language.Theory.Derivation.orIntro
      (by simpa [quote_or] using (Sequent.mem_codeIn_iff (V := V)).mpr hpq)
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case all Î” p h d ih =>
    apply Language.Theory.Derivation.allIntro
      (by simpa [quote_all] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
      âŸ¨by simp [fstidx_quote, quote_image_shift, free_quote], ihâŸ©
  case ex Î” p h t d ih =>
    apply Language.Theory.Derivation.exIntro
      (by simpa [quote_ex] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
      (semiterm_codeIn t)
      âŸ¨by simp [fstidx_quote, Language.substsâ‚], ihâŸ©
  case wk Î” Î“ d h ih =>
    apply Language.Theory.Derivation.wkRule (s' := âŒœÎ”âŒ)
      (by simp)
      (by intro x hx; rcases Sequent.mem_codeIn hx with âŸ¨p, hp, rflâŸ©
          simp [Sequent.mem_codeIn_iff, h hp])
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case shift Î” d ih =>
    simp [quote_derivation_def, Derivation2.codeIn, â†quote_image_shift]
    apply Language.Theory.Derivation.shiftRule
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case cut Î” p d dn ih ihn =>
    apply Language.Theory.Derivation.cutRule
      âŸ¨by simp [fstidx_quote], ihâŸ©
      âŸ¨by simp [fstidx_quote], ihnâŸ©

@[simp] lemma derivationOf_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).DerivationOf âŒœdâŒ âŒœÎ“âŒ :=
  âŸ¨by simp, by simpâŸ©

lemma derivable_of_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).Derivable âŒœÎ“âŒ :=
  âŸ¨âŒœdâŒ, by simpâŸ©

section

variable [L.ConstantInhabited] {T : Theory L} [T.Delta1Definable]

theorem provable_of_provable {p} : T âŠ¢! p â†’ (T.codeIn V).Provable âŒœpâŒ := fun h â†¦ by
  simpa using derivable_of_quote (V := V) (provable_iff_derivable2.mp h).some

/-- Hilbertâ€“Bernays provability condition D1 -/
theorem tprovable_of_provable {p} : T âŠ¢! p â†’ T.tCodeIn V âŠ¢! âŒœpâŒ := fun h â†¦ by
  simpa [Language.Theory.TProvable.iff_provable] using provable_of_provable (V := V) h

end

section

variable {T : Theory â„’â‚’áµ£} [T.Delta1Definable]

theorem provableâ‚_of_provable {Ïƒ} : T âŠ¢! Ïƒ â†’ T.Provableâ‚ (âŒœÏƒâŒ : V) := fun h â†¦
  Language.Theory.Derivable.of_ss Formalized.theory_subset_AddRâ‚€ (provable_of_provable h)

end

end LO.Arith

namespace Nat

lemma double_add_one_div_of_double (n m : â„•) : (2 * n + 1) / (2 * m) = n / m := calc
      (2 * n + 1) / (2 * m)
    = (1 + 2 * n) / 2 / m := by simp [add_comm, Nat.div_div_eq_div_mul]
  _ = n / m := by simp [Nat.add_mul_div_left]

lemma mem_bitIndices_iff {x s : â„•} : x âˆˆ s.bitIndices â†” Odd (s / 2 ^ x) := by
  induction s using Nat.binaryRec generalizing x
  case z => simp [Nat.dvd_zero]
  case f b s ih =>
    cases b <;> simp [ih]
    Â· constructor
      Â· rintro âŸ¨x, hx, rflâŸ©
        rw [show 2 ^ (x + 1) = 2 * 2 ^ x by simp [Nat.pow_add_one, mul_comm], Nat.mul_div_mul_left _ _ (by simp)]
        exact hx
      Â· intro h
        cases' x with x
        Â· simp at h
        Â· refine âŸ¨x, ?_, rflâŸ©
          rwa [show 2 ^ (x + 1) = 2 * 2 ^ x by simp [Nat.pow_add_one, mul_comm], Nat.mul_div_mul_left _ _ (by simp)] at h
    Â· constructor
      Â· rintro (rfl | âŸ¨x, hx, rflâŸ©)
        Â· simp
        Â· rw [show 2 ^ (x + 1) = 2 * 2 ^ x by simp [Nat.pow_add_one, mul_comm], double_add_one_div_of_double]
          exact hx
      Â· intro h
        cases' x with x
        Â· simp
        Â· right; refine âŸ¨x, ?_, rflâŸ©
          rwa [show 2 ^ (x + 1) = 2 * 2 ^ x by simp [Nat.pow_add_one, mul_comm], double_add_one_div_of_double] at h

end Nat

namespace LO.FirstOrder

variable {L : Language} {T : Theory L}

end LO.FirstOrder

namespace LO.Arith

open FirstOrder Encodable

variable {L : Language} [(k : â„•) â†’ Encodable (L.Func k)] [(k : â„•) â†’ Encodable (L.Rel k)] [DefinableLanguage L]

lemma isFormulaSet_sound {s : â„•} : (L.codeIn â„•).IsFormulaSet s â†’ âˆƒ S : Finset (SyntacticFormula L), âŒœSâŒ = s := by
  intro h
  have : âˆ€ x, âˆƒ p : SyntacticFormula L, x âˆˆ s â†’ âŒœpâŒ = x := by
    intro x;
    by_cases hx : x âˆˆ s <;> simp [hx]
    exact (h x hx).sound
  choose ps hps using this
  exact âŸ¨(s.bitIndices.map ps).toFinset, by
    apply mem_ext
    intro x
    constructor
    Â· intro h
      rcases Derivation2.Sequent.mem_codeIn h with âŸ¨p, hp, rflâŸ©
      rcases by simpa using hp with âŸ¨x, hx, rflâŸ©
      simpa [hps x (mem_iff_mem_bitIndices.mpr hx)] using mem_iff_mem_bitIndices.mpr hx
    Â· intro h
      rw [â†hps x h]
      simp [Derivation2.Sequent.mem_codeIn_iff, â†mem_iff_mem_bitIndices]
      exact âŸ¨x, h, rflâŸ©âŸ©

section

variable {T : Theory L} [T.Delta1Definable]

open Derivation2

lemma Language.Theory.Derivation.sound {d : â„•} (h : (T.codeIn â„•).Derivation d) : âˆƒ Î“, âŒœÎ“âŒ = fstIdx d âˆ§ T âŠ¢â‚ƒ! Î“ := by
  induction d using Nat.strongRec
  case ind d ih =>
  rcases h.case with âŸ¨hs, HâŸ©
  rcases isFormulaSet_sound hs with âŸ¨Î“, hÎ“âŸ©
  refine âŸ¨Î“, hÎ“, ?_âŸ©
  rcases H with (âŸ¨s, p, rfl, hp, hnpâŸ© | âŸ¨s, rfl, hvâŸ© |
    âŸ¨s, p, q, dp, dq, rfl, hpq, âŸ¨hp, hdpâŸ©, âŸ¨hq, hdqâŸ©âŸ© | âŸ¨s, p, q, d, rfl, hpq, âŸ¨h, hdâŸ©âŸ© |
    âŸ¨s, p, d, rfl, hps, hd, ddâŸ© | âŸ¨s, p, t, d, rfl, hps, ht, hd, ddâŸ© |
    âŸ¨s, d, rfl, hs, ddâŸ© | âŸ¨s, d, rfl, rfl, ddâŸ© |
    âŸ¨s, p, dâ‚, dâ‚‚, rfl, âŸ¨hâ‚, ddâ‚âŸ©, âŸ¨hâ‚‚, ddâ‚‚âŸ©âŸ© | âŸ¨s, p, rfl, hs, hTâŸ©)
  Â· rcases (hs p (by simp [hp])).sound with âŸ¨p, rflâŸ©
    refine âŸ¨Derivation2.closed Î“ p
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), hÎ“, hp])
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), hÎ“, hp, hnp])âŸ©
  Â· refine âŸ¨Derivation2.verum (by simp [â†Sequent.mem_codeIn_iff (V := â„•), hÎ“, Semiformula.quote_verum, hv])âŸ©
  Â· have fpq : (L.codeIn â„•).IsFormula p âˆ§ (L.codeIn â„•).IsFormula q := by simpa using hs (p ^â‹ q) (by simp [hpq])
    rcases by simpa using hÎ“
    rcases fpq.1.sound with âŸ¨p, rflâŸ©
    rcases fpq.2.sound with âŸ¨q, rflâŸ©
    rcases ih dp (by simp) hdp with âŸ¨Î“p, hÎ“p, âŸ¨bpâŸ©âŸ©
    rcases ih dq (by simp) hdq with âŸ¨Î“q, hÎ“q, âŸ¨bqâŸ©âŸ©
    refine âŸ¨Derivation2.and (p := p) (q := q)
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), Semiformula.quote_and, hpq])
      (bp.cast <| Sequent.quote_inj (V := â„•) (by simp [hÎ“p, hp]))
      (bq.cast <| Sequent.quote_inj (V := â„•) (by simp [hÎ“q, hq]))âŸ©
  Â· have fpq : (L.codeIn â„•).IsFormula p âˆ§ (L.codeIn â„•).IsFormula q := by simpa using hs (p ^â‹ q) (by simp [hpq])
    rcases by simpa using hÎ“
    rcases fpq.1.sound with âŸ¨p, rflâŸ©
    rcases fpq.2.sound with âŸ¨q, rflâŸ©
    rcases ih d (by simp) hd with âŸ¨Î”, hÎ”, âŸ¨bâŸ©âŸ©
    refine âŸ¨Derivation2.or (p := p) (q := q)
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), Semiformula.quote_or, hpq])
      (b.cast <| Sequent.quote_inj (V := â„•) (by simp [hÎ”, h]))âŸ©
  Â· rcases by simpa using hÎ“
    have : (L.codeIn â„•).IsSemiformula 1 p := by simpa using hs (^âˆ€ p) (by simp [hps])
    rcases this.sound with âŸ¨p, rflâŸ©
    rcases ih d (by simp) dd with âŸ¨Î”, hÎ”, âŸ¨bâŸ©âŸ©
    refine âŸ¨Derivation2.all (p := p)
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), Semiformula.quote_all, hps])
      (b.cast <| Sequent.quote_inj (V := â„•) <| by simp [hÎ”, hd, â†free_quote, setShift_quote])âŸ©
  Â· rcases by simpa using hÎ“
    have : (L.codeIn â„•).IsSemiformula 1 p := by simpa using hs (^âˆƒ p) (by simp [hps])
    rcases this.sound with âŸ¨p, rflâŸ©
    rcases ht.sound with âŸ¨t, rflâŸ©
    rcases ih d (by simp) dd with âŸ¨Î”, hÎ”, âŸ¨bâŸ©âŸ©
    refine âŸ¨Derivation2.ex (p := p)
      (by simp [â†Sequent.mem_codeIn_iff (V := â„•), Semiformula.quote_ex, hps]) t
      (b.cast <| Sequent.quote_inj (V := â„•) <| by simp [hÎ”, hd, Language.substsâ‚])âŸ©
  Â· rcases by simpa using hÎ“
    rcases ih d (by simp) dd with âŸ¨Î”, hÎ”, âŸ¨bâŸ©âŸ©
    refine âŸ¨Derivation2.wk (Î” := Î”) b
      (Sequent.subset_of_quote_subset_quote (V := â„•) <| by simp [hÎ”, hs])âŸ©
  Â· rcases ih d (by simp) dd with âŸ¨Î”, hÎ”, âŸ¨bâŸ©âŸ©
    have : Î“ = Finset.image (Rew.hom Rew.shift) Î” :=
      Sequent.quote_inj <| by simpa [â†hÎ”, setShift_quote] using hÎ“
    rcases this
    refine âŸ¨Derivation2.shift bâŸ©
  Â· rcases by simpa using hÎ“
    have : (L.codeIn â„•).IsFormula p := ddâ‚.isFormulaSet p (by simp [hâ‚])
    rcases this.sound with âŸ¨p, rflâŸ©
    rcases ih dâ‚ (by simp) ddâ‚ with âŸ¨Î”â‚, hÎ”â‚, âŸ¨bâ‚âŸ©âŸ©
    have : Î”â‚ = (p â«½ Î“) := Sequent.quote_inj (V := â„•) <| by simp [hÎ”â‚, hâ‚]
    rcases this
    rcases ih dâ‚‚ (by simp) ddâ‚‚ with âŸ¨Î”â‚‚, hÎ”â‚‚, âŸ¨bâ‚‚âŸ©âŸ©
    have : Î”â‚‚ = (~p â«½ Î“) := Sequent.quote_inj (V := â„•) <| by simp [hÎ”â‚‚, hâ‚‚]
    rcases this
    refine âŸ¨Derivation2.cut bâ‚ bâ‚‚âŸ©
  Â· rcases by simpa using hÎ“
    rcases Sequent.mem_codeIn hs with âŸ¨p, hp, rflâŸ©
    refine âŸ¨Derivation2.root p (mem_coded_theory_iff.mp hT) hpâŸ©

lemma Language.Theory.Provable.sound2 {p : SyntacticFormula L} (h : (T.codeIn â„•).Provable âŒœpâŒ) : T âŠ¢â‚ƒ.! p := by
  rcases h with âŸ¨d, hp, hdâŸ©
  rcases hd.sound with âŸ¨Î“, e, bâŸ©
  have : Î“ = {p} := Sequent.quote_inj (V := â„•) <| by simp [e, hp]
  rcases this
  exact b

end

variable [L.ConstantInhabited] {T : Theory L} [T.Delta1Definable]

lemma Language.Theory.Provable.sound {p : SyntacticFormula L} (h : (T.codeIn â„•).Provable âŒœpâŒ) : T âŠ¢! p :=
  provable_iff_derivable2.mpr <| Language.Theory.Provable.sound2 (by simpa using h)

lemma Language.Theory.Provable.soundâ‚€ {Ïƒ : Sentence L} (h : (T.codeIn â„•).Provable âŒœÏƒâŒ) : T âŠ¢! â†‘Ïƒ :=
  provable_iff_derivable2.mpr <| Language.Theory.Provable.sound2 (by simpa using h)

lemma Language.Theory.Provable.complete {p : SyntacticFormula L} :
    T.tCodeIn â„• âŠ¢! âŒœpâŒ â†” T âŠ¢! p :=
  âŸ¨by simpa [Language.Theory.TProvable.iff_provable] using Language.Theory.Provable.sound, tprovable_of_provableâŸ©

end LO.Arith
