import Incompleteness.Arith.D3
import Logic.Logic.HilbertStyle.Supplemental

noncomputable section

open Classical

namespace LO.Arith.Formalized

open LO.FirstOrder LO.FirstOrder.Arith

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

def substNumeral (p x : V) : V := âŒœâ„’â‚’áµ£âŒ.substsâ‚ (numeral x) p

lemma substNumeral_app_quote (Ïƒ : Semisentence â„’â‚’áµ£ 1) (n : â„•) :
    substNumeral âŒœÏƒâŒ (n : V) = âŒœ(Ïƒ/[â€˜â†‘nâ€™] : Sentence â„’â‚’áµ£)âŒ := by
  simp [substNumeral]
  let w : Fin 1 â†’ Semiterm â„’â‚’áµ£ Empty 0 := ![â€˜â†‘nâ€™]
  have : ?[numeral (n : V)] = (âŒœfun i : Fin 1 â†¦ âŒœw iâŒâŒ : V) := nth_ext' 1 (by simp) (by simp) (by simp)
  rw [Language.substsâ‚, this, quote_substs' (L := â„’â‚’áµ£)]

lemma substNumeral_app_quote_quote (Ïƒ Ï€ : Semisentence â„’â‚’áµ£ 1) :
    substNumeral (âŒœÏƒâŒ : V) âŒœÏ€âŒ = âŒœ(Ïƒ/[âŒœÏ€âŒ] : Sentence â„’â‚’áµ£)âŒ := by
  simpa [coe_quote, quote_eq_encode] using substNumeral_app_quote Ïƒ âŒœÏ€âŒ

section

def _root_.LO.FirstOrder.Arith.ssnum : ğšºâ‚.Semisentence 3 := .mkSigma
  â€œy p x. âˆƒ n, !numeralDef n x âˆ§ !pâŒœâ„’â‚’áµ£âŒ.substsâ‚Def y n pâ€ (by simp)

lemma substNumeral_defined : ğšºâ‚-Functionâ‚‚ (substNumeral : V â†’ V â†’ V) via ssnum := by
  intro v; simp [ssnum, âŒœâ„’â‚’áµ£âŒ.substsâ‚_defined.df.iff, substNumeral]

@[simp] lemma eval_ssnum (v) :
    Semiformula.Evalbm V v ssnum.val â†” v 0 = substNumeral (v 1) (v 2) := substNumeral_defined.df.iff v

end

end LO.Arith.Formalized

namespace LO.FirstOrder.Arith

open LO.Arith LO.Arith.Formalized

variable {T : Theory â„’â‚’áµ£} [ğˆğšºâ‚ â‰¼ T]

section Diagonalization

def diag (Î¸ : Semisentence â„’â‚’áµ£ 1) : Semisentence â„’â‚’áµ£ 1 := â€œx. âˆ€ y, !ssnum y x x â†’ !Î¸ yâ€

def fixpoint (Î¸ : Semisentence â„’â‚’áµ£ 1) : Sentence â„’â‚’áµ£ := (diag Î¸)/[âŒœdiag Î¸âŒ]

lemma substs_diag (Î¸ Ïƒ : Semisentence â„’â‚’áµ£ 1) :
    â€œ!(diag Î¸) !!(âŒœÏƒâŒ : Semiterm â„’â‚’áµ£ Empty 0)â€ = â€œâˆ€ x, !ssnum x !!âŒœÏƒâŒ !!âŒœÏƒâŒ â†’ !Î¸ xâ€ := by
  simp [goedelNumber'_def, diag, Rew.q_substs, â†Rew.hom_comp_app, Rew.substs_comp_substs]

lemma fixpoint_eq (Î¸ : Semisentence â„’â‚’áµ£ 1) :
    fixpoint Î¸ = â€œâˆ€ x, !ssnum x !!âŒœdiag Î¸âŒ !!âŒœdiag Î¸âŒ â†’ !Î¸ xâ€ := by
  simp [fixpoint, substs_diag]

theorem diagonal (Î¸ : Semisentence â„’â‚’áµ£ 1) :
    T âŠ¢!. fixpoint Î¸ âŸ· Î¸/[âŒœfixpoint Î¸âŒ] :=
  haveI : ğ„ğ â‰¼ T := System.Subtheory.comp (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  complete (T := T) <| oRing_consequence_of _ _ fun (V : Type) _ _ â†¦ by
    haveI : V âŠ§â‚˜* ğˆğšºâ‚ := ModelsTheory.of_provably_subtheory V ğˆğšºâ‚ T inferInstance inferInstance
    simp [models_iff]
    let Î˜ : V â†’ Prop := fun x â†¦ Semiformula.Evalbm V ![x] Î¸
    calc
      V âŠ§/![] (fixpoint Î¸)
      â†” Î˜ (substNumeral âŒœdiag Î¸âŒ âŒœdiag Î¸âŒ) := by simp [fixpoint_eq]
    _ â†” Î˜ âŒœfixpoint Î¸âŒ                     := by simp [substNumeral_app_quote_quote]; rfl

end Diagonalization

section

variable (U : Theory â„’â‚’áµ£) [U.Delta1Definable]

abbrev _root_.LO.FirstOrder.Theory.bewâ‚ (Ïƒ : Sentence â„’â‚’áµ£) : Sentence â„’â‚’áµ£ := U.provableâ‚/[âŒœÏƒâŒ]

abbrev _root_.LO.FirstOrder.Theory.consistentâ‚ : Sentence â„’â‚’áµ£ := ~U.bewâ‚ âŠ¥

def _root_.LO.FirstOrder.Theory.goedelâ‚ : Sentence â„’â‚’áµ£ := fixpoint (~U.provableâ‚)

end

section

variable {U : Theory â„’â‚’áµ£} [U.Delta1Definable]

theorem provableâ‚_D1 {Ïƒ} : U âŠ¢!. Ïƒ â†’ T âŠ¢!. U.bewâ‚ Ïƒ := by
  intro h
  haveI : ğ„ğ â‰¼ T := System.Subtheory.comp (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  apply complete (T := T) <| oRing_consequence_of _ _ fun (V : Type) _ _ â†¦ by
    haveI : V âŠ§â‚˜* ğˆğšºâ‚ := ModelsTheory.of_provably_subtheory V _ T inferInstance inferInstance
    simpa [models_iff] using provableâ‚_of_provable (T := U) (V := V) h

theorem provableâ‚_D2 {Ïƒ Ï€} : T âŠ¢!. U.bewâ‚ (Ïƒ âŸ¶ Ï€) âŸ¶ U.bewâ‚ Ïƒ âŸ¶ U.bewâ‚ Ï€ :=
  haveI : ğ„ğ â‰¼ T := System.Subtheory.comp (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  complete (T := T) <| oRing_consequence_of _ _ fun (V : Type) _ _ â†¦ by
    haveI : V âŠ§â‚˜* ğˆğšºâ‚ := ModelsTheory.of_provably_subtheory V _ T inferInstance inferInstance
    simp [models_iff]
    intro hÏƒÏ€ hÏƒ
    exact provableâ‚_iff.mpr <| (by simpa using provableâ‚_iff.mp hÏƒÏ€) â¨€ provableâ‚_iff.mp hÏƒ

lemma provableâ‚_sigmaâ‚_complete {Ïƒ : Sentence â„’â‚’áµ£} (hÏƒ : Hierarchy ğšº 1 Ïƒ) :
    T âŠ¢!. Ïƒ âŸ¶ U.bewâ‚ Ïƒ :=
  haveI : ğ„ğ â‰¼ T := System.Subtheory.comp (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  complete (T := T) <| oRing_consequence_of _ _ fun (V : Type) _ _ â†¦ by
    haveI : V âŠ§â‚˜* ğˆğšºâ‚ := ModelsTheory.of_provably_subtheory V _ T inferInstance inferInstance
    simpa [models_iff] using sigmaâ‚_complete (T := U) (V := V) hÏƒ

theorem provableâ‚_D3 {Ïƒ : Sentence â„’â‚’áµ£} :
    T âŠ¢!. U.bewâ‚ Ïƒ âŸ¶ U.bewâ‚ (U.bewâ‚ Ïƒ) := provableâ‚_sigmaâ‚_complete (by simp)

lemma goedel_iff_unprovable_goedel : T âŠ¢!. U.goedelâ‚ âŸ· ~U.bewâ‚ U.goedelâ‚ := by
  simpa [Theory.goedelâ‚, Theory.bewâ‚] using diagonal (~U.provableâ‚)

open LO.System LO.System.FiniteContext

lemma provableâ‚_D2_context {Î“ Ïƒ Ï€} (hÏƒÏ€ : Î“ âŠ¢[T.alt]! (U.bewâ‚ (Ïƒ âŸ¶ Ï€))) (hÏƒ : Î“ âŠ¢[T.alt]! U.bewâ‚ Ïƒ) :
    Î“ âŠ¢[T.alt]! U.bewâ‚ Ï€ := of'! provableâ‚_D2 â¨€ hÏƒÏ€ â¨€ hÏƒ

lemma provableâ‚_D3_context {Î“ Ïƒ} (hÏƒÏ€ : Î“ âŠ¢[T.alt]! U.bewâ‚ Ïƒ) : Î“ âŠ¢[T.alt]! U.bewâ‚ (U.bewâ‚ Ïƒ) := of'! provableâ‚_D3 â¨€ hÏƒÏ€

variable [â„• âŠ§â‚˜* T] [ğ‘â‚€ â‰¼ U]

theorem provableâ‚_sound {Ïƒ} : T âŠ¢!. U.bewâ‚ Ïƒ â†’ U âŠ¢! â†‘Ïƒ := by
  intro h
  have : U.Provableâ‚ (âŒœÏƒâŒ : â„•) := by simpa [modelsâ‚€_iff] using consequence_iff.mp (sound! (T := T) h) â„• inferInstance
  have : U + ğ‘â‚€' âŠ¢! â†‘Ïƒ := Language.Theory.Provable.soundâ‚€ this
  exact add_cobhamR0'.mpr this

end

section

variable [â„• âŠ§â‚˜* T] [T.Delta1Definable] [System.Consistent T]

open LO.System LO.System.FiniteContext

local notation "ğ—š" => T.goedelâ‚

local notation "ğ—–ğ—¼ğ—»" => T.consistentâ‚

local prefix:max "â–¡" => T.bewâ‚

lemma goedel_unprovable : T âŠ¬! â†‘ğ—š := by
  intro h
  have hp : T âŠ¢! â†‘â–¡ğ—š := provableâ‚_D1 h
  have hn : T âŠ¢! ~â†‘â–¡ğ—š := by simpa [provableâ‚€_iff] using and_left! goedel_iff_unprovable_goedel â¨€ h
  exact not_consistent_iff_inconsistent.mpr (inconsistent_of_provable_of_unprovable hp hn) inferInstance

lemma not_goedel_unprovable : T âŠ¬! ~â†‘ğ—š := fun h â†¦ by
  haveI : ğ‘â‚€ â‰¼ T := System.Subtheory.comp (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  have : T âŠ¢!. â–¡ğ—š := System.contraâ‚‚'! (and_right! goedel_iff_unprovable_goedel) â¨€ (by simpa [provableâ‚€_iff] using h)
  have : T âŠ¢! â†‘ğ—š := provableâ‚_sound this
  exact not_consistent_iff_inconsistent.mpr (inconsistent_of_provable_of_unprovable this h) inferInstance

lemma consistent_iff_goedel : T âŠ¢! â†‘ğ—–ğ—¼ğ—» âŸ· â†‘ğ—š := by
  apply iff_intro!
  Â· have bew_G : [~ğ—š] âŠ¢[T.alt]! â–¡ğ—š := deductInv'! <| contraâ‚‚'! <| and_right! goedel_iff_unprovable_goedel
    have bew_not_bew_G : [~ğ—š] âŠ¢[T.alt]! â–¡(~â–¡ğ—š) := by
      have : T âŠ¢!. â–¡(ğ—š âŸ¶ ~â–¡ğ—š) := provableâ‚_D1 <| and_left! goedel_iff_unprovable_goedel
      exact provableâ‚_D2_context (of'! this) bew_G
    have bew_bew_G : [~ğ—š] âŠ¢[T.alt]! â–¡â–¡ğ—š := provableâ‚_D3_context bew_G
    have : [~ğ—š] âŠ¢[T.alt]! â–¡âŠ¥ :=
      provableâ‚_D2_context (provableâ‚_D2_context (of'! <| provableâ‚_D1 <| efq_imply_notâ‚!) bew_not_bew_G) bew_bew_G
    simpa [provableâ‚€_iff] using contraâ‚‚'! (deduct'! this)
  Â· have : [â–¡âŠ¥] âŠ¢[T.alt]! â–¡ğ—š := by
      have : T âŠ¢!. â–¡(âŠ¥ âŸ¶ ğ—š) := provableâ‚_D1 efq!
      exact provableâ‚_D2_context (of'! this) (by simp)
    have : [â–¡âŠ¥] âŠ¢[T.alt]! ~ğ—š :=
      of'! (contraâ‚'! <| and_left! <| goedel_iff_unprovable_goedel) â¨€ this
    simpa [provableâ‚€_iff] using  contraâ‚'! (deduct'! this)

lemma consistent_unprovable : T âŠ¬! â†‘ğ—–ğ—¼ğ—» := fun h â†¦
  goedel_unprovable <| and_left! consistent_iff_goedel â¨€ h

lemma inconsistent_unprovable : T âŠ¬! ~â†‘ğ—–ğ—¼ğ—» := fun h â†¦
  not_goedel_unprovable <| contraâ‚€'! (and_right! (consistent_iff_goedel (T := T))) â¨€ h

end

end LO.FirstOrder.Arith

end
