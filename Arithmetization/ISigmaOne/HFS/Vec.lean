import Arithmetization.ISigmaOne.HFS.Fixpoint

/-!

# Vec

-/

noncomputable section

namespace LO.Arith

open FirstOrder FirstOrder.Arith

variable {V : Type*} [Zero V] [One V] [Add V] [Mul V] [LT V] [V âŠ§â‚˜* ğˆğšºâ‚]

section cons

instance : Cons V V := âŸ¨(âŸªÂ·, Â·âŸ« + 1)âŸ©

scoped infixr:67 " âˆ· " => cons

syntax "?[" term,* "]" : term

macro_rules
  | `(?[$term:term, $terms:term,*]) => `(cons $term ?[$terms,*])
  | `(?[$term:term]) => `(cons $term 0)
  | `(?[]) => `(0)

@[app_unexpander Cons.cons]
def consUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ $term ?[$terms,*]) => `(?[$term, $terms,*])
  | `($_ $term 0) => `(?[$term])
  | _ => throw ()

lemma cons_def (x v : V) : x âˆ· v = âŸªx, vâŸ« + 1 := rfl

@[simp] lemma fstIdx_cons (x v : V) : fstIdx (x âˆ· v) = x := by simp [cons_def, fstIdx]

@[simp] lemma sndIdx_cons (x v : V) : sndIdx (x âˆ· v) = v := by simp [cons_def, sndIdx]

lemma succ_eq_cons (x : V) : x + 1 = Ï€â‚ x âˆ· Ï€â‚‚ x := by simp [cons_def]

@[simp] lemma lt_cons (x v : V) : x < x âˆ· v := by simp [cons_def, lt_succ_iff_le]

@[simp] lemma lt_cons' (x v : V) : v < x âˆ· v := by simp [cons_def, lt_succ_iff_le]

@[simp] lemma zero_lt_cons (x v : V) : 0 < x âˆ· v := by simp [cons_def]

@[simp] lemma cons_ne_zero (x v : V) : x âˆ· v â‰  0 := by simp [cons_def]

@[simp] lemma zero_ne_cons (x v : V) : 0 â‰  x âˆ· v := by symm; simp [cons_def]

lemma nil_or_cons (z : V) : z = 0 âˆ¨ âˆƒ x v, z = x âˆ· v := by
  rcases zero_or_succ z with (rfl | âŸ¨z, rflâŸ©)
  Â· left; rfl
  Â· right; exact âŸ¨Ï€â‚ z, Ï€â‚‚ z, by simp [succ_eq_cons]âŸ©

@[simp] lemma cons_inj (xâ‚ xâ‚‚ vâ‚ vâ‚‚ : V) :
    xâ‚ âˆ· vâ‚ = xâ‚‚ âˆ· vâ‚‚ â†” xâ‚ = xâ‚‚ âˆ§ vâ‚ = vâ‚‚ := by simp [cons_def]

lemma cons_le_cons {xâ‚ xâ‚‚ vâ‚ vâ‚‚ : V} (hx : xâ‚ â‰¤ xâ‚‚) (hv : vâ‚ â‰¤ vâ‚‚) :
    xâ‚ âˆ· vâ‚ â‰¤ xâ‚‚ âˆ· vâ‚‚ := by simpa [cons_def] using pair_le_pair hx hv

end cons

/-!

### N-th element of List

-/

namespace Nth

def Phi (C : Set V) (pr : V) : Prop :=
  (âˆƒ v, pr = âŸªv, 0, fstIdx vâŸ«) âˆ¨ (âˆƒ v i x, pr = âŸªv, i + 1, xâŸ« âˆ§ âŸªsndIdx v, i, xâŸ« âˆˆ C)

private lemma phi_iff (C pr : V) :
    Phi {x | x âˆˆ C} pr â†”
    (âˆƒ v â‰¤ pr, âˆƒ fst â‰¤ v, fst = fstIdx v âˆ§ pr = âŸªv, 0, fstâŸ«) âˆ¨
    (âˆƒ v â‰¤ pr, âˆƒ i â‰¤ pr, âˆƒ x â‰¤ pr, pr = âŸªv, i + 1, xâŸ« âˆ§ âˆƒ snd â‰¤ v, snd = sndIdx v âˆ§ âˆƒ six < C, six = âŸªsnd, i, xâŸ« âˆ§ six âˆˆ C) := by
  constructor
  Â· rintro (âŸ¨v, rflâŸ© | âŸ¨v, i, x, rfl, hCâŸ©)
    Â· left; exact âŸ¨v, by simp, _, by simp, rfl, rflâŸ©
    Â· right; exact âŸ¨v, by simp,
        i, le_trans (le_trans (by simp) (le_pair_left _ _)) (le_pair_right _ _),
        x, le_trans (by simp) (le_pair_right _ _), rfl, _, by simp, rfl, _, lt_of_mem hC, rfl, hCâŸ©
  Â· rintro (âŸ¨v, _, _, _, rfl, rflâŸ© | âŸ¨v, _, i, _, x, _, rfl, _, _, rfl, _, _, rfl, hCâŸ©)
    Â· left; exact âŸ¨v, rflâŸ©
    Â· right; exact âŸ¨v, i, x, rfl, hCâŸ©

def blueprint : Fixpoint.Blueprint 0 where
  core := .ofZero
    (.mkSigma â€œpr C |
    (âˆƒ v <âº pr, âˆƒ fst <âº v, !fstIdxDef fst v âˆ§ !pairâ‚ƒDef pr v 0 fst) âˆ¨
    (âˆƒ v <âº pr, âˆƒ i <âº pr, âˆƒ x <âº pr, !pairâ‚ƒDef pr v (i + 1) x âˆ§
      âˆƒ snd <âº v, !sndIdxDef snd v âˆ§ âˆƒ six < C, !pairâ‚ƒDef six snd i x âˆ§ six âˆˆ C)â€
    (by simp))
    _

def construction : Fixpoint.Construction V blueprint where
  Î¦ := fun _ â†¦ Phi
  defined := .of_zero <| by intro v; simp [phi_iff]
  monotone := by
    rintro C C' hC _ x (h | âŸ¨v, i, x, rfl, hâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨v, i, x, rfl, hC hâŸ©

instance : construction.Finite V where
  finite := by
    rintro C v x (h | âŸ¨v, i, x, rfl, hâŸ©)
    Â· exact âŸ¨0, Or.inl hâŸ©
    Â· exact âŸ¨âŸªsndIdx v, i, xâŸ« + 1, Or.inr âŸ¨v, i, x, rfl, h, by simpâŸ©âŸ©

def Graph : V â†’ Prop := construction.Fixpoint ![]

section

def graphDef : ğšºâ‚-Semisentence 1 := blueprint.fixpointDef

lemma graph_defined : ğšºâ‚-Predicate (Graph : V â†’ Prop) via graphDef :=
  construction.fixpoint_defined

instance graph_definable : ğšºâ‚-Predicate (Graph : V â†’ Prop) := Defined.to_definable _ graph_defined

end

/-- TODO: move-/
@[simp] lemma zero_ne_add_one (x : V) : 0 â‰  x + 1 := ne_of_lt (by simp)

lemma graph_case {pr : V} :
    Graph pr â†”
    (âˆƒ v, pr = âŸªv, 0, fstIdx vâŸ«) âˆ¨ (âˆƒ v i x, pr = âŸªv, i + 1, xâŸ« âˆ§ Graph âŸªsndIdx v, i, xâŸ«) :=
  construction.case

lemma graph_zero {v x : V} :
    Graph âŸªv, 0, xâŸ« â†” x = fstIdx v := by
  constructor
  Â· intro h
    rcases graph_case.mp h with (âŸ¨v, hâŸ© | âŸ¨v, i, x, h, _âŸ©)
    Â· simp at h; rcases h with âŸ¨rfl, rfl, rflâŸ©; rfl
    Â· simp at h
  Â· rintro rfl; exact graph_case.mpr <| Or.inl âŸ¨v, rflâŸ©

lemma graph_succ {v i x : V} :
    Graph âŸªv, i + 1, xâŸ« â†” Graph âŸªsndIdx v, i, xâŸ« := by
  constructor
  Â· intro h
    rcases graph_case.mp h with (âŸ¨v, hâŸ© | âŸ¨v, i, x, h, hvâŸ©)
    Â· simp at h
    Â· simp at h; rcases h with âŸ¨rfl, rfl, rflâŸ©; exact hv
  Â· intro h; exact graph_case.mpr <| Or.inr âŸ¨v, i, x, rfl, hâŸ©

lemma graph_exists (v i : V) : âˆƒ x, Graph âŸªv, i, xâŸ« := by
  suffices âˆ€ i' â‰¤ i, âˆ€ v' â‰¤ v, âˆƒ x, Graph âŸªv', i', xâŸ« from this i (by simp) v (by simp)
  intro i' hi'
  induction i' using induction_iSigmaOne
  Â· definability
  case zero =>
    intro v' _
    exact âŸ¨fstIdx v', graph_case.mpr <| Or.inl âŸ¨v', rflâŸ©âŸ©
  case succ i' ih =>
    intro v' hv'
    rcases ih (le_trans le_self_add hi') (sndIdx v') (le_trans (by simp) hv') with âŸ¨x, hxâŸ©
    exact âŸ¨x, graph_case.mpr <| Or.inr âŸ¨v', i', x, rfl, hxâŸ©âŸ©

lemma graph_unique {v i xâ‚ xâ‚‚ : V} : Graph âŸªv, i, xâ‚âŸ« â†’ Graph âŸªv, i, xâ‚‚âŸ« â†’ xâ‚ = xâ‚‚ := by
  induction i using induction_iPiOne generalizing v xâ‚ xâ‚‚
  Â· definability
  case zero =>
    simp [graph_zero]
    rintro rfl rfl; rfl
  case succ i ih =>
    simp [graph_succ]
    exact ih

lemma graph_existsUnique (v i : V) : âˆƒ! x, Graph âŸªv, i, xâŸ« := by
  rcases graph_exists v i with âŸ¨x, hxâŸ©
  exact ExistsUnique.intro x hx (fun y hy â†¦ graph_unique hy hx)

end Nth

section nth

open Nth

def nth (v i : V) : V := Classical.choose! (graph_existsUnique v i)

scoped notation:max v:max ".[" i "]" => nth v i

lemma nth_graph (v i : V) : Graph âŸªv, i, v.[i]âŸ« :=
  Classical.choose!_spec (graph_existsUnique v i)

lemma nth_eq_of_graph {v i x : V} (h : Graph âŸªv, i, xâŸ«) : nth v i = x := graph_unique (nth_graph v i) h

lemma nth_zero (v : V) : v.[0] = fstIdx v := nth_eq_of_graph (graph_zero.mpr rfl)

lemma nth_succ (v i : V) : v.[i + 1] = (sndIdx v).[i] := nth_eq_of_graph (graph_succ.mpr <| nth_graph _ _)

@[simp] lemma nth_cons_zero (x v : V) : (x âˆ· v).[0] = x := by
  simp [nth_zero]

@[simp] lemma nth_cons_succ (x v i : V) : (x âˆ· v).[i + 1] = v.[i] := by
  simp [nth_succ]

@[simp] lemma nth_cons_one (x v : V) : (x âˆ· v).[1] = v.[0] := by
  simpa using nth_cons_succ x v 0

@[simp] lemma nth_cons_two (x v : V) : (x âˆ· v).[2] = v.[1] := by
  simpa [-nth_cons_succ, one_add_one_eq_two] using nth_cons_succ x v 1

lemma cons_induction (Î“) {P : V â†’ Prop} (hP : (Î“, 1)-Predicate P)
    (nil : P 0) (cons : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  order_induction_hh â„’â‚’áµ£ Î“ 1 hP (by
    intro v ih
    rcases nil_or_cons v with (rfl | âŸ¨x, v, rflâŸ©)
    Â· exact nil
    Â· exact cons _ _ (ih v (by simp)))

@[elab_as_elim]
lemma cons_induction_sigmaâ‚ {P : V â†’ Prop} (hP : ğšºâ‚-Predicate P)
    (nil : P 0) (cons : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  cons_induction ğšº hP nil cons

@[elab_as_elim]
lemma cons_induction_piâ‚ {P : V â†’ Prop} (hP : ğš·â‚-Predicate P)
    (nil : P 0) (cons : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  cons_induction ğš· hP nil cons

section

def _root_.LO.FirstOrder.Arith.nthDef : ğšºâ‚-Semisentence 3 :=
  .mkSigma â€œy v i | âˆƒ pr, !pairâ‚ƒDef pr v i y âˆ§ !graphDef prâ€ (by simp)

lemma nth_defined : ğšºâ‚-Functionâ‚‚ (nth : V â†’ V â†’ V) via nthDef := by
  intro v; simp [nthDef, graph_defined.df.iff]
  constructor
  Â· intro h; rw [h]; exact nth_graph _ _
  Â· intro h; simp [nth_eq_of_graph h]

@[simp] lemma eval_nthDef (v) :
    Semiformula.Evalbm V v nthDef.val â†” v 0 = nth (v 1) (v 2) := nth_defined.df.iff v

instance nth_definable : ğšºâ‚-Functionâ‚‚ (nth : V â†’ V â†’ V) := Defined.to_definable _ nth_defined

instance nth_definable' (Î“) : (Î“, m + 1)-Functionâ‚‚ (nth : V â†’ V â†’ V) := .of_sigmaOne nth_definable _ _

def _root_.LO.FirstOrder.Arith.consDef : ğšºâ‚€-Semisentence 3 :=
  .mkSigma â€œw x v | âˆƒ xv < w, !pairDef xv x v âˆ§ w = xv + 1â€ (by simp)

lemma cons_defined : ğšºâ‚€-Functionâ‚‚ (cons : V â†’ V â†’ V) via consDef := by
  intro v; simp [consDef]
  constructor
  Â· intro h; rw [h]; exact âŸ¨_, by simp [cons_def], rfl, rflâŸ©
  Â· intro âŸ¨_, _, rfl, hâŸ©; exact h

@[simp] lemma eval_cons (v) :
    Semiformula.Evalbm V v consDef.val â†” v 0 = v 1 âˆ· v 2 := cons_defined.df.iff v

instance cons_definable : ğšºâ‚€-Functionâ‚‚ (cons : V â†’ V â†’ V) := Defined.to_definable _ cons_defined

instance cons_definable' (Î“) : Î“-Functionâ‚‚ (cons : V â†’ V â†’ V) := .of_zero cons_definable _

def _root_.LO.FirstOrder.Arith.mkVecâ‚Def : ğšºâ‚€-Semisentence 2 := .mkSigma
  â€œs x | !consDef s x 0â€ (by simp)

lemma mkVecâ‚_defined : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ ?[x]) via mkVecâ‚Def := by
  intro v; simp [mkVecâ‚Def]

@[simp] lemma eval_mkVecâ‚Def (v) :
    Semiformula.Evalbm V v mkVecâ‚Def.val â†” v 0 = ?[v 1] := mkVecâ‚_defined.df.iff v

instance mkVecâ‚_definable : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ ?[x]) := Defined.to_definable _ mkVecâ‚_defined

instance mkVecâ‚_definable' (Î“) : Î“-Functionâ‚ (fun x : V â†¦ ?[x]) := .of_zero mkVecâ‚_definable _

def _root_.LO.FirstOrder.Arith.mkVecâ‚‚Def : ğšºâ‚-Semisentence 3 := .mkSigma
  â€œs x y | âˆƒ sy, !mkVecâ‚Def sy y âˆ§ !consDef s x syâ€ (by simp)

lemma mkVecâ‚‚_defined : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) via mkVecâ‚‚Def := by
  intro v; simp [mkVecâ‚‚Def]

@[simp] lemma eval_mkVecâ‚‚Def (v) :
    Semiformula.Evalbm V v mkVecâ‚‚Def.val â†” v 0 = ?[v 1, v 2] := mkVecâ‚‚_defined.df.iff v

instance mkVecâ‚‚_definable : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) := Defined.to_definable _ mkVecâ‚‚_defined

instance mkVecâ‚‚_definable' (Î“) : (Î“, m + 1)-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) := .of_sigmaOne mkVecâ‚‚_definable _ _

end

lemma cons_absolute (a v : â„•) : ((a âˆ· v : â„•) : V) = (a : V) âˆ· (v : V) := by
  simpa using DefinedFunction.shigmaZero_absolute_func V cons_defined cons_defined ![a, v]

/-- TODO: move-/
lemma piâ‚_zero : Ï€â‚ (0 : V) = 0 := nonpos_iff_eq_zero.mp (piâ‚_le_self 0)

lemma piâ‚‚_zero : Ï€â‚‚ (0 : V) = 0 := nonpos_iff_eq_zero.mp (piâ‚‚_le_self 0)

@[simp] lemma nth_zero_idx (i : V) : (0).[i] = 0 := by
  induction i using induction_iSigmaOne
  Â· definability
  case zero => simp [nth_zero, fstIdx, piâ‚_zero]
  case succ i ih => simp [nth_succ, sndIdx, piâ‚‚_zero, ih]

lemma nth_lt_of_pos {v} (hv : 0 < v) (i : V) : v.[i] < v := by
  induction i using induction_iPiOne generalizing v
  Â· definability
  case zero =>
    rcases zero_or_succ v with (rfl | âŸ¨v, rflâŸ©)
    Â· simp at hv
    Â· simp [succ_eq_cons]
  case succ i ih =>
    rcases zero_or_succ v with (rfl | âŸ¨v, rflâŸ©)
    Â· simp at hv
    Â· simp [succ_eq_cons v]
      rcases eq_zero_or_pos (Ï€â‚‚ v) with (h | h)
      Â· simp [h]
      Â· exact lt_trans (ih h) (by simp)

@[simp] lemma nth_le (v i : V) : v.[i] â‰¤ v := by
  rcases eq_zero_or_pos v with (h | h)
  Â· simp [h]
  Â· exact le_of_lt <| nth_lt_of_pos h i

end nth


/-!

### Inductivly Construction of Function on List

-/

namespace VecRec

structure Blueprint (arity : â„•) where
  nil : ğšºâ‚-Semisentence (arity + 1)
  cons : ğšºâ‚-Semisentence (arity + 4)

namespace Blueprint

variable {arity : â„•} (Î² : Blueprint arity)

def blueprint : Fixpoint.Blueprint arity where
  core := .mkDelta
    (.mkSigma â€œpr C |
        (âˆƒ nil, !Î².nil nil â‹¯ âˆ§ !pairDef pr 0 nil) âˆ¨
        (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C,
          âˆƒ xxs, !consDef xxs x xs âˆ§
          âˆƒ cons, !Î².cons cons x xs ih â‹¯ âˆ§
          !pairDef pr xxs cons âˆ§ :âŸªxs, ihâŸ«:âˆˆ C)â€
      (by simp))
    (.mkPi â€œpr C |
        (âˆ€ nil, !Î².nil nil â‹¯ â†’ !pairDef pr 0 nil) âˆ¨
        (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C,
          âˆ€ xxs, !consDef xxs x xs â†’
          âˆ€ cons, !Î².cons cons x xs ih â‹¯ â†’
          !pairDef pr xxs cons âˆ§ :âŸªxs, ihâŸ«:âˆˆ C)â€
      (by simp))

def graphDef : ğšºâ‚-Semisentence (arity + 1) := Î².blueprint.fixpointDef

def resultDef : ğšºâ‚-Semisentence (arity + 2) :=
  .mkSigma â€œy xs | âˆƒ pr, !pairDef pr xs y âˆ§ !Î².graphDef pr â‹¯â€ (by simp)

end Blueprint

variable (V)

structure Construction {arity : â„•} (Î² : Blueprint arity) where
  nil (param : Fin arity â†’ V) : V
  cons (param : Fin arity â†’ V) (x xs ih) : V
  nil_defined : DefinedFunction nil Î².nil
  cons_defined : DefinedFunction (fun v â†¦ cons (v Â·.succ.succ.succ) (v 0) (v 1) (v 2)) Î².cons

variable {V}

namespace Construction

variable {arity : â„•} {Î² : Blueprint arity} (c : Construction V Î²)

def Phi (param : Fin arity â†’ V) (C : Set V) (pr : V) : Prop :=
  pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x xs ih, pr = âŸªx âˆ· xs, c.cons param x xs ihâŸ« âˆ§ âŸªxs, ihâŸ« âˆˆ C)

private lemma phi_iff (param : Fin arity â†’ V) (C pr : V) :
    c.Phi param {x | x âˆˆ C} pr â†”
    pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C, pr = âŸªx âˆ· xs, c.cons param x xs ihâŸ« âˆ§ âŸªxs, ihâŸ« âˆˆ C) := by
  constructor
  Â· rintro (h | âŸ¨x, xs, ih, rfl, hCâŸ©)
    Â· left; exact h
    Â· right
      exact âŸ¨x, lt_of_lt_of_le (by simp) (le_pair_left _ _),
        xs, lt_of_lt_of_le (by simp) (le_pair_left _ _), ih, lt_of_mem_rng hC, rfl , hCâŸ©
  Â· rintro (h | âŸ¨x, _, xs, _, ih, _, rfl, hCâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨x, xs, ih, rfl, hCâŸ©

def construction : Fixpoint.Construction V Î².blueprint where
  Î¦ := c.Phi
  defined := âŸ¨by
    intro v; simp [Blueprint.blueprint, c.nil_defined.df.iff, c.cons_defined.df.iff], by
    intro v; simpa [Blueprint.blueprint, c.nil_defined.df.iff, c.cons_defined.df.iff] using c.phi_iff _ _ _âŸ©
  monotone := by
    rintro C C' hC _ x (h | âŸ¨v, i, hv, rfl, hâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨v, i, hv, rfl, hC hâŸ©

instance : c.construction.Finite V where
  finite := by
    rintro C v x (h | âŸ¨x, xs, ih, rfl, hâŸ©)
    Â· exact âŸ¨0, Or.inl hâŸ©
    Â· exact âŸ¨âŸªxs, ihâŸ« + 1, Or.inr âŸ¨x, xs, ih, rfl, h, by simpâŸ©âŸ©

variable (param : Fin arity â†’ V)

def Graph : V â†’ Prop := c.construction.Fixpoint param

section

lemma graph_defined : Arith.Defined (fun v â†¦ c.Graph (v Â·.succ) (v 0)) Î².graphDef :=
  c.construction.fixpoint_defined

instance graph_definable : Arith.Definable â„’â‚’áµ£ ğšºâ‚ (fun v â†¦ c.Graph (v Â·.succ) (v 0)) := Defined.to_definable _ c.graph_defined

instance graph_definable' (param) : ğšºâ‚-Predicate (c.Graph param) := by
  simpa using Definable.retractiont (n := 1) c.graph_definable (#0 :> fun i â†¦ &(param i))

end

variable {param}

lemma graph_case {pr : V} :
    c.Graph param pr â†” pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x xs ih, pr = âŸªx âˆ· xs, c.cons param x xs ihâŸ« âˆ§ c.Graph param âŸªxs, ihâŸ«) :=
  c.construction.case

lemma graph_nil {l : V} :
    c.Graph param âŸª0, lâŸ« â†” l = c.nil param := by
  constructor
  Â· intro h
    rcases c.graph_case.mp h with (h | âŸ¨x, xs, ih, h, _âŸ©)
    Â· simp at h; rcases h with âŸ¨rfl, rflâŸ©; rfl
    Â· simp at h
  Â· rintro rfl; exact c.graph_case.mpr <| Or.inl rfl

lemma graph_cons {x xs y : V} :
    c.Graph param âŸªx âˆ· xs, yâŸ« â†” âˆƒ y', y = c.cons param x xs y' âˆ§ c.Graph param âŸªxs, y'âŸ« := by
  constructor
  Â· intro h
    rcases c.graph_case.mp h with (h | âŸ¨x, xs, y, h, hgâŸ©)
    Â· simp at h
    Â· simp at h; rcases h with âŸ¨âŸ¨rfl, rflâŸ©, rflâŸ©
      exact âŸ¨y, rfl, hgâŸ©
  Â· rintro âŸ¨y, rfl, hâŸ©; exact c.graph_case.mpr <| Or.inr âŸ¨x, xs, y, rfl, hâŸ©

variable (param)

lemma graph_exists (xs : V) : âˆƒ y, c.Graph param âŸªxs, yâŸ« := by
  induction xs using cons_induction_sigmaâ‚
  Â· definability
  case nil =>
    exact âŸ¨c.nil param, c.graph_nil.mpr rflâŸ©
  case cons x xs ih =>
    Â· rcases ih with âŸ¨y, hyâŸ©
      exact âŸ¨c.cons param x xs y, c.graph_cons.mpr âŸ¨y, rfl, hyâŸ©âŸ©

variable {param}

lemma graph_unique {xs yâ‚ yâ‚‚ : V} : c.Graph param âŸªxs, yâ‚âŸ« â†’ c.Graph param âŸªxs, yâ‚‚âŸ« â†’ yâ‚ = yâ‚‚ := by
  induction xs using cons_induction_piâ‚ generalizing yâ‚ yâ‚‚
  Â· definability
  case nil =>
    simp [graph_nil]; rintro rfl rfl; rfl
  case cons x v ih =>
    simp [graph_cons]
    rintro lâ‚ rfl hâ‚ lâ‚‚ rfl hâ‚‚
    rcases ih hâ‚ hâ‚‚; rfl

variable (param)

lemma graph_existsUnique (xs : V) : âˆƒ! y, c.Graph param âŸªxs, yâŸ« := by
  rcases c.graph_exists param xs with âŸ¨y, hyâŸ©
  exact ExistsUnique.intro y hy (fun y' hy' â†¦ c.graph_unique hy' hy)

def result (xs : V) : V := Classical.choose! (c.graph_existsUnique param xs)

lemma result_graph (xs : V) : c.Graph param âŸªxs, c.result param xsâŸ« :=
  Classical.choose!_spec (c.graph_existsUnique param xs)

lemma result_eq_of_graph {xs y : V} (h : c.Graph param âŸªxs, yâŸ«) : c.result param xs = y :=
  c.graph_unique (c.result_graph param xs) h

@[simp] lemma result_nil : c.result param (0 : V) = c.nil param := c.result_eq_of_graph param (c.graph_nil.mpr rfl)

@[simp] lemma result_cons (x xs : V) :
    c.result param (x âˆ· xs) = c.cons param x xs (c.result param xs) :=
  c.result_eq_of_graph param (c.graph_cons.mpr âŸ¨_, rfl, c.result_graph param xsâŸ©)

section

lemma result_defined : Arith.DefinedFunction (fun v â†¦ c.result (v Â·.succ) (v 0)) Î².resultDef := by
  intro v; simp [Blueprint.resultDef, c.graph_defined.df.iff]
  constructor
  Â· intro h; rw [h]; exact c.result_graph _ _
  Â· intro h; rw [c.result_eq_of_graph _ h]

@[simp] lemma eval_resultDef (v) :
    Semiformula.Evalbm V v Î².resultDef.val â†” v 0 = c.result (v Â·.succ.succ) (v 1) := c.result_defined.df.iff v

instance result_definable : Arith.DefinableFunction â„’â‚’áµ£ ğšºâ‚ (fun v â†¦ c.result (v Â·.succ) (v 0)) :=
  Defined.to_definable _ c.result_defined

instance result_definable' (Î“ m) :
  Arith.DefinableFunction â„’â‚’áµ£ (Î“, m + 1) (fun v â†¦ c.result (v Â·.succ) (v 0)) := .of_sigmaOne c.result_definable _ _

end

end Construction

end VecRec

/-!

### Length of List

-/

namespace Len

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy | y = 0â€ (by simp)
  cons := .mkSigma â€œy x xs ih | y = ih + 1â€ (by simp)

def construction : VecRec.Construction V blueprint where
  nil _ := 0
  cons _ _ _ ih := ih + 1
  nil_defined := by intro v; simp [blueprint]
  cons_defined := by intro v; simp [blueprint]; rfl

end Len

section len

open Len

def len (v : V) : V := construction.result ![] v

@[simp] lemma len_nil : len (0 : V) = 0 := by simp [len, construction]

@[simp] lemma len_cons (x v : V) : len (x âˆ· v) = len v + 1 := by simp [len, construction]

section

def _root_.LO.FirstOrder.Arith.lenDef : ğšºâ‚-Semisentence 2 := blueprint.resultDef

lemma len_defined : ğšºâ‚-Functionâ‚ (len : V â†’ V) via lenDef := construction.result_defined

@[simp] lemma eval_lenDef (v) :
    Semiformula.Evalbm V v lenDef.val â†” v 0 = len (v 1) := len_defined.df.iff v

instance len_definable : ğšºâ‚-Functionâ‚ (len : V â†’ V) := Defined.to_definable _ len_defined

instance len_definable' (Î“) : (Î“, m + 1)-Functionâ‚ (len : V â†’ V) := .of_sigmaOne len_definable _ _

end

@[simp] lemma len_zero_iff_eq_nil {v : V} : len v = 0 â†” v = 0 := by
  rcases nil_or_cons v with (rfl | âŸ¨x, v, rflâŸ©) <;> simp

lemma nth_lt_len {v i : V} (hl : len v â‰¤ i) : v.[i] = 0 := by
  induction v using cons_induction_piâ‚ generalizing i
  Â· definability
  case nil => simp
  case cons x v ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp at hl
    simpa using ih (by simpa using hl)

end len

lemma nth_ext {vâ‚ vâ‚‚ : V} (hl : len vâ‚ = len vâ‚‚) (H : âˆ€ i < len vâ‚, vâ‚.[i] = vâ‚‚.[i]) : vâ‚ = vâ‚‚ := by
  induction vâ‚ using cons_induction_piâ‚ generalizing vâ‚‚
  Â· definability
  case nil =>
    exact Eq.symm <| len_zero_iff_eq_nil.mp (by simp [â†hl])
  case cons xâ‚ vâ‚ ih =>
    rcases nil_or_cons vâ‚‚ with (rfl | âŸ¨xâ‚‚, vâ‚‚, rflâŸ©)
    Â· simp at hl
    have hx : xâ‚ = xâ‚‚ := by simpa using H 0 (by simp)
    have hv : vâ‚ = vâ‚‚ := ih (by simpa using hl) (by intro i hi; simpa using H (i + 1) (by simpa using hi))
    simp [hx, hv]

lemma le_of_nth_le_nth {vâ‚ vâ‚‚ : V} (hl : len vâ‚ = len vâ‚‚) (H : âˆ€ i < len vâ‚, vâ‚.[i] â‰¤ vâ‚‚.[i]) : vâ‚ â‰¤ vâ‚‚ := by
  induction vâ‚ using cons_induction_piâ‚ generalizing vâ‚‚
  Â· definability
  case nil => simp
  case cons xâ‚ vâ‚ ih =>
    rcases nil_or_cons vâ‚‚ with (rfl | âŸ¨xâ‚‚, vâ‚‚, rflâŸ©)
    Â· simp at hl
    have hx : xâ‚ â‰¤ xâ‚‚ := by simpa using H 0 (by simp)
    have hv : vâ‚ â‰¤ vâ‚‚ := ih (by simpa using hl) (by intro i hi; simpa using H (i + 1) (by simpa using hi))
    exact cons_le_cons hx hv

theorem sigmaOne_skolem_vec {R : V â†’ V â†’ Prop} (hP : ğšºâ‚-Relation R) {l}
    (H : âˆ€ x < l, âˆƒ y, R x y) : âˆƒ v, len v = l âˆ§ âˆ€ i < l, R i v.[i] := by
  have : âˆ€ k â‰¤ l, âˆƒ v, len v = k âˆ§ âˆ€ i < k, R (l - k + i) v.[i] := by
    intro k hk
    induction k using induction_iSigmaOne
    Â· definability
    case zero => exact âŸ¨0, by simpâŸ©
    case succ k ih =>
      rcases ih (le_trans (by simp) hk) with âŸ¨v, hvk, hvâŸ©
      have : âˆƒ y, R (l - (k + 1)) y := H (l - (k + 1)) (by simp [tsub_lt_iff_left hk])
      rcases this with âŸ¨y, hyâŸ©
      exact âŸ¨y âˆ· v, by simp [hvk], fun i hi â†¦ by
        rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
        Â· simpa using hy
        Â· simpa [sub_succ_add_succ (succ_le_iff_lt.mp hk) i] using hv i (by simpa using hi)âŸ©
  simpa using this l (by rfl)


/-!

### Take Last k-Element

-/

namespace TakeLast

def blueprint : VecRec.Blueprint 1 where
  nil := .mkSigma â€œy k | y = 0â€ (by simp)
  cons := .mkSigma â€œy x xs ih k |
    âˆƒ l, !lenDef l xs âˆ§
    (l < k â†’ !consDef y x xs) âˆ§ (k â‰¤ l â†’ y = ih)â€ (by simp)

def construction : VecRec.Construction V blueprint where
  nil _ := 0
  cons (param x xs ih) := if len xs < param 0 then x âˆ· xs else ih
  nil_defined := by intro v; simp [blueprint]
  cons_defined := by
    intro v
    simp [blueprint, Fin.isValue]

    show (v 0 = if len (v 2) < v 4 then v 1 âˆ· v 2 else v 3) â†”
      (len (v 2) < v 4 â†’ v 0 = v 1 âˆ· v 2) âˆ§ (v 4 â‰¤ len (v 2) â†’ v 0 = v 3)
    rcases lt_or_ge (len (v 2)) (v 4) with (hv | hv)
    Â· simp [hv]
    Â· simp [hv, not_lt_of_le hv]

end TakeLast

section takeLast

open TakeLast

def takeLast (v k : V) : V := construction.result ![k] v

@[simp] lemma takeLast_nil : takeLast (0 : V) k = 0 := by simp [takeLast, construction]

lemma takeLast_cons (x v : V) :
    takeLast (x âˆ· v) k = if len v < k then x âˆ· v else takeLast v k := by simp [takeLast, construction]

section

def _root_.LO.FirstOrder.Arith.takeLastDef : ğšºâ‚-Semisentence 3 := blueprint.resultDef

lemma takeLast_defined : ğšºâ‚-Functionâ‚‚ (takeLast : V â†’ V â†’ V) via takeLastDef := construction.result_defined

@[simp] lemma eval_takeLastDef (v) :
    Semiformula.Evalbm V v takeLastDef.val â†” v 0 = takeLast (v 1) (v 2) := takeLast_defined.df.iff v

instance takeLast_definable : ğšºâ‚-Functionâ‚‚ (takeLast : V â†’ V â†’ V) := Defined.to_definable _ takeLast_defined

instance takeLast_definable' (Î“) : (Î“, m + 1)-Functionâ‚‚ (takeLast : V â†’ V â†’ V) := .of_sigmaOne takeLast_definable _ _

end

lemma len_takeLast {v k : V} (h : k â‰¤ len v) : len (takeLast v k) = k := by
  induction v using cons_induction_sigmaâ‚
  Â· definability
  case nil => simp_all
  case cons x v ih =>
    simp [takeLast_cons]
    have : k = len v + 1 âˆ¨ k â‰¤ len v := by
      rcases eq_or_lt_of_le h with (h | h)
      Â· left; simpa using h
      Â· right; simpa [lt_succ_iff_le] using h
    rcases this with (rfl | hkv)
    Â· simp
    Â· simp [not_lt_of_le hkv, ih hkv]

@[simp] lemma takeLast_len_self (v : V) : takeLast v (len v) = v := by
  rcases nil_or_cons v with (rfl | âŸ¨x, v, rflâŸ©) <;> simp [takeLast_cons]

/-- TODO: move -/
@[simp] lemma add_sub_add (a b c : V) : (a + c) - (b + c) = a - b := add_tsub_add_eq_tsub_right a c b

@[simp] lemma takeLast_zero (v : V) : takeLast v 0 = 0 := by
  induction v using cons_induction_sigmaâ‚
  Â· definability
  case nil => simp
  case cons x v ih => simp [takeLast_cons, ih]

lemma takeLast_succ_of_lt {i v : V} (h : i < len v) : takeLast v (i + 1) = v.[len v - (i + 1)] âˆ· takeLast v i := by
  induction v using cons_induction_sigmaâ‚ generalizing i
  Â· definability
  case nil => simp at h
  case cons x v ih =>
    simp [takeLast_cons, lt_succ_iff_le]
    rcases show i = len v âˆ¨ i < len v from eq_or_lt_of_le (by simpa [lt_succ_iff_le] using h) with (rfl | hi)
    Â· simp
    Â· have : len v - i = len v - (i + 1) + 1 := by
        rw [â†sub_sub, sub_add_self_of_le (pos_iff_one_le.mp (tsub_pos_of_lt hi))]
      simpa [not_le_of_lt hi, â†“reduceIte, this, nth_cons_succ, not_lt_of_gt hi] using ih hi

end takeLast

/-!

### Repeat

-/

section repaetVec

def repeatVec.blueprint : PR.Blueprint 1 where
  zero := .mkSigma â€œy x | y = 0â€ (by simp)
  succ := .mkSigma â€œy ih n x | !consDef y x ihâ€ (by simp)

def repeatVec.construction : PR.Construction V repeatVec.blueprint where
  zero := fun _ â†¦ 0
  succ := fun x _ ih â†¦ x 0 âˆ· ih
  zero_defined := by intro v; simp [blueprint]
  succ_defined := by intro v; simp [blueprint]; rfl

/-- `repeatVec x k = x âˆ· x âˆ· x âˆ· ... k times ... âˆ· 0`-/
def repeatVec (x k : V) : V := repeatVec.construction.result ![x] k

@[simp] lemma repeatVec_zero (x : V) : repeatVec x 0 = 0 := by simp [repeatVec, repeatVec.construction]

@[simp] lemma repeatVec_succ (x k : V) : repeatVec x (k + 1) = x âˆ· repeatVec x k := by simp [repeatVec, repeatVec.construction]

section

def _root_.LO.FirstOrder.Arith.repeatVecDef : ğšºâ‚-Semisentence 3 := repeatVec.blueprint.resultDef |>.rew (Rew.substs ![#0, #2, #1])

lemma repeatVec_defined : ğšºâ‚-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) via repeatVecDef :=
  fun v â†¦ by simp [repeatVec.construction.result_defined_iff, repeatVecDef]; rfl

@[simp] lemma eval_repeatVec (v) :
    Semiformula.Evalbm V v repeatVecDef.val â†” v 0 = repeatVec (v 1) (v 2) := repeatVec_defined.df.iff v

instance repeatVec_definable : ğšºâ‚-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) := Defined.to_definable _ repeatVec_defined

@[simp] instance repeatVec_definable' (Î“) : (Î“, m + 1)-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) :=
  .of_sigmaOne repeatVec_definable _ _

end

@[simp] lemma len_repeatVec (x k : V) : len (repeatVec x k) = k := by
  induction k using induction_iSigmaOne
  Â· definability
  case zero => simp
  case succ k ih => simp [ih]

lemma nth_repeatVec (x k : V) {i} (h : i < k) : (repeatVec x k).[i] = x := by
  induction k using induction_iSigmaOne generalizing i
  Â· definability
  case zero => simp at h
  case succ k ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simpa using ih (by simpa using h)

lemma len_repeatVec_of_nth_le {v m : V} (H : âˆ€ i < len v, v.[i] â‰¤ m) : v â‰¤ repeatVec m (len v) :=
  le_of_nth_le_nth (by simp) (fun i hi â†¦ by simp [nth_repeatVec m (len v) hi, H i hi])

end repaetVec

/-!

### Convert to Set

-/

namespace VecToSet

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy | y = 0â€ (by simp)
  cons := .mkSigma â€œy x xs ih | !insertDef y x ihâ€ (by simp)

def construction : VecRec.Construction V blueprint where
  nil _ := âˆ…
  cons (_ x _ ih) := insert x ih
  nil_defined := by intro v; simp [blueprint, emptyset_def]
  cons_defined := by intro v; simp [blueprint]; rfl

end VecToSet

section vecToSet

open VecToSet

def vecToSet (v : V) : V := construction.result ![] v

@[simp] lemma vecToSet_nil : vecToSet (0 : V) = âˆ… := by simp [vecToSet, construction]

@[simp] lemma vecToSet_cons (x v : V) :
    vecToSet (x âˆ· v) = insert x (vecToSet v) := by simp [vecToSet, construction]

section

def _root_.LO.FirstOrder.Arith.vecToSetDef : ğšºâ‚-Semisentence 2 := blueprint.resultDef

lemma vecToSet_defined : ğšºâ‚-Functionâ‚ (vecToSet : V â†’ V) via vecToSetDef := construction.result_defined

@[simp] lemma eval_vecToSetDef (v) :
    Semiformula.Evalbm V v vecToSetDef.val â†” v 0 = vecToSet (v 1) := vecToSet_defined.df.iff v

instance vecToSet_definable : ğšºâ‚-Functionâ‚ (vecToSet : V â†’ V) := Defined.to_definable _ vecToSet_defined

instance vecToSet_definable' (Î“) : (Î“, m + 1)-Functionâ‚ (vecToSet : V â†’ V) := .of_sigmaOne vecToSet_definable _ _

end

lemma mem_vecToSet_iff {v x : V} : x âˆˆ vecToSet v â†” âˆƒ i < len v, x = v.[i] := by
  induction v using cons_induction_sigmaâ‚
  Â· definability
  case nil => simp
  case cons y v ih =>
    simp only [vecToSet_cons, mem_bitInsert_iff, ih, len_cons]
    constructor
    Â· rintro (rfl | âŸ¨i, hi, rflâŸ©)
      Â· exact âŸ¨0, by simpâŸ©
      Â· exact âŸ¨i + 1, by simp [hi]âŸ©
    Â· rintro âŸ¨i, hi, rflâŸ©
      rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
      Â· simp
      Â· right; exact âŸ¨i, by simpa using hi, by simpâŸ©

@[simp] lemma nth_mem_vecToSet {v i : V} (h : i < len v) : v.[i] âˆˆ vecToSet v :=
  mem_vecToSet_iff.mpr âŸ¨i, h, rflâŸ©

end vecToSet

end LO.Arith
