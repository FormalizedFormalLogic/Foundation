import Arithmetization.IDeltaZero.Exponential.Exp
import Arithmetization.IDeltaZero.Exponential.Log

namespace LO.FirstOrder

namespace Arith

noncomputable section

variable {M : Type} [Zero M] [One M] [Add M] [Mul M] [LT M]

namespace Model

section ISigmaâ‚

variable [M âŠ§â‚˜* ğˆğšºâ‚]

def Bit (i a : M) : Prop := LenBit (exp i) a

instance : Membership M M := âŸ¨BitâŸ©

def bitDef : ğšºâ‚€-Sentence 2 := âŸ¨â€œâˆƒ[#0 < #2 + 1] (!expDef [#0, #1] âˆ§ !lenbitDef [#0, #2])â€, by simpâŸ©

lemma bit_defined : ğšºâ‚€-Relation ((Â· âˆˆ Â·) : M â†’ M â†’ Prop) via bitDef := by
  intro v; simp [bitDef, lenbit_defined.pval, exp_defined_deltaZero.pval, â†le_iff_lt_succ]
  constructor
  Â· intro h; exact âŸ¨exp (v 0), by simp [h.le], rfl, hâŸ©
  Â· rintro âŸ¨_, _, rfl, hâŸ©; exact h

@[instance, definability] def mem_definable : DefinableRel â„’â‚’áµ£ ğšº 0 ((Â· âˆˆ Â·) : M â†’ M â†’ Prop) := defined_to_with_param _ bit_defined

open Classical in
noncomputable def bitInsert (i a : M) : M := if i âˆˆ a then a else a + exp i

open Classical in
noncomputable def bitRemove (i a : M) : M := if i âˆˆ a then a - exp i else a

instance : Insert M M := âŸ¨bitInsertâŸ©

lemma insert_eq {i a : M} : insert i a = bitInsert i a := rfl

lemma mem_iff_bit {i a : M} : i âˆˆ a â†” Bit i a := iff_of_eq rfl

lemma exp_le_of_mem {i a : M} (h : i âˆˆ a) : exp i â‰¤ a := LenBit.le h

lemma lt_of_mem {i a : M} (h : i âˆˆ a) : i < a := lt_of_lt_of_le (lt_exp i) (exp_le_of_mem h)

lemma not_mem_of_lt_exp {i a : M} (h : a < exp i) : i âˆ‰ a := fun H â†¦ by have := lt_of_le_of_lt (exp_le_of_mem H) h; simp at this

section

variable {L : Language} [L.ORing] [Structure L M] [Structure.ORing L M] [Structure.Monotone L M]

variable (Î“ : Polarity) (n : â„•)

@[definability] lemma Definable.ball_mem {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial L Î“ n f) (h : Definable L Î“ n (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable L Î“ n (fun v â†¦ âˆ€ x âˆˆ f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆ€[#0 < #1] (!bitDef .[#0, #1] â†’ !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p)))â€,
    by simp; apply Hierarchy.oringEmb; simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, bit_defined.pval, â†le_iff_lt_succ]
        constructor
        Â· rintro h; exact âŸ¨f v, hbf v, rfl, fun x _ hx â†¦ h x hxâŸ©
        Â· rintro âŸ¨_, _, rfl, hâŸ© x hx; exact h x (lt_of_mem hx) hxâŸ©

@[definability] lemma Definable.bex_mem {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial L Î“ n f) (h : Definable L Î“ n (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable L Î“ n (fun v â†¦ âˆƒ x âˆˆ f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆƒ[#0 < #1] (!bitDef .[#0, #1] âˆ§ !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p)))â€,
    by simp; apply Hierarchy.oringEmb; simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, bit_defined.pval, â†le_iff_lt_succ]
        constructor
        Â· rintro âŸ¨x, hx, hâŸ©; exact âŸ¨f v, hbf v, rfl, x, lt_of_mem hx, hx, hâŸ©
        Â· rintro âŸ¨_, _, rfl, x, _, hx, hâŸ©; exact âŸ¨x, hx, hâŸ©âŸ©

end

lemma mem_iff_mul_exp_add_exp_add {i a : M} : i âˆˆ a â†” âˆƒ k, âˆƒ r < exp i, a = k * exp (i + 1) + exp i + r := by
  simp [mem_iff_bit, exp_succ]
  exact lenbit_iff_add_mul (exp_pow2 i) (a := a)

lemma not_mem_iff_mul_exp_add {i a : M} : i âˆ‰ a â†” âˆƒ k, âˆƒ r < exp i, a = k * exp (i + 1) + r := by
  simp [mem_iff_bit, exp_succ]
  exact not_lenbit_iff_add_mul (exp_pow2 i) (a := a)

@[simp] lemma not_mem_zero (i : M) : i âˆ‰ (0 : M) := by simp [mem_iff_bit, Bit]

@[simp] lemma mem_bitInsert_iff {i j a : M} :
    i âˆˆ insert j a â†” i = j âˆ¨ i âˆˆ a := by
  by_cases h : j âˆˆ a <;> simp [h, insert_eq, bitInsert]
  Â· by_cases e : i = j <;> simp [h, e]
  Â· simpa [exp_inj.eq_iff] using
      lenbit_add_pow2_iff_of_not_lenbit (exp_pow2 i) (exp_pow2 j) h

@[simp] lemma mem_bitRemove_iff {i j a : M} :
    i âˆˆ bitRemove j a â†” i â‰  j âˆ§ i âˆˆ a := by
  by_cases h : j âˆˆ a <;> simp [h, bitRemove]
  Â· simpa [exp_inj.eq_iff] using
      lenbit_sub_pow2_iff_of_lenbit (exp_pow2 i) (exp_pow2 j) h
  Â· rintro _ rfl; contradiction

lemma bitRemove_lt_of_mem {i a : M} (h : i âˆˆ a) : bitRemove i a < a := by
  simp [h, bitRemove, tsub_lt_iff_left (exp_le_of_mem h)]

lemma pos_of_nonempty {i a : M} (h : i âˆˆ a) : 0 < a := by
  by_contra A; simp at A; rcases A; simp_all

lemma log_mem_of_pos {a : M} (h : 0 < a) : log a âˆˆ a :=
  mem_iff_mul_exp_add_exp_add.mpr
    âŸ¨0, a - exp log a,
      (tsub_lt_iff_left (exp_log_le_self h)).mpr (by rw [â†two_mul]; exact lt_two_mul_exponential_log h),
      by simp; exact Eq.symm <| add_tsub_self_of_le (exp_log_le_self h)âŸ©

lemma le_log_of_mem {i a : M} (h : i âˆˆ a) : i â‰¤ log a := (exp_le_iff_le_log (pos_of_nonempty h)).mp (exp_le_of_mem h)

lemma succ_mem_iff_mem_div_two {i a : M} : i + 1 âˆˆ a â†” i âˆˆ a / 2 := by simp [mem_iff_bit, Bit, LenBit.iff_rem, exp_succ, div_mul]

lemma lt_length_of_mem {i a : M} (h : i âˆˆ a) : i < â€–aâ€– := by
  simpa [length_of_pos (pos_of_nonempty h), â†le_iff_lt_succ] using le_log_of_mem h

lemma lt_exp_iff {a i : M} : a < exp i â†” âˆ€ j âˆˆ a, j < i :=
  âŸ¨fun h j hj â†¦ exp_monotone.mp <| lt_of_le_of_lt (exp_le_of_mem hj) h,
   by contrapose; simp
      intro (h : exp i â‰¤ a)
      have pos : 0 < a := lt_of_lt_of_le (by simp) h
      exact âŸ¨log a, log_mem_of_pos pos, (exp_le_iff_le_log pos).mp hâŸ©âŸ©

instance : HasSubset M := âŸ¨fun a b â†¦ âˆ€ â¦ƒiâ¦„, i âˆˆ a â†’ i âˆˆ bâŸ©

def bitSubsetDef : ğšºâ‚€-Sentence 2 := âŸ¨â€œâˆ€[#0 < #1] (!bitDef [#0, #1] â†’ !bitDef [#0, #2])â€, by simpâŸ©

lemma bitSubset_defined : ğšºâ‚€-Relation ((Â· âŠ† Â·) : M â†’ M â†’ Prop) via bitSubsetDef := by
  intro v; simp [bitSubsetDef, bit_defined.pval]
  exact âŸ¨by intro h x _ hx; exact h hx, by intro h x hx; exact h x (lt_of_mem hx) hxâŸ©

instance bitSubset_definable : DefinableRel â„’â‚’áµ£ ğšº 0 ((Â· âŠ† Â·) : M â†’ M â†’ Prop) := Defined.to_definableâ‚€ _ bitSubset_defined

lemma mem_exp_add_succ_sub_one (i j : M) : i âˆˆ exp (i + j + 1) - 1 := by
  have : exp (i + j + 1) - 1 = (exp j - 1) * exp (i + 1) + exp i + (exp i - 1) := calc
    exp (i + j + 1) - 1 = exp j * exp (i + 1) - 1                             := by simp [exp_add, â†mul_assoc, mul_comm]
    _                   = exp j * exp (i + 1) - exp (i + 1) + exp (i + 1) - 1 := by rw [sub_add_self_of_le]; exact le_mul_of_pos_left (exp_pos j)
    _                   = (exp j - 1) * exp (i + 1) + exp (i + 1) - 1         := by simp [sub_mul]
    _                   = (exp j - 1) * exp (i + 1) + (exp i + exp i) - 1     := by simp [â†two_mul, â†exp_succ i]
    _                   = (exp j - 1) * exp (i + 1) + (exp i + exp i - 1)     := by rw [add_tsub_assoc_of_le]; simp [â†two_mul, â†pos_iff_one_le]
    _                   = (exp j - 1) * exp (i + 1) + exp i + (exp i - 1)     := by simp [add_assoc, add_tsub_assoc_of_le]
  exact mem_iff_mul_exp_add_exp_add.mpr âŸ¨exp j - 1, exp i - 1, (tsub_lt_iff_left (by simp)).mpr $ by simp, thisâŸ©

/-- under a = {0, 1, 2, ..., a - 1} -/
def under (a : M) : M := exp a - 1

lemma mem_under_iff {i j : M} : i âˆˆ under j â†” i < j := by
  constructor
  Â· intro h
    have : exp i < exp j := calc
      exp i â‰¤ exp j - 1 := exp_le_of_mem h
      _     < exp j     := pred_lt_self_of_pos (exp_pos j)
    exact exp_monotone.mp this
  Â· intro lt
    have := lt_iff_succ_le.mp lt
    let k := j - (i + 1)
    have : j = i + k + 1 := by
      simp [add_assoc, â†sub_sub, k]; rw [sub_add_self_of_le, add_tsub_self_of_le]
      Â· exact le_of_lt lt
      Â· exact le_tsub_of_add_le_left this
    rw [this]; exact mem_exp_add_succ_sub_one i k

lemma eq_zero_of_subset_zero {a : M} : a âŠ† 0 â†’ a = 0 := by
  intro h; by_contra A
  have : log a âˆˆ 0 := h (log_mem_of_pos (pos_iff_ne_zero.mpr A))
  simp_all

lemma subset_div_two {a b : M} : a âŠ† b â†’ a / 2 âŠ† b / 2 := by
  intro ss i hi
  have : i + 1 âˆˆ a := succ_mem_iff_mem_div_two.mpr hi
  exact succ_mem_iff_mem_div_two.mp <| ss this

lemma zero_mem_iff {a : M} : 0 âˆ‰ a â†” 2 âˆ£ a := by simp [mem_iff_bit, Bit, LenBit]

@[simp] lemma zero_not_mem (a : M) : 0 âˆ‰ 2 * a := by simp [mem_iff_bit, Bit, LenBit]

lemma le_of_subset {a b : M} (h : a âŠ† b) : a â‰¤ b := by
  induction b using hierarchy_polynomial_induction_oRing_piâ‚ generalizing a
  Â· definability
  case zero =>
    simp [eq_zero_of_subset_zero h]
  case even b _ IH =>
    have IH : a / 2 â‰¤ b := IH (by simpa using subset_div_two h)
    have : 2 * (a / 2) = a :=
      mul_div_self_of_dvd.mpr (zero_mem_iff.mp $ by intro ha; have : 0 âˆˆ 2 * b := h ha; simp_all)
    simpa [this] using mul_le_mul_left (a := 2) IH
  case odd b IH =>
    have IH : a / 2 â‰¤ b := IH (by simpa [div_mul_add' b 2 one_lt_two] using subset_div_two h)
    exact le_trans (le_two_mul_div_two_add_one a) (by simpa using IH)

lemma mem_ext {a b : M} (h : âˆ€ i, i âˆˆ a â†” i âˆˆ b) : a = b :=
  le_antisymm (le_of_subset fun i hi â†¦ (h i).mp hi) (le_of_subset fun i hi â†¦ (h i).mpr hi)

end ISigmaâ‚

section

variable {n : â„•} [Fact (1 â‰¤ n)] [M âŠ§â‚˜* ğˆğğƒğšº n]

theorem finset_comprehension {P : M â†’ Prop} (hP : Î“(n)-Predicate P) (a : M) :
    haveI : M âŠ§â‚˜* ğˆğšºâ‚ := mod_iSigma_of_le (show 1 â‰¤ n from Fact.out)
    âˆƒ s < exp a, âˆ€ i < a, i âˆˆ s â†” P i := by
  haveI : M âŠ§â‚˜* ğˆğšºâ‚ := mod_iSigma_of_le (show 1 â‰¤ n from Fact.out)
  have : âˆƒ s < exp a, âˆ€ i < a, P i â†’ i âˆˆ s :=
    âŸ¨under a, pred_lt_self_of_pos (by simp), fun i hi _ â†¦ by simpa [mem_under_iff] using hiâŸ©
  rcases this with âŸ¨s, hsn, hsâŸ©
  have : (Î“.alt)(n)-Predicate (fun s â†¦ âˆ€ i < a, P i â†’ i âˆˆ s) := by
    apply Definable.ball_lt; simp; apply Definable.imp
    definability
    apply @Definable.of_sigma_zero M â„’â‚’áµ£ _ _ _ _ mem_definable
  have : âˆƒ t, (âˆ€ i < a, P i â†’ i âˆˆ t) âˆ§ âˆ€ t' < t, âˆƒ x, P x âˆ§ x < a âˆ§ x âˆ‰ t' := by
    simpa using least_number_h (L := â„’â‚’áµ£) Î“.alt n this hs
  rcases this with âŸ¨t, ht, t_minimalâŸ©
  have t_le_s : t â‰¤ s := not_lt.mp (by
    intro lt
    rcases t_minimal s lt with âŸ¨i, hi, hin, hisâŸ©
    exact his (hs i hin hi))
  have : âˆ€ i < a, i âˆˆ t â†’ P i := by
    intro i _ hit
    by_contra Hi
    have : âˆƒ j, P j âˆ§ j < a âˆ§ (j âˆˆ t â†’ j = i) := by
      simpa [not_imp_not] using t_minimal (bitRemove i t) (bitRemove_lt_of_mem hit)
    rcases this with âŸ¨j, Hj, hjn, hmâŸ©
    rcases hm (ht j hjn Hj); contradiction
  exact âŸ¨t, lt_of_le_of_lt t_le_s hsn, fun i hi â†¦ âŸ¨this i hi, ht i hiâŸ©âŸ©

theorem finset_comprehension_exists_unique {P : M â†’ Prop} (hP : Î“(n)-Predicate P) (a : M) :
    haveI : M âŠ§â‚˜* ğˆğšºâ‚ := mod_iSigma_of_le (show 1 â‰¤ n from Fact.out)
    âˆƒ! s, s < exp a âˆ§ âˆ€ i < a, i âˆˆ s â†” P i := by
  haveI : M âŠ§â‚˜* ğˆğšºâ‚ := mod_iSigma_of_le (show 1 â‰¤ n from Fact.out)
  rcases finset_comprehension hP a with âŸ¨s, hs, HsâŸ©
  exact ExistsUnique.intro s âŸ¨hs, HsâŸ© (by
    intro t âŸ¨ht, HtâŸ©
    apply mem_ext
    intro i
    constructor
    Â· intro hi
      have hin : i < a := exp_monotone.mp (lt_of_le_of_lt (exp_le_of_mem hi) ht)
      exact (Hs i hin).mpr ((Ht i hin).mp hi)
    Â· intro hi
      have hin : i < a := exp_monotone.mp (lt_of_le_of_lt (exp_le_of_mem hi) hs)
      exact (Ht i hin).mpr ((Hs i hin).mp hi))

end

section ISigmaâ‚

variable [M âŠ§â‚˜* ğˆğšºâ‚]

instance : Fact (1 â‰¤ 1) := âŸ¨by rflâŸ©

theorem finset_comprehensionâ‚ {P : M â†’ Prop} (hP : Î“(1)-Predicate P) (a : M) :
    âˆƒ s < exp a, âˆ€ i < a, i âˆˆ s â†” P i :=
  finset_comprehension hP a

theorem finset_comprehensionâ‚! {P : M â†’ Prop} (hP : Î“(1)-Predicate P) (a : M) :
    âˆƒ! s, s < exp a âˆ§ (âˆ€ i < a, i âˆˆ s â†” P i) := by
  rcases finset_comprehensionâ‚ hP a with âŸ¨s, hs, HaâŸ©
  exact ExistsUnique.intro s âŸ¨hs, HaâŸ©
    (by
      rintro b âŸ¨hb, HbâŸ©
      apply mem_ext
      intro x
      constructor
      Â· intro hx
        have : x < a := exp_monotone.mp <| LE.le.trans_lt (exp_le_of_mem hx) hb
        exact (Ha x this).mpr <| (Hb x this).mp hx
      Â· intro hx
        have : x < a := exp_monotone.mp <| LE.le.trans_lt (exp_le_of_mem hx) hs
        exact (Hb x this).mpr <| (Ha x this).mp hx)

theorem finite_comprehensionâ‚! {P : M â†’ Prop} (hP : Î“(1)-Predicate P) (fin : âˆƒ m, âˆ€ i, P i â†’ i < m)  :
    âˆƒ! s : M, âˆ€ i, i âˆˆ s â†” P i := by
  rcases fin with âŸ¨m, mhâŸ©
  rcases finset_comprehensionâ‚ hP m with âŸ¨s, hs, HsâŸ©
  have H : âˆ€ i, i âˆˆ s â†” P i :=
    fun i â†¦ âŸ¨
      fun h â†¦ (Hs i (exp_monotone.mp (lt_of_le_of_lt (exp_le_of_mem h) hs))).mp h,
      fun h â†¦ (Hs i (mh i h)).mpr hâŸ©
  exact ExistsUnique.intro s H (fun s' H' â†¦ mem_ext <| fun i â†¦ by simp [H, H'])

lemma domain_exists_unique (s : M) :
    âˆƒ! d : M, âˆ€ x, x âˆˆ d â†” âˆƒ y, âŸªx, yâŸ« âˆˆ s := by
  have : (ğš·)(1)-Predicate fun x â†¦ âˆƒ y, âŸªx, yâŸ« âˆˆ s :=
    DefinablePred.of_iff (fun x â†¦ âˆƒ y < s, âŸªx, yâŸ« âˆˆ s)
      (fun x â†¦ âŸ¨by rintro âŸ¨y, hyâŸ©; exact âŸ¨y, lt_of_le_of_lt (le_pair_right x y) (lt_of_mem hy), hyâŸ©,
                by rintro âŸ¨y, _, hyâŸ©; exact âŸ¨y, hyâŸ©âŸ©)
      (by definability)
  exact finite_comprehensionâ‚!
    this
    (âŸ¨s, fun x â†¦ by rintro âŸ¨y, hyâŸ©; exact lt_of_le_of_lt (le_pair_left x y) (lt_of_mem hy)âŸ©)

lemma range_exists_unique (s : M) :
    âˆƒ! r : M, âˆ€ y, y âˆˆ r â†” âˆƒ x, âŸªx, yâŸ« âˆˆ s := by
  have : (ğš·)(1)-Predicate fun y â†¦ âˆƒ x, âŸªx, yâŸ« âˆˆ s :=
    DefinablePred.of_iff (fun y â†¦ âˆƒ x < s, âŸªx, yâŸ« âˆˆ s)
      (fun y â†¦ âŸ¨by rintro âŸ¨x, hyâŸ©; exact âŸ¨x, lt_of_le_of_lt (le_pair_left x y) (lt_of_mem hy), hyâŸ©,
                by rintro âŸ¨y, _, hyâŸ©; exact âŸ¨y, hyâŸ©âŸ©)
      (by definability)
  exact finite_comprehensionâ‚!
    this
    (âŸ¨s, fun y â†¦ by rintro âŸ¨x, hxâŸ©; exact lt_of_le_of_lt (le_pair_right x y) (lt_of_mem hx)âŸ©)

lemma union_exists_unique (s t : M) :
    âˆƒ! u : M, âˆ€ x, (x âˆˆ u â†” x âˆˆ s âˆ¨ x âˆˆ t) := by
  have : (ğš·)(1)-Predicate fun x â†¦ x âˆˆ s âˆ¨ x âˆˆ t := by definability
  exact finite_comprehensionâ‚! this
    âŸ¨s + t, fun i â†¦ by
      rintro (H | H)
      Â· exact lt_of_lt_of_le (lt_of_mem H) (by simp)
      Â· exact lt_of_lt_of_le (lt_of_mem H) (by simp)âŸ©


end ISigmaâ‚

end Model

end

end Arith

end LO.FirstOrder
