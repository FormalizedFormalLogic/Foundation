import Arithmetization.IOpen

namespace LO.FirstOrder

namespace Arith

noncomputable section

variable {M : Type} [Inhabited M] [DecidableEq M] [ORingSymbol M]
  [Structure â„’â‚’áµ£ M] [Structure.ORing â„’â‚’áµ£ M]
  [ğğ€â».Mod M]

namespace Model

section IOpen

variable [ğˆopen.Mod M]

def Pow2 (a : M) : Prop := 0 < a âˆ§ âˆ€ r â‰¤ a, 1 < r â†’ r âˆ£ a â†’ 2 âˆ£ r

def pow2def : Î£á´¬[0] 1 :=
  âŸ¨â€œ0 < #0 âˆ§ âˆ€[#0 < #1 + 1] (1 < #0 â†’  !dvddef [#0, #1] â†’ !dvddef [2, #0])â€, by simp [Hierarchy.pi_zero_iff_sigma_zero]âŸ©

lemma pow2_defined : Î£á´¬[0]-Predicate (Pow2 : M â†’ Prop) pow2def := by
  intro v
  simp [Semiformula.eval_substs, Matrix.comp_vecCons', Matrix.vecHead, Matrix.constant_eq_singleton,
    Pow2, pow2def, le_iff_lt_succ, dvd_defined.pval]

instance {b s} : DefinablePred b s (Pow2 : M â†’ Prop) := defined_to_with_paramâ‚€ _ pow2_defined

lemma Pow2.pos {a : M} (h : Pow2 a) : 0 < a := h.1

lemma Pow2.dvd {a : M} (h : Pow2 a) {r} (hr : r â‰¤ a) : 1 < r â†’ r âˆ£ a â†’ 2 âˆ£ r := h.2 r hr

@[simp] lemma pow2_one : Pow2 (1 : M) := âŸ¨by simp, by
  intro r hr hhr hd
  rcases show r = 0 âˆ¨ r = 1 from le_one_iff_eq_zero_or_one.mp hr with (rfl | rfl)
  Â· simp
  Â· simp at hhrâŸ©

@[simp] lemma not_pow2_zero : Â¬Pow2 (0 : M) := by
  intro h; have := h.pos; simp at this

lemma Pow2.two_dvd {a : M} (h : Pow2 a) (lt : 1 < a) : 2 âˆ£ a := h.dvd (le_refl _) lt (by simp)

lemma Pow2.two_dvd' {a : M} (h : Pow2 a) (lt : a â‰  1) : 2 âˆ£ a :=
  h.dvd (le_refl _) (by
    by_contra A; simp [le_one_iff_eq_zero_or_one] at A
    rcases A with (rfl | rfl) <;> simp at h lt)
    (by simp)

lemma Pow2.of_dvd {a b : M} (h : b âˆ£ a) : Pow2 a â†’ Pow2 b := by
  intro ha
  have : 0 < b := by
    by_contra e
    have : a = 0 := by simpa [show b = 0 from by simpa using e] using h
    rcases this with rfl
    simpa using ha.pos
  exact âŸ¨this, fun r hr ltr hb â†¦
    ha.dvd (show r â‰¤ a from le_trans hr (le_of_dvd ha.pos h)) ltr (dvd_trans hb h)âŸ©

lemma pow2_mul_two {a : M} : Pow2 (2 * a) â†” Pow2 a :=
  âŸ¨by intro H
      have : âˆ€ r â‰¤ a, 1 < r â†’ r âˆ£ a â†’ 2 âˆ£ r := by
        intro r hr ltr dvd
        exact H.dvd (show r â‰¤ 2 * a from le_trans hr (le_mul_of_one_le_left (by simp) one_le_two)) ltr (Dvd.dvd.mul_left dvd 2)
      exact âŸ¨by simpa using H.pos, thisâŸ©,
   by intro H
      exact âŸ¨by simpa using H.pos, by
        intro r _ hr hd
        rcases two_prime.left_dvd_or_dvd_right_of_dvd_mul hd with (hd | hd)
        Â· exact hd
        Â· exact H.dvd (show r â‰¤ a from le_of_dvd H.pos hd) hr hdâŸ©âŸ©

lemma pow2_mul_four {a : M} : Pow2 (4 * a) â†” Pow2 a := by
  simp [â†two_mul_two_eq_four, mul_assoc, pow2_mul_two]

lemma Pow2.elim {p : M} : Pow2 p â†” p = 1 âˆ¨ âˆƒ q, p = 2 * q âˆ§ Pow2 q :=
  âŸ¨by intro H
      by_cases hp : 1 < p
      Â· have : 2 âˆ£ p := H.two_dvd hp
        rcases this with âŸ¨q, rflâŸ©
        right; exact âŸ¨q, rfl, pow2_mul_two.mp HâŸ©
      Â· have : p = 1 := le_antisymm (by simpa using hp) (pos_iff_one_le.mp H.pos)
        left; exact this,
   by rintro (rfl | âŸ¨q, rfl, hqâŸ©) <;> simp [pow2_one, pow2_mul_two, *]âŸ©

@[simp] lemma pow2_two : Pow2 (2 : M) := Pow2.elim.mpr (Or.inr âŸ¨1, by simpâŸ©)

lemma Pow2.div_two {p : M} (h : Pow2 p) (ne : p â‰  1) : Pow2 (p /â‚‘ 2) := by
  rcases Pow2.elim.mp h with (rfl | âŸ¨q, rfl, pqâŸ©)
  Â· simp at ne
  simpa

lemma Pow2.two_mul_div_two {p : M} (h : Pow2 p) (ne : p â‰  1) : 2 * (p /â‚‘ 2) = p := by
  rcases Pow2.elim.mp h with (rfl | âŸ¨q, rfl, _âŸ©)
  Â· simp at ne
  simp

lemma Pow2.div_two_mul_two {p : M} (h : Pow2 p) (ne : p â‰  1) : (p /â‚‘ 2) * 2 = p := by
  simp [mul_comm, h.two_mul_div_two ne]

lemma Pow2.elim' {p : M} : Pow2 p â†” p = 1 âˆ¨ 1 < p âˆ§ âˆƒ q, p = 2 * q âˆ§ Pow2 q := by
  by_cases hp : 1 < p <;> simp [hp]
  Â· exact Pow2.elim
  Â· have : p = 0 âˆ¨ p = 1 := le_one_iff_eq_zero_or_one.mp (show p â‰¤ 1 from by simpa using hp)
    rcases this with (rfl | rfl) <;> simp


section LenBit

/-- $\mathrm{LenBit} (2^i, a) \iff \text{$i$th-bit of $a$ is $1$}$. -/
def LenBit (i a : M) : Prop := Â¬2 âˆ£ (a /â‚‘ i)

def lenbitdef : Î£á´¬[0] 2 :=
  âŸ¨â€œâˆƒ[#0 < #2 + 1] (!edivdef [#0, #2, #1] âˆ§ Â¬!dvddef [2, #0])â€, by simpâŸ©

lemma lenbit_defined : Î£á´¬[0]-Relation (LenBit : M â†’ M â†’ Prop) lenbitdef := by
  intro v; simp[sqrt_graph, lenbitdef, Matrix.vecHead, Matrix.vecTail, ediv_defined.pval, dvd_defined.pval, LenBit, â†le_iff_lt_succ]
  constructor
  Â· intro h; exact âŸ¨v 1 /â‚‘ v 0, by simp, rfl, hâŸ©
  Â· rintro âŸ¨z, hz, rfl, hâŸ©; exact h

instance {b s} : DefinableRel b s (LenBit : M â†’ M â†’ Prop) := defined_to_with_paramâ‚€ _ lenbit_defined

lemma LenBit.le {i a : M} (h : LenBit i a) : i â‰¤ a := by
  by_contra A; simp [LenBit, show a < i from by simpa using A] at h

lemma not_lenbit_of_lt {i a : M} (h : a < i) : Â¬LenBit i a := by
  intro A; exact not_le.mpr h A.le

@[simp] lemma LenBit.zero (a : M) : Â¬LenBit 0 a := by simp [LenBit]

@[simp] lemma LenBit.on_zero (a : M) : Â¬LenBit a 0 := by simp [LenBit]

lemma LenBit.one (a : M) : LenBit 1 a â†” Â¬2 âˆ£ a := by simp [LenBit]

lemma LenBit.iff_rem {i a : M} : LenBit i a â†” (a /â‚‘ i) mod 2 = 1 := by
  simp [LenBit]; rcases remainder_two (a /â‚‘ i) with (h | h) <;> simp [h, â†remainder_eq_zero_iff_dvd]

lemma not_lenbit_iff_rem {i a : M} : Â¬LenBit i a â†” (a /â‚‘ i) mod 2 = 0 := by
  simp [LenBit, â†remainder_eq_zero_iff_dvd]

@[simp] lemma LenBit.self {a : M} (pos : 0 < a) : LenBit a a := by simp [LenBit.iff_rem, pos, one_lt_two]

lemma LenBit.remainder {i a k : M} (h : 2 * i âˆ£ k) : LenBit i (a mod k) â†” LenBit i a := by
  have : 0 â‰¤ i := zero_le i
  rcases (eq_or_lt_of_le this) with (rfl | pos)
  Â· simp
  rcases h with âŸ¨k', hk'âŸ©
  calc
    LenBit i (a mod k) â†” ((a mod k) /â‚‘ i) mod 2 = 1                             := LenBit.iff_rem
    _                  â†” (2 * k') * (a /â‚‘ k) + ((a mod k) /â‚‘ i) mod 2 = 1       := by simp [mul_assoc]
    _                  â†” (((2 * k') * (a /â‚‘ k) * i + (a mod k)) /â‚‘ i) mod 2 = 1 := by simp [ediv_mul_add_self, pos]
    _                  â†” ((k * (a /â‚‘ k) + (a mod k)) /â‚‘ i) mod 2 = 1            := iff_of_eq (by
                                                                                      congr 3
                                                                                      simp [mul_right_comm _ (a /â‚‘ k), mul_right_comm 2 k' i, â†hk'])
    _                  â†” LenBit i a                                             := by simp [ediv_add_remainder a k, LenBit.iff_rem]

@[simp] lemma LenBit.remainder_two_mul_self {a i : M} : LenBit i (a mod 2 * i) â†” LenBit i a := LenBit.remainder (by simp)

lemma LenBit.add {i a b : M} (h : 2 * i âˆ£ b) : LenBit i (a + b) â†” LenBit i a := by
  have : 0 â‰¤ i := zero_le i
  rcases (eq_or_lt_of_le this) with (rfl | pos)
  Â· simp
  rcases h with âŸ¨b', hb'âŸ©
  have hb' : b = 2 * b' * i := by simp [hb', mul_right_comm]
  calc
    LenBit i (a + b) â†” ((a + b) /â‚‘ i) mod 2 = 1    := LenBit.iff_rem
    _                â†” (a /â‚‘ i) + 2 * b' mod 2 = 1 := by rw [hb', ediv_add_mul_self _ _ pos]
    _                â†” LenBit i a                  := by simp [LenBit.iff_rem]

lemma LenBit.add_self {i a : M} (h : a < i) : LenBit i (a + i) := by
  have pos : 0 < i := by exact pos_of_gt h
  simp [LenBit.iff_rem, ediv_add_self_right _ pos, h, one_lt_two]

lemma LenBit.add_self_of_not_lenbit {a i : M} (pos : 0 < i) (h : Â¬LenBit i a) : LenBit i (a + i) := by
  have : a /â‚‘ i mod 2 = 0 := by simpa [LenBit.iff_rem] using h
  simp [LenBit.iff_rem, ediv_add_self_right, pos]
  rw [remainder_add] <;> simp [this, one_lt_two]

end LenBit

end IOpen

section ISigmaâ‚€

variable [ğˆğšºâ‚€.Mod M]

namespace Pow2

lemma mul {a b : M} (ha : Pow2 a) (hb : Pow2 b) : Pow2 (a * b) := by
  wlog hab : a â‰¤ b
  Â· simpa [mul_comm] using this hb ha (by simp at hab; exact LT.lt.le hab)
  suffices : âˆ€ b : M, âˆ€ a â‰¤ b, Pow2 a â†’ Pow2 b â†’ Pow2 (a * b)
  Â· exact this b a hab ha hb
  intro b
  induction b using hierarchy_order_induction_sigmaâ‚€
  Â· definability
  case ind IH a b IH =>
    intro a hab ha hb
    have : a = 1 âˆ¨ 1 < a âˆ§ âˆƒ a', a = 2 * a' âˆ§ Pow2 a' := Pow2.elim'.mp ha
    rcases this with (rfl | âŸ¨lta, a, rfl, haâŸ©)
    Â· simpa using hb
    Â· have : b = 1 âˆ¨ 1 < b âˆ§ âˆƒ b', b = 2 * b' âˆ§ Pow2 b' := Pow2.elim'.mp hb
      rcases this with (rfl | âŸ¨ltb, b, rfl, hbâŸ©)
      Â· simpa using ha
      Â· have ltb : b < 2 * b := lt_two_mul_self (pos_iff_ne_zero.mpr $ by rintro rfl; simp at ltb)
        have hab : a â‰¤ b := le_of_mul_le_mul_left hab (by simp)
        have : Pow2 (a * b) := IH b ltb a hab (by assumption) (by assumption)
        suffices : Pow2 (4 * a * b)
        Â· have : (2 * a) * (2 * b) = 4 * a * b := by simp [mul_assoc, mul_left_comm a 2 b, â†two_mul_two_eq_four]
          simpa [this]
        simpa [mul_assoc, pow2_mul_four] using this

@[simp] lemma mul_iff {a b : M} : Pow2 (a * b) â†” Pow2 a âˆ§ Pow2 b :=
  âŸ¨fun h â†¦ âŸ¨h.of_dvd (by simp), h.of_dvd (by simp)âŸ©, by rintro âŸ¨ha, hbâŸ©; exact ha.mul hbâŸ©

@[simp] lemma sq_iff {a : M} : Pow2 (a^2) â†” Pow2 a := by
  simp [_root_.sq]

lemma sq {a : M} : Pow2 a â†’ Pow2 (a^2) := by
  simp [_root_.sq]

lemma dvd_of_le {a b : M} (ha : Pow2 a) (hb : Pow2 b) : a â‰¤ b â†’ a âˆ£ b := by
  suffices : âˆ€ b : M, âˆ€ a â‰¤ b, Pow2 a â†’ Pow2 b â†’ a âˆ£ b
  Â· intro hab; exact this b a hab ha hb
  intro b; induction b using hierarchy_order_induction_sigmaâ‚€
  Â· definability
  case ind b IH =>
    intro a hab ha hb
    have : b = 1 âˆ¨ 1 < b âˆ§ âˆƒ b', b = 2 * b' âˆ§ Pow2 b' := Pow2.elim'.mp hb
    rcases this with (rfl | âŸ¨ltb, b, rfl, hbâŸ©)
    Â· rcases le_one_iff_eq_zero_or_one.mp hab with (rfl | rfl) <;> simp
      Â· simp at ha
    Â· have : a = 1 âˆ¨ 1 < a âˆ§ âˆƒ a', a = 2 * a' âˆ§ Pow2 a' := Pow2.elim'.mp ha
      rcases this with (rfl | âŸ¨lta, a, rfl, haâŸ©)
      Â· simp
      Â· have ltb : b < 2 * b := lt_two_mul_self (pos_iff_ne_zero.mpr $ by rintro rfl; simp at ltb)
        have hab : a â‰¤ b := le_of_mul_le_mul_left hab (by simp)
        exact mul_dvd_mul_left 2 <| IH b ltb a hab (by assumption) (by assumption)

lemma le_iff_dvd {a b : M} (ha : Pow2 a) (hb : Pow2 b) : a â‰¤ b â†” a âˆ£ b :=
  âŸ¨Pow2.dvd_of_le ha hb, le_of_dvd hb.posâŸ©

lemma two_le {a : M} (pa : Pow2 a) (ne1 : a â‰  1) : 2 â‰¤ a :=
  le_of_dvd pa.pos (pa.two_dvd' ne1)

lemma le_iff_lt_two {a b : M} (ha : Pow2 a) (hb : Pow2 b) : a â‰¤ b â†” a < 2 * b := by
  constructor
  Â· intro h; exact lt_of_le_of_lt h (lt_two_mul_self hb.pos)
  Â· intro h
    by_cases ea : a = 1
    Â· rcases ea with rfl
      simpa [â†pos_iff_one_le] using hb.pos
    Â· suffices : a âˆ£ b
      Â· exact le_of_dvd hb.pos this
      have : a /â‚‘ 2 âˆ£ b := by
        have : 2 * (a /â‚‘ 2) âˆ£ 2 * b := by
          simpa [ha.two_mul_div_two ea] using dvd_of_le ha (by simpa using hb) (LT.lt.le h)
        exact (mul_dvd_mul_iff_left (by simp)).mp this
      rcases this with âŸ¨b', rflâŸ©
      have hb' : Pow2 b' := by simp at hb; exact hb.2
      have : 2 âˆ£ b' := hb'.two_dvd' (by rintro rfl; simp [ha.two_mul_div_two ea] at h)
      rcases this with âŸ¨b'', rflâŸ©
      simp [â†mul_assoc, ha.div_two_mul_two ea]

lemma lt_iff_two_mul_le {a b : M} (ha : Pow2 a) (hb : Pow2 b) : a < b â†” 2 * a â‰¤ b := by
  by_cases eb : b = 1
  Â· simp [eb, â†lt_two_iff_le_one]
  Â· rw [â†hb.two_mul_div_two eb]; simp [le_iff_lt_two ha (hb.div_two eb)]


lemma sq_or_dsq {a : M} (pa : Pow2 a) : âˆƒ b, a = b^2 âˆ¨ a = 2 * b^2 := by
  suffices : âˆƒ b â‰¤ a, a = b^2 âˆ¨ a = 2 * b^2
  Â· rcases this with âŸ¨b, _, hâŸ©
    exact âŸ¨b, hâŸ©
  revert pa
  induction a using hierarchy_order_induction_sigmaâ‚€
  Â· definability
  case ind a IH =>
    intro pa
    rcases Pow2.elim'.mp pa with (rfl | âŸ¨ha, a, rfl, pa'âŸ©)
    Â· exact âŸ¨1, by simpâŸ©
    Â· have : 0 < a := by simpa [â†pos_iff_one_le] using one_lt_iff_two_le.mp ha
      rcases IH a (lt_mul_of_one_lt_left this one_lt_two) pa' with âŸ¨b, _, (rfl | rfl)âŸ©
      Â· exact âŸ¨b, le_trans (by simp) le_two_mul_left, by right; rflâŸ©
      Â· exact âŸ¨2 * b, by simp; exact le_trans (by simp) le_two_mul_left,
        by left; simp [_root_.sq, mul_assoc, mul_left_comm]âŸ©

lemma sqrt {a : M} (h : Pow2 a) (hsq : (âˆša)^2 = a) : Pow2 (âˆša) := by
  rw [â†hsq] at h; simpa using h

@[simp] lemma Pow2.not_three : Â¬Pow2 (3 : M) := by
  intro h
  have : 2 âˆ£ 3 := h.two_dvd (by simp [â†two_add_one_eq_three])
  simp [â†two_add_one_eq_three, â†remainder_eq_zero_iff_dvd, one_lt_two] at this

lemma four_le {i : M} (hi : Pow2 i) (lt : 2 < i) : 4 â‰¤ i := by
  by_contra A
  have : i â‰¤ 3 := by simpa [â†three_add_one_eq_four, â†le_iff_lt_succ] using A
  rcases le_three_iff_eq_zero_or_one_or_two_or_three.mp this with (rfl | rfl | rfl | rfl) <;> simp at lt hi

lemma mul_add_lt_of_mul_lt_of_pos {a b p q : M} (hp : Pow2 p) (hq : Pow2 q)
    (h : a * p < q) (hb : b < p) (hbq : b < q) : a * p + b < q := by
  rcases zero_le a with (rfl | pos)
  Â· simp [hbq]
  have : p âˆ£ q :=
    dvd_of_le hp hq (le_of_lt $
      lt_of_mul_lt_of_one_le_of_nonneg_left (by simpa [mul_comm a p] using h) (pos_iff_one_le.mp pos) (by simp))
  rcases this with âŸ¨q, rflâŸ©
  have : a < q := lt_of_mul_lt_mul_right (a := p) (by simpa [mul_comm] using h)
  calc
    a * p + b < (a + 1) * p := by simp [add_mul, hb]
    _         â‰¤ p * q       := by simp [mul_comm p q]; exact mul_le_mul_right (lt_iff_succ_le.mp this)

end Pow2

lemma LenBit.remainder_pow2 {a i j : M} (pi : Pow2 i) (pj : Pow2 j) (h : i < j) : LenBit i (a mod j) â†” LenBit i a :=
  LenBit.remainder (by rw [â†Pow2.le_iff_dvd] <;> simp [pi, pj, â†Pow2.lt_iff_two_mul_le, h])

lemma LenBit.add_pow2 {a i j : M} (pi : Pow2 i) (pj : Pow2 j) (h : i < j) : LenBit i (a + j) â†” LenBit i a :=
  LenBit.add (by rw [â†Pow2.le_iff_dvd] <;> simp [pi, pj, â†Pow2.lt_iff_two_mul_le, h])

lemma LenBit.add_pow2_iff_of_lt {a i j : M} (pi : Pow2 i) (pj : Pow2 j) (h : a < j) : LenBit i (a + j) â†” i = j âˆ¨ LenBit i a := by
  rcases show i < j âˆ¨ i = j âˆ¨ i > j from lt_trichotomy i j with (hij | rfl | hij)
  Â· simp [LenBit.add_pow2 pi pj hij, hij.ne]
  Â· simp [LenBit.add_self h]
  Â· have : a + j < i := calc
      a + j < 2 * j  := by simp[two_mul, h]
      _     â‰¤ i      := (pj.lt_iff_two_mul_le pi).mp hij
    simp [not_lenbit_of_lt this, not_lenbit_of_lt (show a < i from lt_trans _ _ _ h hij), hij.ne.symm]

lemma lenbit_iff_add_mul {i a : M} (hi : Pow2 i) :
    LenBit i a â†” âˆƒ k, âˆƒ r < i, a = k * (2 * i) + i + r := by
  constructor
  Â· intro h
    have : 2 * ((a /â‚‘ i) /â‚‘ 2) + 1 = a /â‚‘ i := by simpa [LenBit.iff_rem.mp h] using ediv_add_remainder (a /â‚‘ i) 2
    have : a = ((a /â‚‘ i) /â‚‘ 2) * (2 * i) + i + (a mod i) := calc
      a = i * (a /â‚‘ i) + (a mod i)                  := Eq.symm <| ediv_add_remainder a i
      _ = i * (2 * ((a /â‚‘ i) /â‚‘ 2) + 1) + (a mod i) := by simp [this]
      _ = ((a /â‚‘ i) /â‚‘ 2) * (2 * i) + i + (a mod i) := by simp [mul_add, â†mul_assoc, mul_comm i 2, mul_comm (2 * i)]
    exact âŸ¨(a /â‚‘ i) /â‚‘ 2, a mod i, by simp [hi.pos], thisâŸ©
  Â· rintro âŸ¨k, r, h, rflâŸ©
    simp [LenBit.iff_rem, â†mul_assoc, add_assoc, ediv_mul_add_self, hi.pos, h, one_lt_two]

lemma not_lenbit_iff_add_mul {i a : M} (hi : Pow2 i) :
    Â¬LenBit i a â†” âˆƒ k, âˆƒ r < i, a = k * (2 * i) + r := by
  constructor
  Â· intro h
    have : 2 * ((a /â‚‘ i) /â‚‘ 2) = a /â‚‘ i := by simpa [not_lenbit_iff_rem.mp h] using ediv_add_remainder (a /â‚‘ i) 2
    have : a = ((a /â‚‘ i) /â‚‘ 2) * (2 * i) + (a mod i) := calc
      a = i * (a /â‚‘ i) + (a mod i)              := Eq.symm <| ediv_add_remainder a i
      _ = i * (2 * ((a /â‚‘ i) /â‚‘ 2)) + (a mod i) := by simp [this]
      _ = ((a /â‚‘ i) /â‚‘ 2) * (2 * i) + (a mod i) := by simp [mul_add, â†mul_assoc, mul_comm i 2, mul_comm (2 * i)]
    exact âŸ¨(a /â‚‘ i) /â‚‘ 2, a mod i, by simp [hi.pos], thisâŸ©
  Â· rintro âŸ¨k, r, h, rflâŸ©
    simp [not_lenbit_iff_rem, â†mul_assoc, add_assoc, ediv_mul_add_self, hi.pos, h]

lemma lenbit_mul_add {i j a r : M} (pi : Pow2 i) (pj : Pow2 j) (hr : r < j) :
    LenBit (i * j) (a * j + r) â†” LenBit i a := by
  by_cases h : LenBit i a <;> simp [h]
  Â· rcases (lenbit_iff_add_mul pi).mp h with âŸ¨a, b, hb, rflâŸ©
    have : b * j + r < i * j :=
      pj.mul_add_lt_of_mul_lt_of_pos (by simp[pi, pj]) ((mul_lt_mul_right pj.pos).mpr hb)  hr (lt_of_lt_of_le hr $ le_mul_of_pos_left $ pi.pos)
    exact (lenbit_iff_add_mul (by simp [pi, pj])).mpr âŸ¨a, b * j + r, this, by simp [add_mul, add_assoc, mul_assoc]âŸ©
  Â· rcases (not_lenbit_iff_add_mul pi).mp h with âŸ¨a, b, hb, rflâŸ©
    have : b * j + r < i * j :=
      pj.mul_add_lt_of_mul_lt_of_pos (by simp[pi, pj]) ((mul_lt_mul_right pj.pos).mpr hb) hr (lt_of_lt_of_le hr $ le_mul_of_pos_left $ pi.pos)
    exact (not_lenbit_iff_add_mul (by simp [pi, pj])).mpr âŸ¨a, b * j + r, this, by simp [add_mul, add_assoc, mul_assoc]âŸ©

lemma lenbit_add_pow2_iff_of_not_lenbit {a i j : M} (pi : Pow2 i) (pj : Pow2 j) (h : Â¬LenBit j a) :
    LenBit i (a + j) â†” i = j âˆ¨ LenBit i a := by
  rcases show i < j âˆ¨ i = j âˆ¨ i > j from lt_trichotomy i j with (hij | rfl | hij)
  Â· simp [LenBit.add_pow2 pi pj hij, hij.ne]
  Â· simp [LenBit.add_self_of_not_lenbit pi.pos, h]
  Â· simp [ne_of_gt hij]
    have : 2 * j âˆ£ i := Pow2.dvd_of_le (by simp [pj]) pi <| (pj.lt_iff_two_mul_le pi).mp hij
    rcases this with âŸ¨i, rflâŸ©
    rcases (not_lenbit_iff_add_mul pj).mp h with âŸ¨a, r, hr, rflâŸ©
    simp at pi
    have pi' : Pow2 i := pi.2
    have pj' : Pow2 j := pi.1
    calc
      LenBit (2 * j * i) (a * (2 * j) + r + j) â†” LenBit (i * (2 * j)) (a * (2 * j) + (r + j)) := by
        simp [mul_comm (2 * j), add_assoc]
      _                                        â†” LenBit i a                                   :=
        lenbit_mul_add pi' (by simpa using pj') (by simp [two_mul, hr])
      _                                        â†” LenBit (i * (2 * j)) (a * (2 * j) + r)       :=
        Iff.symm <| lenbit_mul_add pi' (by simpa using pj') (lt_of_lt_of_le hr $ by simp)
      _                                        â†” LenBit (2 * j * i) (a * (2 * j) + r)         := by
        simp [mul_comm]

end ISigmaâ‚€

end Model

end

end Arith

end LO.FirstOrder
