import Arithmetization.Ind

namespace LO.FirstOrder

namespace Arith

noncomputable section

variable {M : Type} [Inhabited M] [DecidableEq M] [ORingSymbol M]
  [Structure ‚Ñí‚Çí·µ£ M] [Structure.ORing ‚Ñí‚Çí·µ£ M]
  [ùêèùêÄ‚Åª.Mod M]

namespace Model

section IOpen

variable [ùêàopen.Mod M]

@[elab_as_elim]
lemma open_induction {P : M ‚Üí Prop}
    (hP : ‚àÉ p : Semiformula ‚Ñí‚Çí·µ£ M 1, p.Open ‚àß ‚àÄ x, P x ‚Üî Semiformula.Eval! M ![x] id p)
    (zero : P 0) (succ : ‚àÄ x, P x ‚Üí P (x + 1)) : ‚àÄ x, P x :=
  induction (C := Semiformula.Open)
    (by rcases hP with ‚ü®p, hp, hhp‚ü©
        exact ‚ü®p.fvEnumInv', (Rew.rewriteMap p.fvEnum').hom p, by simp[hp],
          by  intro x; simp [Semiformula.eval_rewriteMap, hhp]
              exact Semiformula.eval_iff_of_funEqOn p (by intro z hz; simp [Semiformula.fvEnumInv'_fvEnum' _ hz])‚ü©) zero succ

lemma open_leastNumber {P : M ‚Üí Prop}
    (hP : ‚àÉ p : Semiformula ‚Ñí‚Çí·µ£ M 1, p.Open ‚àß ‚àÄ x, P x ‚Üî Semiformula.Eval! M ![x] id p)
    (zero : P 0) {a} (counterex : ¬¨P a) : ‚àÉ x, P x ‚àß ¬¨P (x + 1) := by
  by_contra A
  have : ‚àÄ x, P x := by
    intro x; induction x using open_induction
    ¬∑ exact hP
    case zero => exact zero
    case succ n ih =>
      simp at A
      exact A n ih
  have : P a := this a
  contradiction

lemma remainder (a : M) {b} (pos : 0 < b) : ‚àÉ! u, ‚àÉ v < b, a = b * u + v := by
  have : ‚àÉ! u, b * u ‚â§ a ‚àß a < b * (u + 1) := by
    have : ‚àÉ u, b * u ‚â§ a ‚àß a < b * (u + 1) := by
      have : a < b * (a + 1) ‚Üí ‚àÉ u, b * u ‚â§ a ‚àß a < b * (u + 1) := by
        simpa using open_leastNumber (P := Œª u ‚Ü¶ b * u ‚â§ a) ‚ü®‚Äú&b * #0 ‚â§ &a‚Äù, by simp, by intro x; simp‚ü©
      simp at this
      have hx : a < b * (a + 1) := by
        have : a + 0 < b * a + b :=
          add_lt_add_of_le_of_lt (le_mul_self_of_pos_left pos) pos
        simpa [mul_add] using this
      exact this hx
    rcases this with ‚ü®u, hu‚ü©
    exact ExistsUnique.intro u hu (by
      intro u' hu'
      by_contra ne
      wlog lt : u < u'
      ¬∑ exact this a pos u' hu' u hu (Ne.symm ne) (Ne.lt_of_le ne $ by simpa using lt)
      have : a < a := by calc
        a < b * (u + 1) := hu.2
        _ ‚â§ b * u'      := (_root_.mul_le_mul_left pos).mpr (lt_iff_succ_le.mp lt)
        _ ‚â§ a           := hu'.1
      exact LT.lt.false this)
  have iff : ‚àÄ u, (‚àÉ v < b, a = b * u + v) ‚Üî (b * u ‚â§ a ‚àß a < b * (u + 1)) := by
    intro u; constructor
    ¬∑ rintro ‚ü®v, hv, rfl‚ü©
      simp [mul_add, hv]
    ¬∑ intro h
      let v := a - b * u
      have e : a = b*u + v := by simp [add_tsub_self_of_le h.1]
      have : v < b := by
        by_contra hyv
        have hyv : b ‚â§ v := by simpa using hyv
        have : a < a := by calc
          a < b * (u + 1) := h.2
          _ ‚â§ b * u + v   := by simpa [mul_add] using hyv
          _ = a           := e.symm
        exact LT.lt.false this
      exact ‚ü®v, this, e‚ü©
  exact (exists_unique_congr iff).mpr this

section ediv

lemma ediv_exists_unique (a b : M) : ‚àÉ! u, (0 < b ‚Üí ‚àÉ v < b, a = b * u + v) ‚àß (b = 0 ‚Üí u = 0) := by
  have : 0 ‚â§ b := by exact zero_le b
  rcases this with (rfl | pos) <;> simp [*]
  ¬∑ simpa [pos_iff_ne_zero.mp pos] using remainder a pos

/-- Euclidean division -/
def ediv (a b : M) : M := Classical.choose! (ediv_exists_unique a b)

infix:70 " /‚Çë " => ediv

lemma ediv_spec_of_pos (a : M) (h : 0 < b) : ‚àÉ v < b, a = b * (a /‚Çë b) + v :=
  (Classical.choose!_spec (ediv_exists_unique a b)).1 h

@[simp] lemma ediv_spec_zero (a : M) : a /‚Çë 0 = 0 :=
  (Classical.choose!_spec (ediv_exists_unique a 0)).2 (by simp)

lemma ediv_graph {a b c : M} : c = a /‚Çë b ‚Üî ((0 < b ‚Üí ‚àÉ v < b, a = b * c + v) ‚àß (b = 0 ‚Üí c = 0)) :=
  Classical.choose!_eq_iff _

def edivdef : Œ£·¥¨[0] 3 :=
  ‚ü®‚Äú(0 < #2 ‚Üí ‚àÉ[#0 < #3] (#2 = #3 * #1 + #0)) ‚àß (#2 = 0 ‚Üí #0 = 0)‚Äù, by simp[Hierarchy.pi_zero_iff_sigma_zero]‚ü©

lemma ediv_defined : Œ£·¥¨[0]-Function‚ÇÇ ((¬∑ /‚Çë ¬∑) : M ‚Üí M ‚Üí M) edivdef := by
  intro v; simp[ediv_graph, edivdef, Matrix.vecHead, Matrix.vecTail]

lemma ediv_spec_of_pos' (a : M) (h : 0 < b) : ‚àÉ v < b, a = (a /‚Çë b) * b + v := by
  simpa [mul_comm] using ediv_spec_of_pos a h

@[simp] lemma mul_ediv_le (a b : M) : b * (a /‚Çë b) ‚â§ a := by
  have : 0 ‚â§ b := by exact zero_le b
  rcases this with (rfl | pos) <;> simp [*]
  rcases ediv_spec_of_pos a pos with ‚ü®v, _, e‚ü©
  simpa [‚Üê e] using show b * (a /‚Çë b) ‚â§ b * (a /‚Çë b) + v from le_self_add

@[simp] lemma ediv_le (a b : M) : a /‚Çë b ‚â§ a := by
  have : 0 ‚â§ b := zero_le b
  rcases this with (rfl | pos) <;> simp [*]
  have : 1 * (a /‚Çë b) ‚â§ b * (a /‚Çë b) := mul_le_mul_of_nonneg_right (le_iff_lt_succ.mpr (by simp[pos])) (by simp)
  simpa using le_trans this (mul_ediv_le a b)

instance ediv_polybounded : PolyBounded‚ÇÇ ((¬∑ /‚Çë ¬∑) : M ‚Üí M ‚Üí M) := ‚ü®#0, Œª _ ‚Ü¶ by simp‚ü©

instance : DefinableFunction‚ÇÇ b s ((¬∑ /‚Çë ¬∑) : M ‚Üí M ‚Üí M) := defined_to_with_param‚ÇÄ _ ediv_defined

@[simp] lemma ediv_mul_le (a b : M) : a /‚Çë b * b ‚â§ a := by rw [mul_comm]; exact mul_ediv_le _ _

lemma lt_mul_ediv (a : M) {b} (pos : 0 < b) : a < b * (a /‚Çë b + 1) := by
  rcases ediv_spec_of_pos a pos with ‚ü®v, hv, e‚ü©
  calc a = b * (a /‚Çë b) + v := e
       _ < b * (a /‚Çë b + 1) := by simp [mul_add, hv]

@[simp] lemma ediv_one (a : M) : a /‚Çë 1 = a :=
  le_antisymm (by simp) (le_iff_lt_succ.mpr $ by simpa using lt_mul_ediv a one_pos)

lemma ediv_mul_add (a : M) {b r} (pos : 0 < b) (hr : r < b) : (a * b + r) /‚Çë b = a := by
  rcases ediv_spec_of_pos (a * b + r) pos with ‚ü®v, hv, e‚ü©
  symm; apply eq_of_le_of_not_lt
  ¬∑ have : a * b < ((a * b + r) /‚Çë b + 1) * b := calc
      a * b ‚â§ a * b + r                  := le_self_add
      _     = ((a * b + r) /‚Çë b) * b + v := by simpa [@mul_comm _ _ b] using e
      _     < ((a * b + r) /‚Çë b + 1) * b := by simp [add_mul, hv]
    exact le_iff_lt_succ.mpr <| lt_of_mul_lt_mul_of_nonneg_right this (by simp)
  ¬∑ intro H
    have : ((a * b + r) /‚Çë b) * b < (a + 1) * b := calc
      ((a * b + r) /‚Çë b) * b ‚â§ a * b + r   := by simp
      _                      < (a + 1) * b := by simp [add_mul, hr]
    have : (a * b + r) /‚Çë b ‚â§ a := le_iff_lt_succ.mpr ((mul_lt_mul_right pos).mp this)
    have : a < a := lt_of_lt_of_le H this
    exact LT.lt.false this

lemma ediv_add_mul_self (a c : M) {b} (pos : 0 < b) : (a + c * b) /‚Çë b = a /‚Çë b + c := by
  rcases ediv_spec_of_pos' a pos with ‚ü®r, hr, ex‚ü©
  simpa [add_mul, add_right_comm, ‚Üê ex] using ediv_mul_add (a /‚Çë b + c) pos hr

lemma ediv_mul_add_self (a c : M) {b} (pos : 0 < b) : (a * b + c) /‚Çë b = a + c /‚Çë b := by
  simp [ediv_add_mul_self, pos, add_comm]

@[simp] lemma ediv_mul_left (a : M) {b} (pos : 0 < b) : (a * b) /‚Çë b = a := by
  simpa using ediv_mul_add a pos pos

@[simp] lemma ediv_mul_right (a : M) {b} (pos : 0 < b) : (b * a) /‚Çë b = a := by
  simpa [mul_comm] using ediv_mul_add a pos pos

@[simp] lemma ediv_eq_zero_of_lt (b : M) {a} (h : a < b) : a /‚Çë b = 0 := by
  simpa using ediv_mul_add 0 (pos_of_gt h) h

@[simp] lemma ediv_self {a : M} (hx : 0 < a) : a /‚Çë a = 1 := by
  simpa using ediv_mul_left 1 hx

@[simp] lemma zero_ediv (a : M) : 0 /‚Çë a = 0 := by
  rcases zero_le a with (rfl | pos) <;> simp [*]

@[simp] lemma ediv_mul' (a : M) {b} (pos : 0 < b) : (b * a) /‚Çë b = a := by simp [mul_comm, pos]

@[simp] lemma ediv_add_self_left {a} (pos : 0 < a) (b : M) : (a + b) /‚Çë a = 1 + b /‚Çë a := by
  simpa using ediv_mul_add_self 1 b pos

@[simp] lemma ediv_add_self_right (a : M) {b} (pos : 0 < b) : (a + b) /‚Çë b = a /‚Çë b + 1 := by
  simpa using ediv_add_mul_self a 1 pos

lemma mul_ediv_self_of_dvd {a b : M} : a * (b /‚Çë a) = b ‚Üî a ‚à£ b := by
  rcases zero_le a with (rfl | pos)
  ¬∑ simp[eq_comm]
  ¬∑ constructor
    ¬∑ intro e; rw [‚Üêe]; simp
    ¬∑ rintro ‚ü®r, rfl‚ü©; simp [pos]

end ediv

section remainder

def rem (a b : M) : M := a - b * (a /‚Çë b)

infix:60 " mod " => rem

def remdef : Œ£·¥¨[0] 3 :=
  ‚ü®‚Äú‚àÉ[#0 < #2 + 1] (!edivdef [#0, #2, #3] ‚àß !subdef [#1, #2, #3 * #0])‚Äù, by simp‚ü©

lemma rem_graph (a b c : M) : a = b mod c ‚Üî ‚àÉ x ‚â§ b, (x = b /‚Çë c ‚àß a = b - c * x) := by
  simp [rem]; constructor
  ¬∑ rintro rfl; exact ‚ü®b /‚Çë c, by simp, rfl, by rfl‚ü©
  ¬∑ rintro ‚ü®_, _, rfl, rfl‚ü©; simp

lemma rem_defined : Œ£·¥¨[0]-Function‚ÇÇ ((¬∑ mod ¬∑) : M ‚Üí M ‚Üí M) remdef := by
  intro v; simp [Matrix.vecHead, Matrix.vecTail, remdef,
    rem_graph, Semiformula.eval_substs, ediv_defined.pval, sub_defined.pval, le_iff_lt_succ]

instance : DefinableFunction‚ÇÇ b s ((¬∑ mod ¬∑) : M ‚Üí M ‚Üí M) := defined_to_with_param‚ÇÄ _ rem_defined

lemma ediv_add_remainder (a b : M) : b * (a /‚Çë b) + (a mod b) = a :=
  add_tsub_self_of_le (mul_ediv_le a b)

@[simp] lemma remainder_zero (a : M) : a mod 0 = a := by simp [rem]

@[simp] lemma remainder_self (a : M) : a mod a = 0 := by
  rcases zero_le a with (rfl | h)
  ¬∑ simp
  ¬∑ simp [rem, h]

lemma remainder_mul_add_of_lt (a : M) {b} (pos : 0 < b) {r} (hr : r < b) : (a * b + r) mod b = r := by
  simp [rem, ediv_mul_add a pos hr, mul_comm]

@[simp] lemma remainder_mul_add (a c : M) (pos : 0 < b) : (a * b + c) mod b = c mod b := by
  simp [rem, ediv_mul_add_self, pos, mul_add, ‚Üêsub_sub, show b * a = a * b from mul_comm _ _]

@[simp] lemma remainder_add_mul (a b : M) (pos : 0 < c) : (a + b * c) mod c = a mod c := by
  simp [add_comm a (b * c), pos]

@[simp] lemma remainder_add_mul' (a b : M) (pos : 0 < c) : (a + c * b) mod c = a mod c := by
  simp [mul_comm c b, pos]

@[simp] lemma remainder_mul_add' (a c : M) (pos : 0 < b) : (b * a + c) mod b = c mod b := by
  simp [mul_comm b a, pos]

@[simp] lemma remainder_mul_self_left (a b : M) : (a * b) mod b = 0 := by
  rcases zero_le b with (rfl | h)
  ¬∑ simp
  ¬∑ simpa using remainder_mul_add_of_lt a h h

@[simp] lemma remainder_mul_self_right (a b : M) : (b * a) mod b = 0 := by
  simp [mul_comm]

@[simp] lemma remainder_eq_self_of_lt {a b : M} (h : a < b) : a mod b = a := by
  simpa using remainder_mul_add_of_lt 0 (pos_of_gt h) h

@[simp] lemma remainder_lt (a : M) {b} (pos : 0 < b) : a mod b < b := by
  rcases ediv_spec_of_pos' a pos with ‚ü®r, hr, ha‚ü©
  have : ((a /‚Çë b) * b + r) mod b = r := remainder_mul_add_of_lt _ pos hr
  have : a mod b = r := by simpa [‚Üêha] using this
  simp [this, hr]

@[simp] lemma remainder_le (a b : M) : a mod b ‚â§ a := by
  simp [rem]

instance remainder_polybounded : PolyBounded‚ÇÇ ((¬∑ mod ¬∑) : M ‚Üí M ‚Üí M) := ‚ü®#0, by intro v; simp‚ü©

lemma remainder_eq_zero_iff_dvd {a b : M} : b mod a = 0 ‚Üî a ‚à£ b := by
  simp [rem]
  constructor
  ¬∑ intro H; exact mul_ediv_self_of_dvd.mp (le_antisymm (mul_ediv_le b a) H)
  ¬∑ intro H; simp [mul_ediv_self_of_dvd.mpr H]

@[simp] lemma remainder_add_remove_right {a b : M} (pos : 0 < b) : (a + b) mod b = a mod b := by
  simpa using remainder_add_mul a 1 pos

lemma remainder_add_remove_right_of_dvd {a b m : M} (h : m ‚à£ b) (pos : 0 < m) : (a + b) mod m = a mod m := by
  rcases h with ‚ü®b, rfl‚ü©; simp [pos]

@[simp] lemma remainder_add_remove_left {a b : M} (pos : 0 < a) : (a + b) mod a = b mod a := by
  simpa using remainder_mul_add 1 b pos

lemma remainder_add_remove_left_of_dvd {a b m : M} (h : m ‚à£ a) (pos : 0 < m) : (a + b) mod m = b mod m := by
  rcases h with ‚ü®b, rfl‚ü©; simp [pos]

lemma remainder_add {a b m : M} (pos : 0 < m) : (a + b) mod m = ((a mod m) + (b mod m)) mod m := calc
  (a + b) mod m = ((m * (a /‚Çë m) + (a mod m)) + (m * (b /‚Çë m) + (b mod m))) mod m := by simp [ediv_add_remainder]
  _             = ((a mod m) + (b mod m)) mod m                                   := by simp [add_mul, mul_add, pos, mul_left_comm _ m,
                                                                                          add_assoc, mul_assoc, add_left_comm]

lemma remainder_mul {a b m : M} (pos : 0 < m) : (a * b) mod m = ((a mod m) * (b mod m)) mod m := calc
  (a * b) mod m = ((m * (a /‚Çë m) + (a mod m)) * (m * (b /‚Çë m) + (b mod m))) mod m := by simp [ediv_add_remainder]
  _             = ((a mod m) * (b mod m)) mod m                                   := by simp [add_mul, mul_add, pos, mul_left_comm _ m, add_assoc, mul_assoc]

lemma remainder_two (a : M) : a mod 2 = 0 ‚à® a mod 2 = 1 :=
  le_one_iff_eq_zero_or_one.mp <| lt_two_iff_le_one.mp <| remainder_lt a (b := 2) (by simp)

@[simp] lemma remainder_two_not_zero_iff {a : M} : ¬¨a mod 2 = 0 ‚Üî a mod 2 = 1 := by
  rcases remainder_two a with (h | h) <;> simp [*]

@[simp] lemma remainder_two_not_one_iff {a : M} : ¬¨a mod 2 = 1 ‚Üî a mod 2 = 0 := by
  rcases remainder_two a with (h | h) <;> simp [*]

end remainder

lemma two_dvd_mul {a b : M} : 2 ‚à£ a * b ‚Üí 2 ‚à£ a ‚à® 2 ‚à£ b := by
  intro H; by_contra A
  simp [not_or] at A
  have ha : a mod 2 = 1 := by
    have : a mod 2 = 0 ‚à® a mod 2 = 1 := remainder_two a
    simpa [show a mod 2 ‚â† 0 from by simpa [‚Üêremainder_eq_zero_iff_dvd] using A.1] using this
  have hb : b mod 2 = 1 := by
    have : b mod 2 = 0 ‚à® b mod 2 = 1 :=
      le_one_iff_eq_zero_or_one.mp <| lt_two_iff_le_one.mp <| remainder_lt b (b := 2) (by simp)
    simpa [show b mod 2 ‚â† 0 from by simpa [‚Üêremainder_eq_zero_iff_dvd] using A.2] using this
  have : a * b mod 2 = 1 := by simp [remainder_mul, ha, hb]; exact remainder_eq_self_of_lt one_lt_two
  have : ¬¨2 ‚à£ a * b := by simp [‚Üêremainder_eq_zero_iff_dvd, this]
  contradiction

lemma even_or_odd (a : M) : ‚àÉ x, a = 2 * x ‚à® a = 2 * x + 1 :=
  ‚ü®a /‚Çë 2, by
    have : 2 * (a /‚Çë 2) + (a mod 2) = a := ediv_add_remainder a 2
    rcases remainder_two a with (e | e) <;> { simp[e] at this; simp [this] }‚ü©

lemma two_prime : Prime (2 : M) := ‚ü®by simp, by simp, by intro a b h; exact two_dvd_mul h‚ü©

section sqrt

lemma sqrt_exists_unique (a : M) : ‚àÉ! x, x * x ‚â§ a ‚àß a < (x + 1) * (x + 1) := by
  have : ‚àÉ x, x * x ‚â§ a ‚àß a < (x + 1) * (x + 1) := by
    have : a < (a + 1) * (a + 1) ‚Üí ‚àÉ x, x * x ‚â§ a ‚àß a < (x + 1) * (x + 1) := by
      simpa using open_leastNumber (P := Œª x ‚Ü¶ x * x ‚â§ a) ‚ü®‚Äú#0 * #0 ‚â§ &a‚Äù, by simp, by simp‚ü©
    have hn : a < (a + 1) * (a + 1) := calc
      a ‚â§ a * a             := le_mul_self a
      _ < a * a + 1         := lt_add_one (a * a)
      _ ‚â§ (a + 1) * (a + 1) := by simp [add_mul_self_eq]
    exact this hn
  rcases this with ‚ü®x, hx‚ü©
  exact ExistsUnique.intro x hx (by
    intro y hy
    by_contra ne
    wlog lt : x < y
    ¬∑ exact this a y hy x hx (Ne.symm ne) (Ne.lt_of_le ne $ by simpa using lt)
    have : a < a := calc
      a < (x + 1) * (x + 1) := hx.2
      _ ‚â§ y * y             := mul_self_le_mul_self (by simp) (lt_iff_succ_le.mp lt)
      _ ‚â§ a                 := hy.1
    simp at this)

def sqrt (a : M) : M := Classical.choose! (sqrt_exists_unique a)

prefix:75 "‚àö" => sqrt

@[simp] lemma sqrt_spec_le (a : M) : ‚àöa * ‚àöa ‚â§ a := (Classical.choose!_spec (sqrt_exists_unique a)).1

@[simp] lemma sqrt_spec_lt (a : M) : a < (‚àöa + 1) * (‚àöa + 1) := (Classical.choose!_spec (sqrt_exists_unique a)).2

lemma sqrt_graph {a b : M} : b = ‚àöa ‚Üî b * b ‚â§ a ‚àß a < (b + 1) * (b + 1) := Classical.choose!_eq_iff _

def sqrtdef : Œ£·¥¨[0] 2 :=
  ‚ü®‚Äú#0 * #0 ‚â§ #1 ‚àß #1 < (#0 + 1) * (#0 + 1)‚Äù, by simp[Hierarchy.pi_zero_iff_sigma_zero]‚ü©

lemma sqrt_defined : Œ£·¥¨[0]-Function‚ÇÅ (Œª a : M ‚Ü¶ ‚àöa) sqrtdef := by
  intro v; simp[sqrt_graph, sqrtdef, Matrix.vecHead, Matrix.vecTail]

instance : DefinableFunction‚ÇÅ b s ((‚àö¬∑) : M ‚Üí M) := defined_to_with_param‚ÇÄ _ sqrt_defined

lemma eq_sqrt (x a : M) : x * x ‚â§ a ‚àß a < (x + 1) * (x + 1) ‚Üí x = ‚àöa := Classical.choose_uniq (sqrt_exists_unique a)

lemma sqrt_eq_of_le_of_lt {x a : M} (le : x * x ‚â§ a) (lt : a < (x + 1) * (x + 1)) : ‚àöa = x :=
  Eq.symm <| eq_sqrt x a ‚ü®le, lt‚ü©

lemma sqrt_eq_of_le_of_le {x a : M} (le : x * x ‚â§ a) (h : a ‚â§ x * x + 2 * x) : ‚àöa = x :=
  sqrt_eq_of_le_of_lt le (by simp [add_mul_self_eq]; exact le_iff_lt_succ.mp h)


@[simp] lemma sq_sqrt_le (a : M) : (‚àöa)^2 ‚â§ a := by simp [sq]

@[simp] lemma sqrt_mul_self (a : M) : ‚àö(a * a) = a :=
  Eq.symm <| eq_sqrt a (a * a) (by simp; exact mul_self_lt_mul_self (by simp) (by simp))

@[simp] lemma sqrt_sq (a : M) : ‚àö(a^2) = a := by simp [sq]

@[simp] lemma sqrt_zero : ‚àö(0 : M) = 0 := by simpa using sqrt_mul_self (0 : M)

@[simp] lemma sqrt_one : ‚àö(1 : M) = 1 := by simpa using sqrt_mul_self (1 : M)

lemma sqrt_two : ‚àö(2 : M) = 1 :=
  Eq.symm <| eq_sqrt 1 2 (by simp [one_le_two, one_add_one_eq_two, one_lt_two])

lemma sqrt_three : ‚àö(3 : M) = 1 :=
  Eq.symm <| eq_sqrt 1 3 (by
    simp [one_add_one_eq_two, two_mul_two_eq_four]
    constructor
    ¬∑ simp [‚Üêtwo_add_one_eq_three]
    ¬∑ simp [‚Üêthree_add_one_eq_four])

@[simp] lemma sqrt_four : ‚àö(4 : M) = 2 := by
  simp [‚Üêtwo_mul_two_eq_four]

@[simp] lemma two_ne_square (a : M) : 2 ‚â† a^2 := by
  intro h
  rcases show a = ‚àö2 from by rw [h]; simp with rfl
  simp [sqrt_two] at h

@[simp] lemma sqrt_le_add (a : M) : a ‚â§ ‚àöa * ‚àöa + 2 * ‚àöa :=
  le_iff_lt_succ.mpr (by have := sqrt_spec_lt a; rw [add_mul_self_eq] at this; simpa using this)

@[simp] lemma sqrt_le_self (a : M) : ‚àöa ‚â§ a := by
  by_contra A
  have : a < a := calc
    a ‚â§ a^2    := le_sq a
    _ < (‚àöa)^2 := by simpa [sq] using mul_self_lt_mul_self (by simp) (by simpa using A)
    _ ‚â§ a      := sq_sqrt_le a
  simp_all

instance : PolyBounded‚ÇÅ ((‚àö¬∑) : M ‚Üí M) := ‚ü®#0, by intro v; simp‚ü©

lemma sqrt_lt_self_of_one_lt {a : M} (h : 1 < a) : ‚àöa < a := by
  by_contra A
  have : a * a ‚â§ ‚àöa * ‚àöa := mul_self_le_mul_self (by simp) (by simpa using A)
  have : a * a ‚â§ a := le_trans this (sqrt_spec_le a)
  exact not_lt.mpr this (lt_mul_self h)

lemma sqrt_le_of_le_sq {a b : M} : a ‚â§ b^2 ‚Üí ‚àöa ‚â§ b := by
  intro h; by_contra A
  have : a < a := calc
    a ‚â§ b^2    := h
    _ < (‚àöa)^2 := sq_lt_sq_iff.mp (by simpa using A)
    _ ‚â§ a      := by simp
  simp_all

lemma sq_lt_of_lt_sqrt {a b : M} : a < ‚àöb ‚Üí a^2 < b := by
  intro h; by_contra A
  exact not_le.mpr h (sqrt_le_of_le_sq $ show b ‚â§ a^2 from by simpa using A)

end sqrt

section pair

open Classical

-- https://github.com/leanprover-community/mathlib4/blob/b075cdd0e6ad8b5a3295e7484b2ae59e9b2ec2a7/Mathlib/Data/Nat/Pairing.lean#L37
def pair (a b : M) : M := if a < b then b * b + a else a * a + a + b

notation "‚ü®" a " ; " b "‚ü©" => pair a b

lemma pair_graph {a b c : M} :
    c = ‚ü®a ; b‚ü© ‚Üî (a < b ‚àß c = b * b + a) ‚à® (b ‚â§ a ‚àß c = a * a + a + b) := by
  simp [pair]
  by_cases h : a < b
  ¬∑ simp [h, show ¬¨b ‚â§ a from by simpa using h]
  ¬∑ simp [h, show b ‚â§ a from by simpa using h]

def pairdef : Œ£·¥¨[0] 3 := ‚ü®‚Äú(#1 < #2 ‚àß #0 = #2 * #2 + #1) ‚à® (#2 ‚â§ #1 ‚àß #0 = #1 * #1 + #1 + #2)‚Äù, by simp‚ü©

lemma pair_defined : Œ£·¥¨[0]-Function‚ÇÇ (Œª a b : M ‚Ü¶ ‚ü®a ; b‚ü©) pairdef := by
  intro v; simp [pair_graph, pairdef]

def unpair (a : M) : M √ó M := if a - ‚àöa * ‚àöa < ‚àöa then (a - ‚àöa * ‚àöa, ‚àöa) else (‚àöa, a - ‚àöa * ‚àöa - ‚àöa)

abbrev pi‚ÇÅ (a : M) : M := (unpair a).1

abbrev pi‚ÇÇ (a : M) : M := (unpair a).2

@[simp] lemma pair_unpair (a : M) : ‚ü®pi‚ÇÅ a ; pi‚ÇÇ a‚ü© = a := by
  simp [pi‚ÇÅ, pi‚ÇÇ, unpair]
  split_ifs with h
  ¬∑ simp [pair, h]
  ¬∑ simp; simp [pair, h]
    have : a - ‚àöa * ‚àöa - ‚àöa ‚â§ ‚àöa := by simp [add_comm (2 * ‚àöa), ‚Üêtwo_mul]
    simp [not_lt.mpr this]
    have :‚àöa ‚â§ a - ‚àöa * ‚àöa := by simpa using h
    calc
      ‚àöa * ‚àöa + ‚àöa + (a - ‚àöa * ‚àöa - ‚àöa) = ‚àöa * ‚àöa + (a - ‚àöa * ‚àöa) := by simp [add_assoc]
                                                                        rw [add_tsub_self_of_le, add_tsub_self_of_le] <;> simp [this]
      _                                 = a                       := add_tsub_self_of_le (by simp)

@[simp] lemma unpair_pair (a b : M) : unpair ‚ü®a ; b‚ü© = (a, b) := by
  simp [pair]; split_ifs with h
  ¬∑ have : ‚àö(b * b + a) = b := sqrt_eq_of_le_of_le (by simp) (by simp; exact le_trans (le_of_lt h) (by simp))
    simp [unpair, this, show ¬¨b ‚â§ a from by simpa using h]
  ¬∑ have : ‚àö(a * a + (a + b)) = a :=
      sqrt_eq_of_le_of_le (by simp [add_assoc]) (by simp [add_assoc, two_mul, show b ‚â§ a from by simpa using h])
    simp [unpair, this, add_assoc]

def pairEquiv : M √ó M ‚âÉ M :=
  ‚ü®Function.uncurry pair, unpair, fun ‚ü®a, b‚ü© => unpair_pair a b, pair_unpair‚ü©

end pair

end IOpen

end Model

end

end Arith

end LO.FirstOrder
