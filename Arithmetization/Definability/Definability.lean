import Arithmetization.Lemmata
import Arithmetization.Definability.Init
import Arithmetization.Vorspiel.Graph
import Aesop

namespace LO.FirstOrder

def Defined {k} (R : (Fin k â†’ M) â†’ Prop) [Structure L M] (p : Semisentence L k) : Prop :=
  âˆ€ v, R v â†” Semiformula.PVal! M v p

def DefinedWithParam {k} (R : (Fin k â†’ M) â†’ Prop) [Structure L M] (p : Semiformula L M k) : Prop :=
  âˆ€ v, R v â†” Semiformula.Eval! M v id p

namespace Defined

variable [Structure L M]

lemma pval {k} {R : (Fin k â†’ M) â†’ Prop} {p : Semisentence L k} (h : Defined R p) (v) :
    Semiformula.PVal! M v p â†” R v := (h v).symm

end Defined

namespace DefinedWithParam

variable [Structure L M]

lemma eval {k} {R : (Fin k â†’ M) â†’ Prop} {p : Semiformula L M k} (h : DefinedWithParam R p) (v) :
    Semiformula.Eval! M v id p â†” R v := (h v).symm

end DefinedWithParam

namespace Arith

section definability

variable {M : Type} [Zero M] [One M] [Add M] [Mul M] [LT M] [ğğ€â».Mod M]

abbrev FormulaHierarchy (b : VType) (s : â„•) (L : Language) [L.LT] (Î¼ : Type*) (n) :=
  { p : Semiformula L Î¼  n // Hierarchy b s p }

abbrev SentenceHierarchy (b : VType) (s : â„•) (L : Language) [L.LT] (n) := FormulaHierarchy b s L Empty n

abbrev SigmaSentence (s : â„•) (L : Language) [L.LT] (n) := SentenceHierarchy Î£ s L n

abbrev PiSentence (s : â„•) (L : Language) [L.LT] (n) := SentenceHierarchy Î  s L n

notation "Î£á´¬[" s "]" => SigmaSentence s â„’â‚’áµ£

notation "Î á´¬[" s "]" => PiSentence s â„’â‚’áµ£

namespace FormulaHierarchy

abbrev of_zero (p : FormulaHierarchy b 0 â„’â‚’áµ£ Î¼ k) : FormulaHierarchy b' s â„’â‚’áµ£ Î¼ k :=
  âŸ¨p, p.prop.of_zeroâŸ©

variable (b : VType) (s : â„•) (L : Language) [L.LT] (Î¼ : Type*) (n)

@[simp] lemma hierarchy (p : FormulaHierarchy b s L Î¼ n) : Hierarchy b s p.val := p.prop

@[simp] lemma hierarchy_zero {b b' s} (p : FormulaHierarchy b 0 L Î¼ n) : Hierarchy b' s p.val :=
  Hierarchy.of_zero p.hierarchy

end FormulaHierarchy

namespace SentenceHierarchy

def eq : SentenceHierarchy b s â„’â‚’áµ£ 2 := âŸ¨â€œ#0 = #1â€, by simpâŸ©

def lt : SentenceHierarchy b s â„’â‚’áµ£ 2 := âŸ¨â€œ#0 < #1â€, by simpâŸ©

def le : SentenceHierarchy b s â„’â‚’áµ£ 2 := âŸ¨â€œ#0 â‰¤ #1â€, by simpâŸ©

end SentenceHierarchy

namespace Model

abbrev DefinedPred (b : VType) (s : â„•) (P : M â†’ Prop) (p : SentenceHierarchy b s â„’â‚’áµ£ 1) : Prop :=
  Defined (Î» v â†¦ P (v 0)) p.val

abbrev DefinedRel (b : VType) (s : â„•) (R : M â†’ M â†’ Prop) (p : SentenceHierarchy b s â„’â‚’áµ£ 2) : Prop :=
  Defined (Î» v â†¦ R (v 0) (v 1)) p.val

abbrev DefinedRelâ‚ƒ (b : VType) (s : â„•) (R : M â†’ M â†’ M â†’ Prop) (p : SentenceHierarchy b s â„’â‚’áµ£ 3) : Prop :=
  Defined (Î» v â†¦ R (v 0) (v 1) (v 2)) p.val

abbrev DefinedRelâ‚„ (b : VType) (s : â„•) (R : M â†’ M â†’ M â†’ M â†’ Prop) (p : SentenceHierarchy b s â„’â‚’áµ£ 4) : Prop :=
  Defined (Î» v â†¦ R (v 0) (v 1) (v 2) (v 3)) p.val

abbrev SigmaDefinedPred (s : â„•) (P : M â†’ Prop) (p : Î£á´¬[s] 1) : Prop := DefinedPred Î£ s P p

notation "Î£á´¬[" s "]-Predicate" => SigmaDefinedPred s

abbrev SigmaDefinedRel (s : â„•) (R : M â†’ M â†’ Prop) (p : Î£á´¬[s] 2) : Prop := DefinedRel Î£ s R p

notation "Î£á´¬[" s "]-Relation" => SigmaDefinedRel s

abbrev SigmaDefinedRelâ‚ƒ (s : â„•) (R : M â†’ M â†’ M â†’ Prop) (p : Î£á´¬[s] 3) : Prop := DefinedRelâ‚ƒ Î£ s R p

notation "Î£á´¬[" s "]-Relationâ‚ƒ" => SigmaDefinedRelâ‚ƒ s

abbrev SigmaDefinedRelâ‚„ (s : â„•) (R : M â†’ M â†’ M â†’ M â†’ Prop) (p : Î£á´¬[s] 4) : Prop := DefinedRelâ‚„ Î£ s R p

notation "Î£á´¬[" s "]-Relationâ‚„" => SigmaDefinedRelâ‚„ s

abbrev PiDefinedPred (s : â„•) (t : Set M) (p : Î á´¬[s] 1) : Prop := DefinedPred Î  s t p

notation "Î á´¬[" s "]-Predicate" => PiDefinedPred s

abbrev PiDefinedRel (s : â„•) (R : M â†’ M â†’ Prop) (p : Î á´¬[s] 2) : Prop := DefinedRel Î  s R p

notation "Î á´¬[" s "]-Relation" => PiDefinedRel s

abbrev DefinedFunction (b : VType) (s : â„•) {k} (f : (Fin k â†’ M) â†’ M) (p : SentenceHierarchy b s â„’â‚’áµ£ (k + 1)) : Prop :=
  Defined (fun v => v 0 = f (v Â·.succ)) p.val

abbrev DefinedFunctionâ‚ (b : VType) (s : â„•) (f : M â†’ M) (p : SentenceHierarchy b s â„’â‚’áµ£ 2) : Prop :=
  DefinedFunction b s (fun v => f (v 0)) p

abbrev DefinedFunctionâ‚‚ (b : VType) (s : â„•) (f : M â†’ M â†’ M) (p : SentenceHierarchy b s â„’â‚’áµ£ 3) : Prop :=
  DefinedFunction b s (fun v => f (v 0) (v 1)) p

abbrev DefinedFunctionâ‚ƒ (b : VType) (s : â„•) (f : M â†’ M â†’ M â†’ M) (p : SentenceHierarchy b s â„’â‚’áµ£ 4) : Prop :=
  DefinedFunction b s (fun v => f (v 0) (v 1) (v 2)) p

abbrev SigmaDefinedFunctionâ‚ (s : â„•) (f : M â†’ M) (p : Î£á´¬[s] 2) : Prop := DefinedFunctionâ‚ Î£ s f p

notation "Î£á´¬[" s "]-Functionâ‚" => SigmaDefinedFunctionâ‚ s

abbrev PiDefinedFunctionâ‚ (s : â„•) (f : M â†’ M) (p : Î á´¬[s] 2) : Prop := DefinedFunctionâ‚ Î  s f p

notation "Î á´¬[" s "]-Functionâ‚" => PiDefinedFunctionâ‚ s

abbrev SigmaDefinedFunctionâ‚‚ (s : â„•) (f : M â†’ M â†’ M) (p : Î£á´¬[s] 3) : Prop := DefinedFunctionâ‚‚ Î£ s f p

notation "Î£á´¬[" s "]-Functionâ‚‚" => SigmaDefinedFunctionâ‚‚ s

abbrev PiDefinedFunctionâ‚‚ (s : â„•) (f : M â†’ M â†’ M) (p : Î á´¬[s] 3) : Prop := DefinedFunctionâ‚‚ Î  s f p

notation "Î á´¬[" s "]-Functionâ‚‚" => PiDefinedFunctionâ‚‚ s

abbrev SigmaDefinedFunctionâ‚ƒ (s : â„•) (f : M â†’ M â†’ M â†’ M) (p : Î£á´¬[s] 4) : Prop := DefinedFunctionâ‚ƒ Î£ s f p

notation "Î£á´¬[" s "]-Functionâ‚ƒ" => SigmaDefinedFunctionâ‚ƒ s

abbrev PiDefinedFunctionâ‚ƒ (s : â„•) (f : M â†’ M â†’ M â†’ M) (p : Î á´¬[s] 4) : Prop := DefinedFunctionâ‚ƒ Î  s f p

notation "Î á´¬[" s "]-Functionâ‚ƒ" => PiDefinedFunctionâ‚ƒ s

def DefinedRel.eq : DefinedRel b s ((Â· = Â·) : M â†’ M â†’ Prop) SentenceHierarchy.eq := by intro v; simp [SentenceHierarchy.eq]

def DefinedRel.lt : DefinedRel b s ((Â· < Â·) : M â†’ M â†’ Prop) SentenceHierarchy.lt := by intro v; simp [SentenceHierarchy.lt]

def DefinedRel.le : DefinedRel b s ((Â· â‰¤ Â·) : M â†’ M â†’ Prop) SentenceHierarchy.le := by intro v; simp [SentenceHierarchy.le]

variable (b : VType) (s : â„•)

class Definable {k} (P : (Fin k â†’ M) â†’ Prop) : Prop where
  definable : âˆƒ p : FormulaHierarchy b s â„’â‚’áµ£ M k, DefinedWithParam P p.val

abbrev DefinablePred (P : M â†’ Prop) : Prop := Definable b s (k := 1) (fun v â†¦ P (v 0))

abbrev DefinableRel (P : M â†’ M â†’ Prop) : Prop := Definable b s (k := 2) (fun v â†¦ P (v 0) (v 1))

abbrev DefinableRelâ‚ƒ (P : M â†’ M â†’ M â†’ Prop) : Prop := Definable b s (k := 3) (fun v â†¦ P (v 0) (v 1) (v 2))

abbrev DefinableRelâ‚„ (P : M â†’ M â†’ M â†’ M â†’ Prop) : Prop := Definable b s (k := 4) (fun v â†¦ P (v 0) (v 1) (v 2) (v 3))

abbrev DefinableFunction (f : (Fin k â†’ M) â†’ M) : Prop := Definable b s (k := k + 1) (fun v â†¦ v 0 = f (v Â·.succ))

abbrev DefinableFunctionâ‚ (f : M â†’ M) : Prop := DefinableFunction b s (k := 1) (fun v â†¦ f (v 0))

abbrev DefinableFunctionâ‚‚ (f : M â†’ M â†’ M) : Prop := DefinableFunction b s (k := 2) (fun v â†¦ f (v 0) (v 1))

abbrev DefinableFunctionâ‚ƒ (f : M â†’ M â†’ M â†’ M) : Prop := DefinableFunction b s (k := 3) (fun v â†¦ f (v 0) (v 1) (v 2))

variable {b s}

lemma defined_to_with_param {k} {P : (Fin k â†’ M) â†’ Prop} (p : SentenceHierarchy b s â„’â‚’áµ£ k) (hP : Defined P p.val) :
    Definable b s P := âŸ¨âŸ¨Rew.emb.hom p.val, by simpâŸ©, by intro; simp [hP.pval]âŸ©

lemma defined_to_with_paramâ‚€ {k} {P : (Fin k â†’ M) â†’ Prop} (p : SentenceHierarchy b' 0 â„’â‚’áµ£ k) (hP : Defined P p.val) :
    Definable b s P := âŸ¨âŸ¨Rew.emb.hom p.val, by simpâŸ©, by intro; simp [hP.pval]âŸ©

namespace Definable

lemma of_iff {p : (Fin k â†’ M) â†’ Prop} (q) (h : âˆ€ x, p x â†” q x) (H : Definable b s q) : Definable b s p := by
  rwa [show p = q from by funext v; simp [h]]

lemma finmap {P : (Fin k â†’ M) â†’ Prop} (h : Definable b s P) (f : Fin k â†’ Fin n) :
    Definable b s fun v â†¦ P (fun i â†¦ v (f i)) := by
  rcases h with âŸ¨p, hâŸ©
  exact âŸ¨âŸ¨(Rew.substs (fun i â†¦ #(f i))).hom p, by simpâŸ©, by intro v; simp [h.eval]âŸ©

end Definable

namespace DefinableFunction

lemma of_eq {f : (Fin k â†’ M) â†’ M} (g) (h : âˆ€ v, f v = g v) (H : DefinableFunction b s f) : DefinableFunction b s g := by
  rwa [show g = f from by funext v; simp [h]]

lemma finmap {f : (Fin k â†’ M) â†’ M} (hf : DefinableFunction b s f) (e : Fin k â†’ Fin n) :
    DefinableFunction b s fun v â†¦ f (fun i â†¦ v (e i)) := by
  have := Definable.finmap (n := n + 1) hf (0 :> fun i â†¦ (e i).succ); simp at this
  exact this.of_iff _ (by intro x; simp)

lemma rel {f : (Fin k â†’ M) â†’ M} (h : DefinableFunction b s f) :
  Definable b s (fun v â†¦ v 0 = f (v Â·.succ)) := h

end DefinableFunction

instance DefinableFunctionâ‚.graph {f : M â†’ M} [h : DefinableFunctionâ‚ b s f] :
  DefinableRel b s (Function.Graph f) := h

instance DefinableFunctionâ‚‚.graph {f : M â†’ M â†’ M} [h : DefinableFunctionâ‚‚ b s f] :
  DefinableRelâ‚ƒ b s (Function.Graphâ‚‚ f) := h

instance DefinableFunctionâ‚ƒ.graph {f : M â†’ M â†’ M â†’ M} [h : DefinableFunctionâ‚ƒ b s f] :
  DefinableRelâ‚„ b s (Function.Graphâ‚ƒ f) := h

namespace DefinableRel

instance eq : DefinableRel b s ((Â· = Â·) : M â†’ M â†’ Prop) := âŸ¨âŸ¨â€œ#0 = #1â€, by simpâŸ©, by intro; simpâŸ©

instance lt : DefinableRel b s ((Â· < Â·) : M â†’ M â†’ Prop) := âŸ¨âŸ¨â€œ#0 < #1â€, by simpâŸ©, by intro; simpâŸ©

instance le : DefinableRel b s ((Â· â‰¤ Â·) : M â†’ M â†’ Prop) := âŸ¨âŸ¨â€œ#0 â‰¤ #1â€, by simpâŸ©, by intro; simpâŸ©

end DefinableRel

namespace DefinableFunctionâ‚‚

instance add : DefinableFunctionâ‚‚ b s ((Â· + Â·) : M â†’ M â†’ M) where
  definable := âŸ¨âŸ¨â€œ#0 = #1 + #2â€, by simpâŸ©, by intro _; simpâŸ©

instance mul : DefinableFunctionâ‚‚ b s ((Â· * Â·) : M â†’ M â†’ M) where
  definable := âŸ¨âŸ¨â€œ#0 = #1 * #2â€, by simpâŸ©, by intro _; simpâŸ©

end DefinableFunctionâ‚‚

variable (b s)

class PolyBounded (f : (Fin k â†’ M) â†’ M) : Prop where
  bounded : âˆƒ t : Semiterm â„’â‚’áµ£ M k, âˆ€ v : Fin k â†’ M, f v â‰¤ t.val! M v id

abbrev PolyBoundedâ‚ (f : M â†’ M) : Prop := PolyBounded (k := 1) (fun v => f (v 0))

abbrev PolyBoundedâ‚‚ (f : M â†’ M â†’ M) : Prop := PolyBounded (k := 2) (fun v => f (v 0) (v 1))

abbrev PolyBoundedâ‚ƒ (f : M â†’ M â†’ M â†’ M) : Prop := PolyBounded (k := 3) (fun v => f (v 0) (v 1) (v 2))

variable {b s}

namespace PolyBounded

@[simp] lemma var {k} (i : Fin k) : PolyBounded (fun v : Fin k â†’ M â†¦ v i) := âŸ¨#i, by intro _; simpâŸ©

@[simp] lemma const {k} (c : M) : PolyBounded (fun _ : Fin k â†’ M â†¦ c) := âŸ¨&c, by intro _; simpâŸ©

@[simp] lemma val_id' (t : Semiterm â„’â‚’áµ£ M n) (e : Fin n â†’ Fin k) :
    PolyBounded fun v : Fin k â†’ M => Semiterm.val! M (fun x â†¦ v (e x)) id t :=
  âŸ¨Rew.substs (fun x â†¦ #(e x)) t, by intro _; simp [Semiterm.val_substs]âŸ©

@[simp] lemma val_id (t : Semiterm â„’â‚’áµ£ M k) : PolyBounded fun v : Fin k â†’ M => Semiterm.val! M v id t :=
  âŸ¨t, by intro _; simpâŸ©

lemma finmap {f : (Fin k â†’ M) â†’ M} (hf : PolyBounded f) (e : Fin k â†’ Fin n) :
    PolyBounded fun v â†¦ f (fun i â†¦ v (e i)) := by
  rcases hf with âŸ¨t, htâŸ©
  exact âŸ¨Rew.substs (fun x â†¦ #(e x)) t, by intro; simp [Semiterm.val_substs, ht]âŸ©

lemma comp {k} {f : (Fin l â†’ M) â†’ M} {g : Fin l â†’ (Fin k â†’ M) â†’ M} (hf : PolyBounded f) (hg : âˆ€ i, PolyBounded (g i)) :
    PolyBounded (fun v â†¦ f (g Â· v)) where
  bounded := by
    rcases hf.bounded with âŸ¨tf, htfâŸ©
    choose tg htg using fun i â†¦ (hg i).bounded
    exact âŸ¨Rew.substs tg tf, by
      intro v; simp [Semiterm.val_substs]
      exact le_trans (htf (g Â· v)) (Model.polynomial_mono tf (fun i â†¦ htg i v) (by simp))âŸ©

end PolyBounded

lemma PolyBoundedâ‚.comp {f : M â†’ M} {k} {g : (Fin k â†’ M) â†’ M} (hf : PolyBoundedâ‚ f) (hg : PolyBounded g) :
    PolyBounded (fun v â†¦ f (g v)) := PolyBounded.comp hf (l := 1) (fun _ â†¦ hg)

lemma PolyBoundedâ‚‚.comp {f : M â†’ M â†’ M} {k} {gâ‚ gâ‚‚ : (Fin k â†’ M) â†’ M} (hf : PolyBoundedâ‚‚ f) (hgâ‚ : PolyBounded gâ‚) (hgâ‚‚ : PolyBounded gâ‚‚) :
    PolyBounded (fun v â†¦ f (gâ‚ v) (gâ‚‚ v)) := PolyBounded.comp hf (g := ![gâ‚, gâ‚‚]) (fun i â†¦ by cases i using Fin.cases <;> simp [*])

lemma PolyBoundedâ‚ƒ.comp {f : M â†’ M â†’ M â†’ M} {k} {gâ‚ gâ‚‚ gâ‚ƒ : (Fin k â†’ M) â†’ M}
    (hf : PolyBoundedâ‚ƒ f) (hgâ‚ : PolyBounded gâ‚) (hgâ‚‚ : PolyBounded gâ‚‚) (hgâ‚ƒ : PolyBounded gâ‚ƒ) :
    PolyBounded (fun v â†¦ f (gâ‚ v) (gâ‚‚ v) (gâ‚ƒ v)) := PolyBounded.comp hf (g := ![gâ‚, gâ‚‚, gâ‚ƒ])
      (fun i â†¦ by
        cases' i using Fin.cases with i <;> simp [*]
        cases' i using Fin.cases with i <;> simp [*])

namespace PolyBoundedâ‚‚

instance add : PolyBoundedâ‚‚ ((Â· + Â·) : M â†’ M â†’ M) where
  bounded := âŸ¨áµ€â€œ#0 + #1â€, by intro _; simpâŸ©

instance mul : PolyBoundedâ‚‚ ((Â· * Â·) : M â†’ M â†’ M) where
  bounded := âŸ¨áµ€â€œ#0 * #1â€, by intro _; simpâŸ©

instance hAdd : PolyBoundedâ‚‚ (HAdd.hAdd : M â†’ M â†’ M) where
  bounded := âŸ¨áµ€â€œ#0 + #1â€, by intro _; simpâŸ©

instance hMul : PolyBoundedâ‚‚ (HMul.hMul : M â†’ M â†’ M) where
  bounded := âŸ¨áµ€â€œ#0 * #1â€, by intro _; simpâŸ©

end PolyBoundedâ‚‚

variable (b s)

def Semipolynomial {k} (f : (Fin k â†’ M) â†’ M) := PolyBounded f âˆ§ DefinableFunction b s f

abbrev Semipolynomialâ‚ (f : M â†’ M) : Prop := Semipolynomial b s (k := 1) (fun v => f (v 0))

abbrev Semipolynomialâ‚‚ (f : M â†’ M â†’ M) : Prop := Semipolynomial b s (k := 2) (fun v => f (v 0) (v 1))

abbrev Semipolynomialâ‚ƒ (f : M â†’ M â†’ M â†’ M) : Prop := Semipolynomial b s (k := 3) (fun v => f (v 0) (v 1) (v 2))

variable {b s}

lemma Semipolynomial.bounded {f : (Fin k â†’ M) â†’ M} (h : Semipolynomial b s f) : PolyBounded f := h.1

lemma Semipolynomialâ‚.bounded {f : M â†’ M} (h : Semipolynomialâ‚ b s f) : PolyBoundedâ‚ f := h.1

lemma Semipolynomialâ‚‚.bounded {f : M â†’ M â†’ M} (h : Semipolynomialâ‚‚ b s f) : PolyBoundedâ‚‚ f := h.1

lemma Semipolynomialâ‚ƒ.bounded {f : M â†’ M â†’ M â†’ M} (h : Semipolynomialâ‚ƒ b s f) : PolyBoundedâ‚ƒ f := h.1

lemma Semipolynomial.definable {f : (Fin k â†’ M) â†’ M} (h : Semipolynomial b s f) : DefinableFunction b s f := h.2

lemma Semipolynomialâ‚.definable {f : M â†’ M} (h : Semipolynomialâ‚ b s f) : DefinableFunctionâ‚ b s f := h.2

lemma Semipolynomialâ‚‚.definable {f : M â†’ M â†’ M} (h : Semipolynomialâ‚‚ b s f) : DefinableFunctionâ‚‚ b s f := h.2

lemma Semipolynomialâ‚ƒ.definable {f : M â†’ M â†’ M â†’ M} (h : Semipolynomialâ‚ƒ b s f) : DefinableFunctionâ‚ƒ b s f := h.2

namespace Semipolynomial

lemma of_polybounded_of_definable (f : (Fin k â†’ M) â†’ M) [hb : PolyBounded f] [hf : DefinableFunction b s f] :
    Semipolynomial b s f := âŸ¨hb, hfâŸ©

@[simp] lemma of_polybounded_of_definableâ‚ (f : M â†’ M) [hb : PolyBoundedâ‚ f] [hf : DefinableFunctionâ‚ b s f] :
    Semipolynomialâ‚ b s f := âŸ¨hb, hfâŸ©

@[simp] lemma of_polybounded_of_definableâ‚‚ (f : M â†’ M â†’ M) [hb : PolyBoundedâ‚‚ f] [hf : DefinableFunctionâ‚‚ b s f] :
    Semipolynomialâ‚‚ b s f := âŸ¨hb, hfâŸ©

@[simp] lemma of_polybounded_of_definableâ‚ƒ (f : M â†’ M â†’ M â†’ M) [hb : PolyBoundedâ‚ƒ f] [hf : DefinableFunctionâ‚ƒ b s f] :
    Semipolynomialâ‚ƒ b s f := âŸ¨hb, hfâŸ©

lemma finmap {f : (Fin k â†’ M) â†’ M} (hf : Semipolynomial b s f) (e : Fin k â†’ Fin n) :
    Semipolynomial b s fun v â†¦ f (fun i â†¦ v (e i)) := âŸ¨hf.bounded.finmap e, hf.definable.finmap eâŸ©

end Semipolynomial

namespace Definable

lemma of_zero {P : (Fin k â†’ M) â†’ Prop} (h : Definable b 0 P) : Definable b' s P := by
  rcases h with âŸ¨âŸ¨p, hpâŸ©âŸ©
  exact âŸ¨âŸ¨p.of_zero, by simp [hp]âŸ©âŸ©

lemma const {P : Prop} : Definable b s (fun _ : Fin k â†’ M â†¦ P) := by
  by_cases hP : P
  Â· exact âŸ¨âŸ¨âŠ¤, by simpâŸ©, by intro; simp[hP]âŸ©
  Â· exact âŸ¨âŸ¨âŠ¥, by simpâŸ©, by intro; simp[hP]âŸ©

lemma and {Pâ‚ Pâ‚‚ : (Fin k â†’ M) â†’ Prop} (hâ‚ : Definable b s Pâ‚) (hâ‚‚ : Definable b s Pâ‚‚) :
    Definable b s (fun v â†¦ Pâ‚ v âˆ§ Pâ‚‚ v) := by
  rcases hâ‚ with âŸ¨pâ‚, hâ‚âŸ©; rcases hâ‚‚ with âŸ¨pâ‚‚, hâ‚‚âŸ©
  exact âŸ¨âŸ¨pâ‚ â‹ pâ‚‚, by simpâŸ©, by intro x; simp [hâ‚, hâ‚‚, hâ‚.eval, hâ‚‚.eval]âŸ©

lemma or {Pâ‚ Pâ‚‚ : (Fin k â†’ M) â†’ Prop} (hâ‚ : Definable b s Pâ‚) (hâ‚‚ : Definable b s Pâ‚‚) :
    Definable b s (fun v â†¦ Pâ‚ v âˆ¨ Pâ‚‚ v) := by
  rcases hâ‚ with âŸ¨pâ‚, hâ‚âŸ©; rcases hâ‚‚ with âŸ¨pâ‚‚, hâ‚‚âŸ©
  exact âŸ¨âŸ¨pâ‚ â‹ pâ‚‚, by simpâŸ©, by intro x; simp [hâ‚, hâ‚‚, hâ‚.eval, hâ‚‚.eval]âŸ©

lemma not {P : (Fin k â†’ M) â†’ Prop} (h : Definable b.alt s P) :
    Definable b s (fun v â†¦ Â¬P v) := by
  rcases h with âŸ¨p, hâŸ©; exact âŸ¨âŸ¨~p.val, by simpâŸ©, by intro x; simp [h.eval]âŸ©

lemma imp {Pâ‚ Pâ‚‚ : (Fin k â†’ M) â†’ Prop} (hâ‚ : Definable b.alt s Pâ‚) (hâ‚‚ : Definable b s Pâ‚‚) :
    Definable b s (fun v â†¦ Pâ‚ v â†’ Pâ‚‚ v) := by
  rcases hâ‚ with âŸ¨pâ‚, hâ‚âŸ©; rcases hâ‚‚ with âŸ¨pâ‚‚, hâ‚‚âŸ©
  exact âŸ¨âŸ¨pâ‚ âŸ¶ pâ‚‚, by simpâŸ©, by intro x; simp [hâ‚, hâ‚‚, hâ‚.eval, hâ‚‚.eval]âŸ©

lemma iff {Pâ‚ Pâ‚‚ : (Fin k â†’ M) â†’ Prop} (hâ‚ : Definable b s Pâ‚) (hâ‚' : Definable b.alt s Pâ‚) (hâ‚‚ : Definable b s Pâ‚‚) (hâ‚‚' : Definable b.alt s Pâ‚‚) :
    Definable b s (fun v â†¦ Pâ‚ v â†” Pâ‚‚ v) := by
  simp [iff_iff_implies_and_implies]
  apply and <;>  apply imp <;> simp [*]

lemma iffâ‚€ {Pâ‚ Pâ‚‚ : (Fin k â†’ M) â†’ Prop} (hâ‚ : Definable b 0 Pâ‚) (hâ‚‚ : Definable b 0 Pâ‚‚) :
    Definable b 0 (fun v â†¦ Pâ‚ v â†” Pâ‚‚ v) := iff hâ‚ hâ‚.of_zero hâ‚‚ hâ‚‚.of_zero

lemma ball_lt {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial b s f) (h : Definable b s (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable b s (fun v â†¦ âˆ€ x < f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆ€[#0 < #1] !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p))â€, by simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, â†le_iff_lt_succ]
        constructor
        Â· intro h; exact âŸ¨f v, hbf v, rfl, hâŸ©
        Â· rintro âŸ¨_, _, rfl, hâŸ©; exact hâŸ©

lemma bex_lt {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial b s f) (h : Definable b s (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable b s (fun v â†¦ âˆƒ x < f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆƒ[#0 < #1] !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p))â€, by simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, â†le_iff_lt_succ]
        constructor
        Â· intro h; exact âŸ¨f v, hbf v, rfl, hâŸ©
        Â· rintro âŸ¨_, _, rfl, hâŸ©; exact hâŸ©

lemma ball_le {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial b s f) (h : Definable b s (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable b s (fun v â†¦ âˆ€ x â‰¤ f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆ€[#0 < #1 + 1] !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p))â€, by simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, â†le_iff_lt_succ]
        constructor
        Â· intro h; exact âŸ¨f v, hbf v, rfl, hâŸ©
        Â· rintro âŸ¨_, _, rfl, hâŸ©; exact hâŸ©

lemma bex_le {P : (Fin k â†’ M) â†’ M â†’ Prop} {f : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomial b s f) (h : Definable b s (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable b s (fun v â†¦ âˆƒ x â‰¤ f v, P v x) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  rcases hf.definable with âŸ¨f_graph, hf_graphâŸ©
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨â€œâˆƒ[#0 < !!(Rew.bShift bf) + 1] (!f_graph âˆ§ âˆƒ[#0 < #1 + 1] !((Rew.substs (#0 :> (#Â·.succ.succ))).hom p))â€, by simpâŸ©,
    by  intro v; simp [hf_graph.eval, hp.eval, â†le_iff_lt_succ]
        constructor
        Â· intro h; exact âŸ¨f v, hbf v, rfl, hâŸ©
        Â· rintro âŸ¨_, _, rfl, hâŸ©; exact hâŸ©

lemma all {P : (Fin k â†’ M) â†’ M â†’ Prop} (h : Definable Î  (s + 1) (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable Î  (s + 1) (fun v â†¦ âˆ€ x, P v x) := by
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨âˆ€' p, by simpâŸ©, by intro v; simp [hp.eval]âŸ©

lemma ex {P : (Fin k â†’ M) â†’ M â†’ Prop} (h : Definable Î£ (s + 1) (fun w â†¦ P (w Â·.succ) (w 0))) :
    Definable Î£ (s + 1) (fun v â†¦ âˆƒ x, P v x) := by
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨âŸ¨âˆƒ' p, by simpâŸ©, by intro v; simp [hp.eval]âŸ©

@[simp] lemma val_id' (t : Semiterm â„’â‚’áµ£ M n) (e : Fin n â†’ Fin k) :
    DefinableFunction b s fun v : Fin k â†’ M => Semiterm.val! M (fun x â†¦ v (e x)) id t :=
  âŸ¨âŸ¨â€œ#0 = !!(Rew.substs (fun x â†¦ #(e x).succ) t)â€, by simpâŸ©, by intro v; simp [Semiterm.val_substs]âŸ©

@[simp] lemma val_id (t : Semiterm â„’â‚’áµ£ M k) :
    DefinableFunction b s fun v : Fin k â†’ M => Semiterm.val! M v id t :=
  âŸ¨âŸ¨â€œ#0 = !!(Rew.bShift t)â€, by simpâŸ©, by intro v; simp [Semiterm.val_bShift']âŸ©

end Definable

namespace DefinableFunction

@[simp] lemma const {k} (c : M) : DefinableFunction b s (fun _ : Fin k â†’ M â†¦ c) :=
  âŸ¨âŸ¨â€œ#0 = &câ€, by simpâŸ©, by intro v; simpâŸ©

@[simp] lemma var {k} (i : Fin k) : DefinableFunction b s (fun v : Fin k â†’ M â†¦ v i) :=
  âŸ¨âŸ¨â€œ#0 = !!#i.succâ€, by simpâŸ©, by intro _; simpâŸ©

end DefinableFunction

namespace Semipolynomial

lemma of_iff {g : (Fin k â†’ M) â†’ M} (f) (h : âˆ€ v, f v = g v) (H : Semipolynomial b s f) : Semipolynomial b s g := by
  have : f = g := by funext v; simp [h]
  rcases this; exact H

@[simp] lemma var {k} (i : Fin k) : Semipolynomial b s (fun v : Fin k â†’ M â†¦ v i) := âŸ¨by simp, by simpâŸ©

@[simp] lemma const {k} (c : M) : Semipolynomial b s (fun _ : Fin k â†’ M â†¦ c) := âŸ¨by simp, by simpâŸ©

@[simp] lemma val_id' (t : Semiterm â„’â‚’áµ£ M n) (e : Fin n â†’ Fin k) :
    Semipolynomial b s fun v : Fin k â†’ M => Semiterm.val! M (fun x â†¦ v (e x)) id t := âŸ¨by simp, by simpâŸ©

@[simp] lemma val_id (t : Semiterm â„’â‚’áµ£ M k) : Semipolynomial b s fun v : Fin k â†’ M => Semiterm.val! M v id t := âŸ¨by simp, by simpâŸ©

end Semipolynomial

namespace Definable

lemma compâ‚ {k} {P : M â†’ Prop} {f : (Fin k â†’ M) â†’ M} [hP : DefinablePred b s P] (hf : Semipolynomial b s f) :
    Definable b s (fun v â†¦ P (f v)) := by
  rcases hf.bounded with âŸ¨bf, hbfâŸ©
  have : Definable b s (fun v â†¦ âˆƒ z â‰¤ Semiterm.val! M v id bf, z = f v âˆ§ P z) :=
    bex_le (by simp) (and hf.definable $ by rcases hP with âŸ¨p, hpâŸ©; exact âŸ¨âŸ¨p /[#0], by simpâŸ©, by intro _; simp [hp.eval]âŸ©)
  exact this.of_iff _ (by
    intro v; constructor
    Â· intro h; exact âŸ¨f v, hbf v, rfl, hâŸ©
    Â· rintro âŸ¨_, _, rfl, hâŸ©; exact h)

lemma compâ‚‚ {k} {R : M â†’ M â†’ Prop} {fâ‚ fâ‚‚ : (Fin k â†’ M) â†’ M}
    [hR : DefinableRel b s R] (hfâ‚ : Semipolynomial b s fâ‚) (hfâ‚‚ : Semipolynomial b s fâ‚‚) :
    Definable b s (fun v â†¦ R (fâ‚ v) (fâ‚‚ v)) := by
  rcases hfâ‚.bounded with âŸ¨bfâ‚, hbfâ‚âŸ©
  rcases hfâ‚‚.bounded with âŸ¨bfâ‚‚, hbfâ‚‚âŸ©
  have : Definable b s (fun v â†¦
      âˆƒ zâ‚ â‰¤ Semiterm.val! M v id bfâ‚, âˆƒ zâ‚‚ â‰¤ Semiterm.val! M v id bfâ‚‚, zâ‚ = fâ‚ v âˆ§ zâ‚‚ = fâ‚‚ v âˆ§ R zâ‚ zâ‚‚) :=
    bex_le (Semipolynomial.val_id _) <| bex_le (Semipolynomial.val_id' _ _)
      <| and (hfâ‚.definable.rel.finmap _)
        <| and (by simpa using hfâ‚‚.definable.rel.finmap (0 :> (Â·.succ.succ)))
          <| by simpa using hR.finmap (n := k + 2) ![1, 0]
  exact this.of_iff _ (by
    intro v; constructor
    Â· intro h; exact âŸ¨fâ‚ v, hbfâ‚ v, fâ‚‚ v, hbfâ‚‚ v, rfl, rfl, hâŸ©
    Â· rintro âŸ¨_, _, _, _, rfl, rfl, hâŸ©; exact h)

lemma compâ‚ƒ {k} {R : M â†’ M â†’ M â†’ Prop} {fâ‚ fâ‚‚ fâ‚ƒ : (Fin k â†’ M) â†’ M}
    [hR : DefinableRelâ‚ƒ b s R] (hfâ‚ : Semipolynomial b s fâ‚) (hfâ‚‚ : Semipolynomial b s fâ‚‚) (hfâ‚ƒ : Semipolynomial b s fâ‚ƒ) :
    Definable b s (fun v â†¦ R (fâ‚ v) (fâ‚‚ v) (fâ‚ƒ v)) := by
  rcases hfâ‚.bounded with âŸ¨bfâ‚, hbfâ‚âŸ©
  rcases hfâ‚‚.bounded with âŸ¨bfâ‚‚, hbfâ‚‚âŸ©
  rcases hfâ‚ƒ.bounded with âŸ¨bfâ‚ƒ, hbfâ‚ƒâŸ©
  have : Definable b s (fun v â†¦
      âˆƒ zâ‚ â‰¤ Semiterm.val! M v id bfâ‚, âˆƒ zâ‚‚ â‰¤ Semiterm.val! M v id bfâ‚‚, âˆƒ zâ‚ƒ â‰¤ Semiterm.val! M v id bfâ‚ƒ,
        zâ‚ = fâ‚ v âˆ§ zâ‚‚ = fâ‚‚ v âˆ§ zâ‚ƒ = fâ‚ƒ v âˆ§ R zâ‚ zâ‚‚ zâ‚ƒ) :=
    bex_le (Semipolynomial.val_id _) <| bex_le (Semipolynomial.val_id' _ _)
      <| bex_le (Semipolynomial.val_id' _ _)
        <| and (by simpa using hfâ‚.definable.rel.finmap (n := k + 3) (2 :> (Â·.succ.succ.succ)))
          <| and (by simpa using hfâ‚‚.definable.rel.finmap (n := k + 3) (1 :> (Â·.succ.succ.succ)))
            <| and (by simpa using hfâ‚ƒ.definable.rel.finmap (n := k + 3) (0 :> (Â·.succ.succ.succ)))
              <| by simpa using hR.finmap (n := k + 3) ![2, 1, 0]
  exact this.of_iff _ (by
    intro v; constructor
    Â· intro h; exact âŸ¨fâ‚ v, hbfâ‚ v, fâ‚‚ v, hbfâ‚‚ v, fâ‚ƒ v, hbfâ‚ƒ v, rfl, rfl, rfl, hâŸ©
    Â· rintro âŸ¨_, _, _, _, _, _, rfl, rfl, rfl, hâŸ©; exact h)

lemma compâ‚„ {k} {R : M â†’ M â†’ M â†’ M â†’ Prop} {fâ‚ fâ‚‚ fâ‚ƒ fâ‚„ : (Fin k â†’ M) â†’ M}
    [hR : DefinableRelâ‚„ b s R] (hfâ‚ : Semipolynomial b s fâ‚) (hfâ‚‚ : Semipolynomial b s fâ‚‚) (hfâ‚ƒ : Semipolynomial b s fâ‚ƒ) (hfâ‚„ : Semipolynomial b s fâ‚„) :
    Definable b s (fun v â†¦ R (fâ‚ v) (fâ‚‚ v) (fâ‚ƒ v) (fâ‚„ v)) := by
  rcases hfâ‚.bounded with âŸ¨bfâ‚, hbfâ‚âŸ©
  rcases hfâ‚‚.bounded with âŸ¨bfâ‚‚, hbfâ‚‚âŸ©
  rcases hfâ‚ƒ.bounded with âŸ¨bfâ‚ƒ, hbfâ‚ƒâŸ©
  rcases hfâ‚„.bounded with âŸ¨bfâ‚„, hbfâ‚„âŸ©
  have : Definable b s (fun v â†¦
      âˆƒ zâ‚ â‰¤ Semiterm.val! M v id bfâ‚, âˆƒ zâ‚‚ â‰¤ Semiterm.val! M v id bfâ‚‚, âˆƒ zâ‚ƒ â‰¤ Semiterm.val! M v id bfâ‚ƒ, âˆƒ zâ‚„ â‰¤ Semiterm.val! M v id bfâ‚„,
        zâ‚ = fâ‚ v âˆ§ zâ‚‚ = fâ‚‚ v âˆ§ zâ‚ƒ = fâ‚ƒ v âˆ§ zâ‚„ = fâ‚„ v âˆ§ R zâ‚ zâ‚‚ zâ‚ƒ zâ‚„) :=
    bex_le (Semipolynomial.val_id _) <| bex_le (Semipolynomial.val_id' _ _) <| bex_le (Semipolynomial.val_id' _ _) <| bex_le (Semipolynomial.val_id' _ _)
        <| and (by simpa using hfâ‚.definable.rel.finmap (n := k + 4) (3 :> (Â·.succ.succ.succ.succ)))
        <| and (by simpa using hfâ‚‚.definable.rel.finmap (n := k + 4) (2 :> (Â·.succ.succ.succ.succ)))
        <| and (by simpa using hfâ‚ƒ.definable.rel.finmap (n := k + 4) (1 :> (Â·.succ.succ.succ.succ)))
        <| and (by simpa using hfâ‚„.definable.rel.finmap (n := k + 4) (0 :> (Â·.succ.succ.succ.succ)))
        <| by simpa using hR.finmap (n := k + 4) ![3, 2, 1, 0]
  exact this.of_iff _ (by
    intro v; constructor
    Â· intro h; exact âŸ¨fâ‚ v, hbfâ‚ v, fâ‚‚ v, hbfâ‚‚ v, fâ‚ƒ v, hbfâ‚ƒ v, fâ‚„ v, hbfâ‚„ v, rfl, rfl, rfl, rfl, hâŸ©
    Â· rintro âŸ¨_, _, _, _, _, _, _, _, rfl, rfl, rfl, rfl, hâŸ©; exact h)

end Definable

lemma DefinableFunctionâ‚.comp {k} {f : M â†’ M} {g : (Fin k â†’ M) â†’ M}
    (hf : DefinableFunctionâ‚ b s f) (hg : Semipolynomial b s g) :
    DefinableFunction b s (fun v â†¦ f (g v)) := by
  have := Definable.compâ‚‚ (k := k + 1) (R := Function.Graph f) (Semipolynomial.var 0) (hg.finmap Fin.succ)
  simpa using this

lemma DefinableFunctionâ‚‚.comp {k} {f : M â†’ M â†’ M} {gâ‚ gâ‚‚ : (Fin k â†’ M) â†’ M}
    (hf : DefinableFunctionâ‚‚ b s f) (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) :
    DefinableFunction b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v)) := by
  have := Definable.compâ‚ƒ (k := k + 1) (R := Function.Graphâ‚‚ f) (Semipolynomial.var 0) (hgâ‚.finmap Fin.succ) (hgâ‚‚.finmap Fin.succ)
  simpa using this

lemma DefinableFunctionâ‚ƒ.comp {k} {f : M â†’ M â†’ M â†’ M} {gâ‚ gâ‚‚ gâ‚ƒ : (Fin k â†’ M) â†’ M}
    (hf : DefinableFunctionâ‚ƒ b s f) (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) (hgâ‚ƒ : Semipolynomial b s gâ‚ƒ)  :
    DefinableFunction b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v) (gâ‚ƒ v)) := by
  have := Definable.compâ‚„ (k := k + 1) (R := Function.Graphâ‚ƒ f) (Semipolynomial.var 0) (hgâ‚.finmap Fin.succ) (hgâ‚‚.finmap Fin.succ) (hgâ‚ƒ.finmap Fin.succ)
  simpa using this

lemma Semipolynomialâ‚.comp {k} {f : M â†’ M} {g : (Fin k â†’ M) â†’ M} (hf : Semipolynomialâ‚ b s f) (hg : Semipolynomial b s g) :
    Semipolynomial b s (fun v â†¦ f (g v)) := âŸ¨hf.bounded.comp hg.bounded, hf.definable.comp hgâŸ©

lemma Semipolynomialâ‚‚.comp {k} {f : M â†’ M â†’ M} {gâ‚ gâ‚‚ : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomialâ‚‚ b s f) (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) :
    Semipolynomial b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v)) := âŸ¨hf.bounded.comp hgâ‚.bounded hgâ‚‚.bounded, hf.definable.comp hgâ‚ hgâ‚‚âŸ©

lemma Semipolynomialâ‚ƒ.comp {k} {f : M â†’ M â†’ M â†’ M} {gâ‚ gâ‚‚ gâ‚ƒ : (Fin k â†’ M) â†’ M}
    (hf : Semipolynomialâ‚ƒ b s f) (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) (hgâ‚ƒ : Semipolynomial b s gâ‚ƒ) :
    Semipolynomial b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v) (gâ‚ƒ v)) := âŸ¨hf.bounded.comp hgâ‚.bounded hgâ‚‚.bounded hgâ‚ƒ.bounded, hf.definable.comp hgâ‚ hgâ‚‚ hgâ‚ƒâŸ©

lemma Semipolynomial.compâ‚ {k} {f : M â†’ M} {g : (Fin k â†’ M) â†’ M}
    [hfb : PolyBoundedâ‚ f] [hfd : DefinableFunctionâ‚ b s f] (hg : Semipolynomial b s g) :
    Semipolynomial b s (fun v â†¦ f (g v)) := Semipolynomialâ‚.comp âŸ¨hfb, hfdâŸ© hg

lemma Semipolynomial.compâ‚‚ {k} {f : M â†’ M â†’ M} {gâ‚ gâ‚‚ : (Fin k â†’ M) â†’ M}
    [hfb : PolyBoundedâ‚‚ f] [hfd : DefinableFunctionâ‚‚ b s f] (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) :
    Semipolynomial b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v)) := Semipolynomialâ‚‚.comp âŸ¨hfb, hfdâŸ© hgâ‚ hgâ‚‚

lemma Semipolynomial.compâ‚ƒ {k} {f : M â†’ M â†’ M â†’ M} {gâ‚ gâ‚‚ gâ‚ƒ : (Fin k â†’ M) â†’ M}
    [hfb : PolyBoundedâ‚ƒ f] [hfd : DefinableFunctionâ‚ƒ b s f] (hgâ‚ : Semipolynomial b s gâ‚) (hgâ‚‚ : Semipolynomial b s gâ‚‚) (hgâ‚ƒ : Semipolynomial b s gâ‚ƒ) :
    Semipolynomial b s (fun v â†¦ f (gâ‚ v) (gâ‚‚ v) (gâ‚ƒ v)) := Semipolynomialâ‚ƒ.comp âŸ¨hfb, hfdâŸ© hgâ‚ hgâ‚‚ hgâ‚ƒ

section

-- https://github.com/leanprover-community/mathlib4/blob/77d078e25cc501fae6907bfbcd80821920125266/Mathlib/Tactic/Measurability.lean#L25-L26
open Lean.Parser.Tactic (config)

open Definable

attribute [aesop (rule_sets [Definability]) norm]
  sq
  pow_three
  pow_four
  Definable.const

attribute [aesop 1 (rule_sets [Definability]) safe]
  Semipolynomial.compâ‚
  Semipolynomial.compâ‚‚
  Semipolynomial.compâ‚ƒ
  Definable.compâ‚
  Definable.compâ‚‚
  Definable.compâ‚ƒ
  Definable.compâ‚„
  Definable.const

attribute [aesop 4 (rule_sets [Definability]) safe]
  Definable.not
  Definable.imp
  Definable.iffâ‚€
  Definable.ball_lt
  Definable.ball_le
  Definable.bex_lt
  Definable.bex_le

attribute [aesop 8 (rule_sets [Definability]) safe]
  Definable.and
  Definable.or
  Definable.all
  Definable.ex

macro "definability" : attr =>
  `(attr|aesop 4 (rule_sets [$(Lean.mkIdent `Definability):ident]) safe)

macro "definability" (config)? : tactic =>
  `(tactic| aesop (options := { terminal := true }) (rule_sets [$(Lean.mkIdent `Definability):ident]))

macro "definability?" (config)? : tactic =>
  `(tactic| aesop? (options := { terminal := true }) (rule_sets [$(Lean.mkIdent `Definability):ident]))

example (c : M) : Semipolynomialâ‚‚ Î£ 0 (fun x y : M â†¦ c + 2 * x^2) := by definability

example {ex : M â†’ M} [h : âˆ€ b s, DefinableFunctionâ‚ b s ex] (c : M) :
  DefinableRel Î£ 0 (fun x y : M â†¦ âˆƒ z < x + c * y, ex x = z âˆ§ ex (x + 1) = 2 * z) := by
    simp [Function.Graph.iff_left ex]
    definability

end

end Model

end definability

end Arith


end LO.FirstOrder
