import Arithmetization.Incompleteness.Theory

noncomputable section

open Classical

namespace LO.FirstOrder

open LO.Arith FirstOrder.Arith

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

variable {L : Language} [(k : â„•) â†’ Encodable (L.Func k)] [(k : â„•) â†’ Encodable (L.Rel k)] [DefinableLanguage L]

variable (V)

namespace Derivation3

def Sequent.codeIn (Î“ : Finset (SyntacticFormula L)) : V := âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V)

instance : GoedelQuote (Finset (SyntacticFormula L)) V := âŸ¨Sequent.codeIn VâŸ©

lemma Sequent.codeIn_def (Î“ : Finset (SyntacticFormula L)) : âŒœÎ“âŒ = âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V) := rfl

variable {V}

open Classical

@[simp] lemma Sequent.codeIn_empty : (âŒœ(âˆ… : Finset (SyntacticFormula L))âŒ : V) = âˆ… := by
  simp [Sequent.codeIn_def, emptyset_def]

lemma Sequent.mem_codeIn_iff {Î“ : Finset (SyntacticFormula L)} {p} : âŒœpâŒ âˆˆ (âŒœÎ“âŒ : V) â†” p âˆˆ Î“ := by
  induction Î“ using Finset.induction generalizing p
  case empty => simp [Sequent.codeIn_def]
  case insert a Î“ ha ih =>
    have : exp âŒœaâŒ + âˆ‘ p âˆˆ Î“, exp (âŒœpâŒ : V) = insert (âŒœaâŒ : V) (âŒœÎ“âŒ : V) := by
      simp [insert, bitInsert, (not_iff_not.mpr ih.symm).mp ha, add_comm]
      rw [Sequent.codeIn_def]
    simp [ha, Sequent.codeIn_def]
    rw [this]
    simp [â†ih]

@[simp] lemma Sequent.codeIn_singleton (p : SyntacticFormula L) :
    (âŒœ({p} : Finset (SyntacticFormula L))âŒ : V) = {âŒœpâŒ} := by simp [Sequent.codeIn_def]; rfl

@[simp] lemma Sequent.codeIn_insert (Î“ : Finset (SyntacticFormula L)) (p) : (âŒœ(insert p Î“)âŒ : V) = insert âŒœpâŒ âŒœÎ“âŒ := by
  by_cases hp : p âˆˆ Î“
  Â· simp [Sequent.mem_codeIn_iff, hp, insert_eq_self_of_mem]
  Â· have : (âŒœinsert p Î“âŒ : V) = exp âŒœpâŒ + âŒœÎ“âŒ := by simp [Sequent.codeIn_def, hp]
    simp [Sequent.mem_codeIn_iff, this, insert_eq, bitInsert, hp, add_comm]

lemma Sequent.mem_codeIn {Î“ : Finset (SyntacticFormula L)} (hx : x âˆˆ (âŒœÎ“âŒ : V)) : âˆƒ p âˆˆ Î“, x = âŒœpâŒ := by
  induction Î“ using Finset.induction
  case empty => simp at hx
  case insert a Î“ _ ih =>
    have : x = âŒœaâŒ âˆ¨ x âˆˆ (âŒœÎ“âŒ : V) := by simpa using hx
    rcases this with (rfl | hx)
    Â· exact âŸ¨a, by simpâŸ©
    Â· rcases ih hx with âŸ¨p, hx, rflâŸ©
      exact âŸ¨p, by simp [*]âŸ©

variable (V)

variable {T : SyntacticTheory L} [T.Î”â‚Definable]

def codeIn : {Î“ : Finset (SyntacticFormula L)} â†’ T âŠ¢â‚ƒ Î“ â†’ V
  | _, closed Î” p _ _                         => Arith.axL âŒœÎ”âŒ âŒœpâŒ
  | _, root (Î” := Î”) p _ _                    => Arith.root âŒœÎ”âŒ âŒœpâŒ
  | _, verum (Î” := Î”) _                       => Arith.verumIntro âŒœÎ”âŒ
  | _, and (Î” := Î”) _ (p := p) (q := q) bp bq => Arith.andIntro âŒœÎ”âŒ âŒœpâŒ âŒœqâŒ bp.codeIn bq.codeIn
  | _, or (Î” := Î”) (p := p) (q := q) _ d      => Arith.orIntro âŒœÎ”âŒ âŒœpâŒ âŒœqâŒ d.codeIn
  | _, all (Î” := Î”) (p := p) _ d              => Arith.allIntro âŒœÎ”âŒ âŒœpâŒ d.codeIn
  | _, ex (Î” := Î”) (p := p) _ t d             => Arith.exIntro âŒœÎ”âŒ âŒœpâŒ âŒœtâŒ d.codeIn
  | _, wk (Î“ := Î“) d _                        => Arith.wkRule âŒœÎ“âŒ d.codeIn
  | _, shift (Î” := Î”) d                       => Arith.shiftRule âŒœÎ”.image Rew.shift.homâŒ d.codeIn
  | _, cut (Î” := Î”) (p := p) d dn             => Arith.cutRule âŒœÎ”âŒ âŒœpâŒ d.codeIn dn.codeIn

instance (Î“ : Finset (SyntacticFormula L)) : GoedelQuote (T âŠ¢â‚ƒ Î“) V := âŸ¨codeIn VâŸ©

lemma quote_derivation_def {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (âŒœdâŒ : V) = d.codeIn V := rfl

@[simp] lemma fstidx_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : fstIdx (âŒœdâŒ : V) = âŒœÎ“âŒ := by
  induction d <;> simp [quote_derivation_def, codeIn]

end Derivation3

end LO.FirstOrder

namespace LO.Arith

open FirstOrder FirstOrder.Arith FirstOrder.Semiformula

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

variable {L : Language} [(k : â„•) â†’ Encodable (L.Func k)] [(k : â„•) â†’ Encodable (L.Rel k)] [DefinableLanguage L]

variable {T : SyntacticTheory L} [T.Î”â‚Definable]

open Classical

@[simp] lemma formulaSet_codeIn_finset (Î“ : Finset (SyntacticFormula L)) : (L.codeIn V).IsFormulaSet âŒœÎ“âŒ := by
  intro x hx
  rcases Derivation3.Sequent.mem_codeIn hx with âŸ¨p, _, rflâŸ©;
  apply semiformula_quote (n := 0)

open Derivation3

lemma quote_image_shift (Î“ : Finset (SyntacticFormula L)) : (L.codeIn V).setShift (âŒœÎ“âŒ : V) = âŒœÎ“.image Rew.shift.homâŒ := by
  induction Î“ using Finset.induction
  case empty => simp
  case insert p Î“ _ ih => simp [shift_quote, ih]

@[simp] lemma derivation_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).Derivation âŒœdâŒ := by
  induction d
  case closed p hp hn =>
    exact Language.Theory.Derivation.axL (by simp)
      (by simp [Sequent.mem_codeIn_iff, hp])
      (by simp [Sequent.mem_codeIn_iff, neg_quote, hn])
  case root Î” p hT hp =>
    apply Language.Theory.Derivation.root (by simp)
      (by simp [Sequent.mem_codeIn_iff, hp])
      (mem_coded_theory hT)
  case verum Î” h =>
    exact Language.Theory.Derivation.verumIntro (by simp)
      (by simpa [quote_verum] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
  case and Î” p q hpq dp dq ihp ihq =>
    apply Language.Theory.Derivation.andIntro
      (by simpa [quote_and] using (Sequent.mem_codeIn_iff (V := V)).mpr hpq)
      âŸ¨by simp [fstidx_quote], ihpâŸ©
      âŸ¨by simp [fstidx_quote], ihqâŸ©
  case or Î” p q hpq d ih =>
    apply Language.Theory.Derivation.orIntro
      (by simpa [quote_or] using (Sequent.mem_codeIn_iff (V := V)).mpr hpq)
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case all Î” p h d ih =>
    apply Language.Theory.Derivation.allIntro
      (by simpa [quote_all] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
      âŸ¨by simp [fstidx_quote, quote_image_shift, free_quote], ihâŸ©
  case ex Î” p h t d ih =>
    apply Language.Theory.Derivation.exIntro
      (by simpa [quote_ex] using (Sequent.mem_codeIn_iff (V := V)).mpr h)
      (semiterm_codeIn t)
      âŸ¨by simp [fstidx_quote, â†substs_quote, Language.substsâ‚], ihâŸ©
  case wk Î” Î“ d h ih =>
    apply Language.Theory.Derivation.wkRule (s' := âŒœÎ”âŒ)
      (by simp)
      (by intro x hx; rcases Sequent.mem_codeIn hx with âŸ¨p, hp, rflâŸ©
          simp [Sequent.mem_codeIn_iff, h hp])
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case shift Î” d ih =>
    simp [quote_derivation_def, Derivation3.codeIn, â†quote_image_shift]
    apply Language.Theory.Derivation.shiftRule
      âŸ¨by simp [fstidx_quote], ihâŸ©
  case cut Î” p d dn ih ihn =>
    apply Language.Theory.Derivation.cutRule
      âŸ¨by simp [fstidx_quote], ihâŸ©
      âŸ¨by simp [fstidx_quote, neg_quote], ihnâŸ©

@[simp] lemma derivationOf_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).DerivationOf âŒœdâŒ âŒœÎ“âŒ :=
  âŸ¨by simp, by simpâŸ©

lemma derivable_of_quote {Î“ : Finset (SyntacticFormula L)} (d : T âŠ¢â‚ƒ Î“) : (T.codeIn V).Derivable âŒœÎ“âŒ :=
  âŸ¨âŒœdâŒ, by simpâŸ©

section

variable [L.ConstantInhabited] {T : Theory L} [T.Î”â‚Definable]

theorem provable_of_provable {Ïƒ} : T âŠ¢! Ïƒ â†’ (T.codeIn V).Provable âŒœÏƒâŒ := fun h â†¦ by
  simpa using derivable_of_quote (V := V) (provable_iff_derivable3'.mp h).some

/-- Hilbertâ€“Bernays provability condition D1 -/
theorem tprovable_of_provable {Ïƒ} : T âŠ¢! Ïƒ â†’ T.tCodeIn V âŠ¢! âŒœÏƒâŒ := fun h â†¦ by
  simpa [Language.Theory.TProvable.iff_provable] using provable_of_provable (V := V) h

end

section

variable {T : Theory â„’â‚’áµ£} [T.Î”â‚Definable]

theorem provableâ‚_of_provable {Ïƒ} : T âŠ¢! Ïƒ â†’ T.Provableâ‚ (âŒœÏƒâŒ : V) := fun h â†¦
  Language.Theory.Derivable.of_ss (by simp) (provable_of_provable h)

end

end LO.Arith
