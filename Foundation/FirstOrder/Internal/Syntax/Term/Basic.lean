import Foundation.FirstOrder.Internal.Syntax.Language
import Foundation.FirstOrder.ISigma1.HFS

namespace LO.ISigma1.Metamath

open FirstOrder Arithmetic PeanoMinus IOpen ISigma0

variable {V : Type*} [ORingStruc V] [V ‚äß‚Çò* ùóúùö∫‚ÇÅ]

variable {L : Language} [L.Encodable] [L.LORDefinable]

section term

noncomputable def qqBvar (z : V) : V := ‚ü™0, z‚ü´ + 1

noncomputable def qqFvar (x : V) : V := ‚ü™1, x‚ü´ + 1

noncomputable def qqFunc (k f v : V) : V := ‚ü™2, k, f, v‚ü´ + 1

def qqFuncN (k f v : ‚Ñï) : ‚Ñï := (Nat.pair 2 <| Nat.pair k <| Nat.pair f v) + 1

scoped prefix:max "^#" => qqBvar

scoped prefix:max "^&" => qqFvar

scoped prefix:max "^func " => qqFunc

lemma qqFuncN_eq_qqFunc (k f v : ‚Ñï) : qqFuncN k f v = qqFunc k f v := by simp [qqFunc, qqFuncN, nat_pair_eq]

@[simp] lemma var_lt_qqBvar (z : V) : z < ^#z := lt_succ_iff_le.mpr <| le_pair_right 0 z

@[simp] lemma var_lt_qqFvar (x : V) : x < ^&x := lt_succ_iff_le.mpr <| le_pair_right 1 x

@[simp] lemma arity_lt_qqFunc (k f v : V) : k < ^func k f v :=
  le_iff_lt_succ.mp <| le_trans (le_pair_right 2 k) <| pair_le_pair_right 2 <| le_pair_left k ‚ü™f, v‚ü´

@[simp] lemma func_lt_qqFunc (k f v : V) : f < ^func k f v :=
  le_iff_lt_succ.mp <| le_trans (le_pair_left f v) <| le_trans (le_pair_right k ‚ü™f, v‚ü´) <| le_pair_right 2 ‚ü™k, ‚ü™f, v‚ü´‚ü´

@[simp] lemma terms_lt_qqFunc (k f v : V) : v < ^func k f v :=
  le_iff_lt_succ.mp <| le_trans (le_pair_right f v) <| le_trans (le_pair_right k ‚ü™f, v‚ü´) <| le_pair_right 2 ‚ü™k, ‚ü™f, v‚ü´‚ü´

lemma nth_lt_qqFunc_of_lt {i k f v : V} (hi : i < len v) : v.[i] < ^func k f v :=
  lt_trans (nth_lt_self hi) (terms_lt_qqFunc _ _ _)

@[simp] lemma qqBvar_inj {z z' : V} : ^#z = ^#z' ‚Üî z = z' := by simp [qqBvar]

@[simp] lemma qqFvar_inj {x x' : V} : ^&x = ^&x' ‚Üî x = x' := by simp [qqFvar]

@[simp] lemma qqFunc_inj {k f v k' f' w : V} : ^func k f v = ^func k' f' w ‚Üî k = k' ‚àß f = f' ‚àß v = w := by simp [qqFunc]

def _root_.LO.FirstOrder.Arithmetic.qqBvarDef : ùö∫‚ÇÄ.Semisentence 2 := .mkSigma ‚Äút z. ‚àÉ t' < t, !pairDef t' 0 z ‚àß t = t' + 1‚Äù

lemma qqBvar_defined : ùö∫‚ÇÄ-Function‚ÇÅ (qqBvar : V ‚Üí V) via qqBvarDef := by
  intro v; simp_all [qqBvarDef, qqBvar]

@[simp] lemma eval_qqBvarDef (v) :
    Semiformula.Evalbm V v qqBvarDef.val ‚Üî v 0 = ^#(v 1) := qqBvar_defined.df.iff v

def _root_.LO.FirstOrder.Arithmetic.qqFvarDef : ùö∫‚ÇÄ.Semisentence 2 := .mkSigma ‚Äút x. ‚àÉ t' < t, !pairDef t' 1 x ‚àß t = t' + 1‚Äù

lemma qqFvar_defined : ùö∫‚ÇÄ-Function‚ÇÅ (qqFvar : V ‚Üí V) via qqFvarDef := by
  intro v; simp_all [qqFvarDef, qqFvar]

@[simp] lemma eval_qqFvarDef (v) :
    Semiformula.Evalbm V v qqFvarDef.val ‚Üî v 0 = ^&(v 1) := qqFvar_defined.df.iff v

private lemma qqFunc_graph {x k f v : V} :
    x = ^func k f v ‚Üî ‚àÉ fv < x, fv = ‚ü™f, v‚ü´ ‚àß ‚àÉ kfv < x, kfv = ‚ü™k, fv‚ü´ ‚àß ‚àÉ x' < x, x' = ‚ü™2, kfv‚ü´ ‚àß x = x' + 1 :=
  ‚ü®by rintro rfl
      exact ‚ü®‚ü™f, v‚ü´, lt_succ_iff_le.mpr <| le_trans (le_pair_right _ _) (le_pair_right _ _), rfl,
        ‚ü™k, f, v‚ü´, lt_succ_iff_le.mpr <| by simp, rfl,
        ‚ü™2, k, f, v‚ü´, by simp [qqFunc], rfl, rfl‚ü©,
   by rintro ‚ü®_, _, rfl, _, _, rfl, _, _, rfl, rfl‚ü©; rfl‚ü©

def _root_.LO.FirstOrder.Arithmetic.qqFuncDef : ùö∫‚ÇÄ.Semisentence 4 := .mkSigma
  ‚Äúx k f v. ‚àÉ fv < x, !pairDef fv f v ‚àß ‚àÉ kfv < x, !pairDef kfv k fv ‚àß ‚àÉ x' < x, !pairDef x' 2 kfv ‚àß x = x' + 1‚Äù

lemma qqFunc_defined : ùö∫‚ÇÄ-Function‚ÇÉ (qqFunc : V ‚Üí V ‚Üí V ‚Üí V) via qqFuncDef := by
  intro v; simp [qqFuncDef, qqFunc_graph]

@[simp] lemma eval_qqFuncDef (v) :
    Semiformula.Evalbm V v qqFuncDef.val ‚Üî v 0 = ^func (v 1) (v 2) (v 3) := qqFunc_defined.df.iff v

namespace FormalizedTerm

variable (L)

def Phi (C : Set V) (t : V) : Prop :=
  (‚àÉ z, t = ^#z) ‚à® (‚àÉ x, t = ^&x) ‚à® (‚àÉ k f v : V, L.IsFunc k f ‚àß k = len v ‚àß (‚àÄ i < k, v.[i] ‚àà C) ‚àß t = ^func k f v)

private lemma phi_iff (C : V) (t : V) :
    Phi L {x | x ‚àà C} t ‚Üî
    (‚àÉ z < t, t = ^#z) ‚à®
    (‚àÉ x < t, t = ^&x) ‚à®
    (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, L.IsFunc k f ‚àß k = len v ‚àß (‚àÄ i < k, v.[i] ‚àà C) ‚àß t = ^func k f v) where
  mp := by
    rintro (‚ü®z, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, hkf, hk, hv, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, lt_succ_iff_le.mpr <| by simp, rfl‚ü©
    ¬∑ right; left
      exact ‚ü®x, lt_succ_iff_le.mpr <| by simp, rfl‚ü©
    ¬∑ right; right
      exact ‚ü®k, by simp, f, by simp, v, by simp, hkf, hk, hv, rfl‚ü©
  mpr := by
    unfold Phi
    rintro (‚ü®z, _, rfl‚ü© | ‚ü®x, _, rfl‚ü© | ‚ü®k, _, f, _, v, _, hkf, hk, hv, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, rfl‚ü©
    ¬∑ right; left; exact ‚ü®x, rfl‚ü©
    ¬∑ right; right; exact ‚ü®k, f, v, hkf, hk, hv, rfl‚ü©

def blueprint : Fixpoint.Blueprint 0 where
  core := .mkDelta
    (.mkSigma ‚Äút C.
      (‚àÉ z < t, !qqBvarDef t z) ‚à®
      (‚àÉ x < t, !qqFvarDef t x) ‚à®
      (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, !L.isFunc k f ‚àß !lenDef k v ‚àß (‚àÄ i < k, ‚àÉ u, !nthDef u v i ‚àß u ‚àà C) ‚àß !qqFuncDef t k f v)‚Äù
    (by simp))
    (.mkPi ‚Äút C.
      (‚àÉ z < t, !qqBvarDef t z) ‚à®
      (‚àÉ x < t, !qqFvarDef t x) ‚à®
      (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, !L.isFunc k f ‚àß (‚àÄ l, !lenDef l v ‚Üí k = l) ‚àß (‚àÄ i < k, ‚àÄ u, !nthDef u v i ‚Üí u ‚àà C) ‚àß !qqFuncDef t k f v)‚Äù
    (by simp))

def construction : Fixpoint.Construction V (blueprint L) where
  Œ¶ := fun _ ‚Ü¶ Phi L
  defined := ‚ü®by intro v; simp [blueprint], by intro v; simp [blueprint, phi_iff]‚ü©
  monotone := by
    rintro C C' hC _ x (h | h | ‚ü®k, f, v, hkf, hk, h, rfl‚ü©)
    ¬∑ exact Or.inl h
    ¬∑ exact Or.inr <| Or.inl h
    ¬∑ exact Or.inr <| Or.inr ‚ü®k, f, v, hkf, hk, fun i hi ‚Ü¶ hC (h i hi), rfl‚ü©

instance : (construction L).StrongFinite V where
  strong_finite := by
    rintro C v x (h | h | ‚ü®k, f, v, hkf, hk, h, rfl‚ü©)
    ¬∑ exact Or.inl h
    ¬∑ exact Or.inr <| Or.inl h
    ¬∑ exact Or.inr <| Or.inr ‚ü®k, f, v, hkf, hk, fun i hi ‚Ü¶
        ‚ü®h i hi, lt_of_le_of_lt (nth_le _ _) (by simp)‚ü©, rfl‚ü©

end FormalizedTerm

open FormalizedTerm

variable (L)

def IsUTerm : V ‚Üí Prop := (construction L).Fixpoint ![]

def isUTerm : ùö´‚ÇÅ.Semisentence 1 := (blueprint L).fixpointDefŒî‚ÇÅ

variable {L}

namespace IsUTerm

lemma defined : ùö´‚ÇÅ-Predicate (IsUTerm L (V := V)) via (isUTerm L) := (construction L).fixpoint_definedŒî‚ÇÅ

@[simp] lemma eval (v) : Semiformula.Evalbm V v (isUTerm L).val ‚Üî IsUTerm L (v 0) := defined.df.iff v

instance definable : ùö´‚ÇÅ-Predicate (IsUTerm L (V := V)) := defined.to_definable

instance definable' (Œì) : Œì-[m + 1]-Predicate (IsUTerm L (V := V)) := definable.of_deltaOne

end IsUTerm

variable (L)

def IsUTermVec (n w : V) : Prop := n = len w ‚àß ‚àÄ i < n, IsUTerm L w.[i]

def isUTermVec : ùö´‚ÇÅ.Semisentence 2 := .mkDelta
  (.mkSigma ‚Äún w. !lenDef n w ‚àß ‚àÄ i < n, ‚àÉ u, !nthDef u w i ‚àß !(isUTerm L).sigma u‚Äù)
  (.mkPi ‚Äún w. (‚àÄ l, !lenDef l w ‚Üí n = l) ‚àß ‚àÄ i < n, ‚àÄ u, !nthDef u w i ‚Üí !(isUTerm L).pi u‚Äù)

variable {L}

namespace IsUTermVec

protected lemma lh {n w : V} (h : IsUTermVec L n w) : n = len w := h.1

lemma nth {n w : V} (h : IsUTermVec L n w) {i} : i < n ‚Üí IsUTerm L w.[i] := h.2 i

@[simp] lemma empty : IsUTermVec (V := V) L 0 0 := ‚ü®by simp, by simp‚ü©

@[simp] lemma empty_iff : IsUTermVec L 0 v ‚Üî v = 0 := by
  constructor
  ¬∑ intro h; exact len_zero_iff_eq_nil.mp h.lh.symm
  ¬∑ rintro rfl; simp

lemma two_iff {v : V} : IsUTermVec L 2 v ‚Üî ‚àÉ t‚ÇÅ t‚ÇÇ, IsUTerm L t‚ÇÅ ‚àß IsUTerm L t‚ÇÇ ‚àß v = ?[t‚ÇÅ, t‚ÇÇ] := by
  constructor
  ¬∑ intro h
    rcases eq_doubleton_of_len_eq_two.mp h.lh.symm with ‚ü®t‚ÇÅ, t‚ÇÇ, rfl‚ü©
    exact ‚ü®t‚ÇÅ, t‚ÇÇ, by simpa using h.nth (show 0 < 2 by simp), by simpa using h.nth (show 1 < 2 by simp), rfl‚ü©
  ¬∑ rintro ‚ü®t‚ÇÅ, t‚ÇÇ, h‚ÇÅ, h‚ÇÇ, rfl‚ü©; exact ‚ü®by simp [one_add_one_eq_two], by simp [lt_two_iff_le_one, le_one_iff_eq_zero_or_one, h‚ÇÅ, h‚ÇÇ]‚ü©

@[simp] lemma cons {n w t : V} (h : IsUTermVec L n w) (ht : IsUTerm L t) : IsUTermVec L (n + 1) (t ‚à∑ w) :=
  ‚ü®by simp [h.lh], fun i hi ‚Ü¶ by
    rcases zero_or_succ i with (rfl | ‚ü®i, rfl‚ü©)
    ¬∑ simpa
    ¬∑ simpa using h.nth (by simpa using hi)‚ü©

@[simp] lemma cons‚ÇÅ_iff {t : V} :
    IsUTermVec L 1 (?[t] : V) ‚Üî IsUTerm L t := by
  constructor
  ¬∑ intro h; simpa using h.nth (i := 0) (by simp)
  ¬∑ intro h; simpa using empty.cons h

@[simp] lemma mkSeq‚ÇÇ_iff {t‚ÇÅ t‚ÇÇ : V} :
    IsUTermVec L 2 (?[t‚ÇÅ, t‚ÇÇ] : V) ‚Üî IsUTerm L t‚ÇÅ ‚àß IsUTerm L t‚ÇÇ := by
  constructor
  ¬∑ intro h; exact ‚ü®by simpa using h.nth (i := 0) (by simp), by simpa using h.nth (i := 1) (by simp)‚ü©
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    simpa [one_add_one_eq_two] using (cons‚ÇÅ_iff.mpr h‚ÇÇ).cons h‚ÇÅ

section

lemma defined : ùö´‚ÇÅ-Relation (IsUTermVec (V := V) L) via (isUTermVec L) :=
  ‚ü®by intro v; simp [isUTermVec, HierarchySymbol.Semiformula.val_sigma, IsUTerm.defined.proper.iff'],
   by intro v; simp [isUTermVec, HierarchySymbol.Semiformula.val_sigma, IsUTermVec]‚ü©

@[simp] lemma eval (v) :
    Semiformula.Evalbm V v (isUTermVec L).val ‚Üî IsUTermVec L (v 0) (v 1) := defined.df.iff v

instance definable : ùö´‚ÇÅ-Relation (IsUTermVec (V := V) L) := defined.to_definable

instance definable' (Œì) : Œì-[m + 1]-Relation (IsUTermVec (V := V) L) := definable.of_deltaOne

end

end IsUTermVec

namespace IsUTerm

lemma case_iff {t : V} :
    IsUTerm L t ‚Üî
    (‚àÉ z, t = ^#z) ‚à®
    (‚àÉ x, t = ^&x) ‚à®
    (‚àÉ k f v : V, L.IsFunc k f ‚àß IsUTermVec L k v ‚àß t = ^func k f v) := by
  simpa [construction, Phi, IsUTermVec, and_assoc] using (construction L).case

alias ‚ü®case, mk‚ü© := case_iff

@[simp] lemma bvar {z : V} : IsUTerm L ^#z :=
  mk (Or.inl ‚ü®z, rfl‚ü©)

@[simp] lemma fvar (x : V) : IsUTerm L ^&x :=
  mk (Or.inr <| Or.inl ‚ü®x, rfl‚ü©)

@[simp] lemma func_iff {k f v : V} :
    IsUTerm L (^func k f v) ‚Üî L.IsFunc k f ‚àß IsUTermVec L k v :=
  ‚ü®by intro h
      rcases h.case with (‚ü®_, h‚ü© | ‚ü®x, h‚ü© | ‚ü®k', f', w, hkf, ‚ü®hk, hv‚ü©, h‚ü©)
      ¬∑ simp [qqFunc, qqBvar] at h
      ¬∑ simp [qqFunc, qqFvar] at h
      ¬∑ rcases (show k = k' ‚àß f = f' ‚àß v = w by simpa [qqFunc] using h) with ‚ü®rfl, rfl, rfl‚ü©
        exact ‚ü®hkf, hk, hv‚ü©,
   by rintro ‚ü®hkf, hk, hv‚ü©; exact mk <| Or.inr <| Or.inr ‚ü®k, f, v, hkf, ‚ü®hk, hv‚ü©, rfl‚ü©‚ü©

lemma func {k f v : V} (hkf : L.IsFunc k f) (hv : IsUTermVec L k v) :
    IsUTerm L (^func k f v) := func_iff.mpr ‚ü®hkf, hv‚ü©

lemma induction (Œì) {P : V ‚Üí Prop} (hP : Œì-[1]-Predicate P)
    (hbvar : ‚àÄ z, P (^#z)) (hfvar : ‚àÄ x, P (^&x))
    (hfunc : ‚àÄ k f v, L.IsFunc k f ‚Üí IsUTermVec L k v ‚Üí (‚àÄ i < k, P v.[i]) ‚Üí P (^func k f v)) :
    ‚àÄ t, IsUTerm L t ‚Üí P t :=
  (construction L).induction (v := ![]) hP (by
    rintro C hC x (‚ü®z, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, hkf, hk, h, rfl‚ü©)
    ¬∑ exact hbvar z
    ¬∑ exact hfvar x
    ¬∑ exact hfunc k f v hkf ‚ü®hk, fun i hi ‚Ü¶ hC _ (h i hi) |>.1‚ü© (fun i hi ‚Ü¶ hC _ (h i hi) |>.2))

end IsUTerm

end term

namespace Language.TermRec

structure Blueprint (arity : ‚Ñï) where
  bvar : ùö∫‚ÇÅ.Semisentence (arity + 2)
  fvar : ùö∫‚ÇÅ.Semisentence (arity + 2)
  func : ùö∫‚ÇÅ.Semisentence (arity + 5)

namespace Blueprint

variable (L) (Œ≤ : Blueprint arity)

def blueprint : Fixpoint.Blueprint arity := ‚ü®.mkDelta
  (.mkSigma ‚Äúpr C.
    ‚àÉ t <‚Å∫ pr, ‚àÉ y <‚Å∫ pr, !pairDef pr t y ‚àß !(isUTerm L).sigma t ‚àß
    ( (‚àÉ z < t, !qqBvarDef t z ‚àß !Œ≤.bvar y z ‚ãØ) ‚à®
      (‚àÉ x < t, !qqFvarDef t x ‚àß !Œ≤.fvar y x ‚ãØ) ‚à®
      (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, ‚àÉ rv, !repeatVecDef rv C k ‚àß ‚àÉ w <‚Å∫ rv,
        (!lenDef k w ‚àß ‚àÄ i < k, ‚àÉ vi, !nthDef vi v i ‚àß ‚àÉ v'i, !nthDef v'i w i ‚àß :‚ü™vi, v'i‚ü´:‚àà C) ‚àß
        !qqFuncDef t k f v ‚àß !Œ≤.func y k f v w ‚ãØ) )‚Äù)
  (.mkPi ‚Äúpr C.
    ‚àÉ t <‚Å∫ pr, ‚àÉ y <‚Å∫ pr, !pairDef pr t y ‚àß !(isUTerm L).pi t ‚àß
    ( (‚àÉ z < t, !qqBvarDef t z ‚àß !Œ≤.bvar.graphDelta.pi y z ‚ãØ) ‚à®
      (‚àÉ x < t, !qqFvarDef t x ‚àß !Œ≤.fvar.graphDelta.pi y x ‚ãØ) ‚à®
      (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, ‚àÄ rv, !repeatVecDef rv C k ‚Üí ‚àÉ w <‚Å∫ rv,
        ((‚àÄ l, !lenDef l w ‚Üí k = l) ‚àß ‚àÄ i < k, ‚àÄ vi, !nthDef vi v i ‚Üí ‚àÄ v'i, !nthDef v'i w i ‚Üí :‚ü™vi, v'i‚ü´:‚àà C) ‚àß
        !qqFuncDef t k f v ‚àß !Œ≤.func.graphDelta.pi y k f v w ‚ãØ) )‚Äù)‚ü©

def graph : ùö∫‚ÇÅ.Semisentence (arity + 2) := .mkSigma
  ‚Äút y. ‚àÉ pr <‚Å∫ (t + y + 1)¬≤, !pairDef pr t y ‚àß !(Œ≤.blueprint L).fixpointDef pr ‚ãØ‚Äù

def result : ùö∫‚ÇÅ.Semisentence (arity + 2) := .mkSigma
  ‚Äúy t. (!(isUTerm L).pi t ‚Üí !(Œ≤.graph L) t y ‚ãØ) ‚àß (¬¨!(isUTerm L).sigma t ‚Üí y = 0)‚Äù

def resultVec : ùö∫‚ÇÅ.Semisentence (arity + 3) := .mkSigma
  ‚Äúw' k w.
    (!(isUTermVec L).pi k w ‚Üí !lenDef k w' ‚àß ‚àÄ i < k, ‚àÉ z, !nthDef z w i ‚àß ‚àÉ z', !nthDef z' w' i ‚àß !(Œ≤.graph L).val z z' ‚ãØ) ‚àß
    (¬¨!(isUTermVec L).sigma k w ‚Üí w' = 0)‚Äù

end Blueprint

variable (L V)

structure Construction {k : ‚Ñï} (œÜ : Blueprint k) where
  bvar : (Fin k ‚Üí V) ‚Üí V ‚Üí V
  fvar : (Fin k ‚Üí V) ‚Üí V ‚Üí V
  func : (Fin k ‚Üí V) ‚Üí V ‚Üí V ‚Üí V ‚Üí V ‚Üí V
  bvar_defined : ùö∫‚ÇÅ.DefinedFunction (fun v ‚Ü¶ bvar (v ¬∑.succ) (v 0)) œÜ.bvar
  fvar_defined : ùö∫‚ÇÅ.DefinedFunction (fun v ‚Ü¶ fvar (v ¬∑.succ) (v 0)) œÜ.fvar
  func_defined : ùö∫‚ÇÅ.DefinedFunction (fun v ‚Ü¶ func (v ¬∑.succ.succ.succ.succ) (v 0) (v 1) (v 2) (v 3)) œÜ.func

variable {V}

namespace Construction

variable {arity : ‚Ñï} {Œ≤ : Blueprint arity} (c : Construction V Œ≤)

def Phi (param : Fin arity ‚Üí V) (C : Set V) (pr : V) : Prop :=
  IsUTerm L (œÄ‚ÇÅ pr) ‚àß
  ( (‚àÉ z, pr = ‚ü™^#z, c.bvar param z‚ü´) ‚à®
    (‚àÉ x, pr = ‚ü™^&x, c.fvar param x‚ü´) ‚à®
    (‚àÉ k f v w, (k = len w ‚àß ‚àÄ i < k, ‚ü™v.[i], w.[i]‚ü´ ‚àà C) ‚àß pr = ‚ü™^func k f v, c.func param k f v w‚ü´) )

lemma seq_bound {k s m : V} (Ss : Seq s) (hk : k = lh s) (hs : ‚àÄ i z, ‚ü™i, z‚ü´ ‚àà s ‚Üí z < m) :
    s < Exp.exp ((k + m + 1)^2) := lt_exp_iff.mpr <| fun p hp ‚Ü¶ by
  have : p < ‚ü™k, m‚ü´ := by
    simpa [hk] using
      pair_lt_pair (Ss.lt_lh_of_mem (show ‚ü™œÄ‚ÇÅ p, œÄ‚ÇÇ p‚ü´ ‚àà s by simpa using hp)) (hs (œÄ‚ÇÅ p) (œÄ‚ÇÇ p) (by simpa using hp))
  exact lt_of_lt_of_le this (by simp)

private lemma phi_iff (param : Fin arity ‚Üí V) (C pr : V) :
    c.Phi L param {x | x ‚àà C} pr ‚Üî
    ‚àÉ t ‚â§ pr, ‚àÉ y ‚â§ pr, pr = ‚ü™t, y‚ü´ ‚àß IsUTerm L t ‚àß
    ( (‚àÉ z < t, t = ^#z ‚àß y = c.bvar param z) ‚à®
      (‚àÉ x < t, t = ^&x ‚àß y = c.fvar param x) ‚à®
      (‚àÉ k < t, ‚àÉ f < t, ‚àÉ v < t, ‚àÉ w ‚â§ repeatVec C k,
        (k = len w ‚àß ‚àÄ i < k, ‚ü™v.[i], w.[i]‚ü´ ‚àà C) ‚àß
        t = ^func k f v ‚àß y = c.func param k f v w) ) := by
  constructor
  ¬∑ rintro ‚ü®ht, H‚ü©
    refine ‚ü®œÄ‚ÇÅ pr, by simp, œÄ‚ÇÇ pr, by simp, by simp, ht, ?_‚ü©
    rcases H with (‚ü®z, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, w, ‚ü®hk, hw‚ü©, hk, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, by simp‚ü©
    ¬∑ right; left; exact ‚ü®x, by simp‚ü©
    ¬∑ right; right
      refine ‚ü®k, by simp, f, by simp, v, by simp, w, ?_, ‚ü®hk, hw‚ü©, by simp‚ü©
      ¬∑ rcases hk; apply len_repeatVec_of_nth_le (fun i hi ‚Ü¶ le_of_lt <| lt_of_mem_rng <| hw i hi)
  ¬∑ rintro ‚ü®t, _, y, _, rfl, ht, H‚ü©
    refine ‚ü®by simpa using ht, ?_‚ü©
    rcases H with (‚ü®z, _, rfl, rfl‚ü© | ‚ü®x, _, rfl, rfl‚ü© | ‚ü®k, _, f, _, v, _, w, _, ‚ü®hk, hw‚ü©, rfl, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, rfl‚ü©
    ¬∑ right; left; exact ‚ü®x, rfl‚ü©
    ¬∑ right; right
      exact ‚ü®k, f, v, w, ‚ü®hk, fun i hi ‚Ü¶ hw i hi‚ü©, rfl‚ü©

/-- TODO: move-/
@[simp] lemma cons_app_9 {n : ‚Ñï} (a : Œ±) (s : Fin n.succ.succ.succ.succ.succ.succ.succ.succ.succ ‚Üí Œ±) : (a :> s) 9 = s 8 := rfl

@[simp] lemma cons_app_10 {n : ‚Ñï} (a : Œ±) (s : Fin n.succ.succ.succ.succ.succ.succ.succ.succ.succ.succ ‚Üí Œ±) : (a :> s) 10 = s 9 := rfl

@[simp] lemma cons_app_11 {n : ‚Ñï} (a : Œ±) (s : Fin n.succ.succ.succ.succ.succ.succ.succ.succ.succ.succ.succ ‚Üí Œ±) : (a :> s) 11 = s 10 := rfl

def construction : Fixpoint.Construction V (Œ≤.blueprint L) where
  Œ¶ := c.Phi L
  defined :=
  ‚ü®by intro v
      /-
      simp? [HierarchySymbol.Semiformula.val_sigma, Blueprint.blueprint,
        eval_isUTermDef L, IsUTerm.defined.proper.iff',
        c.bvar_defined.iff, c.bvar_defined.graph_delta.proper.iff',
        c.fvar_defined.iff, c.fvar_defined.graph_delta.proper.iff',
        c.func_defined.iff, c.func_defined.graph_delta.proper.iff']
      -/
      simp only [Nat.succ_eq_add_one, Blueprint.blueprint, Nat.reduceAdd, HierarchySymbol.Semiformula.val_sigma,
        HierarchySymbol.Semiformula.sigma_mkDelta,
        HierarchySymbol.Semiformula.val_mkSigma, Semiformula.eval_bexLTSucc', Semiterm.val_bvar,
        Matrix.cons_val_one, Matrix.vecHead, LogicalConnective.HomClass.map_and,
        Semiformula.eval_substs, Matrix.comp_vecCons', Matrix.cons_val_two, Matrix.vecTail,
        Function.comp_apply, Matrix.cons_val_succ, Matrix.cons_val_zero, Matrix.cons_val_fin_one,
        Matrix.constant_eq_singleton, pair_defined_iff, Fin.isValue, Fin.succ_zero_eq_one,
        Matrix.cons_val_four, IsUTerm.eval, LogicalConnective.HomClass.map_or,
        Semiformula.eval_bexLT, eval_qqBvarDef, Matrix.cons_app_five, c.bvar_defined.iff,
        LogicalConnective.Prop.and_eq, eval_qqFvarDef, c.fvar_defined.iff, Matrix.cons_val_three,
        Semiformula.eval_ex, Matrix.cons_app_seven, Matrix.cons_app_six, eval_repeatVec,
        eval_lenDef, Semiformula.eval_ballLT, eval_nthDef, Semiformula.eval_operator_three, cons_app_11,
        cons_app_10, cons_app_9, Matrix.cons_app_eight, eval_memRel, exists_eq_left, eval_qqFuncDef,
        Fin.succ_one_eq_two, c.func_defined.iff, LogicalConnective.Prop.or_eq,
        HierarchySymbol.Semiformula.pi_mkDelta, HierarchySymbol.Semiformula.val_mkPi, IsUTerm.defined.proper.iff',
        c.bvar_defined.graph_delta.proper.iff', HierarchySymbol.Semiformula.graphDelta_val,
        c.fvar_defined.graph_delta.proper.iff', Semiformula.eval_all,
        LogicalConnective.HomClass.map_imply, Semiformula.eval_operator_two, Structure.Eq.eq,
        LogicalConnective.Prop.arrow_eq, forall_eq, c.func_defined.graph_delta.proper.iff']
      ,
    by  intro v
        /-
        simpa? [HierarchySymbol.Semiformula.val_sigma, Blueprint.blueprint, eval_isUTermDef L,
          c.bvar_defined.iff, c.fvar_defined.iff, c.func_defined.iff]
        using c.phi_iff _ _ _
        -/
        simpa only [Nat.succ_eq_add_one, Blueprint.blueprint,
          Nat.reduceAdd, HierarchySymbol.Semiformula.val_sigma,
          HierarchySymbol.Semiformula.val_mkDelta, HierarchySymbol.Semiformula.val_mkSigma,
          Semiformula.eval_bexLTSucc', Semiterm.val_bvar, Matrix.cons_val_one, Matrix.vecHead,
          LogicalConnective.HomClass.map_and, Semiformula.eval_substs, Matrix.comp_vecCons',
          Matrix.cons_val_two, Matrix.vecTail, Function.comp_apply, Matrix.cons_val_succ,
          Matrix.cons_val_zero, Matrix.cons_val_fin_one, Matrix.constant_eq_singleton,
          pair_defined_iff, Fin.isValue, Fin.succ_zero_eq_one, IsUTerm.eval,
          LogicalConnective.HomClass.map_or, Semiformula.eval_bexLT, eval_qqBvarDef,
          c.bvar_defined.iff, LogicalConnective.Prop.and_eq, eval_qqFvarDef, c.fvar_defined.iff,
          Matrix.cons_val_three, Semiformula.eval_ex, Matrix.cons_app_seven, Matrix.cons_app_six,
          Matrix.cons_app_five, Matrix.cons_val_four, eval_repeatVec, eval_lenDef,
          Semiformula.eval_ballLT, eval_nthDef, Semiformula.eval_operator_three, cons_app_11,
          cons_app_10, cons_app_9, Matrix.cons_app_eight, eval_memRel, exists_eq_left,
          eval_qqFuncDef, Fin.succ_one_eq_two, c.func_defined.iff,
          LogicalConnective.Prop.or_eq] using c.phi_iff L _ _ _‚ü©
  monotone := by
    unfold Phi
    rintro C C' hC v pr ‚ü®ht, H‚ü©
    refine ‚ü®ht, ?_‚ü©
    rcases H with (‚ü®z, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, w, ‚ü®hk, hw‚ü©, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, rfl‚ü©
    ¬∑ right; left; exact ‚ü®x, rfl‚ü©
    ¬∑ right; right; exact ‚ü®k, f, v, w, ‚ü®hk, fun i hi ‚Ü¶ hC (hw i hi)‚ü©, rfl‚ü©

instance : (c.construction L).Finite where
  finite {C param pr h} := by
    rcases h with ‚ü®hp, (h | h | ‚ü®k, f, v, w, ‚ü®hk, hw‚ü©, rfl‚ü©)‚ü©
    ¬∑ exact ‚ü®0, hp, Or.inl h‚ü©
    ¬∑ exact ‚ü®0, hp, Or.inr <| Or.inl h‚ü©
    ¬∑ exact ‚ü®‚ü™v, w‚ü´ + 1, hp, Or.inr <| Or.inr
        ‚ü®k, f, v, w,
          ‚ü®hk, fun i hi ‚Ü¶ ‚ü®hw i hi, lt_succ_iff_le.mpr <| pair_le_pair (by simp) (by simp)‚ü©‚ü©, rfl‚ü©‚ü©

def Graph (param : Fin arity ‚Üí V) (x y : V) : Prop := (c.construction L).Fixpoint param ‚ü™x, y‚ü´

variable {param : Fin arity ‚Üí V} {n : V}

variable {L c}

lemma Graph.case_iff {t y : V} :
    c.Graph L param t y ‚Üî
    IsUTerm L t ‚àß
    ( (‚àÉ z, t = ^#z ‚àß y = c.bvar param z) ‚à®
      (‚àÉ x, t = ^&x ‚àß y = c.fvar param x) ‚à®
      (‚àÉ k f v w, (k = len w ‚àß ‚àÄ i < k, c.Graph L param v.[i] w.[i]) ‚àß
      t = ^func k f v ‚àß y = c.func param k f v w) ) :=
  Iff.trans (c.construction L).case (by apply and_congr (by simp); simp; rfl)

variable (c)

lemma graph_defined : ùö∫‚ÇÅ.Defined (fun v ‚Ü¶ c.Graph L (v ¬∑.succ.succ) (v 0) (v 1)) (Œ≤.graph L) := by
  intro v; simp [Blueprint.graph, (c.construction L).fixpoint_defined.iff, Graph]

@[simp] lemma eval_graphDef (v) :
    Semiformula.Evalbm V v (Œ≤.graph L).val ‚Üî c.Graph L (v ¬∑.succ.succ) (v 0) (v 1) := (graph_defined c).df.iff v

instance graph_definable : ùö∫‚ÇÅ.Boldface (fun v ‚Ü¶ c.Graph L (v ¬∑.succ.succ) (v 0) (v 1)) :=
  (graph_defined c).to_definable

instance graph_definable‚ÇÇ (param) : ùö∫-[0 + 1]-Relation (c.Graph L param) := by
  simpa using HierarchySymbol.Boldface.retractiont (n := 2) (graph_definable c) (#0 :> #1 :> fun i ‚Ü¶ &(param i))

lemma graph_dom_isUTerm {t y} :
    c.Graph L param t y ‚Üí IsUTerm L t := fun h ‚Ü¶ Graph.case_iff.mp h |>.1

lemma graph_bvar_iff {z} :
    c.Graph L param ^#z y ‚Üî y = c.bvar param z := by
  constructor
  ¬∑ intro H
    rcases Graph.case_iff.mp H with ‚ü®_, (‚ü®_, h, rfl‚ü© | ‚ü®_, h, _‚ü© | ‚ü®_, _, _, _, _, h, _‚ü©)‚ü©
    ¬∑ rcases (by simpa using h); rfl
    ¬∑ simp [qqBvar, qqFvar] at h
    ¬∑ simp [qqBvar, qqFunc] at h
  ¬∑ rintro rfl; exact Graph.case_iff.mpr ‚ü®by simp, Or.inl ‚ü®z, by simp‚ü©‚ü©

lemma graph_fvar_iff (x) :
    c.Graph L param ^&x y ‚Üî y = c.fvar param x := by
  constructor
  ¬∑ intro H
    rcases Graph.case_iff.mp H with ‚ü®_, (‚ü®_, h, _‚ü© | ‚ü®_, h, rfl‚ü© | ‚ü®_, _, _, _, _, h, _‚ü©)‚ü©
    ¬∑ simp [qqFvar, qqBvar] at h
    ¬∑ rcases (by simpa using h); rfl
    ¬∑ simp [qqFvar, qqFunc] at h
  ¬∑ rintro rfl; exact Graph.case_iff.mpr ‚ü®by simp, Or.inr <| Or.inl ‚ü®x, by simp‚ü©‚ü©

lemma graph_func {k f v w} (hkr : L.IsFunc k f) (hv : IsUTermVec L k v)
    (hkw : k = len w) (hw : ‚àÄ i < k, c.Graph L param v.[i] w.[i]) :
    c.Graph L param (^func k f v) (c.func param k f v w) := by
  exact Graph.case_iff.mpr ‚ü®by simp [hkr, hv], Or.inr <| Or.inr ‚ü®k, f, v, w, ‚ü®hkw, hw‚ü©, by simp‚ü©‚ü©

lemma graph_func_inv {k f v y} :
    c.Graph L param (^func k f v) y ‚Üí ‚àÉ w,
      (k = len w ‚àß ‚àÄ i < k, c.Graph L param v.[i] w.[i]) ‚àß
      y = c.func param k f v w := by
  intro H
  rcases Graph.case_iff.mp H with ‚ü®_, (‚ü®_, h, _‚ü© | ‚ü®_, h, rfl‚ü© | ‚ü®k', f', v', w, hw, h, rfl‚ü©)‚ü©
  ¬∑ simp [qqFunc, qqBvar] at h
  ¬∑ simp [qqFunc, qqFvar] at h
  ¬∑ rcases show k = k' ‚àß f = f' ‚àß v = v' by simpa [qqFunc, qqFunc] using h with ‚ü®rfl, rfl, rfl‚ü©
    exact ‚ü®w, hw, by rfl‚ü©

variable {c} (param n)

lemma graph_exists {t : V} : IsUTerm L t ‚Üí ‚àÉ y, c.Graph L param t y := by
  apply IsUTerm.induction ùö∫ (P := fun t ‚Ü¶ ‚àÉ y, c.Graph L param t y) (by definability)
  case hbvar =>
    intro z; exact ‚ü®c.bvar param z, by simp [c.graph_bvar_iff]‚ü©
  case hfvar =>
    intro x; exact ‚ü®c.fvar param x, by simp [c.graph_fvar_iff]‚ü©
  case hfunc =>
    intro k f v hkf hv ih
    have : ‚àÉ w, len w = k ‚àß ‚àÄ i < k, c.Graph L param v.[i] w.[i] := sigmaOne_skolem_vec (by definability) ih
    rcases this with ‚ü®w, hwk, hvw‚ü©
    exact ‚ü®c.func param k f v w, c.graph_func hkf hv (Eq.symm hwk) hvw‚ü©

lemma graph_unique {t y‚ÇÅ y‚ÇÇ : V} : c.Graph L param t y‚ÇÅ ‚Üí c.Graph L param t y‚ÇÇ ‚Üí y‚ÇÅ = y‚ÇÇ := by
  revert y‚ÇÅ y‚ÇÇ
  suffices IsUTerm L t ‚Üí ‚àÄ y‚ÇÅ y‚ÇÇ, c.Graph L param t y‚ÇÅ ‚Üí c.Graph L param t y‚ÇÇ ‚Üí y‚ÇÅ = y‚ÇÇ
  by intro u‚ÇÅ u‚ÇÇ h‚ÇÅ h‚ÇÇ; exact this (c.graph_dom_isUTerm h‚ÇÅ) u‚ÇÅ u‚ÇÇ h‚ÇÅ h‚ÇÇ
  intro ht
  apply IsUTerm.induction ùö∑ ?_ ?_ ?_ ?_ t ht
  ¬∑ definability
  ¬∑ intro z; simp [c.graph_bvar_iff]
  ¬∑ intro x; simp [c.graph_fvar_iff]
  ¬∑ intro k f v _ _ ih y‚ÇÅ y‚ÇÇ h‚ÇÅ h‚ÇÇ
    rcases c.graph_func_inv h‚ÇÅ with ‚ü®w‚ÇÅ, ‚ü®hk‚ÇÅ, hv‚ÇÅ‚ü©, rfl‚ü©
    rcases c.graph_func_inv h‚ÇÇ with ‚ü®w‚ÇÇ, ‚ü®hk‚ÇÇ, hv‚ÇÇ‚ü©, rfl‚ü©
    have : w‚ÇÅ = w‚ÇÇ :=
      nth_ext (by simp [‚Üêhk‚ÇÅ, ‚Üêhk‚ÇÇ]) (fun i hi ‚Ü¶
        ih i (by simpa [hk‚ÇÅ] using hi) _ _
          (hv‚ÇÅ i (by simpa [hk‚ÇÅ] using hi)) (hv‚ÇÇ i (by simpa [hk‚ÇÅ] using hi)))
    rw [this]

variable (L c)

lemma graph_existsUnique {t : V} (ht : IsUTerm L t) : ‚àÉ! y, c.Graph L param t y := by
  rcases graph_exists param ht with ‚ü®y, hy‚ü©
  exact ExistsUnique.intro y hy (fun y' h' ‚Ü¶ graph_unique param h' hy)

lemma graph_existsUnique_total (t : V) : ‚àÉ! y,
    (IsUTerm L t ‚Üí c.Graph L param t y) ‚àß (¬¨IsUTerm L t ‚Üí y = 0) := by
  by_cases ht : IsUTerm L t <;> simp [ht, c.graph_existsUnique L param]

noncomputable def result (t : V) : V := Classical.choose! (c.graph_existsUnique_total L param t)

def result_prop {t : V} (ht : IsUTerm L t) : c.Graph L param t (c.result L param t) :=
  Classical.choose!_spec (c.graph_existsUnique_total L param t) |>.1 ht

def result_prop_not {t : V} (ht : ¬¨IsUTerm L t) : c.result L param t = 0 :=
  Classical.choose!_spec (c.graph_existsUnique_total L param t) |>.2 ht

variable {L c param}

lemma result_eq_of_graph {t y} (ht : IsUTerm L t) (h : c.Graph L param t y) :
    c.result L param t = y := Eq.symm <| Classical.choose_uniq (c.graph_existsUnique_total L param t) (by simp [h, ht])

@[simp] lemma result_bvar (z) : c.result L param (^#z) = c.bvar param z :=
  c.result_eq_of_graph (by simp) (by simp [c.graph_bvar_iff])

@[simp] lemma result_fvar (x) : c.result L param (^&x) = c.fvar param x :=
  c.result_eq_of_graph (by simp) (by simp [c.graph_fvar_iff])

lemma result_func {k f v w} (hkf : L.IsFunc k f) (hv : IsUTermVec L k v)
    (hkw : k = len w) (hw : ‚àÄ i < k, c.result L param v.[i] = w.[i]) :
    c.result L param (^func k f v) = c.func param k f v w :=
  c.result_eq_of_graph (by simp [hkf, hv]) (c.graph_func hkf hv hkw (fun i hi ‚Ü¶ by
    simpa [hw i hi] using c.result_prop L param (hv.nth hi)))

section vec

lemma graph_existsUnique_vec {k w : V} (hw : IsUTermVec L k w) :
    ‚àÉ! w' : V, k = len w' ‚àß ‚àÄ i < k, c.Graph L param w.[i] w'.[i] := by
  have : ‚àÄ i < k, ‚àÉ y, c.Graph L param w.[i] y := by
    intro i hi; exact ‚ü®c.result L param w.[i], c.result_prop L param (hw.nth hi)‚ü©
  rcases sigmaOne_skolem_vec (by definability) this
    with ‚ü®w', hw'k, hw'‚ü©
  refine ExistsUnique.intro w' ‚ü®hw'k.symm, hw'‚ü© ?_
  intro w'' ‚ü®hkw'', hw''‚ü©
  refine nth_ext (by simp [hw'k, ‚Üêhkw'']) (by
    intro i hi;
    exact c.graph_unique param (hw'' i (by simpa [hkw''] using hi)) (hw' i (by simpa [hkw''] using hi)))

variable (L c param)

lemma graph_existsUnique_vec_total (k w : V) : ‚àÉ! w',
    (IsUTermVec L k w ‚Üí k = len w' ‚àß ‚àÄ i < k, c.Graph L param w.[i] w'.[i]) ‚àß
    (¬¨IsUTermVec L k w ‚Üí w' = 0) := by
  by_cases h : IsUTermVec L k w <;> simp [h, c.graph_existsUnique_vec]

noncomputable def resultVec (k w : V) : V := Classical.choose! (c.graph_existsUnique_vec_total L param k w)

@[simp] lemma resultVec_lh {k w : V} (hw : IsUTermVec L k w) : len (c.resultVec L param k w) = k :=
  Eq.symm <| Classical.choose!_spec (c.graph_existsUnique_vec_total L param k w) |>.1 hw |>.1

lemma graph_of_mem_resultVec {k w : V} (hw : IsUTermVec L k w) {i : V} (hi : i < k) :
    c.Graph L param w.[i] (c.resultVec L param k w).[i] :=
  Classical.choose!_spec (c.graph_existsUnique_vec_total L param k w) |>.1 hw |>.2 i hi

lemma nth_resultVec {k w i : V} (hw : IsUTermVec L k w) (hi : i < k) :
    (c.resultVec L param k w).[i] = c.result L param w.[i] :=
  c.result_eq_of_graph (hw.nth hi) (c.graph_of_mem_resultVec L param hw hi) |>.symm

@[simp] def resultVec_of_not {k w : V} (hw : ¬¨IsUTermVec L k w) : c.resultVec L param k w = 0 :=
  Classical.choose!_spec (c.graph_existsUnique_vec_total L param k w) |>.2 hw

@[simp] lemma resultVec_nil :
    c.resultVec L param 0 0 = 0 := len_zero_iff_eq_nil.mp (by simp)

lemma resultVec_cons {k w t : V} (hw : IsUTermVec L k w) (ht : IsUTerm L t) :
    c.resultVec L param (k + 1) (t ‚à∑ w) = c.result L param t ‚à∑ c.resultVec L param k w :=
  nth_ext (by simp [hw, hw.cons ht]) (by
    intro i hi
    have hi : i < k + 1 := by simpa [hw.cons ht, resultVec_lh] using hi
    rw [c.nth_resultVec L param (hw.cons ht) hi]
    rcases zero_or_succ i with (rfl | ‚ü®i, rfl‚ü©)
    ¬∑ simp
    ¬∑ simp [c.nth_resultVec L param hw (by simpa using hi)])

end vec

variable (c)

@[simp] lemma result_func' {k f v} (hkf : L.IsFunc k f) (hv : IsUTermVec L k v) :
    c.result L param (^func k f v) = c.func param k f v (c.resultVec L param k v) :=
  c.result_func hkf hv (by simp [hv]) (fun i hi ‚Ü¶ c.nth_resultVec L param hv hi |>.symm)

section

lemma result_defined : ùö∫‚ÇÅ.DefinedFunction (fun v ‚Ü¶ c.result L (v ¬∑.succ) (v 0)) (Œ≤.result L) := by
  intro v
  simp [Blueprint.result, HierarchySymbol.Semiformula.val_sigma, IsUTerm.defined.proper.iff',
    c.eval_graphDef, result, Classical.choose!_eq_iff]

@[simp] lemma result_graphDef (v) :
    Semiformula.Evalbm V v (Œ≤.result L).val ‚Üî v 0 = c.result L (v ¬∑.succ.succ) (v 1) := (result_defined c).df.iff v

private lemma resultVec_graph {w' k w} :
    w' = c.resultVec L param k w ‚Üî
    ( (IsUTermVec L k w ‚Üí k = len w' ‚àß ‚àÄ i < k, c.Graph L param w.[i] w'.[i]) ‚àß
      (¬¨IsUTermVec L k w ‚Üí w' = 0) ) :=
  Classical.choose!_eq_iff (c.graph_existsUnique_vec_total L param k w)

lemma resultVec_defined : ùö∫‚ÇÅ.DefinedFunction (fun v ‚Ü¶ c.resultVec L (v ¬∑.succ.succ) (v 0) (v 1)) (Œ≤.resultVec L) := by
  intro v
  simpa [Blueprint.resultVec, HierarchySymbol.Semiformula.val_sigma, IsUTermVec.defined.proper.iff',
    c.eval_graphDef] using c.resultVec_graph

lemma eval_resultVec (v : Fin (arity + 3) ‚Üí V) :
    Semiformula.Evalbm V v (Œ≤.resultVec L).val ‚Üî
    v 0 = c.resultVec L (v ¬∑.succ.succ.succ) (v 1) (v 2) := c.resultVec_defined.df.iff v

end

end Construction

end Language.TermRec

namespace IsUTerm.BV

def blueprint : Language.TermRec.Blueprint 0 where
  bvar := .mkSigma ‚Äúy z. y = z + 1‚Äù
  fvar := .mkSigma ‚Äúy x. y = 0‚Äù
  func := .mkSigma ‚Äúy k f v v'. !listMaxDef y v'‚Äù

variable (L)

noncomputable def construction : Language.TermRec.Construction V blueprint where
  bvar (_ z)        := z + 1
  fvar (_ _)        := 0
  func (_ _ _ _ v') := listMax v'
  bvar_defined := by intro v; simp [blueprint]
  fvar_defined := by intro v; simp [blueprint]
  func_defined := by intro v; simp [blueprint]

end IsUTerm.BV

section bv

open IsUTerm.BV

variable (L)

noncomputable def termBV (t : V) : V := construction.result L ![] t

noncomputable def termBVVec (k v : V) : V := construction.resultVec L ![] k v

def termBVGraph : ùö∫‚ÇÅ.Semisentence 2 := blueprint.result L

def termBVVecGraph : ùö∫‚ÇÅ.Semisentence 3 := blueprint.resultVec L

variable {L}

@[simp] lemma termBV_bvar (z : V) :
    termBV L ^#z = z + 1 := by simp [termBV, construction]

@[simp] lemma termBV_fvar (x : V) :
    termBV L ^&x = 0 := by simp [termBV, construction]

@[simp] lemma termBV_func {k f v : V} (hkf : L.IsFunc k f) (hv : IsUTermVec L k v) :
    termBV L (^func k f v) = listMax (termBVVec L k v) := by
  simp [termBV, construction, hkf, hv]; rfl

@[simp] lemma len_termBVVec {v : V} (hv : IsUTermVec L k v) :
    len (termBVVec L k v) = k := construction.resultVec_lh L _ hv

@[simp] lemma nth_termBVVec {v : V} (hv : IsUTermVec L k v) {i} (hi : i < k) :
    (termBVVec L k v).[i] = termBV L v.[i] := construction.nth_resultVec L _ hv hi

@[simp] lemma termBVVec_nil : termBVVec L 0 0 = 0 := construction.resultVec_nil L _

lemma termBVVec_cons {k t ts : V} (ht : IsUTerm L t) (hts : IsUTermVec L k ts) :
    termBVVec L (k + 1) (t ‚à∑ ts) = termBV L t ‚à∑ termBVVec L k ts :=
  construction.resultVec_cons L ![] hts ht

section

lemma termBV.defined : ùö∫‚ÇÅ-Function‚ÇÅ (termBV (V := V) L) via (termBVGraph L) := construction.result_defined

instance termBV.definable : ùö∫‚ÇÅ-Function‚ÇÅ (termBV (V := V) L) := termBV.defined.to_definable

instance termBV.definable' : Œì-[k + 1]-Function‚ÇÅ (termBV (V := V) L) := termBV.definable.of_sigmaOne

lemma termBVVec.defined : ùö∫‚ÇÅ-Function‚ÇÇ (termBVVec (V := V) L) via (termBVVecGraph L) :=
  construction.resultVec_defined

instance termBVVec.definable : ùö∫‚ÇÅ-Function‚ÇÇ (termBVVec (V := V) L) := termBVVec.defined.to_definable

instance termBVVec.definable' : Œì-[i + 1]-Function‚ÇÇ (termBVVec (V := V) L) := termBVVec.definable.of_sigmaOne

end

end bv

section isSemiterm

variable (L)

def IsSemiterm (n t : V) : Prop := IsUTerm L t ‚àß termBV L t ‚â§ n

def IsSemitermVec (k n v : V) : Prop := IsUTermVec L k v ‚àß ‚àÄ i < k, termBV L v.[i] ‚â§ n

def isSemiterm : ùö´‚ÇÅ.Semisentence 2 := .mkDelta
  (.mkSigma ‚Äún p. !(isUTerm L).sigma p ‚àß ‚àÉ b, !(termBVGraph L) b p ‚àß b ‚â§ n‚Äù)
  (.mkPi ‚Äún p. !(isUTerm L).pi p ‚àß ‚àÄ b, !(termBVGraph L) b p ‚Üí b ‚â§ n‚Äù)

def isSemitermVec : ùö´‚ÇÅ.Semisentence 3 := .mkDelta
  (.mkSigma ‚Äúk n ps. !(isUTermVec L).sigma k ps ‚àß ‚àÄ i < k, ‚àÉ p, !nthDef p ps i ‚àß ‚àÉ b, !(termBVGraph L) b p ‚àß b ‚â§ n‚Äù)
  (.mkPi ‚Äúk n ps. !(isUTermVec L).pi k ps ‚àß ‚àÄ i < k, ‚àÄ p, !nthDef p ps i ‚Üí ‚àÄ b, !(termBVGraph L) b p ‚Üí b ‚â§ n‚Äù)

abbrev IsTerm (t : V) : Prop := IsSemiterm L 0 t

abbrev IsTermVec (k v : V) : Prop := IsSemitermVec L k 0 v

variable {L}

lemma IsSemiterm.isUTerm {n t : V} (h : IsSemiterm L n t) : IsUTerm L t := h.1

lemma IsSemiterm.bv {n t : V} (h : IsSemiterm L n t) : termBV L t ‚â§ n := h.2

@[simp] lemma IsSemitermVec.isUTerm {k n v : V} (h : IsSemitermVec L k n v) : IsUTermVec L k v := h.1

lemma IsSemitermVec.bv {k n v : V} (h : IsSemitermVec L k n v) {i} (hi : i < k) : termBV L v.[i] ‚â§ n := h.2 i hi

lemma IsSemitermVec.lh {k n v : V} (h : IsSemitermVec L k n v) : len v = k := h.isUTerm.lh.symm

lemma IsSemitermVec.nth {k n v : V} (h : IsSemitermVec L k n v) {i} (hi : i < k) :
    IsSemiterm L n v.[i] := ‚ü®h.isUTerm.nth hi, h.bv hi‚ü©

lemma IsUTerm.isSemiterm {t : V} (h : IsUTerm L t) : IsSemiterm L (termBV L t) t := ‚ü®h, by simp‚ü©

lemma IsUTermVec.isSemitermVec {k v : V} (h : IsUTermVec L k v) : IsSemitermVec L k (listMax (termBVVec L k v)) v :=
  ‚ü®h, fun i hi ‚Ü¶ le_trans (by rw [nth_termBVVec h hi]) (nth_le_listMax (i := i) (by simp [h, hi]))‚ü©

lemma IsSemitermVec.iff {k n v : V} : IsSemitermVec L k n v ‚Üî (len v = k ‚àß ‚àÄ i < k, IsSemiterm L n v.[i]) := by
  constructor
  ¬∑ intro h; exact ‚ü®h.lh, fun i hi ‚Ü¶ h.nth hi‚ü©
  ¬∑ intro ‚ü®hk, h‚ü©; exact ‚ü®‚ü®hk.symm, fun i hi ‚Ü¶ h i hi |>.isUTerm‚ü©, fun i hi ‚Ü¶ h i hi |>.bv‚ü©

@[simp] lemma IsSemiterm.bvar {n z : V} :
    IsSemiterm L n ^#z ‚Üî z < n := by simp [IsSemiterm, succ_le_iff_lt]

@[simp] lemma IsSemiterm.fvar (n x : V) :
    IsSemiterm L n ^&x := by simp [IsSemiterm]

@[simp] lemma IsSemiterm.func {n k f v : V} :
    IsSemiterm L n (^func k f v) ‚Üî L.IsFunc k f ‚àß IsSemitermVec L k n v := by
  simp only [IsSemiterm, IsUTerm.func_iff]
  constructor
  ¬∑ rintro ‚ü®‚ü®hf, hbv‚ü©, hv‚ü©
    exact ‚ü®hf, hbv, by
      intro i hi
      have : listMax (termBVVec L k v) ‚â§ n := by simpa [termBV_func, hf, hbv] using hv
      simpa [hbv, hi] using listMaxss_le_iff.mp this i (by simp [hbv, hi])‚ü©
  ¬∑ rintro ‚ü®hf, h‚ü©
    exact ‚ü®‚ü®hf, h.isUTerm‚ü©, by
      simp only [hf, h.isUTerm, termBV_func]
      apply listMaxss_le_iff.mpr
      intro i hi
      have hi : i < k := by simpa [hf, h.isUTerm] using hi
      simpa [hf, h.isUTerm, hi] using (h.nth hi).bv‚ü©

@[simp] lemma IsSemitermVec.empty (n : V) : IsSemitermVec L 0 n 0 := ‚ü®by simp, by simp‚ü©

@[simp] lemma IsSemitermVec.empty_iff {n : V} : IsSemitermVec L 0 n v ‚Üî v = 0 := by
  constructor
  ¬∑ intro h; exact len_zero_iff_eq_nil.mp h.lh
  ¬∑ rintro rfl; simp

@[simp] lemma IsSemitermVec.cons_iff {n w t : V} :
    IsSemitermVec L (k + 1) n (t ‚à∑ w) ‚Üî IsSemiterm L n t ‚àß IsSemitermVec L k n w := by
  constructor
  ¬∑ intro h
    exact ‚ü®by simpa using h.nth (i := 0) (by simp),
      IsSemitermVec.iff.mpr ‚ü®by simpa using h.lh, fun i hi ‚Ü¶ by simpa using h.nth (show i + 1 < k + 1 by simp [hi])‚ü©‚ü©
  ¬∑ rintro ‚ü®ht, hw‚ü©
    exact ‚ü®hw.isUTerm.cons ht.isUTerm, by
    intro i hi
    rcases zero_or_succ i with (rfl | ‚ü®i, rfl‚ü©)
    ¬∑ simp [ht.bv]
    ¬∑ simpa using hw.nth (by simpa using hi) |>.bv‚ü©

lemma SemitermVec.cons {n m w t : V} (h : IsSemitermVec L n m w) (ht : IsSemiterm L m t) : IsSemitermVec L (n + 1) m (t ‚à∑ w) :=
  IsSemitermVec.cons_iff.mpr ‚ü®ht, h‚ü©

@[simp] lemma IsSemitermVec.singleton {n t : V} :
    IsSemitermVec L 1 n ?[t] ‚Üî IsSemiterm L n t := by
  rw [show (1 : V) = 0 + 1 by simp, IsSemitermVec.cons_iff]; simp

@[simp] lemma IsSemitermVec.doubleton {n t‚ÇÅ t‚ÇÇ : V} :
    IsSemitermVec L 2 n ?[t‚ÇÅ, t‚ÇÇ] ‚Üî IsSemiterm L n t‚ÇÅ ‚àß IsSemiterm L n t‚ÇÇ := by
  rw [show (2 : V) = 1 + 1 by simp [one_add_one_eq_two], IsSemitermVec.cons_iff]; simp

section

lemma IsSemiterm.defined : ùö´‚ÇÅ-Relation (IsSemiterm (V := V) L) via (isSemiterm L) where
  left := by
    intro v
    simp [isSemiterm, HierarchySymbol.Semiformula.val_sigma,
      IsUTerm.defined.df.iff, IsUTerm.defined.proper.iff',
      termBV.defined.df.iff]
  right := by
    intro v
    simp [isSemiterm, HierarchySymbol.Semiformula.val_sigma,
      IsUTerm.defined.df.iff, termBV.defined.df.iff]; rfl

instance IsSemiterm.definable : ùö´‚ÇÅ-Relation (IsSemiterm (V := V) L) := IsSemiterm.defined.to_definable

instance IsSemiterm.definable' (Œì m) : Œì-[m + 1]-Relation (IsSemiterm (V := V) L) := IsSemiterm.definable.of_deltaOne

lemma IsSemitermVec.defined : ùö´‚ÇÅ-Relation‚ÇÉ (IsSemitermVec (V := V) L) via (isSemitermVec L) where
  left := by
    intro v
    simp [isSemitermVec, HierarchySymbol.Semiformula.val_sigma,
      IsUTermVec.defined.df.iff, IsUTermVec.defined.proper.iff',
      termBV.defined.df.iff]
  right := by
    intro v
    simp [isSemitermVec, HierarchySymbol.Semiformula.val_sigma,
      IsUTermVec.defined.df.iff, termBV.defined.df.iff]; rfl

instance IsSemitermVec.definable : ùö´‚ÇÅ-Relation‚ÇÉ (IsSemitermVec (V := V) L) := IsSemitermVec.defined.to_definable

instance IsSemitermVec.definable' (Œì m) : Œì-[m + 1]-Relation‚ÇÉ (IsSemitermVec (V := V) L) := IsSemitermVec.definable.of_deltaOne

end

lemma IsSemiterm.case_iff {n t : V} :
    IsSemiterm L n t ‚Üî
    (‚àÉ z < n, t = ^#z) ‚à®
    (‚àÉ x, t = ^&x) ‚à®
    (‚àÉ k f v : V, L.IsFunc k f ‚àß IsSemitermVec L k n v ‚àß t = ^func k f v) := by
  constructor
  ¬∑ intro h
    rcases h.isUTerm.case with (‚ü®z, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, hf, _, rfl‚ü©)
    ¬∑ left; exact ‚ü®z, by simpa [succ_le_iff_lt] using h.bv, rfl‚ü©
    ¬∑ right; left; exact ‚ü®x, rfl‚ü©
    ¬∑ right; right; exact ‚ü®k, f, v, hf, by simp_all, rfl‚ü©
  ¬∑ rintro (‚ü®z, hz, rfl‚ü© | ‚ü®x, rfl‚ü© | ‚ü®k, f, v, hf, hv, rfl‚ü©)
    ¬∑ simp [hz]
    ¬∑ simp
    ¬∑ simp [hf, hv]

alias ‚ü®IsSemiterm.case, IsSemiterm.mk‚ü© := IsSemiterm.case_iff

lemma IsSemiterm.induction (Œì) {P : V ‚Üí Prop} (hP : Œì-[1]-Predicate P)
    (hbvar : ‚àÄ z < n, P (^#z)) (hfvar : ‚àÄ x, P (^&x))
    (hfunc : ‚àÄ k f v, L.IsFunc k f ‚Üí IsSemitermVec L k n v ‚Üí (‚àÄ i < k, P v.[i]) ‚Üí P (^func k f v)) :
    ‚àÄ t, IsSemiterm L n t ‚Üí P t := by
  intro t ht
  suffices IsSemiterm L n t ‚Üí P t by exact this ht
  apply IsUTerm.induction Œì ?_ ?_ ?_ ?_ t ht.isUTerm
  ¬∑ definability
  ¬∑ intro z; simp only [bvar]; exact hbvar z
  ¬∑ intro x _; exact hfvar x
  ¬∑ intro k f v hf _ ih h
    have hv : IsSemitermVec L k n v := by simp_all
    exact hfunc k f v hf hv (fun i hi ‚Ü¶ ih i hi (hv.nth hi))

@[simp] lemma IsSemitermVec.nil (k : V): IsSemitermVec L 0 k 0 := ‚ü®by simp, by simp‚ü©

@[simp] lemma IsSemitermVec.cons {k n w t : V} (h : IsSemitermVec L n k w) (ht : IsSemiterm L k t) : IsSemitermVec L (n + 1) k (t ‚à∑ w) :=
  ‚ü®h.isUTerm.cons ht.isUTerm, by
    intro i hi
    rcases zero_or_succ i with (rfl | ‚ü®i, rfl‚ü©)
    ¬∑ simp [ht.bv]
    ¬∑ simp [h.bv (by simpa using hi)]‚ü©

lemma IsSemiterm.sigma1_induction {P : V ‚Üí Prop} (hP : ùö∫‚ÇÅ-Predicate P)
    (hbvar : ‚àÄ z < n, P (^#z)) (hfvar : ‚àÄ x, P (^&x))
    (hfunc : ‚àÄ k f v, L.IsFunc k f ‚Üí IsSemitermVec L k n v ‚Üí (‚àÄ i < k, P v.[i]) ‚Üí P (^func k f v)) :
    ‚àÄ t, IsSemiterm L n t ‚Üí P t := IsSemiterm.induction _ hP hbvar hfvar hfunc

end isSemiterm

end LO.ISigma1.Metamath
