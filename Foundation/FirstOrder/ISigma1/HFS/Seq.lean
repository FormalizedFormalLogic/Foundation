import Foundation.FirstOrder.ISigma1.HFS.Basic

/-!

# Sequence

-/

namespace LO.ISigma1

open FirstOrder Arithmetic PeanoMinus IOpen ISigma0

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğ—œğšºâ‚]

def Seq (s : V) : Prop := IsMapping s âˆ§ âˆƒ l, domain s = under l

def Seq.isMapping {s : V} (h : Seq s) : IsMapping s := h.1

private lemma seq_iff (s : V) : Seq s â†” IsMapping s âˆ§ âˆƒ l â‰¤ 2 * s, âˆƒ d â‰¤ 2 * s, d = domain s âˆ§ d = under l :=
  âŸ¨by rintro âŸ¨hs, l, hâŸ©
      exact âŸ¨hs, l, (by
      calc
        l â‰¤ domain s := by simp [h]
        _ â‰¤ 2 * s    := by simp), âŸ¨domain s , by simp,  rfl, hâŸ©âŸ©,
   by rintro âŸ¨hs, l, _, _, _, rfl, hâŸ©; exact âŸ¨hs, l, hâŸ©âŸ©

def _root_.LO.FirstOrder.Arithmetic.seqDef : ğšºâ‚€.Semisentence 1 := .mkSigma
  â€œs. !isMappingDef s âˆ§ âˆƒ l <âº 2 * s, âˆƒ d <âº 2 * s, !domainDef d s âˆ§ !underDef d lâ€

lemma seq_defined : ğšºâ‚€-Predicate (Seq : V â†’ Prop) via seqDef := by
  intro v; simp [seqDef, seq_iff]

@[simp] lemma seq_defined_iff (v) :
    Semiformula.Evalbm V v seqDef.val â†” Seq (v 0) := seq_defined.df.iff v

instance seq_definable : ğšºâ‚€-Predicate (Seq : V â†’ Prop) := seq_defined.to_definable

instance seq_definable' (â„Œ) : â„Œ-Predicate (Seq : V â†’ Prop) := seq_definable.of_zero

section

open Lean PrettyPrinter Delaborator

syntax ":Seq " first_order_term : first_order_formula

scoped macro_rules
  | `(â¤«formula($type)[$binders* | $fbinders* | :Seq $t:first_order_term]) =>
    `(â¤«formula($type)[$binders* | $fbinders* | !seqDef.val $t])

end

lemma lh_exists_uniq (s : V) : âˆƒ! l, (Seq s â†’ domain s = under l) âˆ§ (Â¬Seq s â†’ l = 0) := by
  by_cases h : Seq s
  Â· rcases h with âŸ¨h, l, hlâŸ©
    exact ExistsUnique.intro l
      (by simp [show Seq s from âŸ¨h, l, hlâŸ©, hl])
      (by simp [show Seq s from âŸ¨h, l, hlâŸ©, hl])
  Â· simp [h]

noncomputable def lh (s : V) : V := Classical.choose! (lh_exists_uniq s)

lemma lh_prop (s : V) : (Seq s â†’ domain s = under (lh s)) âˆ§ (Â¬Seq s â†’ lh s = 0) := Classical.choose!_spec (lh_exists_uniq s)

lemma lh_prop_of_not_seq {s : V} (h : Â¬Seq s) : lh s = 0 := (lh_prop s).2 h

lemma Seq.domain_eq {s : V} (h : Seq s) : domain s = under (lh s) := (lh_prop s).1 h

@[simp] lemma lh_bound (s : V) : lh s â‰¤ 2 * s := by
  by_cases hs : Seq s
  Â· calc
      lh s â‰¤ under (lh s) := le_under _
      _    â‰¤ 2 * s        := by simp [â†hs.domain_eq]
  Â· simp [lh_prop_of_not_seq hs]

private lemma lh_graph (l s : V) : l = lh s â†” (Seq s â†’ âˆƒ d â‰¤ 2 * s, d = domain s âˆ§ d = under l) âˆ§ (Â¬Seq s â†’ l = 0) :=
  âŸ¨by
    rintro rfl
    by_cases Hs : Seq s <;> simp [Hs, â†Seq.domain_eq, lh_prop_of_not_seq], by
    rintro âŸ¨h, hnâŸ©
    by_cases Hs : Seq s
    Â· rcases h Hs with âŸ¨_, _, rfl, hâŸ©; simpa [h] using Hs.domain_eq
    Â· simp [lh_prop_of_not_seq Hs, hn Hs]âŸ©

def _root_.LO.FirstOrder.Arithmetic.lhDef : ğšºâ‚€.Semisentence 2 := .mkSigma
  â€œl s. (!seqDef s â†’ âˆƒ d <âº 2 * s, !domainDef d s âˆ§ !underDef d l) âˆ§ (Â¬!seqDef s â†’ l = 0)â€

lemma lh_defined : ğšºâ‚€-Functionâ‚ (lh : V â†’ V) via lhDef := by
  intro v; simp [lhDef, -exists_eq_right_right, lh_graph]

@[simp] lemma lh_defined_iff (v) :
    Semiformula.Evalbm V v lhDef.val â†” v 0 = lh (v 1) := lh_defined.df.iff v

instance lh_definable : ğšºâ‚€-Functionâ‚ (lh : V â†’ V) := lh_defined.to_definable

instance lh_definable' (â„Œ) : â„Œ-Functionâ‚ (lh : V â†’ V) := lh_definable.of_zero

instance : Boundedâ‚ (lh : V â†’ V) := âŸ¨â€˜x. 2 * xâ€™, fun _ â†¦ by simpâŸ©

lemma Seq.exists {s : V} (h : Seq s) {x : V} (hx : x < lh s) : âˆƒ y, âŸªx, yâŸ« âˆˆ s := h.isMapping x (by simpa [h.domain_eq] using hx) |>.exists

lemma Seq.nth_exists_uniq {s : V} (h : Seq s) {x : V} (hx : x < lh s) : âˆƒ! y, âŸªx, yâŸ« âˆˆ s := h.isMapping x (by simpa [h.domain_eq] using hx)

noncomputable def Seq.nth {s : V} (h : Seq s) {x : V} (hx : x < lh s) : V := Classical.choose! (h.nth_exists_uniq hx)

@[simp] lemma Seq.nth_mem {s : V} (h : Seq s) {x : V} (hx : x < lh s) :
    âŸªx, h.nth hxâŸ« âˆˆ s := Classical.choose!_spec (h.nth_exists_uniq hx)

lemma Seq.nth_uniq {s : V} (h : Seq s) {x y : V} (hx : x < lh s) (hy : âŸªx, yâŸ« âˆˆ s) : y = h.nth hx :=
    (h.nth_exists_uniq hx).unique hy (by simp)

@[simp] lemma Seq.nth_lt {s : V} (h : Seq s) {x} (hx : x < lh s) : h.nth hx < s := lt_of_mem_rng (h.nth_mem hx)

lemma Seq.lh_eq_of {s : V} (H : Seq s) {l} (h : domain s = under l) : lh s = l := by
  simpa [H.domain_eq] using h

lemma Seq.lt_lh_iff {s : V} (h : Seq s) {i} : i < lh s â†” i âˆˆ domain s := by simp [h.domain_eq]

lemma Seq.lt_lh_of_mem {s : V} (h : Seq s) {i x} (hix : âŸªi, xâŸ« âˆˆ s) : i < lh s := by
  simpa [h.lt_lh_iff, mem_domain_iff] using âŸ¨x, hixâŸ©

noncomputable def seqCons (s x : V) : V := insert âŸªlh s, xâŸ« s

section znth

def znth_existsUnique (s i : V) : âˆƒ! x, (Seq s âˆ§ i < lh s â†’ âŸªi, xâŸ« âˆˆ s) âˆ§ (Â¬(Seq s âˆ§ i < lh s) â†’ x = 0) := by
  by_cases h : Seq s âˆ§ i < lh s
  Â· simpa [h] using h.1.nth_exists_uniq h.2
  Â· simp [h]

noncomputable def znth (s i : V) : V := Classical.choose! (znth_existsUnique s i)

protected lemma Seq.znth {s i : V} (h : Seq s) (hi : i < lh s) : âŸªi, znth s iâŸ« âˆˆ s := Classical.choose!_spec (znth_existsUnique s i) |>.1 âŸ¨h, hiâŸ©

lemma Seq.znth_eq_of_mem {s i : V} (h : Seq s) (hi : âŸªi, xâŸ« âˆˆ s) : znth s i = x :=
  h.isMapping.uniq (h.znth (h.lt_lh_of_mem hi)) hi

lemma znth_prop_not {s i : V} (h : Â¬Seq s âˆ¨ lh s â‰¤ i) : znth s i = 0 :=
  Classical.choose!_spec (znth_existsUnique s i) |>.2 (by simpa [-not_and, not_and_or] using h)

def _root_.LO.FirstOrder.Arithmetic.znthDef : ğšºâ‚€.Semisentence 3 := .mkSigma
  â€œx s i. âˆƒ l <âº 2 * s, !lhDef l s âˆ§ (:Seq s âˆ§ i < l â†’ i âˆ¼[s] x) âˆ§ (Â¬(:Seq s âˆ§ i < l) â†’ x = 0)â€

private lemma znth_graph {x s i : V} : x = znth s i â†” âˆƒ l â‰¤ 2 * s, l = lh s âˆ§ (Seq s âˆ§ i < l â†’ âŸªi, xâŸ« âˆˆ s) âˆ§ (Â¬(Seq s âˆ§ i < l) â†’ x = 0) := by
  simp [znth, Classical.choose!_eq_iff_right]

lemma znth_defined : ğšºâ‚€-Functionâ‚‚ (znth : V â†’ V â†’ V) via znthDef := by
  intro v;
  simpa [znthDef, -not_and, not_and_or] using znth_graph (V := V)

@[simp] lemma eval_znthDef (v) :
    Semiformula.Evalbm V v znthDef.val â†” v 0 = znth (v 1) (v 2) := znth_defined.df.iff v

instance znth_definable : ğšºâ‚€-Functionâ‚‚ (znth : V â†’ V â†’ V) := znth_defined.to_definable

instance znth_definable' (â„Œ) : â„Œ-Functionâ‚‚ (znth : V â†’ V â†’ V) := znth_definable.of_zero

end znth

-- infixr:67 " ::Ë¢ " => seqCons

infixr:67 " â€' " => seqCons

@[simp] lemma seq_empty : Seq (âˆ… : V) := âŸ¨by simp, 0, by simpâŸ©

@[simp] lemma lh_empty : lh (âˆ… : V) = 0 := by
  have : under (lh âˆ… : V) = under 0 := by simpa using Eq.symm <| Seq.domain_eq (V := V) (s := âˆ…) (by simp)
  exact under_inj.mp this

lemma Seq.isempty_of_lh_eq_zero {s : V} (Hs : Seq s) (h : lh s = 0) : s = âˆ… := by simpa [h] using Hs.domain_eq

@[simp] lemma Seq.subset_seqCons (s x : V) : s âŠ† s â€' x := by simp [seqCons]

lemma Seq.lt_seqCons {s} (hs : Seq s) (x : V) : s < s â€' x :=
  lt_iff_le_and_ne.mpr <| âŸ¨le_of_subset <| by simp, by
    simp only [seqCons, ne_eq]; intro A
    have : âŸªlh s, xâŸ« âˆˆ s := by simpa [â†A] using mem_insert âŸªlh s, xâŸ« s
    simpa using hs.lt_lh_of_mem thisâŸ©

@[simp] lemma Seq.mem_seqCons (s x : V) : âŸªlh s, xâŸ« âˆˆ s â€' x := by simp [seqCons]

protected lemma Seq.seqCons {s : V} (h : Seq s) (x : V) : Seq (s â€' x) :=
  âŸ¨h.isMapping.insert (by simp [h.domain_eq]), lh s + 1, by simp [seqCons, h.domain_eq]âŸ©

@[simp] lemma Seq.lh_seqCons (x : V) {s} (h : Seq s) : lh (s â€' x) = lh s + 1 := by
  have : under (lh s + 1) = under (lh (s â€' x)) := by
    simpa [seqCons, h.domain_eq] using (h.seqCons x).domain_eq
  exact Eq.symm <| under_inj.mp this

lemma mem_seqCons_iff {i x z s : V} : âŸªi, xâŸ« âˆˆ s â€' z â†” (i = lh s âˆ§ x = z) âˆ¨ âŸªi, xâŸ« âˆˆ s := by simp [seqCons]

@[simp] lemma lh_mem_seqCons (s z : V) : âŸªlh s, zâŸ« âˆˆ s â€' z := by simp [seqCons]

@[simp] lemma lh_mem_seqCons_iff {s x z : V} (H : Seq s) : âŸªlh s, xâŸ« âˆˆ s â€' z â†” x = z := by
  suffices âŸªlh s, xâŸ« âˆˆ s â†’ x = z by simpa [seqCons]
  intro h; have := H.lt_lh_of_mem h; simp at this

lemma Seq.mem_seqCons_iff_of_lt {s x z : V} (hi : i < lh s) : âŸªi, xâŸ« âˆˆ s â€' z â†” âŸªi, xâŸ« âˆˆ s := by
  suffices i = lh s â†’ x = z â†’ âŸªi, xâŸ« âˆˆ s by simpa [seqCons, hi]
  rintro rfl; simp at hi

@[simp] lemma lh_not_mem {s} (Ss : Seq s) (x : V) : âŸªlh s, xâŸ« âˆ‰ s := fun h â†¦ by have := Ss.lt_lh_of_mem h; simp at this

section

lemma seqCons_graph (t x s : V) :
    t = s â€' x â†” âˆƒ l â‰¤ 2 * s, l = lh s âˆ§ âˆƒ p â‰¤ (2 * s + x + 1)^2, p = âŸªl, xâŸ« âˆ§ t = insert p s :=
  âŸ¨by rintro rfl
      exact âŸ¨lh s, by simp, rfl, âŸªlh s, xâŸ«,
        le_trans (pair_le_pair_left (by simp) x) (pair_polybound (2 * s) x), rfl, by rflâŸ©,
   by rintro âŸ¨l, _, rfl, p, _, rfl, rflâŸ©; rflâŸ©

def _root_.LO.FirstOrder.Arithmetic.seqConsDef : ğšºâ‚€.Semisentence 3 := .mkSigma
  â€œt s x. âˆƒ l <âº 2 * s, !lhDef l s âˆ§ âˆƒ p <âº (2 * s + x + 1)Â², !pairDef p l x âˆ§ !insertDef t p sâ€

lemma seqCons_defined : ğšºâ‚€-Functionâ‚‚ (seqCons : V â†’ V â†’ V) via seqConsDef := by
  intro v; simp [seqConsDef, seqCons_graph]

@[simp] lemma seqCons_defined_iff (v) :
    Semiformula.Evalbm V v seqConsDef.val â†” v 0 = v 1 â€' v 2 := seqCons_defined.df.iff v

instance seqCons_definable : ğšºâ‚€-Functionâ‚‚ (seqCons : V â†’ V â†’ V) := seqCons_defined.to_definable

instance seqCons_definable' (â„Œ) : â„Œ-Functionâ‚‚ (seqCons : V â†’ V â†’ V) := seqCons_definable.of_zero

@[simp] lemma natCast_empty : ((âˆ… : â„•) : V) = âˆ… := by simp [emptyset_def]

lemma seqCons_absolute (s a : â„•) : ((s â€' a : â„•) : V) = (s : V) â€' (a : V) := by
  simpa using DefinedFunction.shigmaZero_absolute_func V seqCons_defined seqCons_defined ![s, a]

end

lemma Seq.restr {s : V} (H : Seq s) {i : V} (hi : i â‰¤ lh s) : Seq (s â†¾ under i) :=
  âŸ¨H.isMapping.restr (under i), i, domain_restr_of_subset_domain (by simp [H.domain_eq, hi])âŸ©

lemma Seq.restr_lh {s : V} (H : Seq s) {i : V} (hi : i â‰¤ lh s) : lh (s â†¾ under i) = i :=
  (H.restr hi).lh_eq_of (domain_restr_of_subset_domain <| by simp [H.domain_eq, hi])

lemma domain_bitRemove_of_isMapping_of_mem {x y s : V} (hs : IsMapping s) (hxy : âŸªx, yâŸ« âˆˆ s) :
    domain (bitRemove âŸªx, yâŸ« s) = bitRemove x (domain s) := by
  suffices âˆ€ xâ‚, (âˆƒ yâ‚, (xâ‚ = x â†’ yâ‚ â‰  y) âˆ§ âŸªxâ‚, yâ‚âŸ« âˆˆ s) â†” xâ‚ â‰  x âˆ§ âˆƒ y, âŸªxâ‚, yâŸ« âˆˆ s by
    apply mem_ext; simpa [mem_domain_iff]
  intro xâ‚
  constructor
  Â· rintro âŸ¨yâ‚, hyâ‚, hxâ‚yâ‚âŸ©; exact âŸ¨by rintro rfl; exact hyâ‚ rfl (hs.uniq hxâ‚yâ‚ hxy), yâ‚, hxâ‚yâ‚âŸ©
  Â· intro âŸ¨hx, yâ‚, hxâ‚yâ‚âŸ©
    exact âŸ¨yâ‚, by intro _; contradiction, hxâ‚yâ‚âŸ©

lemma Seq.eq_of_eq_of_subset {sâ‚ sâ‚‚ : V} (Hâ‚ : Seq sâ‚) (Hâ‚‚ : Seq sâ‚‚)
    (hl : lh sâ‚ = lh sâ‚‚) (h : sâ‚ âŠ† sâ‚‚) : sâ‚ = sâ‚‚ := by
  apply mem_ext; intro u
  constructor
  Â· intro hu; exact h hu
  Â· intro hu
    have : Ï€â‚ u < lh sâ‚ := by
      simpa [hl] using Hâ‚‚.lt_lh_of_mem (show âŸªÏ€â‚ u, Ï€â‚‚ uâŸ« âˆˆ sâ‚‚ from by simpa using hu)
    have : âˆƒ y, âŸªÏ€â‚ u, yâŸ« âˆˆ sâ‚ := Hâ‚.exists this
    rcases this with âŸ¨y, hyâŸ©
    have : y = Ï€â‚‚ u := Hâ‚‚.isMapping.uniq (h hy) (show âŸªÏ€â‚ u, Ï€â‚‚ uâŸ« âˆˆ sâ‚‚ from by simpa using hu)
    rcases this with rfl
    simpa using hy

lemma subset_pair {s t : V} (h : âˆ€ i x, âŸªi, xâŸ« âˆˆ s â†’ âŸªi, xâŸ« âˆˆ t) : s âŠ† t := by
  intro u hu
  simpa using h (Ï€â‚ u) (Ï€â‚‚ u) (by simpa using hu)

lemma Seq.lh_ext {sâ‚ sâ‚‚ : V} (Hâ‚ : Seq sâ‚) (Hâ‚‚ : Seq sâ‚‚) (h : lh sâ‚ = lh sâ‚‚)
    (H : âˆ€ i xâ‚ xâ‚‚, âŸªi, xâ‚âŸ« âˆˆ sâ‚ â†’ âŸªi, xâ‚‚âŸ« âˆˆ sâ‚‚ â†’ xâ‚ = xâ‚‚) : sâ‚ = sâ‚‚ := Hâ‚.eq_of_eq_of_subset Hâ‚‚ h <| subset_pair <| by
      intro i x hx
      have hi : i < lh sâ‚‚ := by simpa [â† h] using Hâ‚.lt_lh_of_mem hx
      rcases H i _ _ hx (Hâ‚‚.nth_mem hi)
      simp

@[simp] lemma Seq.seqCons_ext {aâ‚ aâ‚‚ sâ‚ sâ‚‚ : V} (Hâ‚ : Seq sâ‚) (Hâ‚‚ : Seq sâ‚‚) :
    sâ‚ â€' aâ‚ = sâ‚‚ â€' aâ‚‚ â†” aâ‚ = aâ‚‚ âˆ§ sâ‚ = sâ‚‚ :=
  âŸ¨by intro h
      have hsâ‚sâ‚‚ : lh sâ‚ = lh sâ‚‚ := by simpa [Hâ‚, Hâ‚‚] using congr_arg lh h
      have hsâ‚ : âŸªlh sâ‚, aâ‚âŸ« âˆˆ sâ‚‚ â€' aâ‚‚ := by simpa [h] using lh_mem_seqCons sâ‚ aâ‚
      have hsâ‚‚ : âŸªlh sâ‚, aâ‚‚âŸ« âˆˆ sâ‚‚ â€' aâ‚‚ := by simp [hsâ‚sâ‚‚]
      have haâ‚aâ‚‚ : aâ‚ = aâ‚‚ := (Hâ‚‚.seqCons aâ‚‚).isMapping.uniq hsâ‚ hsâ‚‚
      have : sâ‚ âŠ† sâ‚‚ := subset_pair <| by
        intro i x hix
        have : i = lh sâ‚‚ âˆ§ x = aâ‚‚ âˆ¨ âŸªi, xâŸ« âˆˆ sâ‚‚ := by
          simpa [mem_seqCons_iff, h] using Seq.subset_seqCons sâ‚ aâ‚ hix
        rcases this with (âŸ¨rfl, rflâŸ© | hixâ‚‚)
        Â· have := Hâ‚.lt_lh_of_mem hix; simp [hsâ‚sâ‚‚] at this
        Â· assumption
      exact âŸ¨haâ‚aâ‚‚, Hâ‚.eq_of_eq_of_subset Hâ‚‚ hsâ‚sâ‚‚ thisâŸ©,
   by rintro âŸ¨rfl, rflâŸ©; rflâŸ©

/-- TODO: move to Lemmata.lean-/
lemma ne_zero_iff_one_le {a : V} : a â‰  0 â†” 1 â‰¤ a := Iff.trans pos_iff_ne_zero.symm (pos_iff_one_le (a := a))

lemma Seq.cases_iff {s : V} : Seq s â†” s = âˆ… âˆ¨ âˆƒ x s', Seq s' âˆ§ s = s' â€' x := âŸ¨fun h â†¦ by
  by_cases hs : lh s = 0
  Â· left
    simpa [hs] using h.domain_eq
  Â· right
    let i := lh s - 1
    have hi : i < lh s := pred_lt_self_of_pos (pos_iff_ne_zero.mpr hs)
    have lhs_eq : lh s = i + 1 := Eq.symm <| tsub_add_cancel_of_le <| ne_zero_iff_one_le.mp hs
    let s' := bitRemove âŸªi, h.nth hiâŸ« s
    have his : âŸªi, h.nth hiâŸ« âˆˆ s := h.nth_mem hi
    have hdoms' : domain s' = under i := by
      simp only [domain_bitRemove_of_isMapping_of_mem h.isMapping his, h.domain_eq, s']
      apply mem_ext
      simpa [lhs_eq, and_or_left] using fun j hj â†¦ ne_of_lt hj
    have hs' : Seq s' := âŸ¨ h.isMapping.of_subset (by simp [s']), i, hdoms' âŸ©
    have hs'i : lh s' = i := by simpa [hs'.domain_eq] using hdoms'
    exact âŸ¨h.nth hi, s', hs', mem_ext <| fun v â†¦ by
      simp only [seqCons, hs'i, mem_bitInsert_iff]
      simp [s']
      by_cases hv : v = âŸªi, h.nth hiâŸ« <;> simp [hv]âŸ©,
  by  rintro (rfl | âŸ¨x, s', hs', rflâŸ©)
      Â· simp
      Â· exact hs'.seqCons xâŸ©

alias âŸ¨Seq.cases, _âŸ© := Seq.cases_iff

@[elab_as_elim]
theorem seq_induction (Î“) {P : V â†’ Prop} (hP : Î“-[1]-Predicate P)
  (hnil : P âˆ…) (hcons : âˆ€ s x, Seq s â†’ P s â†’ P (s â€' x)) :
    âˆ€ {s : V}, Seq s â†’ P s := by
  intro s sseq
  induction s using ISigma1.order_induction
  Â· exact Î“
  Â· definability
  case ind s ih =>
    have : s = âˆ… âˆ¨ âˆƒ x s', Seq s' âˆ§ s = s' â€' x := sseq.cases
    rcases this with (rfl | âŸ¨x, s, hs, rflâŸ©)
    Â· exact hnil
    Â· exact hcons s x hs (ih s (hs.lt_seqCons x) hs)

/-- `!âŸ¦x, y, z, ...âŸ§` notation for `Seq` -/
syntax "!âŸ¦" term,* "âŸ§" : term

macro_rules
  | `(!âŸ¦$terms:term,*, $term:termâŸ§) => `(seqCons !âŸ¦$terms,*âŸ§ $term)
  | `(!âŸ¦$term:termâŸ§) => `(seqCons âˆ… $term)
  | `(!âŸ¦âŸ§) => `(âˆ…)

@[app_unexpander seqCons]
def vecConsUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ !âŸ¦$term2, $terms,*âŸ§ $term) => `(!âŸ¦$term2, $terms,*, $termâŸ§)
  | `($_ !âŸ¦$term2âŸ§ $term) => `(!âŸ¦$term2, $termâŸ§)
  | `($_ âˆ… $term) => `(!âŸ¦$termâŸ§)
  | _ => throw ()

@[simp] lemma singleton_seq (x : V) : Seq !âŸ¦xâŸ§ := by apply Seq.seqCons; simp

@[simp] lemma doubleton_seq (x y : V) : Seq !âŸ¦x, yâŸ§ := by apply Seq.seqCons; simp

@[simp] lemma mem_singleton_seq_iff (x y : V) : âŸª0, xâŸ« âˆˆ !âŸ¦yâŸ§ â†” x = y := by simp [mem_seqCons_iff]

section

def _root_.LO.FirstOrder.Arithmetic.mkSeqâ‚Def : ğšºâ‚€.Semisentence 2 := .mkSigma
  â€œs x. !seqConsDef s 0 xâ€

lemma mkSeqâ‚_defined : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ !âŸ¦xâŸ§) via mkSeqâ‚Def := by
  intro v; simp [mkSeqâ‚Def]; rfl

@[simp] lemma eval_mkSeqâ‚Def (v) :
    Semiformula.Evalbm V v mkSeqâ‚Def.val â†” v 0 = !âŸ¦v 1âŸ§ := mkSeqâ‚_defined.df.iff v

instance mkSeqâ‚_definable : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ !âŸ¦xâŸ§) := mkSeqâ‚_defined.to_definable

instance mkSeqâ‚_definable' (Î“) : Î“-Functionâ‚ (fun x : V â†¦ !âŸ¦xâŸ§) := mkSeqâ‚_definable.of_zero

def _root_.LO.FirstOrder.Arithmetic.mkSeqâ‚‚Def : ğšºâ‚.Semisentence 3 := .mkSigma
  â€œs x y. âˆƒ sx, !mkSeqâ‚Def sx x âˆ§ !seqConsDef s sx yâ€

lemma mkSeqâ‚‚_defined : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ !âŸ¦x, yâŸ§) via mkSeqâ‚‚Def := by
  intro v; simp [mkSeqâ‚‚Def]

@[simp] lemma eval_mkSeqâ‚‚Def (v) :
    Semiformula.Evalbm V v mkSeqâ‚‚Def.val â†” v 0 = !âŸ¦v 1, v 2âŸ§ := mkSeqâ‚‚_defined.df.iff v

instance mkSeqâ‚‚_definable : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ !âŸ¦x, yâŸ§) := mkSeqâ‚‚_defined.to_definable

instance mkSeqâ‚‚_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚‚ (fun x y : V â†¦ !âŸ¦x, yâŸ§) := mkSeqâ‚‚_definable.of_sigmaOne

end

theorem sigmaOne_skolem_seq {R : V â†’ V â†’ Prop} (hP : ğšºâ‚-Relation R) {l}
    (H : âˆ€ x < l, âˆƒ y, R x y) : âˆƒ s, Seq s âˆ§ lh s = l âˆ§ âˆ€ i x, âŸªi, xâŸ« âˆˆ s â†’ R i x := by
  rcases sigmaOne_skolem hP (show âˆ€ x âˆˆ under l, âˆƒ y, R x y by simpa using H) with âŸ¨s, ms, sdom, hâŸ©
  have : Seq s := âŸ¨ms, l, sdomâŸ©
  exact âŸ¨s, this, by simpa [this.domain_eq] using sdom, hâŸ©

theorem sigmaOne_skolem_seq! {R : V â†’ V â†’ Prop} (hP : ğšºâ‚-Relation R) {l}
    (H : âˆ€ x < l, âˆƒ! y, R x y) : âˆƒ! s, Seq s âˆ§ lh s = l âˆ§ âˆ€ i x, âŸªi, xâŸ« âˆˆ s â†’ R i x := by
  have : âˆ€ x < l, âˆƒ y, R x y := fun x hx â†¦ (H x hx).exists
  rcases sigmaOne_skolem_seq hP this with âŸ¨s, Ss, rfl, hsâŸ©
  exact ExistsUnique.intro s âŸ¨Ss, rfl, hsâŸ© (by
    rintro s' âŸ¨Ss', hss', hs'âŸ©
    exact Seq.lh_ext Ss' Ss hss' (fun i xâ‚ xâ‚‚ hâ‚ hâ‚‚ â†¦ H i (Ss.lt_lh_of_mem hâ‚‚) |>.unique (hs' i xâ‚ hâ‚) (hs i xâ‚‚ hâ‚‚)))

section seqToVec

noncomputable def vecToSeq : {n : â„•} â†’ (Fin n â†’ V) â†’ V
  | 0,     _ => âˆ…
  | n + 1, v => vecToSeq (v Â·.castSucc) â€' v (Fin.last n)

@[simp] lemma vecToSeq_nil : vecToSeq ![] = (âˆ… : V) := by simp [vecToSeq]

@[simp] lemma vecToSeq_vecCons {n} (v : Fin n â†’ V) (a : V) :
    vecToSeq (v <: a) = vecToSeq v â€' a := by simp [vecToSeq]

@[simp] lemma vecToSeq_seq {n} (v : Fin n â†’ V) : Seq (vecToSeq v) := by
  induction' n with n ih
  Â· simp [vecToSeq]
  Â· exact (ih _).seqCons _

@[simp] lemma lh_vecToSeq {n} (v : Fin n â†’ V) : lh (vecToSeq v) = n := by
  induction' n with n ih <;> simp [vecToSeq, *]

lemma mem_vectoSeq {n : â„•} (v : Fin n â†’ V) (i : Fin n) : âŸª(i : V), v iâŸ« âˆˆ vecToSeq v := by
  induction' n with n ih
  Â· exact i.elim0
  Â· cases' i using Fin.lastCases with i
    Â· simp [vecToSeq, mem_seqCons_iff]
    Â· simpa [vecToSeq, mem_seqCons_iff] using Or.inr <| ih (v Â·.castSucc) i

end seqToVec

end LO.ISigma1
