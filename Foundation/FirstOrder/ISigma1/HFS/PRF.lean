import Foundation.FirstOrder.ISigma1.HFS.Seq

/-!

# Primitive Recursive Functions in $\mathsf{I} \Sigma_1$

-/

namespace LO.ISigma1

open FirstOrder Arith PeanoMinus IOpen ISigma0

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

namespace PR

structure Blueprint (k : â„•) where
  zero : ğšºâ‚.Semisentence (k + 1)
  succ : ğšºâ‚.Semisentence (k + 3)

def Blueprint.cseqDef (p : Blueprint k) : ğšºâ‚.Semisentence (k + 1) := .mkSigma
  â€œs.
    :Seq s
    âˆ§ (âˆƒ z < s, !p.zero z â‹¯ âˆ§ 0 âˆ¼[s] z)
    âˆ§ (âˆ€ i < 2 * s,
        (âˆƒ l <âº 2 * s, !lhDef l s âˆ§ i + 1 < l) â†’
        âˆ€ z < s, i âˆ¼[s] z â†’ âˆƒ u < s, !p.succ u z i â‹¯ âˆ§ i + 1 âˆ¼[s] u)â€ (by simp)

def Blueprint.resultDef (p : Blueprint k) : ğšºâ‚.Semisentence (k + 2) := .mkSigma
  â€œz u. âˆƒ s, !p.cseqDef s â‹¯ âˆ§ u âˆ¼[s] zâ€ (by simp)

def Blueprint.resultDeltaDef (p : Blueprint k) : ğš«â‚.Semisentence (k + 2) := p.resultDef.graphDelta

variable (V)

structure Construction {k : â„•} (p : Blueprint k) where
  zero : (Fin k â†’ V) â†’ V
  succ : (Fin k â†’ V) â†’ V â†’ V â†’ V
  zero_defined : ğšºâ‚.DefinedFunction zero p.zero
  succ_defined : ğšºâ‚.DefinedFunction (fun v â†¦ succ (v Â·.succ.succ) (v 1) (v 0)) p.succ

variable {V}

namespace Construction

variable {k : â„•} {p : Blueprint k} (c : Construction V p) (v : Fin k â†’ V)

def CSeq (s : V) : Prop := Seq s âˆ§ âŸª0, c.zero vâŸ« âˆˆ s âˆ§ âˆ€ i < lh s - 1, âˆ€ z, âŸªi, zâŸ« âˆˆ s â†’ âŸªi + 1, c.succ v i zâŸ« âˆˆ s

private lemma cseq_iff (s : V) : c.CSeq v s â†”
    Seq s
    âˆ§ (âˆƒ z < s, z = c.zero v âˆ§ âŸª0, zâŸ« âˆˆ s)
    âˆ§ (âˆ€ i < 2 * s,
      (âˆƒ l â‰¤ 2 * s, l = lh s âˆ§ i + 1 < l) â†’ âˆ€ z < s, âŸªi, zâŸ« âˆˆ s â†’ âˆƒ u < s, u = c.succ v i z âˆ§ âŸªi + 1, uâŸ« âˆˆ s) :=
  âŸ¨by rintro âŸ¨Hs, hz, hsâŸ©
      exact âŸ¨Hs, âŸ¨c.zero v, lt_of_mem_rng hz, rfl, hzâŸ©, fun i _ hi z _ hiz â†¦
      âŸ¨c.succ v i z, by
        have := hs i (by rcases hi with âŸ¨l, _, rfl, hlâŸ©; simp [lt_tsub_iff_right, hl]) z hiz
        exact âŸ¨lt_of_mem_rng this, rfl, thisâŸ©âŸ©âŸ©,
   by rintro âŸ¨Hs, âŸ¨z, _, rfl, hzâŸ©, hâŸ©
      exact âŸ¨Hs, hz, fun i hi z hiz â†¦ by
        rcases h i
          (lt_of_lt_of_le hi (by simpa using le_trans (lh_bound _) (by simp)))
          âŸ¨lh s, by simp [lt_succ_iff_le], rfl, by simpa [lt_tsub_iff_right] using hiâŸ© z (lt_of_mem_rng hiz) hiz with âŸ¨_, _, rfl, hâŸ©
        exact hâŸ©âŸ©

lemma cseq_defined : ğšºâ‚.Defined (fun v â†¦ c.CSeq (v Â·.succ) (v 0) : (Fin (k + 1) â†’ V) â†’ Prop) p.cseqDef := by
  intro v; simp [Blueprint.cseqDef, cseq_iff, c.zero_defined.df.iff, c.succ_defined.df.iff]

@[simp] lemma cseq_defined_iff (v) :
    Semiformula.Evalbm V v p.cseqDef.val â†” c.CSeq (v Â·.succ) (v 0) := c.cseq_defined.df.iff v

variable {c v}

namespace CSeq

variable {s : V}

lemma seq (h : c.CSeq v s) : Seq s := h.1

lemma zero (h : c.CSeq v s) : âŸª0, c.zero vâŸ« âˆˆ s := h.2.1

lemma succ (h : c.CSeq v s) : âˆ€ i < lh s - 1, âˆ€ z, âŸªi, zâŸ« âˆˆ s â†’ âŸªi + 1, c.succ v i zâŸ« âˆˆ s := h.2.2

lemma unique {sâ‚ sâ‚‚ : V} (Hâ‚ : c.CSeq v sâ‚) (Hâ‚‚ : c.CSeq v sâ‚‚) (hâ‚â‚‚ : lh sâ‚ â‰¤ lh sâ‚‚) {i} (hi : i < lh sâ‚) {zâ‚ zâ‚‚} :
    âŸªi, zâ‚âŸ« âˆˆ sâ‚ â†’ âŸªi, zâ‚‚âŸ« âˆˆ sâ‚‚ â†’ zâ‚ = zâ‚‚ := by
  revert zâ‚ zâ‚‚
  suffices âˆ€ zâ‚ < sâ‚, âˆ€ zâ‚‚ < sâ‚‚, âŸªi, zâ‚âŸ« âˆˆ sâ‚ â†’ âŸªi, zâ‚‚âŸ« âˆˆ sâ‚‚ â†’ zâ‚ = zâ‚‚
  by intro zâ‚ zâ‚‚ hzâ‚ hzâ‚‚; exact this zâ‚ (lt_of_mem_rng hzâ‚) zâ‚‚ (lt_of_mem_rng hzâ‚‚) hzâ‚ hzâ‚‚
  intro zâ‚ hzâ‚ zâ‚‚ hzâ‚‚ hâ‚ hâ‚‚
  induction i using ISigma1.sigma1_succ_induction generalizing zâ‚ zâ‚‚
  Â· definability
  case zero =>
    have : zâ‚ = c.zero v := Hâ‚.seq.isMapping.uniq hâ‚ Hâ‚.zero
    have : zâ‚‚ = c.zero v := Hâ‚‚.seq.isMapping.uniq hâ‚‚ Hâ‚‚.zero
    simp_all
  case succ i ih =>
    have hi' : i < lh sâ‚ := lt_of_le_of_lt (by simp) hi
    let z' := Hâ‚.seq.nth hi'
    have ihâ‚ : âŸªi, z'âŸ« âˆˆ sâ‚ := Hâ‚.seq.nth_mem hi'
    have ihâ‚‚ : âŸªi, z'âŸ« âˆˆ sâ‚‚ := by
      have : z' = Hâ‚‚.seq.nth (lt_of_lt_of_le hi' hâ‚â‚‚) :=
        ih hi' z' (by simp [z']) (Hâ‚‚.seq.nth (lt_of_lt_of_le hi' hâ‚â‚‚)) (by simp [z']) (by simp [z']) (by simp)
      simp [this]
    have hâ‚' : âŸªi + 1, c.succ v i z'âŸ« âˆˆ sâ‚ := Hâ‚.succ i (by simp [lt_tsub_iff_right, hi]) z' ihâ‚
    have hâ‚‚' : âŸªi + 1, c.succ v i z'âŸ« âˆˆ sâ‚‚ :=
      Hâ‚‚.succ i (by simpa [lt_tsub_iff_right] using lt_of_lt_of_le hi hâ‚â‚‚) z' ihâ‚‚
    have eâ‚ : zâ‚ = c.succ v i z' := Hâ‚.seq.isMapping.uniq hâ‚ hâ‚'
    have eâ‚‚ : zâ‚‚ = c.succ v i z' := Hâ‚‚.seq.isMapping.uniq hâ‚‚ hâ‚‚'
    simp [eâ‚, eâ‚‚]

end CSeq

lemma CSeq.initial : c.CSeq v !âŸ¦c.zero vâŸ§ :=
  âŸ¨by simp, by simp [seqCons], by simpâŸ©

lemma CSeq.successor {s l z : V} (Hs : c.CSeq v s) (hl : l + 1 = lh s) (hz : âŸªl, zâŸ« âˆˆ s) :
    c.CSeq v (s â€' c.succ v l z) :=
  âŸ¨ Hs.seq.seqCons _, by simp [seqCons, Hs.zero], by
    simp only [Hs.seq.lh_seqCons, add_tsub_cancel_right]
    intro i hi w hiw
    have hiws : âŸªi, wâŸ« âˆˆ s := by
      rcases show i = lh s âˆ§ w = c.succ v l z âˆ¨ âŸªi, wâŸ« âˆˆ s by
        simpa [mem_seqCons_iff] using hiw with (âŸ¨rfl, rflâŸ© | h)
      Â· simp at hi
      Â· assumption
    have : i â‰¤ l := by simpa [â†hl, lt_succ_iff_le] using hi
    rcases this with (rfl | hil)
    Â· have : w = z := Hs.seq.isMapping.uniq hiws hz
      simp [this, hl]
    Â· simp only [mem_seqCons_iff]; right
      exact Hs.succ i (by simp [â†hl, hil]) w hiws âŸ©

variable (c v)

open Classical in
lemma CSeq.exists (l : V) : âˆƒ s, c.CSeq v s âˆ§ l + 1 = lh s := by
  induction l using ISigma1.sigma1_succ_induction
  Â· apply HierarchySymbol.Boldface.ex
    apply HierarchySymbol.Boldface.and
    Â· exact âŸ¨p.cseqDef.rew (Rew.embSubsts <| #0 :> fun i â†¦ &(v i)), by
        intro w; simpa [Matrix.comp_vecCons'] using c.cseq_defined_iff (w 0 :> v) |>.symmâŸ©
    Â· definability
  case zero =>
    exact âŸ¨!âŸ¦c.zero vâŸ§, CSeq.initial, by simpâŸ©
  case succ l ih =>
    rcases ih with âŸ¨s, Hs, hlsâŸ©
    have hl : l < lh s := by simp [â†hls]
    have : âˆƒ z, âŸªl, zâŸ« âˆˆ s := Hs.seq.exists hl
    rcases this with âŸ¨z, hzâŸ©
    exact âŸ¨s â€' c.succ v l z, Hs.successor hls hz, by simp [Hs.seq, hls]âŸ©

lemma cSeq_result_existsUnique (l : V) : âˆƒ! z, âˆƒ s, c.CSeq v s âˆ§ l + 1 = lh s âˆ§ âŸªl, zâŸ« âˆˆ s := by
  rcases CSeq.exists c v l with âŸ¨s, Hs, hâŸ©
  have : âˆƒ z, âŸªl, zâŸ« âˆˆ s := Hs.seq.exists (show l < lh s from by simp [â†h])
  rcases this with âŸ¨z, hzâŸ©
  exact ExistsUnique.intro z âŸ¨s, Hs, h, hzâŸ© (by
    rintro z' âŸ¨s', Hs', h', hz'âŸ©
    exact Eq.symm <| Hs.unique Hs' (by simp [â†h, â†h']) (show l < lh s from by simp [â†h]) hz hz')

noncomputable def result (u : V) : V := Classical.choose! (c.cSeq_result_existsUnique v u)

lemma result_spec (u : V) : âˆƒ s, c.CSeq v s âˆ§ u + 1 = lh s âˆ§ âŸªu, c.result v uâŸ« âˆˆ s :=
  Classical.choose!_spec (c.cSeq_result_existsUnique v u)

@[simp] theorem result_zero : c.result v 0 = c.zero v := by
  rcases c.result_spec v 0 with âŸ¨s, Hs, _, h0âŸ©
  exact Hs.seq.isMapping.uniq h0 Hs.zero

@[simp] theorem result_succ (u : V) : c.result v (u + 1) = c.succ v u (c.result v u) := by
  rcases c.result_spec v u with âŸ¨s, Hs, hk, hâŸ©
  have : CSeq c v (s â€' c.succ v u (result c v u) ) := Hs.successor hk h
  exact Eq.symm
    <| Classical.choose_uniq (c.cSeq_result_existsUnique v (u + 1))
    âŸ¨_, this, by simp [Hs.seq, hk], by simp [hk]âŸ©

lemma result_graph (z u : V) : z = c.result v u â†” âˆƒ s, c.CSeq v s âˆ§ âŸªu, zâŸ« âˆˆ s :=
  âŸ¨by rintro rfl
      rcases c.result_spec v u with âŸ¨s, Hs, _, hâŸ©
      exact âŸ¨s, Hs, hâŸ©,
   by rintro âŸ¨s, Hs, hâŸ©
      rcases c.result_spec v u with âŸ¨s', Hs', hu, h'âŸ©
      exact Eq.symm <| Hs'.unique Hs
        (by simpa [â†hu, succ_le_iff_lt] using Hs.seq.lt_lh_iff.mpr (mem_domain_of_pair_mem h))
        (by simp [â†hu]) h' hâŸ©

set_option linter.flexible false in
lemma result_defined : ğšºâ‚.DefinedFunction (fun v â†¦ c.result (v Â·.succ) (v 0) : (Fin (k + 1) â†’ V) â†’ V) p.resultDef := by
  intro v; simp [Blueprint.resultDef, result_graph]
  apply exists_congr; intro x
  simp [c.cseq_defined_iff]

lemma result_defined_delta : ğš«â‚.DefinedFunction (fun v â†¦ c.result (v Â·.succ) (v 0) : (Fin (k + 1) â†’ V) â†’ V) p.resultDeltaDef :=
  c.result_defined.graph_delta

@[simp] lemma result_defined_iff (v) :
    Semiformula.Evalbm V v p.resultDef.val â†” v 0 = c.result (v Â·.succ.succ) (v 1) := c.result_defined.df.iff v

instance result_definable : ğšºâ‚.BoldfaceFunction (fun v â†¦ c.result (v Â·.succ) (v 0) : (Fin (k + 1) â†’ V) â†’ V) :=
  c.result_defined.to_definable

instance result_definable_deltaâ‚ : ğš«â‚.BoldfaceFunction (fun v â†¦ c.result (v Â·.succ) (v 0) : (Fin (k + 1) â†’ V) â†’ V) :=
  c.result_defined_delta.to_definable

attribute [irreducible] Blueprint.resultDef

end Construction

end PR

end LO.ISigma1
