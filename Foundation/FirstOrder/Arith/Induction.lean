import Foundation.FirstOrder.Arith.Definability
import Foundation.FirstOrder.PeanoMinus.Functions

/-!
# Induction schemata of Arithmetic

-/

namespace LO

open FirstOrder

variable {L : Language} [L.ORing] {Î¾ : Type*} [DecidableEq Î¾]

namespace FirstOrder

def succInd {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ!Ï† 0 â†’ (âˆ€ x, !Ï† x â†’ !Ï† (x + 1)) â†’ âˆ€ x, !Ï† xâ€

def orderInd {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ(âˆ€ x, (âˆ€ y < x, !Ï† y) â†’ !Ï† x) â†’ âˆ€ x, !Ï† xâ€

def leastNumber {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ(âˆƒ x, !Ï† x) â†’ âˆƒ z, !Ï† z âˆ§ âˆ€ x < z, Â¬!Ï† xâ€

end FirstOrder

variable (L)

def InductionScheme (Î“ : Semiformula L â„• 1 â†’ Prop) : Theory L :=
  { Ïˆ | âˆƒ Ï† : Semiformula L â„• 1, Î“ Ï† âˆ§ Ïˆ = succInd Ï† }

abbrev IOpen : Theory â„’â‚’áµ£ := ğğ€â» + InductionScheme â„’â‚’áµ£ Semiformula.Open

notation "ğˆopen" => IOpen

abbrev InductionOnHierarchy (Î“ : Polarity) (k : â„•) : Theory â„’â‚’áµ£ := ğğ€â» + InductionScheme â„’â‚’áµ£ (Arith.Hierarchy Î“ k)

prefix:max "ğˆğğƒ" => InductionOnHierarchy

abbrev ISigma (k : â„•) : Theory â„’â‚’áµ£ := ğˆğğƒğšº k

prefix:max "ğˆğšº" => ISigma

notation "ğˆğšºâ‚€" => ISigma 0

abbrev IPi (k : â„•) : Theory â„’â‚’áµ£ := ğˆğğƒğš· k

prefix:max "ğˆğš·" => IPi

notation "ğˆğš·â‚€" => IPi 0

notation "ğˆğšºâ‚" => ISigma 1

notation "ğˆğš·â‚" => IPi 1

abbrev Peano : Theory â„’â‚’áµ£ := ğğ€â» + InductionScheme â„’â‚’áµ£ Set.univ

notation "ğğ€" => Peano

variable {L}

variable {C C' : Semiformula â„’â‚’áµ£ â„• 1 â†’ Prop}

lemma coe_InductionOnHierarchy_subset_InductionOnHierarchy :
    (InductionScheme â„’â‚’áµ£ (Arith.Hierarchy Î“ Î½) : Theory L) âŠ† InductionScheme L (Arith.Hierarchy Î“ Î½) := by
  simp only [InductionScheme, Set.image_subset_iff, Set.preimage_setOf_eq, Set.setOf_subset_setOf, forall_exists_index, and_imp]
  rintro _ Ï† Hp rfl
  exact âŸ¨Semiformula.lMap (Language.oringEmb : â„’â‚’áµ£ â†’áµ¥ L) Ï†, Arith.Hierarchy.oringEmb Hp,
    by simp [succInd, Semiformula.lMap_substs]âŸ©

lemma InductionScheme_subset (h : âˆ€ {Ï† : Semiformula â„’â‚’áµ£ â„• 1},  C Ï† â†’ C' Ï†) : InductionScheme â„’â‚’áµ£ C âŠ† InductionScheme â„’â‚’áµ£ C' := by
  intro _; simp only [InductionScheme, Set.mem_setOf_eq, forall_exists_index, and_imp]; rintro Ï† hp rfl; exact âŸ¨Ï†, h hp, rflâŸ©

lemma ISigma_subset_mono {sâ‚ sâ‚‚} (h : sâ‚ â‰¤ sâ‚‚) : ğˆğšº sâ‚ âŠ† ğˆğšº sâ‚‚ :=
  Set.union_subset_union_right _ (InductionScheme_subset (fun H â†¦ H.mono h))

lemma ISigma_weakerThan_of_le {sâ‚ sâ‚‚} (h : sâ‚ â‰¤ sâ‚‚) : ğˆğšº sâ‚ âª¯ ğˆğšº sâ‚‚ :=
  Entailment.WeakerThan.ofSubset (ISigma_subset_mono h)

instance : ğğ€â» âª¯ ğˆğğƒÎ“ n := Entailment.WeakerThan.ofSubset (by simp [InductionOnHierarchy, Theory.add_def])

instance : ğ„ğ âª¯ ğˆğğƒÎ“ n := Entailment.WeakerThan.trans (inferInstanceAs (ğ„ğ âª¯ ğğ€â»)) inferInstance

instance : ğ„ğ âª¯ ğˆopen := Entailment.WeakerThan.trans (inferInstanceAs (ğ„ğ âª¯ ğğ€â»)) inferInstance

instance : ğˆopen âª¯ ğˆğšºi :=
  Entailment.WeakerThan.ofSubset <| Set.union_subset_union_right _  <| InductionScheme_subset Arith.Hierarchy.of_open

instance : ğˆğšºâ‚€ âª¯ ğˆğšºâ‚ :=
  ISigma_weakerThan_of_le (by decide)

instance : ğˆğšºi âª¯ ğğ€ :=
  Entailment.WeakerThan.ofSubset <| Set.union_subset_union_right _  <| InductionScheme_subset (by intros; trivial)

lemma mem_InductionScheme_of_mem {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : C Ï†) :
    succInd Ï† âˆˆ InductionScheme â„’â‚’áµ£ C := by
  simpa [InductionScheme] using âŸ¨Ï†, hp, rflâŸ©

lemma mem_IOpen_of_qfree {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : Ï†.Open) :
    succInd Ï† âˆˆ InductionScheme â„’â‚’áµ£ Semiformula.Open := by
  exact âŸ¨Ï†, hp, rflâŸ©

end LO

namespace LO.InductionScheme

open FirstOrder Arith PeanoMinus

variable {V : Type*} [ORingStruc V]

section

variable {C : Semiformula â„’â‚’áµ£ â„• 1 â†’ Prop} [V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ C]

private lemma induction_eval {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : C Ï†) (v) :
    Semiformula.Evalm V ![0] v Ï† â†’
    (âˆ€ x, Semiformula.Evalm V ![x] v Ï† â†’ Semiformula.Evalm V ![x + 1] v Ï†) â†’
    âˆ€ x, Semiformula.Evalm V ![x] v Ï† := by
  have : V âŠ§â‚˜ succInd Ï† :=
    ModelsTheory.models (T := InductionScheme _ C) V (by simpa using mem_InductionScheme_of_mem hp)
  revert v
  simpa [models_iff, succInd, Semiformula.eval_substs, Semiformula.eval_rew_q Rew.toS, Function.comp, Matrix.constant_eq_singleton] using this

@[elab_as_elim]
lemma induction {P : V â†’ Prop}
    (hP : âˆƒ e : â„• â†’ V, âˆƒ Ï† : Semiformula â„’â‚’áµ£ â„• 1, C Ï† âˆ§ âˆ€ x, P x â†” Semiformula.Evalm V ![x] e Ï†) :
    P 0 â†’ (âˆ€ x, P x â†’ P (x + 1)) â†’ âˆ€ x, P x := by
  rcases hP with âŸ¨e, Ï†, Cp, hpâŸ©; simpa [â†hp] using induction_eval (V := V) Cp e

end

variable [V âŠ§â‚˜* ğğ€â»]

section

variable (Î“ : Polarity) (m : â„•) [V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ (Arith.Hierarchy Î“ m)]

lemma induction_h {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  induction (P := P) (C := Hierarchy Î“ m) (by
    rcases hP with âŸ¨Ï†, hpâŸ©
    haveI : Inhabited V := Classical.inhabited_of_nonempty'

    exact âŸ¨Ï†.val.enumarateFVar, (Rew.rewriteMap Ï†.val.idxOfFVar) â–¹ Ï†.val, by simp [hp],
      by  intro x; simp [Semiformula.eval_rewriteMap]
          have : (Semiformula.Evalm V ![x] fun x â†¦ Ï†.val.enumarateFVar (Ï†.val.idxOfFVar x)) Ï†.val â†” (Semiformula.Evalm V ![x] id) Ï†.val :=
            Semiformula.eval_iff_of_funEqOn _ (by
              intro x hx; simp [Semiformula.enumarateFVar_idxOfFVar (Semiformula.mem_fvarList_iff_fvar?.mpr hx)])
          simp [this, hp.df.iff]âŸ©)
    zero succ

lemma order_induction_h {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x := by
  suffices âˆ€ x, âˆ€ y < x, P y by
    intro x; exact this (x + 1) x (by simp only [lt_add_iff_pos_right, lt_one_iff_eq_zero])
  intro x; induction x using induction_h
  Â· exact Î“
  Â· exact m
  Â· suffices Î“-[m].BoldfacePred fun x â†¦ âˆ€ y < x, P y by exact this
    exact HierarchySymbol.Boldface.ball_blt (by simp) (hP.retraction ![0])
  case zero => simp
  case succ x IH =>
    intro y hxy
    rcases show y < x âˆ¨ y = x from lt_or_eq_of_le (le_iff_lt_succ.mpr hxy) with (lt | rfl)
    Â· exact IH y lt
    Â· exact ind y IH
  case inst => exact inferInstance

private lemma neg_induction_h {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (nzero : Â¬P 0) (nsucc : âˆ€ x, Â¬P x â†’ Â¬P (x + 1)) : âˆ€ x, Â¬P x := by
  by_contra A
  have : âˆƒ x, P x := by simpa using A
  rcases this with âŸ¨a, haâŸ©
  have : âˆ€ x â‰¤ a, P (a - x) := by
    intro x; induction x using induction_h
    Â· exact Î“
    Â· exact m
    Â· suffices Î“-[m].BoldfacePred fun x â†¦ x â‰¤ a â†’ P (a - x) by exact this
      apply HierarchySymbol.Boldface.imp
      Â· apply HierarchySymbol.Boldface.bcompâ‚‚ (by definability) (by definability)
      Â· apply HierarchySymbol.Boldface.bcompâ‚ (by definability)
    case zero =>
      intro _; simpa using ha
    case succ x IH =>
      intro hx
      have : P (a - x) := IH (le_of_add_le_left hx)
      exact (not_imp_not.mp <| nsucc (a - (x + 1))) (by
        rw [â†PeanoMinus.sub_sub, sub_add_self_of_le]
        Â· exact this
        Â· exact le_tsub_of_add_le_left hx)
    case inst => exact inferInstance
  have : P 0 := by simpa using this a (by rfl)
  contradiction

lemma models_InductionScheme_alt : V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ (Arith.Hierarchy Î“.alt m) := by
  suffices
      âˆ€ (Ï† : Semiformula â„’â‚’áµ£ â„• 1), Hierarchy Î“.alt m Ï† â†’
      âˆ€ (f : â„• â†’ V),
        Semiformula.Evalm V ![0] f Ï† â†’
        (âˆ€ x, Semiformula.Evalm V ![x] f Ï† â†’ Semiformula.Evalm V ![x + 1] f Ï†) â†’
        âˆ€ x, Semiformula.Evalm V ![x] f Ï† by
    simp only [InductionScheme, Semantics.RealizeSet.setOf_iff, forall_exists_index, and_imp]
    rintro _ Ï† hÏ† rfl
    simpa [models_iff, succInd, Semiformula.eval_rew_q,
        Semiformula.eval_substs, Function.comp, Matrix.constant_eq_singleton]
    using this Ï† hÏ†
  intro Ï† hp v
  simpa using
    neg_induction_h Î“ m (P := fun x â†¦ Â¬Semiformula.Evalm V ![x] v Ï†)
      (.mkPolarity (âˆ¼(Rew.rewriteMap v â–¹ Ï†)) (by simpa using hp)
      (by intro x; simp [â†Matrix.fun_eq_vec_one, Semiformula.eval_rewriteMap]))

instance : V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ (Arith.Hierarchy Î“.alt m) := models_InductionScheme_alt Î“ m

lemma least_number_h {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    {x} (h : P x) : âˆƒ y, P y âˆ§ âˆ€ z < y, Â¬P z := by
  by_contra A
  have A : âˆ€ z, P z â†’ âˆƒ w < z, P w := by simpa using A
  have : âˆ€ z, âˆ€ w < z, Â¬P w := by
    intro z
    induction z using induction_h
    Â· exact Î“.alt
    Â· exact m
    Â· suffices Î“.alt-[m].BoldfacePred fun z â†¦ âˆ€ w < z, Â¬P w by exact this
      apply HierarchySymbol.Boldface.ball_blt (by definability)
      apply HierarchySymbol.Boldface.not
      apply HierarchySymbol.Boldface.bcompâ‚ (hP := by simpa using hP) (by definability)
    case zero => simp
    case succ x IH =>
      intro w hx hw
      rcases le_iff_lt_or_eq.mp (lt_succ_iff_le.mp hx) with (hx | rfl)
      Â· exact IH w hx hw
      Â· have : âˆƒ v < w, P v := A w hw
        rcases this with âŸ¨v, hvw, hvâŸ©
        exact IH v hvw hv
    case inst => infer_instance
  exact this (x + 1) x (by simp) h

end

end LO.InductionScheme
