import Foundation.FirstOrder.SetTheory.Basic
import Mathlib.SetTheory.ZFC.Class

/-! # Standard model of set theory -/

namespace ZFSet

/-- ? -/
noncomputable instance allFunctionDefinable (f : (Fin n â†’ ZFSet.{u}) â†’ ZFSet.{u}) : ZFSet.Definable n f where
  out v := Quotient.out <| f fun i â†¦ ZFSet.mk (v i)

noncomputable def chooseâ‚ (x : ZFSet) : ZFSet := Classical.epsilon fun z â†¦ z âˆˆ x

lemma chooseâ‚_mem_self {x : ZFSet} (h : x â‰  âˆ…) : x.chooseâ‚ âˆˆ x := Classical.epsilon_spec (by contrapose! h; ext; simp_all)

noncomputable def choice' (ğ“§ : ZFSet) : ZFSet := image chooseâ‚ ğ“§

lemma choice'_uniqueExists {ğ“§ X : ZFSet}
    (he : âˆ… âˆ‰ ğ“§)
    (pairwise_disjoint : âˆ€ X âˆˆ ğ“§, âˆ€ Y âˆˆ ğ“§, (âˆƒ z, z âˆˆ X âˆ§ z âˆˆ Y) â†’ X = Y)
    (hX : X âˆˆ ğ“§) : âˆƒ! x, x âˆˆ ğ“§.choice' âˆ§ x âˆˆ X := by
  apply ExistsUnique.intro X.chooseâ‚
  Â· exact âŸ¨ZFSet.mem_image.mpr âŸ¨X, hX, rflâŸ©, chooseâ‚_mem_self <| by rintro rfl; contradictionâŸ©
  Â· rintro y âŸ¨hy, hyxâŸ©
    rcases ZFSet.mem_image.mp hy with âŸ¨Y, hY, rflâŸ©
    have : X = Y :=
      pairwise_disjoint X hX Y hY âŸ¨Y.chooseâ‚, hyx, chooseâ‚_mem_self <| by rintro rfl; contradictionâŸ©
    rcases this
    rfl

end ZFSet

namespace LO.FirstOrder.SetTheory

namespace Standard

@[simp] lemma isEmpty_iff_eq_empty {x : ZFSet.{u}} :
    IsEmpty x â†” x = âˆ… := by
  simpa [IsEmpty] using Iff.symm (ZFSet.eq_empty x)

instance models_zf : ZFSet.{u} âŠ§â‚˜* ğ—­ğ—™ where
  models_set Ï† hÏ† := by
    rcases hÏ†
    case axiom_of_equality h =>
      have : ZFSet.{u} âŠ§â‚˜* (ğ—˜ğ—¤ : Theory â„’â‚›â‚‘â‚œ) := inferInstance
      simpa [models_iff] using modelsTheory_iff.mp this h
    case axiom_of_empty_set =>
      suffices âˆƒ x, âˆ€ y : ZFSet.{u}, y âˆ‰ x by simpa [models_iff, Axiom.empty]
      exact âŸ¨âˆ…, by simpâŸ©
    case axiom_of_extentionality =>
      simp [models_iff, Axiom.extentionality, ZFSet.ext_iff]
    case axiom_of_pairing =>
      suffices
          âˆ€ x y : ZFSet.{u}, âˆƒ z, âˆ€ v : ZFSet.{u}, v âˆˆ z â†” v = x âˆ¨ v = y by
        simpa [models_iff, Axiom.pairing]
      intro x y
      exact âŸ¨{x, y}, by simpâŸ©
    case axiom_of_union =>
      suffices
          âˆ€ x : ZFSet.{u}, âˆƒ y, âˆ€ z : ZFSet.{u}, z âˆˆ y â†” âˆƒ v âˆˆ x, z âˆˆ v by
        simpa [models_iff, Axiom.union]
      intro x
      exact âŸ¨x.sUnion, by simpâŸ©
    case axiom_of_power_set =>
      suffices
          âˆ€ x : ZFSet.{u}, âˆƒ y, âˆ€ z : ZFSet.{u}, z âˆˆ y â†” z âŠ† x by
        simpa [models_iff, Axiom.power]
      intro x
      exact âŸ¨x.powerset, by simpâŸ©
    case axiom_of_infinity =>
      suffices
          âˆƒ Ï‰, (âˆ… âˆˆ Ï‰) âˆ§
            âˆ€ x âˆˆ Ï‰, âˆ€ y : ZFSet.{u}, (âˆ€ z, z âˆˆ y â†” z = x âˆ¨ z âˆˆ x) â†’ y âˆˆ Ï‰ by
        simpa [models_iff, Axiom.infinity, val_isSucc_iff]
      refine âŸ¨ZFSet.omega, ?_, ?_âŸ©
      Â· simp
      Â· intro x hx y  hy
        have : y = insert x x := by
          ext; simp_all
        simpa [this] using ZFSet.omega_succ hx
    case axiom_of_foundation =>
      suffices
          âˆ€ x : ZFSet.{u}, IsNonempty x â†’ âˆƒ y âˆˆ x, âˆ€ z âˆˆ x, z âˆ‰ y by
        simpa [models_iff, Axiom.foundation]
      intro x hx
      rcases hx with âŸ¨y, hxâŸ©
      refine âŸ¨ZFSet.mem_wf.min x.toSet âŸ¨y, by simpa using hxâŸ©,
        WellFounded.min_mem _ _ _,
        fun _ hx â†¦ ZFSet.mem_wf.not_lt_min _ _ (by simpa using hx)âŸ©
    case axiom_of_separation Ï† =>
      let P (f : â„• â†’ ZFSet.{u}) (x : ZFSet.{u}) : Prop :=
        Semiformula.Eval (standardStructure ZFSet.{u}) ![x] f Ï†
      suffices
          âˆ€ (f : â„• â†’ ZFSet.{u}) (x : ZFSet.{u}),
          âˆƒ y, âˆ€ z : ZFSet.{u}, z âˆˆ y â†” z âˆˆ x âˆ§ P f z by
        simpa [models_iff, Axiom.separationSchema, Matrix.constant_eq_singleton, P]
      intro f x
      refine âŸ¨ZFSet.sep (P f) x, ?_âŸ©
      intro z; simp
    case axiom_of_replacement Ï† =>
      let R (f : â„• â†’ ZFSet.{u}) (x y : ZFSet.{u}) : Prop :=
        Semiformula.Eval (standardStructure ZFSet.{u}) ![x, y] f Ï†
      suffices
          âˆ€ f : â„• â†’ ZFSet.{u},
          (âˆ€ x, âˆƒ! y, R f x y) â†’
          âˆ€ X : ZFSet.{u}, âˆƒ Y : ZFSet.{u}, âˆ€ y, y âˆˆ Y â†” âˆƒ x âˆˆ X, R f x y by
        simpa [models_iff, Axiom.replacementSchema, Matrix.constant_eq_singleton, Matrix.comp_vecCons']
      intro f h X
      have : âˆ€ x, âˆƒ y, R f x y := fun x â†¦ (h x).exists
      choose F hF using this
      have (x y : ZFSet) : R f x y â†” F x = y :=
        âŸ¨fun hxy â†¦ (h x).unique (hF x) hxy, by rintro rfl; exact hF xâŸ©
      refine âŸ¨ZFSet.image F X, fun _ â†¦ by simp [this]âŸ©

instance models_ac : ZFSet.{u} âŠ§â‚˜* ğ—”ğ—– where
  models_set Ï† hÏ† := by
    rcases hÏ†
    suffices
        âˆ€ ğ“§ : ZFSet.{u},
          (âˆ€ X âˆˆ ğ“§, IsNonempty X) â†’
          (âˆ€ X âˆˆ ğ“§, âˆ€ Y âˆˆ ğ“§, (âˆƒ x âˆˆ X, x âˆˆ Y) â†’ X = Y) â†’
          âˆƒ C, âˆ€ X âˆˆ ğ“§, âˆƒ! x, x âˆˆ C âˆ§ x âˆˆ X by
      simpa [models_iff, Axiom.choice]
    intro ğ“§ nonempty pairwise_disjoint
    refine âŸ¨ğ“§.choice', ?_âŸ©
    intro X hX
    exact ğ“§.choice'_uniqueExists
      (by intro h; rcases nonempty âˆ… h; simp_all) pairwise_disjoint hX

instance models_zfc : ZFSet.{u} âŠ§â‚˜* ğ—­ğ—™ğ—– := inferInstance

instance models_z : ZFSet.{u} âŠ§â‚˜* ğ—­ := ModelsTheory.of_ss (inferInstanceAs (ZFSet.{u} âŠ§â‚˜* ğ—­ğ—™)) Zermelo_subset_ZermeloFraenkel

end Standard

instance : Entailment.Consistent ğ—­ := consistent_of_model ğ—­ ZFSet.{0}

instance : Entailment.Consistent ğ—­ğ—™ := consistent_of_model ğ—­ğ—™ ZFSet.{0}

instance : Entailment.Consistent ğ—­ğ—™ğ—– := consistent_of_model ğ—­ğ—™ğ—– ZFSet.{0}

end LO.FirstOrder.SetTheory
