import Foundation.FirstOrder.Basic

namespace LO.FirstOrder

/-- Skolem function of rank 1 -/
def Language.skolemFunctionâ‚ (L : Language) : Language where
  Func k := Semisentence L (k + 1)
  Rel _ := PEmpty

abbrev Semisentence.skolemâ‚ {L : Language} (Ï† : Semisentence L (k + 1)) : L.skolemFunctionâ‚.Func k := Ï†

namespace Structure

variable (L : Language)

variable (M : Type*) [Nonempty M] [ð“¼ : Structure L M]

noncomputable instance skolem : Structure L.skolemFunctionâ‚ M where
  func _ Ï† v := Classical.epsilon fun z â†¦ Semiformula.Evalb ð“¼ (z :> v) Ï†
  rel _ r _ := PEmpty.elim r

variable {L M}

@[simp] lemma val_skolem_func (Ï† : Semisentence L (k + 1)) :
    (skolem L M).func Ï†.skolemâ‚ v = Classical.epsilon fun z â†¦ Ï†.Evalb ð“¼ (z :> v) := rfl

variable (L)

def SkolemHull (s : Set M) : Set M := Set.range fun t : Term L.skolemFunctionâ‚ s â†¦ t.valm M ![] (â†‘)

variable {L}

namespace SkolemHull

open Semiformula

variable {s : Set M}

lemma mem_iff :
    x âˆˆ SkolemHull L s â†” âˆƒ t : Term L.skolemFunctionâ‚ s, t.valm M ![] (â†‘) = x := by
  simp [SkolemHull]

@[simp] lemma val_mem (t : Term L.skolemFunctionâ‚ s) : t.valm M ![] (â†‘) âˆˆ SkolemHull L s := by simp [SkolemHull]

lemma subset : s âŠ† SkolemHull L s := fun x hx â†¦ by
  let t : Term L.skolemFunctionâ‚ s := &âŸ¨x, hxâŸ©
  have : x = t.valm M ![] (â†‘) := by simp [t]
  simp [this]

lemma closed {v : Fin k â†’ M} (hv : âˆ€ i, v i âˆˆ SkolemHull L s)
    {Ï† : Semisentence L (k + 1)} (H : âˆƒ z, M âŠ§/(z :> v) Ï†) :
    âˆƒ z âˆˆ SkolemHull L s, M âŠ§/(z :> v) Ï† := by
  choose u hu using fun i â†¦ mem_iff.mp (hv i)
  let t : Term L.skolemFunctionâ‚ s := .func Ï†.skolemâ‚ u
  refine âŸ¨t.valm M ![] (â†‘), by simp, ?_âŸ©
  suffices M âŠ§/((Classical.epsilon fun z â†¦ M âŠ§/(z :> v) Ï†) :> v) Ï† by
    simpa [t, Semiterm.val_func, hu]
  exact Classical.epsilon_spec H

variable [Operator.Eq L] [Structure.Eq L M]

lemma closed_func {v : Fin k â†’ M} (hv : âˆ€ i, v i âˆˆ SkolemHull L s)
    {f : L.Func k} : Structure.func f v âˆˆ SkolemHull L s := by
  have : âˆƒ z âˆˆ SkolemHull L s, M âŠ§/(z :> v) â€œ#0 = !!(Semiterm.func f fun i â†¦ #i.succ)â€ :=
    closed hv (Ï† := â€œ#0 = !!(Semiterm.func f fun i â†¦ #i.succ)â€)
      (by simp [Semiterm.val_func])
  rcases this with âŸ¨z, hz, eâŸ©
  have : z = func f v := by simpa [Semiterm.val_func] using e
  rcases this; assumption

variable (ð“¼ s)

instance str : Structure L (SkolemHull L s) where
  func k f v := âŸ¨func f fun i â†¦ (v i : M), closed_func (by simp)âŸ©
  rel k R v := Structure.rel R fun i â†¦ (v i : M)

instance set_nonempty : (SkolemHull L s).Nonempty := by
  have : âˆƒ z, M âŠ§/![z] (âŠ¤ : Semisentence L 1) := by simp
  have : âˆƒ z, z âˆˆ SkolemHull L s := by
    simpa using closed (s := s) (by simp) this
  exact this

instance nonempty : Nonempty (SkolemHull L s) :=
  Set.Nonempty.to_subtype (set_nonempty _ _)

variable {ð“¼ s}

@[simp] lemma str_func_def (f : L.Func k) (v : Fin k â†’ SkolemHull L s) :
    (str ð“¼ s).func f v = âŸ¨ð“¼.func f fun i â†¦ (v i : M), closed_func (by simp)âŸ© := rfl

@[simp] lemma str_rel_def (R : L.Rel k) (v : Fin k â†’ SkolemHull L s) :
    (str ð“¼ s).rel R v â†” ð“¼.rel R fun i â†¦ (v i : M) := by rfl

@[simp] lemma str_val (t : Semiterm L Î¾ n) (b : Fin n â†’ SkolemHull L s) (f : Î¾ â†’ SkolemHull L s) :
    (t.val (M := SkolemHull L s) (str ð“¼ s) b f : M) = t.val ð“¼ (b Â·) (f Â·) :=
  match t with
  |        #x => by simp
  |        &x => by simp
  | .func F v => by simp [Semiterm.val_func, str_val]

@[simp] lemma str_eval {Ï† : Semisentence L n} :
    (SkolemHull L s) âŠ§/b Ï† â†” M âŠ§/(b Â·) Ï† :=
  match Ï† with
  | .rel R v | .nrel R v => by simp [Semiformula.eval_rel, Semiformula.eval_nrel, Empty.eq_elim]
  | âŠ¤ | âŠ¥ => by simp
  | Ï† â‹ Ïˆ | Ï† â‹Ž Ïˆ => by simp [str_eval (Ï† := Ï†), str_eval (Ï† := Ïˆ)]
  | âˆ€' Ï† => by
    suffices
        (âˆƒ x âˆˆ SkolemHull L s, (âˆ¼Ï†).Evalb ð“¼ (x :> (b Â·))) â†” (âˆƒ x, (âˆ¼Ï†).Evalb ð“¼ (x :> (b Â·))) by
      apply not_iff_not.mp
      simpa [str_eval (Ï† := Ï†), Matrix.comp_vecCons']
    constructor
    Â· rintro âŸ¨x, _, HâŸ©
      exact âŸ¨x, HâŸ©
    Â· intro h
      exact closed (s := s) (by simp) h
  | âˆƒ' Ï† => by
    suffices
        (âˆƒ x âˆˆ SkolemHull L s, Ï†.Evalb ð“¼ (x :> (b Â·))) â†” (âˆƒ x, Ï†.Evalb ð“¼ (x :> (b Â·))) by
      simpa [str_eval (Ï† := Ï†), Matrix.comp_vecCons']
    constructor
    Â· rintro âŸ¨x, _, HâŸ©
      exact âŸ¨x, HâŸ©
    Â· intro h
      exact closed (s := s) (by simp) h

instance elementary : (SkolemHull L s) â‰¡â‚‘[L] M where
  models {Ï†} := by simp [models_iff, Matrix.empty_eq]

end SkolemHull

end Structure

end LO.FirstOrder
