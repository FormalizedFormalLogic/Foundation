import Foundation.FirstOrder.Basic.BinderNotation
import Foundation.FirstOrder.Basic.Semantics.Elementary
import Foundation.FirstOrder.Basic.Soundness

namespace Matrix

variable {Î± : Type*}

def iget [Inhabited Î±] (v : Fin k â†’ Î±) (x : â„•) : Î± := if h : x < k then v âŸ¨x, hâŸ© else default

end Matrix

namespace LO

namespace FirstOrder

variable {L : Language} {Î¼ : Type*} [Semiformula.Operator.Eq L]

namespace Theory

section Eq

variable (L)

abbrev Eq.refl : Sentence L := â€œâˆ€ x, x = xâ€

abbrev Eq.symm : Sentence L := â€œâˆ€ x y, x = y â†’ y = xâ€

abbrev Eq.trans : Sentence L := â€œâˆ€ x y z, x = y â†’ y = z â†’ x = zâ€

variable {L}

abbrev Eq.funcExt {k} (f : L.Func k) : Sentence L :=
  let Ïƒ : Semisentence L (k + k) :=
    (Matrix.conj fun i : Fin k â†¦ â€œ#(i.addCast k) = #(i.addNat k)â€) â
      op(=).operator ![Semiterm.func f (fun i â†¦ #(i.addCast k)), Semiterm.func f (fun i â†¦ #(i.addNat k))]
  âˆ€* Ïƒ

abbrev Eq.relExt {k} (r : L.Rel k) : Sentence L :=
  let Ïƒ : Semisentence L (k + k) :=
    (Matrix.conj fun i : Fin k â†¦ â€œ#(i.addCast k) = #(i.addNat k)â€) â
      Semiformula.rel r (fun i â†¦ #(i.addCast k)) â Semiformula.rel r (fun i â†¦ #(i.addNat k))
  âˆ€* Ïƒ

inductive eqAxiom : Theory L
  | refl : eqAxiom (Eq.refl L)
  | symm : eqAxiom (Eq.symm L)
  | trans : eqAxiom (Eq.trans L)
  | funcExt {k} (f : L.Func k) : eqAxiom (Eq.funcExt f)
  | relExt {k} (r : L.Rel k) : eqAxiom (Eq.relExt r)

notation "ğ—˜ğ—¤" => eqAxiom

lemma Eq.defeq :
    ğ—˜ğ—¤ = {Eq.refl L, Eq.symm L, Eq.trans L}
      âˆª Set.range (fun f : (k : â„•) Ã— L.Func k â†¦ Eq.funcExt f.2)
      âˆª Set.range (fun f : (k : â„•) Ã— L.Rel k â†¦ Eq.relExt f.2) := by
  ext Ï†; constructor
  Â· rintro âŸ¨âŸ©
    case refl => simp
    case symm => simp
    case trans => simp
    case funcExt k f =>
      left; right; exact âŸ¨âŸ¨k, fâŸ©, rflâŸ©
    case relExt k r =>
      right; exact âŸ¨âŸ¨k, râŸ©, rflâŸ©
  Â· rintro (((rfl | rfl | rfl) | âŸ¨f, rflâŸ©) | âŸ¨r, rflâŸ©)
    Â· exact eqAxiom.refl
    Â· exact eqAxiom.symm
    Â· exact eqAxiom.trans
    Â· exact eqAxiom.funcExt _
    Â· exact eqAxiom.relExt _

@[simp] lemma EqAxiom.finite [L.Finite] : Set.Finite (ğ—˜ğ—¤ : Theory L) := by
  haveI : Fintype ((k : â„•) Ã— L.Func k) := Language.Finite.func
  haveI : Fintype ((k : â„•) Ã— L.Rel k) := Language.Finite.rel
  rw [Eq.defeq]
  simp [Set.finite_range]

end Eq

end Theory

namespace Structure

namespace Eq

@[simp] lemma models_eqAxiom {M : Type u} [Nonempty M] [Structure L M] [Structure.Eq L M] : M âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L) := âŸ¨by
  intro Ïƒ h
  cases h <;> try { simp [models_iff, Semiterm.val_func, Semiformula.eval_rel, *]; try simp_all }âŸ©

variable (L)

instance models_eqAxiom' (M : Type u) [Nonempty M] [Structure L M] [Structure.Eq L M] : M âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L) := Structure.Eq.models_eqAxiom

variable {M : Type u} [Nonempty M] [Structure L M]

def eqv (a b : M) : Prop := (@Semiformula.Operator.Eq.eq L _).val ![a, b]

variable {L}

variable [H : M âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L)]

open Semiterm Theory Semiformula

lemma eqv_refl (a : M) : eqv L a a := by
  have : M âŠ§â‚˜ â€œâˆ€ x, x = xâ€ := H.realize _ (Theory.eqAxiom.refl (L := L))
  have : âˆ€ x : M, op(=)[L].val ![x, x] := by simpa [models_iff] using this
  simpa using this a

lemma eqv_symm {a b : M} : eqv L a b â†’ eqv L b a := by
  have : M âŠ§â‚˜ â€œâˆ€ x y, x = y â†’ y = xâ€ := H.realize _ (Theory.eqAxiom.symm (L := L))
  have : âˆ€ x y : M, op(=)[L].val ![x, y] â†’ op(=)[L].val ![y, x] := by simpa [models_iff] using this
  simpa using this a b

lemma eqv_trans {a b c : M} : eqv L a b â†’ eqv L b c â†’ eqv L a c := by
  have : M âŠ§â‚˜ â€œâˆ€ x y z, x = y â†’ y = z â†’ x = zâ€ := H.realize _ (Theory.eqAxiom.trans (L := L))
  have : âˆ€ x y z : M, op(=)[L].val ![x, y] â†’ op(=)[L].val ![y, z] â†’ op(=)[L].val ![x, z] := by simpa [models_iff] using this
  simpa using this a b c

lemma eqv_funcExt {k} (f : L.Func k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    eqv L (func f v) (func f w) := by
  have : M âŠ§â‚˜ Eq.funcExt f := H.realize _ (eqAxiom.funcExt f)
  have :
      âˆ€ m : Fin (k + k) â†’ M,
      (âˆ€ (i : Fin k), op(=)[L].val ![m (Fin.addCast k i), m (i.addNat k)]) â†’
        op(=)[L].val ![func f fun i â†¦ m (Fin.addCast k i), func f fun i â†¦ m (i.addNat k)] := by
    simpa [models_iff, Semiterm.val_func] using this
  have := this (Matrix.vecAppend rfl v w) (fun i â†¦ by simpa [Matrix.vecAppend_eq_ite] using h i)
  simpa [Semiterm.val_func, Matrix.vecAppend_eq_ite] using this

lemma eqv_relExt_aux {k} (r : L.Rel k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    rel r v â†’ rel r w := by
  have : M âŠ§â‚˜ Eq.relExt r := H.realize _ (eqAxiom.relExt r)
  have :
      âˆ€ m : Fin (k + k) â†’ M,
      (âˆ€ (i : Fin k), op(=)[L].val ![m (Fin.addCast k i), m (i.addNat k)]) â†’
        (rel r fun i â†¦ m (Fin.addCast k i)) â†’ rel r fun i â†¦ m (i.addNat k) := by
    simpa [models_iff, Semiterm.val_func] using this
  have := this (Matrix.vecAppend rfl v w) (fun i â†¦ by simpa [Matrix.vecAppend_eq_ite] using h i)
  simpa [Semiterm.val_func, Matrix.vecAppend_eq_ite] using this

lemma eqv_relExt {k} (r : L.Rel k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    rel r v â†” rel r w := by
  constructor
  Â· exact eqv_relExt_aux r h
  Â· exact eqv_relExt_aux r (fun i => eqv_symm (h i))

lemma eqv_equivalence : Equivalence (eqv L (M := M)) where
  refl := eqv_refl
  symm := eqv_symm
  trans := eqv_trans

variable (L M)

def eqvSetoid : Setoid M := Setoid.mk (eqv L) eqv_equivalence

def QuotEq := Quotient (eqvSetoid L M)

variable {L M}

instance QuotEq.inhabited : Nonempty (QuotEq L M) := Nonempty.map (âŸ¦Â·âŸ§) inferInstance

lemma of_eq_of {a b : M} : (âŸ¦aâŸ§ : QuotEq L M) = âŸ¦bâŸ§ â†” eqv L a b := Quotient.eq (r := eqvSetoid L M)

namespace QuotEq

def func â¦ƒkâ¦„ (f : L.Func k) (v : Fin k â†’ QuotEq L M) : QuotEq L M :=
  Quotient.liftVec (s := eqvSetoid L M) (âŸ¦Structure.func f Â·âŸ§) (fun _ _ hvw => of_eq_of.mpr (eqv_funcExt f hvw)) v

def Rel â¦ƒkâ¦„ (r : L.Rel k) (v : Fin k â†’ QuotEq L M) : Prop :=
  Quotient.liftVec (s := eqvSetoid L M) (Structure.rel r) (fun _ _ hvw =>eq_iff_iff.mpr <| eqv_relExt r hvw) v

instance struc : Structure L (QuotEq L M) where
  func := QuotEq.func
  rel := QuotEq.Rel

lemma funk_mk {k} (f : L.Func k) (v : Fin k â†’ M) : Structure.func (M := QuotEq L M) f (fun i => âŸ¦v iâŸ§) = âŸ¦Structure.func f vâŸ§ :=
  Quotient.liftVec_mk (s := eqvSetoid L M) _ _ _

lemma rel_mk {k} (r : L.Rel k) (v : Fin k â†’ M) : Structure.rel (M := QuotEq L M) r (fun i => âŸ¦v iâŸ§) â†” Structure.rel r v :=
  of_eq <| Quotient.liftVec_mk (s := eqvSetoid L M) _ _ _

lemma val_mk {e} {Îµ} (t : Semiterm L Î¼ n) :
    Semiterm.valm (QuotEq L M) (fun i â†¦ âŸ¦e iâŸ§) (fun i â†¦ âŸ¦Îµ iâŸ§) t = âŸ¦Semiterm.valm M e Îµ tâŸ§ :=
  by induction t <;> simp [*, funk_mk, Semiterm.val_func]

lemma eval_mk {e} {Îµ} {Ï† : Semiformula L Î¼ n} :
    Semiformula.Evalm (QuotEq L M) (fun i â†¦ âŸ¦e iâŸ§) (fun i â†¦ âŸ¦Îµ iâŸ§) Ï† â†” Semiformula.Evalm M e Îµ Ï† := by
  induction Ï† using Semiformula.rec'
  case hall n Ï† ih =>
    constructor
    Â· intro h a; exact (ih (e := a :> e)).mp (by simpa [Matrix.comp_vecCons] using h âŸ¦aâŸ§)
    Â· intro h a;
      induction' a using Quotient.ind with a
      simpa [Matrix.comp_vecCons] using ih.mpr (h a)
  case hex n Ï† ih =>
    constructor
    Â· intro âŸ¨a, hâŸ©
      induction' a using Quotient.ind with a
      exact âŸ¨a, (ih (e := a :> e)).mp (by simpa [Matrix.comp_vecCons] using h)âŸ©
    Â· intro âŸ¨a, hâŸ©; exact âŸ¨âŸ¦aâŸ§, by simpa [Matrix.comp_vecCons] using ih.mpr hâŸ©
  case _ => simp [*]
  case _ => simp [*]
  case _ => simp [*, Semiformula.eval_rel, val_mk, rel_mk]
  case _ => simp [*, Semiformula.eval_nrel, val_mk, rel_mk]
  case _ => simp [*]
  case _ => simp [*]

lemma eval_mkâ‚€ {Îµ} {Ï† : Formula L Î¾} :
    Semiformula.Evalfm (QuotEq L M) (fun i => âŸ¦Îµ iâŸ§) Ï† â†” Semiformula.Evalfm (L := L) M Îµ Ï† := by
  simpa [Matrix.empty_eq] using eval_mk (H := H) (e := ![]) (Îµ := Îµ) (Ï† := Ï†)

lemma models_iff {Ïƒ : Sentence L} : QuotEq L M âŠ§â‚˜ Ïƒ â†” M âŠ§â‚˜ Ïƒ := by
  simpa [Empty.eq_elim] using eval_mkâ‚€ (M := M) (Ï† := Ïƒ) (Îµ := Empty.elim)

variable (L M)

lemma elementaryEquiv : QuotEq L M â‰¡â‚‘[L] M := fun _ => models_iff

variable {L M}

lemma rel_eq (a b : QuotEq L M) : (@Semiformula.Operator.Eq.eq L _).val (M := QuotEq L M) ![a, b] â†” a = b := by
  induction' a using Quotient.ind with a
  induction' b using Quotient.ind with b
  rw [of_eq_of]; simp [eqv, Semiformula.Operator.val];
  simpa [Evalm, Matrix.fun_eq_vec_two, Empty.eq_elim] using
    eval_mk (H := H) (e := ![a, b]) (Îµ := Empty.elim) (Ï† := Semiformula.Operator.Eq.eq.sentence)

instance structureEq : Structure.Eq L (QuotEq L M) := âŸ¨rel_eqâŸ©

end QuotEq

end Eq

end Structure

lemma consequence_iff_eq {T : Theory L} [ğ—˜ğ—¤ âª¯ T] {Ïƒ : Sentence L} :
    T âŠ¨[Struc.{v, u} L] Ïƒ â†” (âˆ€ (M : Type v) [Nonempty M] [Structure L M] [Structure.Eq L M], M âŠ§â‚˜* T â†’ M âŠ§â‚˜ Ïƒ) := by
  simp only [consequence_iff, Nonempty.forall]
  constructor
  Â· intro h M x s _ hM; exact h M x hM
  Â· intro h M x s hM
    haveI : Nonempty M := âŸ¨xâŸ©
    have H : M âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L) := models_of_subtheory hM
    have e : Structure.Eq.QuotEq L M â‰¡â‚‘[L] M := Structure.Eq.QuotEq.elementaryEquiv L M
    exact e.models.mp $ h (Structure.Eq.QuotEq L M) âŸ¦xâŸ§ (e.modelsTheory.mpr hM)

lemma consequence_iff_eq' {T : Theory L} [ğ—˜ğ—¤ âª¯ T] {Ïƒ : Sentence L} :
    T âŠ¨[Struc.{v, u} L] Ïƒ â†” (âˆ€ (M : Type v) [Nonempty M] [Structure L M] [Structure.Eq L M] [M âŠ§â‚˜* T], M âŠ§â‚˜ Ïƒ) := by
  rw [consequence_iff_eq]

lemma satisfiable_iff_eq {T : Theory L} [ğ—˜ğ—¤ âª¯ T] :
    Semantics.Satisfiable (Struc.{v, u} L) T â†” (âˆƒ (M : Type v) (_ : Nonempty M) (_ : Structure L M) (_ : Structure.Eq L M), M âŠ§â‚˜* T) := by
  simp only [satisfiable_iff, Nonempty.exists, exists_prop]
  constructor
  Â· intro âŸ¨M, x, s, hMâŸ©;
    haveI : Nonempty M := âŸ¨xâŸ©
    have H : M âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L) := models_of_subtheory hM
    have e : Structure.Eq.QuotEq L M â‰¡â‚‘[L] M := Structure.Eq.QuotEq.elementaryEquiv L M
    exact âŸ¨Structure.Eq.QuotEq L M, âŸ¦xâŸ§, inferInstance, inferInstance, e.modelsTheory.mpr hMâŸ©
  Â· intro âŸ¨M, i, s, _, hMâŸ©; exact âŸ¨M, i, s, hMâŸ©

instance {T : Theory L} [ğ—˜ğ—¤ âª¯ T] (sat : Semantics.Satisfiable (Struc.{v, u} L) T) :
    ModelOfSat sat âŠ§â‚˜* (ğ—˜ğ—¤ : Theory L) := models_of_subtheory (ModelOfSat.models sat)

def ModelOfSatEq {T : Theory L} [ğ—˜ğ—¤ âª¯ T] (sat : Semantics.Satisfiable (Struc.{v, u} L) T) : Type _ :=
  Structure.Eq.QuotEq L (ModelOfSat sat)

namespace ModelOfSatEq

variable {T : Theory L} [ğ—˜ğ—¤ âª¯ T] (sat : Semantics.Satisfiable (Struc.{v, u} L) T)

noncomputable instance : Nonempty (ModelOfSatEq sat) := Structure.Eq.QuotEq.inhabited

noncomputable instance struc : Structure L (ModelOfSatEq sat) := Structure.Eq.QuotEq.struc

noncomputable instance : Structure.Eq L (ModelOfSatEq sat) := Structure.Eq.QuotEq.structureEq

lemma models : ModelOfSatEq sat âŠ§â‚˜* T :=
  have e : ModelOfSatEq sat â‰¡â‚‘[L] ModelOfSat sat := Structure.Eq.QuotEq.elementaryEquiv L (ModelOfSat sat)
  e.modelsTheory.mpr (ModelOfSat.models _)

instance mod : ModelOfSatEq sat âŠ§â‚˜* T := models sat

open Semiterm Semiformula

noncomputable instance [Operator.Zero L] : Zero (ModelOfSatEq sat) := âŸ¨(@Operator.Zero.zero L _).val ![]âŸ©

instance strucZero [Operator.Zero L] : Structure.Zero L (ModelOfSatEq sat) := âŸ¨rflâŸ©

noncomputable instance [Operator.One L] : One (ModelOfSatEq sat) := âŸ¨(@Operator.One.one L _).val ![]âŸ©

instance [Operator.One L] : Structure.One L (ModelOfSatEq sat) := âŸ¨rflâŸ©

noncomputable instance [Operator.Add L] : Add (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Add.add L _).val ![x, y]âŸ©

instance [Operator.Add L] : Structure.Add L (ModelOfSatEq sat) := âŸ¨fun _ _ => rflâŸ©

noncomputable instance [Operator.Mul L] : Mul (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Mul.mul L _).val ![x, y]âŸ©

instance [Operator.Mul L] : Structure.Mul L (ModelOfSatEq sat) := âŸ¨fun _ _ => rflâŸ©

instance [Operator.LT L] : LT (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.LT.lt L _).val ![x, y]âŸ©

instance [Operator.LT L] : Structure.LT L (ModelOfSatEq sat) := âŸ¨fun _ _ => iff_of_eq rflâŸ©

instance [Operator.Mem L] : Membership (ModelOfSatEq sat) (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Mem.mem L _).val ![y, x]âŸ©

instance [Operator.Mem L] : Structure.Mem L (ModelOfSatEq sat) := âŸ¨fun _ _ => iff_of_eq rflâŸ©

end ModelOfSatEq

namespace Semiformula

def existsUnique {Î¾} (Ï† : Semiformula L Î¾ (n + 1)) : Semiformula L Î¾ n :=
  â€œâˆƒ y, !Ï† y â‹¯ âˆ§ âˆ€ z, !Ï† z â‹¯ â†’ z = yâ€

prefix:64 "âˆƒ'! " => existsUnique

variable {M : Type*} [s : Structure L M] [Structure.Eq L M]

@[simp] lemma eval_existsUnique {e Îµ} {Ï† : Semiformula L Î¾ (n + 1)} :
    Eval s e Îµ (âˆƒ'! Ï†) â†” âˆƒ! x, Eval s (x :> e) Îµ Ï† := by
  simp [existsUnique, Semiformula.eval_substs, Matrix.comp_vecCons', ExistsUnique]

end Semiformula

namespace BinderNotation

open Lean PrettyPrinter Delaborator SubExpr

syntax:max "âˆƒ! " first_order_formula:0 : first_order_formula
syntax:max "âˆƒ! " ident ", " first_order_formula:0 : first_order_formula

macro_rules
  | `(â¤«formula($type)[ $binders* | $fbinders* | âˆƒ! $Ï†:first_order_formula ]) => do
    let v := mkIdent (Name.mkSimple ("var" ++ toString binders.size))
    let binders' := binders.insertIdx 0 v
    `(âˆƒ'! â¤«formula($type)[ $binders'* | $fbinders* | $Ï†])
  | `(â¤«formula($type)[ $binders* | $fbinders* | âˆƒ! $x, $Ï† ])                 => do
    if binders.elem x then Macro.throwErrorAt x "error: variable is duplicated." else
    let binders' := binders.insertIdx 0 x
    `(âˆƒ'! â¤«formula($type)[ $binders'* | $fbinders* | $Ï† ])

end BinderNotation

end FirstOrder

end LO
