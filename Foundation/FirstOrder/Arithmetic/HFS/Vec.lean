import Foundation.FirstOrder.Arithmetic.HFS.Fixpoint

/-!

# Vec

-/

namespace LO.ISigma1

open FirstOrder Arithmetic PeanoMinus IOpen ISigma0

variable {V : Type*} [ORingStructure V] [V âŠ§â‚˜* ğ—œğšºâ‚]

section adjoin

noncomputable instance : Adjoin V V := âŸ¨(âŸªÂ·, Â·âŸ« + 1)âŸ©

scoped infixr:67 " âˆ· " => adjoin

syntax "?[" term,* "]" : term

macro_rules
  | `(?[$term:term, $terms:term,*]) => `(adjoin $term ?[$terms,*])
  | `(?[$term:term]) => `(adjoin $term 0)
  | `(?[]) => `(0)

@[app_unexpander Adjoin.adjoin]
def adjoinUnexpander : Lean.PrettyPrinter.Unexpander
  | `($_ $term ?[$terms,*]) => `(?[$term, $terms,*])
  | `($_ $term 0) => `(?[$term])
  | _ => throw ()

lemma adjoin_def (x v : V) : x âˆ· v = âŸªx, vâŸ« + 1 := rfl

@[simp] lemma fstIdx_adjoin (x v : V) : fstIdx (x âˆ· v) = x := by simp [adjoin_def, fstIdx]

@[simp] lemma sndIdx_adjoin (x v : V) : sndIdx (x âˆ· v) = v := by simp [adjoin_def, sndIdx]

lemma succ_eq_adjoin (x : V) : x + 1 = Ï€â‚ x âˆ· Ï€â‚‚ x := by simp [adjoin_def]

@[simp] lemma lt_adjoin (x v : V) : x < x âˆ· v := by simp [adjoin_def, lt_succ_iff_le]

@[simp] lemma lt_adjoin' (x v : V) : v < x âˆ· v := by simp [adjoin_def, lt_succ_iff_le]

@[simp] lemma zero_lt_adjoin (x v : V) : 0 < x âˆ· v := by simp [adjoin_def]

@[simp] lemma adjoin_ne_zero (x v : V) : x âˆ· v â‰  0 := by simp [adjoin_def]

@[simp] lemma zero_ne_adjoin (x v : V) : 0 â‰  x âˆ· v := by symm; simp [adjoin_def]

lemma nil_or_adjoin (z : V) : z = 0 âˆ¨ âˆƒ x v, z = x âˆ· v := by
  rcases zero_or_succ z with (rfl | âŸ¨z, rflâŸ©)
  Â· left; rfl
  Â· right; exact âŸ¨Ï€â‚ z, Ï€â‚‚ z, by simp [succ_eq_adjoin]âŸ©

@[simp] lemma adjoin_inj (xâ‚ xâ‚‚ vâ‚ vâ‚‚ : V) :
    xâ‚ âˆ· vâ‚ = xâ‚‚ âˆ· vâ‚‚ â†” xâ‚ = xâ‚‚ âˆ§ vâ‚ = vâ‚‚ := by simp [adjoin_def]

lemma adjoin_le_adjoin {xâ‚ xâ‚‚ vâ‚ vâ‚‚ : V} (hx : xâ‚ â‰¤ xâ‚‚) (hv : vâ‚ â‰¤ vâ‚‚) :
    xâ‚ âˆ· vâ‚ â‰¤ xâ‚‚ âˆ· vâ‚‚ := by simpa [adjoin_def] using pair_le_pair hx hv

section

def _root_.LO.FirstOrder.Arithmetic.adjoinDef : ğšºâ‚€.Semisentence 3 :=
  .mkSigma â€œw x v. âˆƒ xv < w, !pairDef xv x v âˆ§ w = xv + 1â€

instance adjoin_defined : ğšºâ‚€-Functionâ‚‚ (adjoin : V â†’ V â†’ V) via adjoinDef := .mk fun v â†¦ by simp_all [adjoinDef, adjoin_def]

instance adjoin_definable : ğšºâ‚€-Functionâ‚‚ (adjoin : V â†’ V â†’ V) := adjoin_defined.to_definable

instance adjoin_definable' (â„Œ) : â„Œ-Functionâ‚‚ (adjoin : V â†’ V â†’ V) := adjoin_definable.of_zero

def _root_.LO.FirstOrder.Arithmetic.mkVecâ‚Def : ğšºâ‚€.Semisentence 2 := .mkSigma
  â€œs x. !adjoinDef s x 0â€

instance mkVecâ‚_defined : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ ?[x]) via mkVecâ‚Def := .mk fun v â†¦ by simp [mkVecâ‚Def]

instance mkVecâ‚_definable : ğšºâ‚€-Functionâ‚ (fun x : V â†¦ ?[x]) := mkVecâ‚_defined.to_definable

instance mkVecâ‚_definable' (â„Œ) : â„Œ-Functionâ‚ (fun x : V â†¦ ?[x]) := mkVecâ‚_definable.of_zero

def _root_.LO.FirstOrder.Arithmetic.mkVecâ‚‚Def : ğšºâ‚.Semisentence 3 := .mkSigma
  â€œs x y. âˆƒ sy, !mkVecâ‚Def sy y âˆ§ !adjoinDef s x syâ€

instance mkVecâ‚‚_defined : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) via mkVecâ‚‚Def := .mk fun v â†¦ by simp [mkVecâ‚‚Def]

instance mkVecâ‚‚_definable : ğšºâ‚-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) := mkVecâ‚‚_defined.to_definable

instance mkVecâ‚‚_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚‚ (fun x y : V â†¦ ?[x, y]) := mkVecâ‚‚_definable.of_sigmaOne

end

end adjoin

/-!

### N-th element of List

-/

namespace Nth

def Phi (C : Set V) (pr : V) : Prop :=
  (âˆƒ v, pr = âŸªv, 0, fstIdx vâŸ«) âˆ¨ (âˆƒ v i x, pr = âŸªv, i + 1, xâŸ« âˆ§ âŸªsndIdx v, i, xâŸ« âˆˆ C)

private lemma phi_iff (C pr : V) :
    Phi {x | x âˆˆ C} pr â†”
    (âˆƒ v â‰¤ pr, âˆƒ fst â‰¤ v, fst = fstIdx v âˆ§ pr = âŸªv, 0, fstâŸ«) âˆ¨
    (âˆƒ v â‰¤ pr, âˆƒ i â‰¤ pr, âˆƒ x â‰¤ pr, pr = âŸªv, i + 1, xâŸ« âˆ§ âˆƒ snd â‰¤ v, snd = sndIdx v âˆ§ âˆƒ six < C, six = âŸªsnd, i, xâŸ« âˆ§ six âˆˆ C) := by
  constructor
  Â· rintro (âŸ¨v, rflâŸ© | âŸ¨v, i, x, rfl, hCâŸ©)
    Â· left; exact âŸ¨v, by simp, _, by simp, rfl, rflâŸ©
    Â· right; exact âŸ¨v, by simp,
        i, le_trans (le_trans (by simp) (le_pair_left _ _)) (le_pair_right _ _),
        x, le_trans (by simp) (le_pair_right _ _), rfl, _, by simp, rfl, _, lt_of_mem hC, rfl, hCâŸ©
  Â· rintro (âŸ¨v, _, _, _, rfl, rflâŸ© | âŸ¨v, _, i, _, x, _, rfl, _, _, rfl, _, _, rfl, hCâŸ©)
    Â· left; exact âŸ¨v, rflâŸ©
    Â· right; exact âŸ¨v, i, x, rfl, hCâŸ©

def blueprint : Fixpoint.Blueprint 0 where
  core := .ofZero
    (.mkSigma â€œpr C.
    (âˆƒ v <âº pr, âˆƒ fst <âº v, !fstIdxDef fst v âˆ§ !pairâ‚ƒDef pr v 0 fst) âˆ¨
    (âˆƒ v <âº pr, âˆƒ i <âº pr, âˆƒ x <âº pr, !pairâ‚ƒDef pr v (i + 1) x âˆ§
      âˆƒ snd <âº v, !sndIdxDef snd v âˆ§ âˆƒ six < C, !pairâ‚ƒDef six snd i x âˆ§ six âˆˆ C)â€
    (by simp))
    _

def adjointruction : Fixpoint.Construction V blueprint where
  Î¦ := fun _ â†¦ Phi
  defined := .of_zero <| .mk fun v â†¦ by simp [phi_iff]
  monotone := by
    rintro C C' hC _ x (h | âŸ¨v, i, x, rfl, hâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨v, i, x, rfl, hC hâŸ©

instance : adjointruction.Finite V where
  finite := by
    rintro C v x (h | âŸ¨v, i, x, rfl, hâŸ©)
    Â· exact âŸ¨0, Or.inl hâŸ©
    Â· exact âŸ¨âŸªsndIdx v, i, xâŸ« + 1, Or.inr âŸ¨v, i, x, rfl, h, by simpâŸ©âŸ©

def Graph : V â†’ Prop := adjointruction.Fixpoint ![]

section

def graphDef : ğšºâ‚.Semisentence 1 := blueprint.fixpointDef

instance graph_defined : ğšºâ‚-Predicate (Graph : V â†’ Prop) via graphDef := adjointruction.fixpoint_defined

instance graph_definable : ğšºâ‚-Predicate (Graph : V â†’ Prop) := graph_defined.to_definable

instance graph_definable' : ğšº-[0 + 1]-Predicate (Graph : V â†’ Prop) := graph_definable

end

/-- TODO: move-/
@[simp] lemma zero_ne_add_one (x : V) : 0 â‰  x + 1 := ne_of_lt (by simp)

lemma graph_case {pr : V} :
    Graph pr â†”
    (âˆƒ v, pr = âŸªv, 0, fstIdx vâŸ«) âˆ¨ (âˆƒ v i x, pr = âŸªv, i + 1, xâŸ« âˆ§ Graph âŸªsndIdx v, i, xâŸ«) :=
  adjointruction.case

lemma graph_zero {v x : V} :
    Graph âŸªv, 0, xâŸ« â†” x = fstIdx v := by
  constructor
  Â· intro h
    rcases graph_case.mp h with (âŸ¨w, hâŸ© | âŸ¨w, i, x, h, _âŸ©)
    Â· rcases show v = w âˆ§ x = fstIdx w by simpa using h with âŸ¨rfl, rfl, rflâŸ©; rfl
    Â· simp at h
  Â· rintro rfl; exact graph_case.mpr <| Or.inl âŸ¨v, rflâŸ©

lemma graph_succ {v i x : V} :
    Graph âŸªv, i + 1, xâŸ« â†” Graph âŸªsndIdx v, i, xâŸ« := by
  constructor
  Â· intro h
    rcases graph_case.mp h with (âŸ¨w, hâŸ© | âŸ¨w, j, y, h, hwâŸ©)
    Â· simp at h
    Â· rcases show v = w âˆ§ i = j âˆ§ x = y by simpa using h with âŸ¨rfl, rfl, rflâŸ©; exact hw
  Â· intro h; exact graph_case.mpr <| Or.inr âŸ¨v, i, x, rfl, hâŸ©

lemma graph_exists (v i : V) : âˆƒ x, Graph âŸªv, i, xâŸ« := by
  suffices âˆ€ i' â‰¤ i, âˆ€ v' â‰¤ v, âˆƒ x, Graph âŸªv', i', xâŸ« from this i (by simp) v (by simp)
  intro i' hi'
  induction i' using ISigma1.sigma1_succ_induction
  Â· definability
  case zero =>
    intro v' _
    exact âŸ¨fstIdx v', graph_case.mpr <| Or.inl âŸ¨v', rflâŸ©âŸ©
  case succ i' ih =>
    intro v' hv'
    rcases ih (le_trans le_self_add hi') (sndIdx v') (le_trans (by simp) hv') with âŸ¨x, hxâŸ©
    exact âŸ¨x, graph_case.mpr <| Or.inr âŸ¨v', i', x, rfl, hxâŸ©âŸ©

lemma graph_unique {v i xâ‚ xâ‚‚ : V} : Graph âŸªv, i, xâ‚âŸ« â†’ Graph âŸªv, i, xâ‚‚âŸ« â†’ xâ‚ = xâ‚‚ := by
  induction i using ISigma1.pi1_succ_induction generalizing v xâ‚ xâ‚‚
  Â· definability
  case zero =>
    simp only [graph_zero]
    rintro rfl rfl; rfl
  case succ i ih =>
    simp only [graph_succ]
    exact ih

lemma graph_existsUnique (v i : V) : âˆƒ! x, Graph âŸªv, i, xâŸ« := by
  rcases graph_exists v i with âŸ¨x, hxâŸ©
  exact ExistsUnique.intro x hx (fun y hy â†¦ graph_unique hy hx)

end Nth

section nth

open Nth

noncomputable def nth (v i : V) : V := Classical.choose! (graph_existsUnique v i)

scoped notation:max v:max ".[" i "]" => nth v i

lemma nth_graph (v i : V) : Graph âŸªv, i, v.[i]âŸ« :=
  Classical.choose!_spec (graph_existsUnique v i)

lemma nth_eq_of_graph {v i x : V} (h : Graph âŸªv, i, xâŸ«) : nth v i = x := graph_unique (nth_graph v i) h

lemma nth_zero (v : V) : v.[0] = fstIdx v := nth_eq_of_graph (graph_zero.mpr rfl)

lemma nth_succ (v i : V) : v.[i + 1] = (sndIdx v).[i] := nth_eq_of_graph (graph_succ.mpr <| nth_graph _ _)

@[simp] lemma nth_adjoin_zero (x v : V) : (x âˆ· v).[0] = x := by
  simp [nth_zero]

@[simp] lemma nth_adjoin_succ (x v i : V) : (x âˆ· v).[i + 1] = v.[i] := by
  simp [nth_succ]

@[simp] lemma nth_adjoin_one (x v : V) : (x âˆ· v).[1] = v.[0] := by
  simpa using nth_adjoin_succ x v 0

@[simp] lemma nth_adjoin_two (x v : V) : (x âˆ· v).[2] = v.[1] := by
  simpa [-nth_adjoin_succ, one_add_one_eq_two] using nth_adjoin_succ x v 1

lemma adjoin_cases (x : V) : x = 0 âˆ¨ âˆƒ y v, x = y âˆ· v := by
  rcases zero_or_succ x with (rfl | âŸ¨z, rflâŸ©)
  Â· simp
  Â· right; exact âŸ¨Ï€â‚ z, Ï€â‚‚ z, by simp [adjoin]âŸ©

lemma adjoin_induction (Î“) {P : V â†’ Prop} (hP : Î“-[1]-Predicate P)
    (nil : P 0) (adjoin : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  ISigma1.order_induction Î“ hP (by
    intro v ih
    rcases nil_or_adjoin v with (rfl | âŸ¨x, v, rflâŸ©)
    Â· exact nil
    Â· exact adjoin _ _ (ih v (by simp)))

@[elab_as_elim]
lemma adjoin_ISigma1.sigma1_succ_induction {P : V â†’ Prop} (hP : ğšºâ‚-Predicate P)
    (nil : P 0) (adjoin : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  adjoin_induction ğšº hP nil adjoin

@[elab_as_elim]
lemma adjoin_ISigma1.pi1_succ_induction {P : V â†’ Prop} (hP : ğš·â‚-Predicate P)
    (nil : P 0) (adjoin : âˆ€ x v, P v â†’ P (x âˆ· v)) : âˆ€ v, P v :=
  adjoin_induction ğš· hP nil adjoin

section

def _root_.LO.FirstOrder.Arithmetic.nthDef : ğšºâ‚.Semisentence 3 :=
  .mkSigma â€œy v i. âˆƒ pr, !pairâ‚ƒDef pr v i y âˆ§ !graphDef prâ€

set_option linter.flexible false in
instance nth_defined : ğšºâ‚-Functionâ‚‚ (nth : V â†’ V â†’ V) via nthDef := .mk fun v â†¦ by
  simp [nthDef]
  constructor
  Â· intro h; simp [nth_eq_of_graph h]
  Â· intro h; rw [h]; exact nth_graph _ _

instance nth_definable : ğšºâ‚-Functionâ‚‚ (nth : V â†’ V â†’ V) := nth_defined.to_definable

instance nth_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚‚ (nth : V â†’ V â†’ V) := nth_definable.of_sigmaOne

end

lemma adjoin_absolute (a v : â„•) : ((a âˆ· v : â„•) : V) = (a : V) âˆ· (v : V) := by
  simpa using DefinedFunction.shigmaZero_absolute_func V adjoin_defined adjoin_defined ![a, v]

/-- TODO: move-/
lemma piâ‚_zero : Ï€â‚ (0 : V) = 0 := nonpos_iff_eq_zero.mp (piâ‚_le_self 0)

lemma piâ‚‚_zero : Ï€â‚‚ (0 : V) = 0 := nonpos_iff_eq_zero.mp (piâ‚‚_le_self 0)

@[simp] lemma nth_zero_idx (i : V) : (0).[i] = 0 := by
  induction i using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp [nth_zero, fstIdx, piâ‚_zero]
  case succ i ih => simp [nth_succ, sndIdx, piâ‚‚_zero, ih]

lemma nth_lt_of_pos {v} (hv : 0 < v) (i : V) : v.[i] < v := by
  induction i using ISigma1.pi1_succ_induction generalizing v
  Â· definability
  case zero =>
    rcases zero_or_succ v with (rfl | âŸ¨v, rflâŸ©)
    Â· simp at hv
    Â· simp [succ_eq_adjoin]
  case succ i ih =>
    rcases zero_or_succ v with (rfl | âŸ¨v, rflâŸ©)
    Â· simp at hv
    Â· simp only [succ_eq_adjoin v, nth_adjoin_succ]
      rcases eq_zero_or_pos (Ï€â‚‚ v) with (h | h)
      Â· simp [h]
      Â· exact lt_trans (ih h) (by simp)

@[simp] lemma nth_le (v i : V) : v.[i] â‰¤ v := by
  rcases eq_zero_or_pos v with (h | h)
  Â· simp [h]
  Â· exact le_of_lt <| nth_lt_of_pos h i

end nth


/-!

### Inductivly Construction of Function on List

-/

namespace VecRec

structure Blueprint (arity : â„•) where
  nil : ğšºâ‚.Semisentence (arity + 1)
  adjoin : ğšºâ‚.Semisentence (arity + 4)

namespace Blueprint

variable {arity : â„•} (Î² : Blueprint arity)

def blueprint : Fixpoint.Blueprint arity where
  core := .mkDelta
    (.mkSigma â€œpr C.
        (âˆƒ nil, !Î².nil nil â‹¯ âˆ§ !pairDef pr 0 nil) âˆ¨
        (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C,
          âˆƒ xxs, !adjoinDef xxs x xs âˆ§
          âˆƒ adjoin, !Î².adjoin adjoin x xs ih â‹¯ âˆ§
          !pairDef pr xxs adjoin âˆ§ :âŸªxs, ihâŸ«:âˆˆ C)â€
      (by simp))
    (.mkPi â€œpr C.
        (âˆ€ nil, !Î².nil nil â‹¯ â†’ !pairDef pr 0 nil) âˆ¨
        (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C,
          âˆ€ xxs, !adjoinDef xxs x xs â†’
          âˆ€ adjoin, !Î².adjoin adjoin x xs ih â‹¯ â†’
          !pairDef pr xxs adjoin âˆ§ :âŸªxs, ihâŸ«:âˆˆ C)â€
      (by simp))

def graphDef : ğšºâ‚.Semisentence (arity + 1) := Î².blueprint.fixpointDef

def resultDef : ğšºâ‚.Semisentence (arity + 2) :=
  .mkSigma â€œy xs. âˆƒ pr, !pairDef pr xs y âˆ§ !Î².graphDef pr â‹¯â€

end Blueprint

variable (V)

structure Construction {arity : â„•} (Î² : Blueprint arity) where
  nil (param : Fin arity â†’ V) : V
  adjoin (param : Fin arity â†’ V) (x xs ih) : V
  nil_defined : ğšºâ‚.DefinedFunction nil Î².nil
  adjoin_defined : ğšºâ‚.DefinedFunction (fun v â†¦ adjoin (v Â·.succ.succ.succ) (v 0) (v 1) (v 2)) Î².adjoin

variable {V}

namespace Construction

variable {arity : â„•} {Î² : Blueprint arity} (c : Construction V Î²)

def Phi (param : Fin arity â†’ V) (C : Set V) (pr : V) : Prop :=
  pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x xs ih, pr = âŸªx âˆ· xs, c.adjoin param x xs ihâŸ« âˆ§ âŸªxs, ihâŸ« âˆˆ C)

private lemma phi_iff (param : Fin arity â†’ V) (C pr : V) :
    c.Phi param {x | x âˆˆ C} pr â†”
    pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x < pr, âˆƒ xs < pr, âˆƒ ih < C, pr = âŸªx âˆ· xs, c.adjoin param x xs ihâŸ« âˆ§ âŸªxs, ihâŸ« âˆˆ C) := by
  constructor
  Â· rintro (h | âŸ¨x, xs, ih, rfl, hCâŸ©)
    Â· left; exact h
    Â· right
      exact âŸ¨x, lt_of_lt_of_le (by simp) (le_pair_left _ _),
        xs, lt_of_lt_of_le (by simp) (le_pair_left _ _), ih, lt_of_mem_rng hC, rfl , hCâŸ©
  Â· rintro (h | âŸ¨x, _, xs, _, ih, _, rfl, hCâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨x, xs, ih, rfl, hCâŸ©

def adjointruction : Fixpoint.Construction V Î².blueprint where
  Î¦ := c.Phi
  defined := .mk âŸ¨by
    intro v; simp [Blueprint.blueprint, c.nil_defined.iff, c.adjoin_defined.iff], by
    intro v
    symm
    simpa [Blueprint.blueprint, c.nil_defined.iff, c.adjoin_defined.iff] using c.phi_iff _ _ _âŸ©
  monotone := by
    rintro C C' hC _ x (h | âŸ¨v, i, hv, rfl, hâŸ©)
    Â· left; exact h
    Â· right; exact âŸ¨v, i, hv, rfl, hC hâŸ©

instance : c.adjointruction.Finite V where
  finite := by
    rintro C v x (h | âŸ¨x, xs, ih, rfl, hâŸ©)
    Â· exact âŸ¨0, Or.inl hâŸ©
    Â· exact âŸ¨âŸªxs, ihâŸ« + 1, Or.inr âŸ¨x, xs, ih, rfl, h, by simpâŸ©âŸ©

variable (param : Fin arity â†’ V)

def Graph : V â†’ Prop := c.adjointruction.Fixpoint param

section

lemma graph_defined : ğšºâ‚.Defined (fun v â†¦ c.Graph (v Â·.succ) (v 0)) Î².graphDef :=
  c.adjointruction.fixpoint_defined

instance graph_definable : ğšºâ‚.Definable (fun v â†¦ c.Graph (v Â·.succ) (v 0)) := c.graph_defined.to_definable

instance graph_definable' (param) : ğšºâ‚-Predicate (c.Graph param) := by
  simpa using HierarchySymbol.Definable.retractiont (n := 1) c.graph_definable (#0 :> fun i â†¦ &(param i))

instance graph_definable'' (param) : ğšº-[0 + 1]-Predicate (c.Graph param) := c.graph_definable' param

end

variable {param}

lemma graph_case {pr : V} :
    c.Graph param pr â†” pr = âŸª0, c.nil paramâŸ« âˆ¨ (âˆƒ x xs ih, pr = âŸªx âˆ· xs, c.adjoin param x xs ihâŸ« âˆ§ c.Graph param âŸªxs, ihâŸ«) :=
  c.adjointruction.case

lemma graph_nil {l : V} :
    c.Graph param âŸª0, lâŸ« â†” l = c.nil param := by
  constructor
  Â· intro h
    rcases c.graph_case.mp h with (h | âŸ¨x, xs, ih, h, _âŸ©)
    Â· rcases show l = c.nil param by simpa using h with âŸ¨rfl, rflâŸ©; rfl
    Â· simp at h
  Â· rintro rfl; exact c.graph_case.mpr <| Or.inl rfl

lemma graph_adjoin {x xs y : V} :
    c.Graph param âŸªx âˆ· xs, yâŸ« â†” âˆƒ y', y = c.adjoin param x xs y' âˆ§ c.Graph param âŸªxs, y'âŸ« := by
  constructor
  Â· intro h
    rcases c.graph_case.mp h with (h | âŸ¨z, zs, v, h, hgâŸ©)
    Â· simp at h
    Â· rcases show (x = z âˆ§ xs = zs) âˆ§ y = c.adjoin param z zs v by simpa using h with âŸ¨âŸ¨rfl, rflâŸ©, rflâŸ©
      exact âŸ¨v, rfl, hgâŸ©
  Â· rintro âŸ¨y, rfl, hâŸ©; exact c.graph_case.mpr <| Or.inr âŸ¨x, xs, y, rfl, hâŸ©

variable (param)

lemma graph_exists (xs : V) : âˆƒ y, c.Graph param âŸªxs, yâŸ« := by
  induction xs using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil =>
    exact âŸ¨c.nil param, c.graph_nil.mpr rflâŸ©
  case adjoin x xs ih =>
    Â· rcases ih with âŸ¨y, hyâŸ©
      exact âŸ¨c.adjoin param x xs y, c.graph_adjoin.mpr âŸ¨y, rfl, hyâŸ©âŸ©

variable {param}

lemma graph_unique {xs yâ‚ yâ‚‚ : V} : c.Graph param âŸªxs, yâ‚âŸ« â†’ c.Graph param âŸªxs, yâ‚‚âŸ« â†’ yâ‚ = yâ‚‚ := by
  induction xs using adjoin_ISigma1.pi1_succ_induction generalizing yâ‚ yâ‚‚
  Â· definability
  case nil =>
    simp only [graph_nil]; rintro rfl rfl; rfl
  case adjoin x v ih =>
    simp only [graph_adjoin, forall_exists_index, and_imp]
    rintro lâ‚ rfl hâ‚ lâ‚‚ rfl hâ‚‚
    rcases ih hâ‚ hâ‚‚; rfl

variable (param)

lemma graph_existsUnique (xs : V) : âˆƒ! y, c.Graph param âŸªxs, yâŸ« := by
  rcases c.graph_exists param xs with âŸ¨y, hyâŸ©
  exact ExistsUnique.intro y hy (fun y' hy' â†¦ c.graph_unique hy' hy)

noncomputable def result (xs : V) : V := Classical.choose! (c.graph_existsUnique param xs)

lemma result_graph (xs : V) : c.Graph param âŸªxs, c.result param xsâŸ« :=
  Classical.choose!_spec (c.graph_existsUnique param xs)

lemma result_eq_of_graph {xs y : V} (h : c.Graph param âŸªxs, yâŸ«) : c.result param xs = y :=
  c.graph_unique (c.result_graph param xs) h

@[simp] lemma result_nil : c.result param (0 : V) = c.nil param := c.result_eq_of_graph param (c.graph_nil.mpr rfl)

@[simp] lemma result_adjoin (x xs : V) :
    c.result param (x âˆ· xs) = c.adjoin param x xs (c.result param xs) :=
  c.result_eq_of_graph param (c.graph_adjoin.mpr âŸ¨_, rfl, c.result_graph param xsâŸ©)

section

set_option linter.flexible false in
lemma result_defined : ğšºâ‚.DefinedFunction (fun v â†¦ c.result (v Â·.succ) (v 0)) Î².resultDef := .mk fun v â†¦ by
  simp [Blueprint.resultDef, c.graph_defined.iff]
  constructor
  Â· intro h; symm; simpa using c.result_eq_of_graph _ h
  Â· intro h; rw [h]; exact c.result_graph _ _

@[simp] lemma eval_resultDef (v) :
    Semiformula.Evalbm V v Î².resultDef.val â†” v 0 = c.result (v Â·.succ.succ) (v 1) := c.result_defined.iff

instance result_definable : ğšºâ‚.DefinableFunction (fun v â†¦ c.result (v Â·.succ) (v 0)) :=
  c.result_defined.to_definable

instance result_definable' (Î“ m) :
    Î“-[m + 1].DefinableFunction (fun v â†¦ c.result (v Â·.succ) (v 0)) := c.result_definable.of_sigmaOne

end

end Construction

end VecRec

/-!

### Length of List

-/

namespace Len

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy. y = 0â€
  adjoin := .mkSigma â€œy x xs ih. y = ih + 1â€

def adjointruction : VecRec.Construction V blueprint where
  nil _ := 0
  adjoin _ _ _ ih := ih + 1
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint]

end Len

section len

open Len

noncomputable def len (v : V) : V := adjointruction.result ![] v

@[simp] lemma len_nil : len (0 : V) = 0 := by simp [len, adjointruction]

@[simp] lemma len_adjoin (x v : V) : len (x âˆ· v) = len v + 1 := by simp [len, adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.lenDef : ğšºâ‚.Semisentence 2 := blueprint.resultDef

instance len_defined : ğšºâ‚-Functionâ‚ (len : V â†’ V) via lenDef := adjointruction.result_defined

instance len_definable : ğšºâ‚-Functionâ‚ (len : V â†’ V) := len_defined.to_definable

instance len_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚ (len : V â†’ V) := len_definable.of_sigmaOne

end

@[simp] lemma len_zero_iff_eq_nil {v : V} : len v = 0 â†” v = 0 := by
  rcases nil_or_adjoin v with (rfl | âŸ¨x, v, rflâŸ©) <;> simp

lemma nth_lt_len {v i : V} (hl : len v â‰¤ i) : v.[i] = 0 := by
  induction v using adjoin_ISigma1.pi1_succ_induction generalizing i
  Â· definability
  case nil => simp
  case adjoin x v ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp at hl
    simpa using ih (by simpa using hl)

@[simp] lemma len_le (v : V) : len v â‰¤ v := by
  induction v using adjoin_ISigma1.pi1_succ_induction
  Â· definability
  case nil => simp
  case adjoin x v ih =>
    simp only [len_adjoin]
    simp only [adjoin, add_le_add_iff_right]
    exact le_trans ih (le_pair_right x v)

end len

lemma nth_ext {vâ‚ vâ‚‚ : V} (hl : len vâ‚ = len vâ‚‚) (H : âˆ€ i < len vâ‚, vâ‚.[i] = vâ‚‚.[i]) : vâ‚ = vâ‚‚ := by
  induction vâ‚ using adjoin_ISigma1.pi1_succ_induction generalizing vâ‚‚
  Â· definability
  case nil =>
    exact Eq.symm <| len_zero_iff_eq_nil.mp (by simp [â†hl])
  case adjoin xâ‚ vâ‚ ih =>
    rcases nil_or_adjoin vâ‚‚ with (rfl | âŸ¨xâ‚‚, vâ‚‚, rflâŸ©)
    Â· simp at hl
    have hx : xâ‚ = xâ‚‚ := by simpa using H 0 (by simp)
    have hv : vâ‚ = vâ‚‚ := ih (by simpa using hl) (by intro i hi; simpa using H (i + 1) (by simpa using hi))
    simp [hx, hv]

lemma nth_ext' (l : V) {vâ‚ vâ‚‚ : V} (hlâ‚ : len vâ‚ = l) (hlâ‚‚ : len vâ‚‚ = l) (H : âˆ€ i < l, vâ‚.[i] = vâ‚‚.[i]) : vâ‚ = vâ‚‚ := by
  rcases hlâ‚‚; exact nth_ext hlâ‚ (by simpa [hlâ‚] using H)

lemma le_of_nth_le_nth {vâ‚ vâ‚‚ : V} (hl : len vâ‚ = len vâ‚‚) (H : âˆ€ i < len vâ‚, vâ‚.[i] â‰¤ vâ‚‚.[i]) : vâ‚ â‰¤ vâ‚‚ := by
  induction vâ‚ using adjoin_ISigma1.pi1_succ_induction generalizing vâ‚‚
  Â· definability
  case nil => simp
  case adjoin xâ‚ vâ‚ ih =>
    rcases nil_or_adjoin vâ‚‚ with (rfl | âŸ¨xâ‚‚, vâ‚‚, rflâŸ©)
    Â· simp at hl
    have hx : xâ‚ â‰¤ xâ‚‚ := by simpa using H 0 (by simp)
    have hv : vâ‚ â‰¤ vâ‚‚ := ih (by simpa using hl) (by intro i hi; simpa using H (i + 1) (by simpa using hi))
    exact adjoin_le_adjoin hx hv

lemma nth_lt_self {v i : V} (hi : i < len v) : v.[i] < v := by
  induction v using adjoin_ISigma1.pi1_succ_induction generalizing i
  Â· definability
  case nil => simp at hi
  case adjoin x v ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simpa using lt_trans (ih (by simpa using hi)) (by simp)

theorem sigmaOne_skolem_vec {R : V â†’ V â†’ Prop} (hP : ğšºâ‚-Relation R) {l}
    (H : âˆ€ x < l, âˆƒ y, R x y) : âˆƒ v, len v = l âˆ§ âˆ€ i < l, R i v.[i] := by
  have : âˆ€ k â‰¤ l, âˆƒ v, len v = k âˆ§ âˆ€ i < k, R (l - k + i) v.[i] := by
    intro k hk
    induction k using ISigma1.sigma1_succ_induction
    Â· definability
    case zero => exact âŸ¨0, by simpâŸ©
    case succ k ih =>
      rcases ih (le_trans (by simp) hk) with âŸ¨v, hvk, hvâŸ©
      have : âˆƒ y, R (l - (k + 1)) y := H (l - (k + 1)) (by simp [tsub_lt_iff_left hk])
      rcases this with âŸ¨y, hyâŸ©
      exact âŸ¨y âˆ· v, by simp [hvk], fun i hi â†¦ by
        rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
        Â· simpa using hy
        Â· simpa [sub_succ_add_succ (succ_le_iff_lt.mp hk) i] using hv i (by simpa using hi)âŸ©
  simpa using this l (by rfl)

lemma eq_singleton_iff_len_eq_one {v : V} : len v = 1 â†” âˆƒ x, v = ?[x] := by
  constructor
  Â· intro h; exact âŸ¨v.[0], nth_ext (by simp [h]) (by simp [h])âŸ©
  Â· rintro âŸ¨x, rflâŸ©; simp

lemma eq_doubleton_of_len_eq_two {v : V} : len v = 2 â†” âˆƒ x y, v = ?[x, y] := by
  constructor
  Â· intro h; exact âŸ¨v.[0], v.[1],
      nth_ext (by simp [h, one_add_one_eq_two]) (by simp [lt_two_iff_le_one, le_one_iff_eq_zero_or_one, h])âŸ©
  Â· rintro âŸ¨x, y, rflâŸ©; simp [one_add_one_eq_two]


/-!

### Maximum of List

-/

namespace ListMax

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy. y = 0â€
  adjoin := .mkSigma â€œy x xs ih. !FirstOrder.Arithmetic.max y x ihâ€

noncomputable def adjointruction : VecRec.Construction V blueprint where
  nil _ := 0
  adjoin _ x _ ih := max x ih
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint]

end ListMax

section listMax

open ListMax

noncomputable def listMax (v : V) : V := adjointruction.result ![] v

@[simp] lemma listMax_nil : listMax (0 : V) = 0 := by simp [listMax, adjointruction]

@[simp] lemma listMax_adjoin (x v : V) : listMax (x âˆ· v) = max x (listMax v) := by simp [listMax, adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.listMaxDef : ğšºâ‚.Semisentence 2 := blueprint.resultDef

instance listMax_defined : ğšºâ‚-Functionâ‚ (listMax : V â†’ V) via listMaxDef := adjointruction.result_defined

instance listMax_definable : ğšºâ‚-Functionâ‚ (listMax : V â†’ V) := listMax_defined.to_definable

instance listMax_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚ (listMax : V â†’ V) := listMax_definable.of_sigmaOne

end

lemma nth_le_listMax {i v : V} (h : i < len v) : v.[i] â‰¤ listMax v := by
  induction v using adjoin_ISigma1.pi1_succ_induction generalizing i
  Â· definability
  case nil => simp
  case adjoin x v ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simp [ih (by simpa using h)]

lemma listMaxss_le {v z : V} (h : âˆ€ i < len v, v.[i] â‰¤ z) : listMax v â‰¤ z := by
  induction v using adjoin_ISigma1.pi1_succ_induction
  Â· definability
  case nil => simp
  case adjoin x v ih =>
    simp only [listMax_adjoin, max_le_iff]
    constructor
    Â· simpa using h 0 (by simp)
    Â· exact ih (fun i hi â†¦ by simpa using h (i + 1) (by simp [hi]))

lemma listMaxss_le_iff {v z : V} : listMax v â‰¤ z â†” âˆ€ i < len v, v.[i] â‰¤ z := by
  constructor
  Â· intro h i hi; exact le_trans (nth_le_listMax hi) h
  Â· exact listMaxss_le

/-
lemma nth_le_listMaxs (v : V) (hv : v â‰  0) : âˆƒ i < len v, v.[i] = listMax v := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp at hv
  case adjoin x v ih =>
    simp
-/

end listMax

/-!

### Take Last k-Element

-/

namespace TakeLast

def blueprint : VecRec.Blueprint 1 where
  nil := .mkSigma â€œy k. y = 0â€
  adjoin := .mkSigma â€œy x xs ih k.
    âˆƒ l, !lenDef l xs âˆ§
    (l < k â†’ !adjoinDef y x xs) âˆ§ (k â‰¤ l â†’ y = ih)â€

noncomputable def adjointruction : VecRec.Construction V blueprint where
  nil _ := 0
  adjoin (param x xs ih) := if len xs < param 0 then x âˆ· xs else ih
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by
    suffices
      (len (v 2) < v 4 â†’ v 0 = v 1 âˆ· v 2) âˆ§ (v 4 â‰¤ len (v 2) â†’ v 0 = v 3) â†”
      (v 0 = if len (v 2) < v 4 then v 1 âˆ· v 2 else v 3) by
      simpa [blueprint, Fin.isValue]
    rcases lt_or_ge (len (v 2)) (v 4) with (hv | hv)
    Â· simp [hv]
    Â· simp [hv, not_lt_of_ge hv]

end TakeLast

section takeLast

open TakeLast

noncomputable def takeLast (v k : V) : V := adjointruction.result ![k] v

@[simp] lemma takeLast_nil : takeLast (0 : V) k = 0 := by simp [takeLast, adjointruction]

lemma takeLast_adjoin (x v : V) :
    takeLast (x âˆ· v) k = if len v < k then x âˆ· v else takeLast v k := by simp [takeLast, adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.takeLastDef : ğšºâ‚.Semisentence 3 := blueprint.resultDef

instance takeLast_defined : ğšºâ‚-Functionâ‚‚ (takeLast : V â†’ V â†’ V) via takeLastDef := adjointruction.result_defined

instance takeLast_definable : ğšºâ‚-Functionâ‚‚ (takeLast : V â†’ V â†’ V) := takeLast_defined.to_definable

instance takeLast_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚‚ (takeLast : V â†’ V â†’ V) := takeLast_definable.of_sigmaOne

end

lemma len_takeLast {v k : V} (h : k â‰¤ len v) : len (takeLast v k) = k := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp_all
  case adjoin x v ih =>
    have : k = len v + 1 âˆ¨ k â‰¤ len v := by
      rcases eq_or_lt_of_le h with (h | h)
      Â· left; simpa using h
      Â· right; simpa [lt_succ_iff_le] using h
    rcases this with (rfl | hkv)
    Â· simp [takeLast_adjoin]
    Â· simp [takeLast_adjoin, not_lt_of_ge hkv, ih hkv]

@[simp] lemma takeLast_len_self (v : V) : takeLast v (len v) = v := by
  rcases nil_or_adjoin v with (rfl | âŸ¨x, v, rflâŸ©) <;> simp [takeLast_adjoin]

/-- TODO: move -/
@[simp] lemma add_sub_add (a b c : V) : (a + c) - (b + c) = a - b := add_tsub_add_eq_tsub_right a c b

@[simp] lemma takeLast_zero (v : V) : takeLast v 0 = 0 := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin x v ih => simp [takeLast_adjoin, ih]

lemma takeLast_succ_of_lt {i v : V} (h : i < len v) : takeLast v (i + 1) = v.[len v - (i + 1)] âˆ· takeLast v i := by
  induction v using adjoin_ISigma1.sigma1_succ_induction generalizing i
  Â· definability
  case nil => simp at h
  case adjoin x v ih =>
    rcases show i = len v âˆ¨ i < len v from eq_or_lt_of_le (by simpa [lt_succ_iff_le] using h) with (rfl | hi)
    Â· simp [takeLast_adjoin]
    Â· have : len v - i = len v - (i + 1) + 1 := by
        rw [â†PeanoMinus.sub_sub, sub_add_self_of_le (pos_iff_one_le.mp (tsub_pos_of_lt hi))]
      simpa [takeLast_adjoin, lt_succ_iff_le, not_le_of_gt hi, this, not_lt_of_gt hi] using ih hi

end takeLast

/-!

### Concatation

-/

namespace Concat

def blueprint : VecRec.Blueprint 1 where
  nil := .mkSigma â€œy z. !adjoinDef y z 0â€
  adjoin := .mkSigma â€œy x xs ih z. !adjoinDef y x ihâ€

noncomputable def adjointruction : VecRec.Construction V blueprint where
  nil param := ?[param 0]
  adjoin (_ x _ ih) := x âˆ· ih
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint, Fin.isValue]

end Concat

section concat

open Concat

noncomputable def concat (v z : V) : V := adjointruction.result ![z] v

@[simp] lemma concat_nil (z : V) : concat 0 z = ?[z] := by simp [concat, adjointruction]

@[simp] lemma concat_adjoin (x v z : V) : concat (x âˆ· v) z = x âˆ· concat v z := by simp [concat, adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.concatDef : ğšºâ‚.Semisentence 3 := blueprint.resultDef

instance concat_defined : ğšºâ‚-Functionâ‚‚ (concat : V â†’ V â†’ V) via concatDef := adjointruction.result_defined

instance concat_definable : ğšºâ‚-Functionâ‚‚ (concat : V â†’ V â†’ V) := concat_defined.to_definable

instance concat_definable' (Î“ m) : Î“-[m + 1]-Functionâ‚‚ (concat : V â†’ V â†’ V) := concat_definable.of_sigmaOne

end

@[simp] lemma len_concat (v z : V) : len (concat v z) = len v + 1 := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin x v ih => simp [ih]

lemma concat_nth_lt (v z : V) {i} (hi : i < len v) : (concat v z).[i] = v.[i] := by
  induction v using adjoin_ISigma1.sigma1_succ_induction generalizing i
  Â· definability
  case nil => simp at hi
  case adjoin x v ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simp [ih (by simpa using hi)]

@[simp] lemma concat_nth_len (v z : V) : (concat v z).[len v] = z := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin x v ih => simp [ih]

lemma concat_nth_len' (v z : V) {i} (hi : len v = i) : (concat v z).[i] = z := by
  rcases hi; simp

end concat

/-!

### Membership

-/

section vec_membership

def MemVec (x v : V) : Prop := âˆƒ i < len v, x = v.[i]

scoped infix:40 " âˆˆáµ¥ " => MemVec

@[simp] lemma not_memVec_empty (x : V) : Â¬x âˆˆáµ¥ 0 := by rintro âŸ¨i, h, _âŸ©; simp at h

lemma nth_mem_memVec {i v : V} (h : i < len v) : v.[i] âˆˆáµ¥ v := âŸ¨i, by simp [h]âŸ©

@[simp] lemma memVec_insert_fst {x v : V} : x âˆˆáµ¥ x âˆ· v := âŸ¨0, by simpâŸ©

@[simp] lemma memVec_adjoin_iff {x y v : V} : x âˆˆáµ¥ y âˆ· v â†” x = y âˆ¨ x âˆˆáµ¥ v := by
  constructor
  Â· rintro âŸ¨i, h, rflâŸ©
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· right; simpa using nth_mem_memVec (by simpa using h)
  Â· rintro (rfl | hx)
    Â· simp
    Â· rcases hx with âŸ¨i, hi, rflâŸ©
      exact âŸ¨i + 1, by simp [hi]âŸ©

lemma le_of_memVec {x v : V} (h : x âˆˆáµ¥ v) : x â‰¤ v := by
  rcases h with âŸ¨i, _, rflâŸ©; simp

section

def _root_.LO.FirstOrder.Arithmetic.memVecDef : ğš«â‚.Semisentence 2 := .mkDelta
  (.mkSigma â€œx v. âˆƒ l, !lenDef l v âˆ§ âˆƒ i < l, !nthDef x v iâ€)
  (.mkPi â€œx v. âˆ€ l, !lenDef l v â†’ âˆƒ i < l, âˆ€ vi, !nthDef vi v i â†’ x = viâ€)

instance memVec_defined : ğš«â‚-Relation (MemVec : V â†’ V â†’ Prop) via memVecDef :=
  âŸ¨by intro v; simp [memVecDef], by intro v; simp [memVecDef, MemVec]âŸ©

instance memVec_definable : ğš«â‚-Relation (MemVec : V â†’ V â†’ Prop) := memVec_defined.to_definable

instance memVec_definable' (Î“ m) : Î“-[m + 1]-Relation (MemVec : V â†’ V â†’ Prop) := memVec_definable.of_deltaOne

end

end vec_membership

/-!

### Subset

-/

section vec_subset

def SubsetVec (v w : V) : Prop := âˆ€ x, x âˆˆáµ¥ v â†’ x âˆˆáµ¥ w

scoped infix:30 " âŠ†áµ¥ " => SubsetVec

@[simp, refl] lemma SubsetVec.refl (v : V) : v âŠ†áµ¥ v := fun _ hx â†¦ hx

@[simp] lemma subsetVec_insert_tail (x v : V) : v âŠ†áµ¥ x âˆ· v := by intro y hy; simp [hy]

section

def _root_.LO.FirstOrder.Arithmetic.subsetVecDef : ğš«â‚.Semisentence 2 := .mkDelta
  (.mkSigma â€œv w. âˆ€ x <âº v, !memVecDef.pi x v â†’ !memVecDef.sigma x wâ€)
  (.mkPi â€œv w. âˆ€ x <âº v, !memVecDef.sigma x v â†’ !memVecDef.pi x wâ€)

set_option linter.flexible false in
instance subsetVec_defined : ğš«â‚-Relation (SubsetVec : V â†’ V â†’ Prop) via subsetVecDef :=
  âŸ¨by intro v; simp [subsetVecDef, HierarchySymbol.Semiformula.val_sigma, memVec_defined.proper.iff'],
   by intro v
      simp [subsetVecDef, HierarchySymbol.Semiformula.val_sigma, memVec_defined.proper.iff']
      constructor
      Â· intro h x hx; exact h x (le_of_memVec hx) hx
      Â· intro h x _; exact h xâŸ©

instance subsetVec_definable : ğš«â‚-Relation (SubsetVec : V â†’ V â†’ Prop) := subsetVec_defined.to_definable

instance subsetVec_definable' (Î“ m) : Î“-[m + 1]-Relation (SubsetVec : V â†’ V â†’ Prop) := subsetVec_definable.of_deltaOne

end

end vec_subset

/-!

### Repeat

-/

section repaetVec

def repeatVec.blueprint : PR.Blueprint 1 where
  zero := .mkSigma â€œy x. y = 0â€
  succ := .mkSigma â€œy ih n x. !adjoinDef y x ihâ€

noncomputable def repeatVec.adjointruction : PR.Construction V repeatVec.blueprint where
  zero := fun _ â†¦ 0
  succ := fun x _ ih â†¦ x 0 âˆ· ih
  zero_defined := .mk fun v â†¦ by simp [blueprint]
  succ_defined := .mk fun v â†¦ by simp [blueprint]

/-- `repeatVec x k = x âˆ· x âˆ· x âˆ· ... k times ... âˆ· 0`-/
noncomputable def repeatVec (x k : V) : V := repeatVec.adjointruction.result ![x] k

@[simp] lemma repeatVec_zero (x : V) : repeatVec x 0 = 0 := by simp [repeatVec, repeatVec.adjointruction]

@[simp] lemma repeatVec_succ (x k : V) : repeatVec x (k + 1) = x âˆ· repeatVec x k := by simp [repeatVec, repeatVec.adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.repeatVecDef : ğšºâ‚.Semisentence 3 := repeatVec.blueprint.resultDef |>.rew (Rew.subst ![#0, #2, #1])

instance repeatVec_defined : ğšºâ‚-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) via repeatVecDef := .mk
  fun v â†¦ by simp [repeatVec.adjointruction.result_defined_iff, repeatVecDef]; rfl

instance repeatVec_definable : ğšºâ‚-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) := repeatVec_defined.to_definable

instance repeatVec_definable' (Î“) : Î“-[m + 1]-Functionâ‚‚ (repeatVec : V â†’ V â†’ V) := repeatVec_definable.of_sigmaOne

end

@[simp] lemma len_repeatVec (x k : V) : len (repeatVec x k) = k := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih => simp [ih]

@[simp] lemma le_repaetVec (x k : V) : k â‰¤ repeatVec x k := by
  simpa using len_le (repeatVec x k)

lemma nth_repeatVec (x k : V) {i} (h : i < k) : (repeatVec x k).[i] = x := by
  induction k using ISigma1.sigma1_succ_induction generalizing i
  Â· definability
  case zero => simp at h
  case succ k ih =>
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simpa using ih (by simpa using h)

lemma len_repeatVec_of_nth_le {v m : V} (H : âˆ€ i < len v, v.[i] â‰¤ m) : v â‰¤ repeatVec m (len v) :=
  le_of_nth_le_nth (by simp) (fun i hi â†¦ by simp [nth_repeatVec m (len v) hi, H i hi])

end repaetVec

/-!

### Convert to Set

-/

namespace VecToSet

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy. y = 0â€
  adjoin := .mkSigma â€œy x xs ih. !insertDef y x ihâ€

noncomputable def adjointruction : VecRec.Construction V blueprint where
  nil _ := âˆ…
  adjoin (_ x _ ih) := insert x ih
  nil_defined := .mk fun v â†¦ by simp [blueprint, emptyset_def]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint]

end VecToSet

section vecToSet

open VecToSet

noncomputable def vecToSet (v : V) : V := adjointruction.result ![] v

@[simp] lemma vecToSet_nil : vecToSet (0 : V) = âˆ… := by simp [vecToSet, adjointruction]

@[simp] lemma vecToSet_adjoin (x v : V) :
    vecToSet (x âˆ· v) = insert x (vecToSet v) := by simp [vecToSet, adjointruction]

section

def _root_.LO.FirstOrder.Arithmetic.vecToSetDef : ğšºâ‚.Semisentence 2 := blueprint.resultDef

instance vecToSet_defined : ğšºâ‚-Functionâ‚ (vecToSet : V â†’ V) via vecToSetDef := adjointruction.result_defined

instance vecToSet_definable : ğšºâ‚-Functionâ‚ (vecToSet : V â†’ V) := vecToSet_defined.to_definable

instance vecToSet_definable' (Î“) : Î“-[m + 1]-Functionâ‚ (vecToSet : V â†’ V) := vecToSet_definable.of_sigmaOne

end

lemma mem_vecToSet_iff {v x : V} : x âˆˆ vecToSet v â†” âˆƒ i < len v, x = v.[i] := by
  induction v using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin y v ih =>
    simp only [vecToSet_adjoin, mem_bitInsert_iff, ih, len_adjoin]
    constructor
    Â· rintro (rfl | âŸ¨i, hi, rflâŸ©)
      Â· exact âŸ¨0, by simpâŸ©
      Â· exact âŸ¨i + 1, by simp [hi]âŸ©
    Â· rintro âŸ¨i, hi, rflâŸ©
      rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
      Â· simp
      Â· right; exact âŸ¨i, by simpa using hi, by simpâŸ©

@[simp] lemma nth_mem_vecToSet {v i : V} (h : i < len v) : v.[i] âˆˆ vecToSet v :=
  mem_vecToSet_iff.mpr âŸ¨i, h, rflâŸ©

end vecToSet

end LO.ISigma1
