import Foundation.FirstOrder.Arithmetic.Definability
import Foundation.FirstOrder.PeanoMinus.Functions
import Foundation.FirstOrder.TrueArithmetic.Basic

/-!
# Induction schemata of Arithmetic

-/

namespace LO

open FirstOrder

variable {L : Language} [L.ORing] {Î¾ : Type*} [DecidableEq Î¾]

namespace FirstOrder

def succInd {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ!Ï† 0 â†’ (âˆ€ x, !Ï† x â†’ !Ï† (x + 1)) â†’ âˆ€ x, !Ï† xâ€

def orderInd {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ(âˆ€ x, (âˆ€ y < x, !Ï† y) â†’ !Ï† x) â†’ âˆ€ x, !Ï† xâ€

def leastNumber {Î¾} (Ï† : Semiformula L Î¾ 1) : Formula L Î¾ := â€œ(âˆƒ x, !Ï† x) â†’ âˆƒ z, !Ï† z âˆ§ âˆ€ x < z, Â¬!Ï† xâ€

end FirstOrder

variable (L)

def InductionScheme (Î“ : Semiformula L â„• 1 â†’ Prop) : Theory L :=
  { Ïˆ | âˆƒ Ï† : Semiformula L â„• 1, Î“ Ï† âˆ§ Ïˆ = succInd Ï† }

abbrev IOpen : ArithmeticTheory := ğğ€â» + InductionScheme â„’â‚’áµ£ Semiformula.Open

notation "ğˆOpen" => IOpen

abbrev InductionOnHierarchy (Î“ : Polarity) (k : â„•) : ArithmeticTheory := ğğ€â» + InductionScheme â„’â‚’áµ£ (Arithmetic.Hierarchy Î“ k)

prefix:max "ğˆğğƒ " => InductionOnHierarchy

abbrev ISigma (k : â„•) : ArithmeticTheory := ğˆğğƒ ğšº k

prefix:max "ğˆğšº" => ISigma

notation "ğˆğšºâ‚€" => ISigma 0

abbrev IPi (k : â„•) : ArithmeticTheory := ğˆğğƒ ğš· k

prefix:max "ğˆğš·" => IPi

notation "ğˆğš·â‚€" => IPi 0

notation "ğˆğšºâ‚" => ISigma 1

notation "ğˆğš·â‚" => IPi 1

abbrev Peano : ArithmeticTheory := ğğ€â» + InductionScheme â„’â‚’áµ£ Set.univ

notation "ğğ€" => Peano

variable {L}

variable {C C' : Semiformula â„’â‚’áµ£ â„• 1 â†’ Prop}

lemma InductionScheme_subset (h : âˆ€ {Ï† : Semiformula â„’â‚’áµ£ â„• 1},  C Ï† â†’ C' Ï†) : InductionScheme â„’â‚’áµ£ C âŠ† InductionScheme â„’â‚’áµ£ C' := by
  intro _; simp only [InductionScheme, Set.mem_setOf_eq, forall_exists_index, and_imp]; rintro Ï† hp rfl; exact âŸ¨Ï†, h hp, rflâŸ©

lemma ISigma_subset_mono {sâ‚ sâ‚‚} (h : sâ‚ â‰¤ sâ‚‚) : ğˆğšº sâ‚ âŠ† ğˆğšº sâ‚‚ :=
  Set.union_subset_union_right _ (InductionScheme_subset (fun H â†¦ H.mono h))

lemma ISigma_weakerThan_of_le {sâ‚ sâ‚‚} (h : sâ‚ â‰¤ sâ‚‚) : ğˆğšº sâ‚ âª¯ ğˆğšº sâ‚‚ :=
  Entailment.WeakerThan.ofSubset (ISigma_subset_mono h)

instance : ğğ€â» âª¯ ğˆğğƒ Î“ n := Entailment.WeakerThan.ofSubset (by simp [InductionOnHierarchy, Theory.add_def])

instance : ğ„ğ âª¯ ğˆğğƒ Î“ n := Entailment.WeakerThan.trans (inferInstanceAs (ğ„ğ âª¯ ğğ€â»)) inferInstance

instance : ğ„ğ âª¯ ğˆOpen := Entailment.WeakerThan.trans (inferInstanceAs (ğ„ğ âª¯ ğğ€â»)) inferInstance

instance : ğˆOpen âª¯ ğˆğğƒ Î“ n :=
  Entailment.WeakerThan.ofSubset <| Set.union_subset_union_right _  <| InductionScheme_subset Arithmetic.Hierarchy.of_open

instance : ğˆğšºâ‚€ âª¯ ğˆğšºâ‚ :=
  ISigma_weakerThan_of_le (by decide)

instance : ğˆğšºi âª¯ ğğ€ :=
  Entailment.WeakerThan.ofSubset <| Set.union_subset_union_right _  <| InductionScheme_subset (by intros; trivial)

lemma mem_InductionScheme_of_mem {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : C Ï†) :
    succInd Ï† âˆˆ InductionScheme â„’â‚’áµ£ C := by
  simpa [InductionScheme] using âŸ¨Ï†, hp, rflâŸ©

lemma mem_IOpen_of_qfree {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : Ï†.Open) :
    succInd Ï† âˆˆ InductionScheme â„’â‚’áµ£ Semiformula.Open := by
  exact âŸ¨Ï†, hp, rflâŸ©

instance : ğğ€â» âª¯ ğˆOpen := inferInstance

instance : ğˆOpen âª¯ ğˆğšºâ‚€ := inferInstance

instance : ğˆğšºâ‚ âª¯ ğğ€ := inferInstance

end LO

namespace LO

open FirstOrder Arithmetic PeanoMinus

variable {V : Type*} [ORingStruc V]

namespace InductionScheme

variable {C : Semiformula â„’â‚’áµ£ â„• 1 â†’ Prop} [V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ C]

private lemma induction_eval {Ï† : Semiformula â„’â‚’áµ£ â„• 1} (hp : C Ï†) (v) :
    Semiformula.Evalm V ![0] v Ï† â†’
    (âˆ€ x, Semiformula.Evalm V ![x] v Ï† â†’ Semiformula.Evalm V ![x + 1] v Ï†) â†’
    âˆ€ x, Semiformula.Evalm V ![x] v Ï† := by
  have : V âŠ§â‚˜ succInd Ï† :=
    ModelsTheory.models (T := InductionScheme _ C) V (by simpa using mem_InductionScheme_of_mem hp)
  revert v
  simpa [models_iff, succInd, Semiformula.eval_substs, Semiformula.eval_rew_q Rew.toS, Function.comp, Matrix.constant_eq_singleton] using this

@[elab_as_elim]
lemma succ_induction {P : V â†’ Prop}
    (hP : âˆƒ e : â„• â†’ V, âˆƒ Ï† : Semiformula â„’â‚’áµ£ â„• 1, C Ï† âˆ§ âˆ€ x, P x â†” Semiformula.Evalm V ![x] e Ï†) :
    P 0 â†’ (âˆ€ x, P x â†’ P (x + 1)) â†’ âˆ€ x, P x := by
  rcases hP with âŸ¨e, Ï†, Cp, hpâŸ©; simpa [â†hp] using induction_eval (V := V) Cp e

end InductionScheme

namespace InductionOnHierarchy

section

variable (Î“ : Polarity) (m : â„•) [V âŠ§â‚˜* ğˆğğƒ Î“ m]

instance : V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ (Hierarchy Î“ m) := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)

lemma succ_induction {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  haveI : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)
  InductionScheme.succ_induction (P := P) (C := Hierarchy Î“ m) (by
    rcases hP with âŸ¨Ï†, hpâŸ©
    haveI : Inhabited V := Classical.inhabited_of_nonempty'
    exact âŸ¨Ï†.val.enumarateFVar, (Rew.rewriteMap Ï†.val.idxOfFVar) â–¹ Ï†.val, by simp,
      by  intro x; simp [Semiformula.eval_rewriteMap]
          have : (Semiformula.Evalm V ![x] fun x â†¦ Ï†.val.enumarateFVar (Ï†.val.idxOfFVar x)) Ï†.val â†” (Semiformula.Evalm V ![x] id) Ï†.val :=
            Semiformula.eval_iff_of_funEqOn _ (by
              intro x hx; simp [Semiformula.enumarateFVar_idxOfFVar (Semiformula.mem_fvarList_iff_fvar?.mpr hx)])
          simp [this, hp.df.iff]âŸ©)
    zero succ

lemma order_induction {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x := by
  haveI : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)
  suffices âˆ€ x, âˆ€ y < x, P y by
    intro x; exact this (x + 1) x (by simp only [lt_add_iff_pos_right, lt_one_iff_eq_zero])
  intro x; induction x using succ_induction
  Â· exact Î“
  Â· exact m
  Â· suffices Î“-[m].BoldfacePred fun x â†¦ âˆ€ y < x, P y by exact this
    exact HierarchySymbol.Boldface.ball_blt (by simp) (hP.retraction ![0])
  case zero => simp
  case succ x IH =>
    intro y hxy
    rcases show y < x âˆ¨ y = x from lt_or_eq_of_le (le_iff_lt_succ.mpr hxy) with (lt | rfl)
    Â· exact IH y lt
    Â· exact ind y IH
  case inst => infer_instance

private lemma neg_succ_induction {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (nzero : Â¬P 0) (nsucc : âˆ€ x, Â¬P x â†’ Â¬P (x + 1)) : âˆ€ x, Â¬P x := by
  haveI : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)
  by_contra A
  have : âˆƒ x, P x := by simpa using A
  rcases this with âŸ¨a, haâŸ©
  have : âˆ€ x â‰¤ a, P (a - x) := by
    intro x; induction x using succ_induction
    Â· exact Î“
    Â· exact m
    Â· suffices Î“-[m].BoldfacePred fun x â†¦ x â‰¤ a â†’ P (a - x) by exact this
      apply HierarchySymbol.Boldface.imp
      Â· apply HierarchySymbol.Boldface.bcompâ‚‚ (by definability) (by definability)
      Â· apply HierarchySymbol.Boldface.bcompâ‚ (by definability)
    case zero =>
      intro _; simpa using ha
    case succ x IH =>
      intro hx
      have : P (a - x) := IH (le_of_add_le_left hx)
      exact (not_imp_not.mp <| nsucc (a - (x + 1))) (by
        rw [â†PeanoMinus.sub_sub, sub_add_self_of_le]
        Â· exact this
        Â· exact le_tsub_of_add_le_left hx)
    case inst => infer_instance
  have : P 0 := by simpa using this a (by rfl)
  contradiction

instance models_InductionScheme_alt : V âŠ§â‚˜* InductionScheme â„’â‚’áµ£ (Arithmetic.Hierarchy Î“.alt m) := by
  suffices
      âˆ€ (Ï† : Semiformula â„’â‚’áµ£ â„• 1), Hierarchy Î“.alt m Ï† â†’
      âˆ€ (f : â„• â†’ V),
        Semiformula.Evalm V ![0] f Ï† â†’
        (âˆ€ x, Semiformula.Evalm V ![x] f Ï† â†’ Semiformula.Evalm V ![x + 1] f Ï†) â†’
        âˆ€ x, Semiformula.Evalm V ![x] f Ï† by
    simp only [InductionScheme, Semantics.RealizeSet.setOf_iff, forall_exists_index, and_imp]
    rintro _ Ï† hÏ† rfl
    simpa [models_iff, succInd, Semiformula.eval_rew_q,
        Semiformula.eval_substs, Function.comp, Matrix.constant_eq_singleton]
    using this Ï† hÏ†
  intro Ï† hp v
  simpa using
    neg_succ_induction Î“ m (P := fun x â†¦ Â¬Semiformula.Evalm V ![x] v Ï†)
      (.mkPolarity (âˆ¼(Rew.rewriteMap v â–¹ Ï†)) (by simpa using hp)
      (by intro x; simp [â†Matrix.fun_eq_vec_one, Semiformula.eval_rewriteMap]))

instance models_alt : V âŠ§â‚˜* ğˆğğƒ Î“.alt m := by
  haveI : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)
  simp only [InductionOnHierarchy, ModelsTheory.add_iff]; constructor <;> infer_instance

lemma least_number {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    {x} (h : P x) : âˆƒ y, P y âˆ§ âˆ€ z < y, Â¬P z := by
  haveI : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğğƒ Î“ m)
  by_contra A
  have A : âˆ€ z, P z â†’ âˆƒ w < z, P w := by simpa using A
  have : âˆ€ z, âˆ€ w < z, Â¬P w := by
    intro z
    induction z using succ_induction
    Â· exact Î“.alt
    Â· exact m
    Â· suffices Î“.alt-[m].BoldfacePred fun z â†¦ âˆ€ w < z, Â¬P w by exact this
      apply HierarchySymbol.Boldface.ball_blt (by definability)
      apply HierarchySymbol.Boldface.not
      apply HierarchySymbol.Boldface.bcompâ‚ (hP := by simpa using hP) (by definability)
    case zero => simp
    case succ x IH =>
      intro w hx hw
      rcases le_iff_lt_or_eq.mp (lt_succ_iff_le.mp hx) with (hx | rfl)
      Â· exact IH w hx hw
      Â· have : âˆƒ v < w, P v := A w hw
        rcases this with âŸ¨v, hvw, hvâŸ©
        exact IH v hvw hv
    case inst => infer_instance
  exact this (x + 1) x (by simp) h

end

section

variable (Î“ : SigmaPiDelta) (m : â„•) [V âŠ§â‚˜* ğˆğğƒ ğšº m]

lemma succ_induction_sigma {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  match Î“ with
  | ğšº => succ_induction ğšº m hP zero succ
  | ğš· =>
    haveI : V âŠ§â‚˜* ğˆğğƒ ğš· m := models_alt ğšº m
    succ_induction ğš· m hP zero succ
  | ğš« => succ_induction ğšº m hP.of_delta zero succ

lemma order_induction_sigma {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x :=
  match Î“ with
  | ğšº => order_induction ğšº m hP ind
  | ğš· =>
    haveI : V âŠ§â‚˜* ğˆğğƒ ğš· m := models_alt ğšº m
    order_induction ğš· m hP ind
  | ğš« => order_induction ğšº m hP.of_delta ind

lemma least_number_sigma {P : V â†’ Prop} (hP : Î“-[m].BoldfacePred P)
    {x} (h : P x) : âˆƒ y, P y âˆ§ âˆ€ z < y, Â¬P z :=
  match Î“ with
  | ğšº => least_number ğšº m hP h
  | ğš· =>
    haveI : V âŠ§â‚˜* ğˆğğƒ ğš· m := models_alt ğšº m
    least_number ğš· m hP h
  | ğš« => least_number ğšº m hP.of_delta h

end

instance [V âŠ§â‚˜* ğˆğğƒ ğšº m] : V âŠ§â‚˜* ğˆğğƒ Î“ m := by
  rcases Î“
  Â· infer_instance
  Â· exact models_alt ğšº m

instance [V âŠ§â‚˜* ğˆğğƒ ğš· m] : V âŠ§â‚˜* ğˆğğƒ Î“ m := by
  rcases Î“
  Â· exact models_alt ğš· m
  Â· infer_instance

lemma mod_ISigma_of_le {nâ‚ nâ‚‚} (h : nâ‚ â‰¤ nâ‚‚) [V âŠ§â‚˜* ğˆğšº nâ‚‚] : V âŠ§â‚˜* ğˆğšº nâ‚ :=
  ModelsTheory.of_ss inferInstance (ISigma_subset_mono h)

instance [V âŠ§â‚˜* ğˆğšºâ‚] : V âŠ§â‚˜* ğˆğšºâ‚€ := mod_ISigma_of_le (show 0 â‰¤ 1 from by simp)

instance [V âŠ§â‚˜* ğˆğšºn] : V âŠ§â‚˜* ğˆğš·n := inferInstance

instance [V âŠ§â‚˜* ğˆğš·n] : V âŠ§â‚˜* ğˆğšºn := inferInstance

lemma models_ISigma_iff_models_IPi {n} : V âŠ§â‚˜* ğˆğšº n â†” V âŠ§â‚˜* ğˆğš· n :=
  âŸ¨fun _ â†¦ inferInstance, fun _ â†¦ inferInstanceâŸ©

instance [V âŠ§â‚˜* ğˆğšº n] : V âŠ§â‚˜* ğˆğğƒ Î“ n :=
  match Î“ with
  | ğšº => inferInstance
  | ğš· => inferInstance

end InductionOnHierarchy

@[elab_as_elim] lemma ISigma0.succ_induction [V âŠ§â‚˜* ğˆğšºâ‚€]
    {P : V â†’ Prop} (hP : ğšºâ‚€.BoldfacePred P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  InductionOnHierarchy.succ_induction ğšº 0 hP zero succ

@[elab_as_elim] lemma ISigma1.sigma1_succ_induction [V âŠ§â‚˜* ğˆğšºâ‚]
    {P : V â†’ Prop} (hP : ğšºâ‚-Predicate P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  InductionOnHierarchy.succ_induction ğšº 1 hP zero succ

@[elab_as_elim] lemma ISigma1.pi1_succ_induction [V âŠ§â‚˜* ğˆğšºâ‚]
    {P : V â†’ Prop} (hP : ğš·â‚-Predicate P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  InductionOnHierarchy.succ_induction ğš· 1 hP zero succ

@[elab_as_elim] lemma ISigma0.order_induction [V âŠ§â‚˜* ğˆğšºâ‚€]
    {P : V â†’ Prop} (hP : ğšºâ‚€-Predicate P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x :=
  InductionOnHierarchy.order_induction ğšº 0 hP ind

@[elab_as_elim] lemma ISigma1.sigma1_order_induction [V âŠ§â‚˜* ğˆğšºâ‚]
    {P : V â†’ Prop} (hP : ğšºâ‚-Predicate P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x :=
  InductionOnHierarchy.order_induction ğšº 1 hP ind

@[elab_as_elim] lemma ISigma1.pi1_order_induction [V âŠ§â‚˜* ğˆğšºâ‚]
    {P : V â†’ Prop} (hP : ğš·â‚-Predicate P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x :=
  InductionOnHierarchy.order_induction ğš· 1 hP ind

lemma ISigma0.least_number [V âŠ§â‚˜* ğˆğšºâ‚€] {P : V â†’ Prop} (hP : ğšºâ‚€-Predicate P)
    {x} (h : P x) : âˆƒ y, P y âˆ§ âˆ€ z < y, Â¬P z :=
  InductionOnHierarchy.least_number ğšº 0 hP h

@[elab_as_elim] lemma ISigma1.succ_induction [V âŠ§â‚˜* ğˆğšºâ‚] (Î“)
    {P : V â†’ Prop} (hP : Î“-[1]-Predicate P)
    (zero : P 0) (succ : âˆ€ x, P x â†’ P (x + 1)) : âˆ€ x, P x :=
  InductionOnHierarchy.succ_induction_sigma Î“ 1 hP zero succ

@[elab_as_elim] lemma ISigma1.order_induction [V âŠ§â‚˜* ğˆğšºâ‚] (Î“)
    {P : V â†’ Prop} (hP : Î“-[1]-Predicate P)
    (ind : âˆ€ x, (âˆ€ y < x, P y) â†’ P x) : âˆ€ x, P x :=
  InductionOnHierarchy.order_induction_sigma Î“ 1 hP ind


instance [V âŠ§â‚˜* ğˆOpen] : V âŠ§â‚˜* ğğ€â» := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆOpen)

instance [V âŠ§â‚˜* ğˆğšºâ‚€] : V âŠ§â‚˜* ğˆOpen := models_of_subtheory <| inferInstanceAs (V âŠ§â‚˜* ğˆğšºâ‚€)

instance [V âŠ§â‚˜* ğˆğšºâ‚] : V âŠ§â‚˜* ğˆğšºâ‚€ := inferInstance

def mod_ISigma_of_le {nâ‚ nâ‚‚} (h : nâ‚ â‰¤ nâ‚‚) [V âŠ§â‚˜* ğˆğšº nâ‚‚] : V âŠ§â‚˜* ğˆğšº nâ‚ :=
  ModelsTheory.of_ss inferInstance (ISigma_subset_mono h)

lemma models_succInd (Ï† : Semiformula â„’â‚’áµ£ â„• 1) : â„• âŠ§â‚˜ succInd Ï† := by
  suffices
    âˆ€ f : â„• â†’ â„•,
      Semiformula.Evalm â„• ![0] f Ï† â†’
      (âˆ€ x, Semiformula.Evalm â„• ![x] f Ï† â†’ Semiformula.Evalm â„• ![x + 1] f Ï†) â†’
        âˆ€ x, Semiformula.Evalm â„• ![x] f Ï† by
    simpa [succInd, models_iff, Matrix.constant_eq_singleton, Semiformula.eval_substs]
  intro e hzero hsucc x; induction' x with x ih
  Â· exact hzero
  Â· exact hsucc x ih

instance models_ISigma (Î“ k) : â„• âŠ§â‚˜* ğˆğğƒ Î“ k := by
  simp only [ModelsTheory.add_iff, instModelsTheoryNat, InductionScheme,
    Semantics.RealizeSet.setOf_iff, forall_exists_index, and_imp, true_and]
  rintro _ Ï† _ rfl; simp [models_succInd]

instance models_ISigmaZero : â„• âŠ§â‚˜* ğˆğšºâ‚€ := inferInstance

instance models_ISigmaOne : â„• âŠ§â‚˜* ğˆğšºâ‚ := inferInstance

instance models_Peano : â„• âŠ§â‚˜* ğğ€ := by
  simp only [Peano, InductionScheme, ModelsTheory.add_iff, instModelsTheoryNat,
    Semantics.RealizeSet.setOf_iff, forall_exists_index, and_imp, true_and]
  rintro _ Ï† _ rfl; simp [models_succInd]

instance : Entailment.Consistent (ğˆğğƒ Î“ k) := (ğˆğğƒ Î“ k).consistent_of_sound (Eq âŠ¥) rfl

instance : Entailment.Consistent ğğ€ := ğğ€.consistent_of_sound (Eq âŠ¥) rfl

instance : ğğ€ âª¯ ğ“ğ€ := inferInstance

instance (T : ArithmeticTheory) [ğğ€â» âª¯ T] : ğ‘â‚€ âª¯ T :=
  Entailment.WeakerThan.trans (inferInstanceAs (ğ‘â‚€ âª¯ ğğ€â»)) inferInstance

instance (T : ArithmeticTheory) [ğˆğšºâ‚€ âª¯ T] : ğğ€â» âª¯ T :=
  Entailment.WeakerThan.trans (inferInstanceAs (ğğ€â» âª¯ ğˆğšºâ‚€)) inferInstance

instance (T : ArithmeticTheory) [ğˆğšºâ‚ âª¯ T] : ğğ€â» âª¯ T :=
  Entailment.WeakerThan.trans (inferInstanceAs (ğğ€â» âª¯ ğˆğšºâ‚)) inferInstance

instance (T : ArithmeticTheory) [ğğ€ âª¯ T] : ğğ€â» âª¯ T :=
  Entailment.WeakerThan.trans (inferInstanceAs (ğğ€â» âª¯ ğğ€)) inferInstance

end LO
