import Foundation.FirstOrder.Arithmetic.PeanoMinus.Basic
import Foundation.FirstOrder.Arithmetic.Definability
import Mathlib.Algebra.GroupWithZero.Divisibility
import Mathlib.Algebra.Prime.Lemmas
import Foundation.Vorspiel.ExistsUnique

/-!
# Functions and relations defined in $\mathsf{PA^-}$

This file provides functions and relations defined in $\mathsf{PA^-}

-/

namespace LO.FirstOrder.Arithmetic

variable {V : Type*} [ORingStructure V] [V âŠ§â‚˜* ğ—£ğ—”â»]

variable {a b c : V}

/-! ### (Modified) Subtraction -/

section sub

lemma sub_existsUnique (a b : V) : âˆƒ! c, (a â‰¥ b â†’ a = b + c) âˆ§ (a < b â†’ c = 0) := by
  have : b â‰¤ a âˆ¨ a < b := le_or_gt b a
  rcases this with (hxy | hxy)
  Â· have : âˆƒ c, a = b + c := exists_add_of_le hxy
    rcases this with âŸ¨c, rflâŸ©
    simp [hxy]
  Â· simp [hxy]

noncomputable def sub (a b : V) : V := Classical.choose! (sub_existsUnique a b)

noncomputable scoped instance : Sub V := âŸ¨subâŸ©

lemma sub_spec_of_ge (h : a â‰¥ b) : a = b + (a - b) := (Classical.choose!_spec (sub_existsUnique a b)).1 h

lemma sub_spec_of_lt (h : a < b) : a - b = 0 := (Classical.choose!_spec (sub_existsUnique a b)).2 h

lemma sub_eq_iff : c = a - b â†” ((a â‰¥ b â†’ a = b + c) âˆ§ (a < b â†’ c = 0)) := Classical.choose!_eq_iff_right (sub_existsUnique a b)

@[simp] lemma sub_le_self (a b : V) : a - b â‰¤ a := by
  have : b â‰¤ a âˆ¨ a < b := le_or_gt b a
  rcases this with (hxy | hxy) <;> simp
  Â· simpa [â† sub_spec_of_ge hxy] using show a - b â‰¤ b + (a - b) from le_add_self
  Â· simp [sub_spec_of_lt hxy]

open FirstOrder.Arithmetic.HierarchySymbol.Definable

def _root_.LO.FirstOrder.Arithmetic.subDef : ğšºâ‚€.Semisentence 3 :=
  .mkSigma â€œz x y. (x â‰¥ y â†’ x = y + z) âˆ§ (x < y â†’ z = 0)â€

instance sub_defined : ğšºâ‚€-Functionâ‚‚ ((Â· - Â·) : V â†’ V â†’ V) via subDef := .mk <| by intro v; simp [FirstOrder.Arithmetic.subDef, sub_eq_iff]

instance sub_definable (â„Œ : HierarchySymbol) : â„Œ.DefinableFunctionâ‚‚ ((Â· - Â·) : V â†’ V â†’ V) := sub_defined.to_definableâ‚€

instance sub_polybounded : Boundedâ‚‚ ((Â· - Â·) : V â†’ V â†’ V) := âŸ¨#0, Î» _ â†¦ by simpâŸ©

@[simp] lemma sub_self (a : V) : a - a = 0 :=
  add_eq_left.mp (sub_spec_of_ge (a := a) (b := a) (by rfl)).symm

lemma sub_spec_of_le (h : a â‰¤ b) : a - b = 0 := by
  rcases lt_or_eq_of_le h with (lt | rfl) <;> simp [sub_spec_of_lt, *]

lemma sub_add_self_of_le (h : b â‰¤ a) : a - b + b = a := by symm; rw [add_comm]; exact sub_spec_of_ge h

lemma add_tsub_self_of_le (h : b â‰¤ a) : b + (a - b) = a := by symm; exact sub_spec_of_ge h

@[simp] lemma add_sub_self : (a + b) - b = a := by
  symm; simpa [add_comm b] using sub_spec_of_ge (show b â‰¤ a + b from le_add_self)

@[simp] lemma add_sub_self' : (b + a) - b = a := by simp [add_comm]

@[simp] lemma zero_sub (a : V) : 0 - a = 0 := sub_spec_of_le (by simp)

@[simp] lemma sub_zero (a : V) : a - 0 = a := by
  simpa using sub_add_self_of_le (show 0 â‰¤ a from zero_le a)

lemma sub_remove_left (e : a = b + c) : a - c = b := by simp [e]

lemma sub_sub : a - b - c = a - (b + c) := by
  by_cases ha : b + c â‰¤ a
  Â· exact sub_remove_left <| sub_remove_left <| by
      simp [add_assoc, show c + b = b + c from add_comm _ _, sub_add_self_of_le, ha]
  Â· suffices a - b - c = 0 by simpa [sub_spec_of_lt (show a < b + c from not_le.mp ha)]
    by_cases hc : c â‰¤ a - b
    Â· by_cases hb : b â‰¤ a
      Â· have : a < a := calc
          a < b + c       := not_le.mp ha
          _ â‰¤ b + (a - b) := by simp [hc]
          _ = a           := add_tsub_self_of_le hb
        simp at this
      Â· simp [show a - b = 0 from sub_spec_of_lt (not_le.mp hb)]
    Â· exact sub_spec_of_lt (not_le.mp hc)

@[simp] lemma pos_sub_iff_lt : 0 < a - b â†” b < a :=
  âŸ¨by contrapose; simpa using sub_spec_of_le,
   by intro h; by_contra hs
      have : a = b := by
        simpa [show a - b = 0 by simpa using hs] using sub_spec_of_ge (show b â‰¤ a from LT.lt.le h)
      simp [this] at hâŸ©

@[simp] lemma sub_eq_zero_iff_le : a - b = 0 â†” a â‰¤ b :=
  not_iff_not.mp (by simp [â†pos_iff_ne_zero])

instance : OrderedSub V where
  tsub_le_iff_right := by
    intro a b c
    by_cases h : b â‰¤ a
    Â· calc
        a - b â‰¤ c â†” (a - b) + b â‰¤ c + b := by simp
        _         â†” a â‰¤ c + b           := by rw [sub_add_self_of_le h]
    Â· suffices a â‰¤ c + b by simpa [sub_spec_of_lt (show a < b from by simpa using h)]
      exact le_trans (le_of_lt <| show a < b from by simpa using h) (by simp)

lemma pred_lt_self_of_pos (h : 0 < a) : a - 1 < a := by
  rcases zero_or_succ a with (rfl | âŸ¨a, rflâŸ©)
  Â· simp_all
  Â· simp

protected lemma tsub_lt_iff_left (h : b â‰¤ a) : a - b < c â†” a < c + b := AddLECancellable.tsub_lt_iff_right (add_le_cancel b) h

lemma sub_mul (h : b â‰¤ a) : (a - b) * c = a * c - b * c := by
  have : a = (a - b) + b := (tsub_eq_iff_eq_add_of_le h).mp rfl
  calc
    (a - b) * c = (a - b) * c + b * c - b * c := by simp
    _           = (a - b + b) * c - b * c     := by simp [add_mul]
    _           = a * c - b * c               := by simp [sub_add_self_of_le h]

lemma mul_sub (h : b â‰¤ a) : c * (a - b) = c * a - c * b := by simp [mul_comm c, sub_mul, h]

lemma add_sub_of_le (h : c â‰¤ b) (a : V) : a + b - c = a + (b - c) := add_tsub_assoc_of_le h a

lemma sub_succ_add_succ {x y : V} (h : y < x) (z) : x - (y + 1) + (z + 1) = x - y + z := calc
  x - (y + 1) + (z + 1) = x - (y + 1) + 1 + z := by simp [add_assoc, add_comm]
  _                     = x - y - 1 + 1 + z   := by simp [sub_sub]
  _                     = x - y + z           := by
    simp [show x - y - 1 + 1 = x - y from sub_add_self_of_le <| one_le_of_zero_lt _ <| pos_sub_iff_lt.mpr h]

lemma le_sub_one_of_lt {a b : V} (h : a < b) : a â‰¤ b - 1 := by
  have : 1 â‰¤ b := one_le_of_zero_lt _ (pos_of_gt h)
  simp [le_iff_lt_succ, sub_add_self_of_le this, h]

instance : AddCancelCommMonoid V where
 add_left_cancel x y z e := by simpa using congr_arg (Â· - x) e

end sub

/-! ### Divisibility -/

section Dvd

lemma le_mul_self_of_pos_left (hy : 0 < b) : a â‰¤ b * a := by
  have : 1 * a â‰¤ b * a := mul_le_mul_of_nonneg_right (one_le_of_zero_lt b hy) (by simp)
  simpa using this

lemma le_mul_self_of_pos_right (hy : 0 < b) : a â‰¤ a * b := by
  simpa [mul_comm a b] using le_mul_self_of_pos_left hy

open Classical

lemma dvd_iff_bounded {a b : V} : a âˆ£ b â†” âˆƒ c â‰¤ b, b = a * c := by
  by_cases hx : a = 0
  Â· simp [hx, show âˆƒ x, x â‰¤ b from âŸ¨0, by simpâŸ©]
  Â· constructor
    Â· rintro âŸ¨c, rflâŸ©; exact âŸ¨c, le_mul_self_of_pos_left (pos_iff_ne_zero.mpr hx), rflâŸ©
    Â· rintro âŸ¨c, hz, rflâŸ©; exact dvd_mul_right a c

def _root_.LO.FirstOrder.Arithmetic.dvd : ğšºâ‚€.Semisentence 2 :=
  .mkSigma â€œx y. âˆƒ z <âº y, y = x * zâ€

instance dvd_defined : ğšºâ‚€-Relation (fun a b : V â†¦ a âˆ£ b) via dvd := .mk fun v â†¦ by simp [dvd_iff_bounded, dvd]

instance dvd_definable (â„Œ : HierarchySymbol) : â„Œ.DefinableRel ((Â· âˆ£ Â·) : V â†’ V â†’ Prop) := dvd_defined.to_definableâ‚€

section

syntax:45 first_order_term:45 " âˆ£ " first_order_term:0 : first_order_formula

macro_rules
  | `(â¤«formula(lit)[ $binders* | $fbinders* | $t:first_order_term âˆ£ $u:first_order_term]) => `(â¤«formula(lit)[ $binders* | $fbinders* | !dvd.val $t $u])

end

end Dvd

lemma le_of_dvd (h : 0 < b) : a âˆ£ b â†’ a â‰¤ b := by
  rintro âŸ¨c, rflâŸ©
  exact le_mul_self_of_pos_right
    (pos_iff_ne_zero.mpr (show c â‰  0 from by rintro rfl; simp at h))

lemma not_dvd_of_lt (pos : 0 < b) : b < a â†’ Â¬a âˆ£ b := by
  intro hb h; exact not_le.mpr hb (le_of_dvd pos h)

lemma dvd_antisymm : a âˆ£ b â†’ b âˆ£ a â†’ a = b := by
  intro hx hy
  rcases show a = 0 âˆ¨ 0 < a from eq_zero_or_pos a with (rfl | ltx)
  Â· simp [show b = 0 from by simpa using hx]
  Â· rcases show b = 0 âˆ¨ 0 < b from eq_zero_or_pos b with (rfl | lty)
    Â· simp [show a = 0 from by simpa using hy]
    Â· exact le_antisymm (le_of_dvd lty hx) (le_of_dvd ltx hy)

lemma dvd_one_iff : a âˆ£ 1 â†” a = 1 := âŸ¨by { intro hx; exact dvd_antisymm hx (by simp) }, by rintro rfl; simpâŸ©

theorem units_eq_one (u : VË£) : u = 1 :=
  Units.ext <| dvd_one_iff.mp âŸ¨u.inv, u.val_inv.symmâŸ©

@[simp] lemma unit_iff_eq_one {a : V} : IsUnit a â†” a = 1 :=
  âŸ¨by rintro âŸ¨u, rflâŸ©; simp [units_eq_one u], by rintro rfl; simpâŸ©

/-! ### Prime number -/

section Prime

instance : CancelCommMonoidWithZero V where

open Classical in
lemma eq_one_or_eq_of_dvd_of_prime {p a : V} (pp : Prime p) (hxp : a âˆ£ p) : a = 1 âˆ¨ a = p := by
  have : p âˆ£ a âˆ¨ a âˆ£ 1 := Prime.left_dvd_or_dvd_right_of_dvd_mul pp (show a âˆ£ p * 1 from by simpa using hxp)
  rcases this with (hx | hx)
  Â· right; exact dvd_antisymm hxp hx
  Â· left; exact dvd_one_iff.mp hx

def IsPrime (a : V) : Prop := 1 < a âˆ§ âˆ€ b â‰¤ a, b âˆ£ a â†’ b = 1 âˆ¨ b = a
-- TODO: prove IsPrime a â†” Prime a

def _root_.LO.FirstOrder.Arithmetic.isPrime : ğšºâ‚€.Semisentence 1 :=
  .mkSigma â€œx. 1 < x âˆ§ âˆ€ y <âº x, !dvd.val y x â†’ y = 1 âˆ¨ y = xâ€

instance isPrime_defined : ğšºâ‚€-Predicate (Î» a : V â†¦ IsPrime a) via isPrime := .mk fun v â†¦ by
  simp [Semiformula.eval_substs, Matrix.comp_vecCons', Matrix.constant_eq_singleton, IsPrime, isPrime]

end Prime

/-! ### Minimum -/

section min

def min.dfn : ğšºâ‚€.Semisentence 3 :=
  .mkSigma â€œz x y. (x â‰¤ y â†’ z = x) âˆ§ (x â‰¥ y â†’ z = y)â€

set_option linter.flexible false in
instance min_defined : ğšºâ‚€-Functionâ‚‚[V] min via min.dfn := .mk fun v â†¦ by
  simp [min.dfn]; grind

instance min_definable (â„Œ) : â„Œ-Functionâ‚‚[V] min := min_defined.to_definableâ‚€

instance min_polybounded : Boundedâ‚‚ (min : V â†’ V â†’ V) := âŸ¨#0, Î» _ â†¦ by simpâŸ©

end min

/-! ### Maximum -/

section max

def max.dfn : ğšºâ‚€.Semisentence 3 :=
  .mkSigma â€œz x y. (x â‰¥ y â†’ z = x) âˆ§ (x â‰¤ y â†’ z = y)â€

set_option linter.flexible false in
instance max_defined : ğšºâ‚€-Functionâ‚‚[V] max via max.dfn := .mk fun v â†¦ by simp [max.dfn]; grind

instance max_definable (Î“) : Î“-Functionâ‚‚[V] max := max_defined.to_definableâ‚€

instance max_polybounded : Boundedâ‚‚ (max : V â†’ V â†’ V) := âŸ¨â€˜#0 + #1â€™, Î» v â†¦ by simpâŸ©

end max

end LO.FirstOrder.Arithmetic
