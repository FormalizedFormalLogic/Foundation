/-
  Formalizing Yablo's Paradox.

  *References*
  - C. CieÅ›liÅ„ski, R. Urbaniak, "GÃ¶delizing the Yablo Sequence"
-/

import Foundation.FirstOrder.Internal.DerivabilityCondition

namespace LO.PeanoMinus

open ORingStruc

variable {M : Type*} [ORingStruc M] [M âŠ§â‚˜* ğ—£ğ—”â»]

lemma numeral_lt_of_numeral_succ_lt {n : â„•} {m : M} : (numeral (n + 1) : M) < m â†’ (numeral n < m) := by
  apply PeanoMinus.lt_trans;
  simp;

end LO.PeanoMinus


namespace LO.ISigma1.Metamath.InternalArithmetic

open FirstOrder Arithmetic PeanoMinus IOpen ISigma0

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğ—œğšºâ‚]

lemma substNumeral_app_quote_nat_model (Ïƒ : Semisentence â„’â‚’áµ£ 1) (n : â„•) :
  substNumeral âŒœÏƒâŒ (n : V) = âŒœ(Ïƒ/[.numeral n] : Sentence â„’â‚’áµ£)âŒ := by
  simp [
    substNumeral, Sentence.quote_def, Semiformula.quote_def,
    Rewriting.emb_substs_eq_substs_coeâ‚
  ];

lemma substNumeral_app_quote_nat_Nat (Ïƒ : Semisentence â„’â‚’áµ£ 1) (n : â„•) :
  substNumeral âŒœÏƒâŒ n = âŒœ(Ïƒ/[.numeral n] : Sentence â„’â‚’áµ£)âŒ := by
  simp [
    substNumeral, Sentence.quote_def, Semiformula.quote_def,
    Rewriting.emb_substs_eq_substs_coeâ‚
  ];

end LO.ISigma1.Metamath.InternalArithmetic

namespace LO.FirstOrder

open FirstOrder Arithmetic PeanoMinus IOpen ISigma0 ISigma1 Metamath InternalArithmetic

namespace Theory

variable {V} [ORingStruc V] [V âŠ§â‚˜* ğ—œğšºâ‚]
         {T U : ArithmeticTheory} [T.Î”â‚]

def YabloSystem (T : ArithmeticTheory) [T.Î”â‚] (Ï† n : V) : Prop := âˆ€ m, n < m â†’ Â¬T.Provable (substNumeral Ï† m)

def yabloSystem (T : ArithmeticTheory) [T.Î”â‚] : ğš·â‚.Semisentence 2 := .mkPi
  â€œÏ† n. âˆ€ m, n < m â†’ âˆ€ nÏƒ, !ssnum nÏƒ Ï† m â†’ Â¬!T.provable (nÏƒ)â€

lemma yabloSystem.defined : ğš·â‚-Relation[V] (T.YabloSystem) via T.yabloSystem := by
  intro f;
  simp [Theory.YabloSystem, Theory.yabloSystem];

@[simp]
lemma yabloSystem.eval (v) : Semiformula.Evalbm V v T.yabloSystem.val â†” T.YabloSystem (v 0) (v 1) := yabloSystem.defined.df.iff v

instance yabloSystem.definable : ğš·â‚-Relation[V] (T.YabloSystem) := yabloSystem.defined.to_definable

def yablo (T : ArithmeticTheory) [T.Î”â‚] : ArithmeticSemisentence 1 := parameterizedFixedpoint (T.yabloSystem)

abbrev yabloPred (T : ArithmeticTheory) [T.Î”â‚] (n : â„•) : ArithmeticSentence := T.yablo/[.numeral n]

end Theory



namespace Arithmetic

variable {T : ArithmeticTheory} [T.Î”â‚]

open Theory

-- Lemmata
section

variable {V : Type} [ORingStruc V] [V âŠ§â‚˜* ğ—œğšºâ‚]
variable {U : ArithmeticTheory} [ğ—œğšºâ‚ âª¯ U]

lemma yablo_diagonal : U âŠ¢ âˆ€' (T.yablo â­¤ (T.yabloSystem)/[âŒœT.yabloâŒ, #0]) := parameterized_diagonalâ‚ _

lemma yablo_diagonal_modeled (n : V) : V âŠ§/![n] (T.yablo) â†” âˆ€ m, n < m â†’ Â¬T.Provable (substNumeral âŒœT.yabloâŒ m) := by
  have : V âŠ§â‚˜ âˆ€' (T.yablo â­¤ â†‘(T.yabloSystem)/[âŒœT.yabloâŒ, #0]) := models_of_provable (T := ğ—œğšºâ‚) (by assumption) $ yablo_diagonal;
  have : âˆ€ (n : V), V âŠ§/![n] (T.yablo) â†” T.YabloSystem âŒœT.yabloâŒ n := by simpa [models_iff, Matrix.comp_vecCons'] using this;
  apply this;

lemma yablo_diagonal_neg_modeled (n : V) : Â¬V âŠ§/![n] (T.yablo) â†” âˆƒ m, n < m âˆ§ T.Provable (substNumeral âŒœT.yabloâŒ m) := by
  simpa using yablo_diagonal_modeled n |>.not;

lemma iff_yablo_provable (n : â„•) : U âŠ¢ T.yabloPred n â†” U âŠ¢ â€œâˆ€ m, â†‘n < m â†’ âˆ€ nÏƒ, !ssnum nÏƒ âŒœT.yabloâŒ m â†’ Â¬!T.provable (nÏƒ)â€ := by
  suffices U âŠ¢ T.yablo/[n] â­¤ â€œâˆ€ m, â†‘n < m â†’ âˆ€ nÏƒ, !ssnum nÏƒ âŒœT.yabloâŒ m â†’ Â¬!T.provable (nÏƒ)â€ by
    constructor <;> . intro h; cl_prover [h, this];
  apply oRing_provable_of.{0};
  intro V _ _;
  haveI : V âŠ§â‚˜* ğ—œğšºâ‚ := ModelsTheory.of_provably_subtheory V ğ—œğšºâ‚ U inferInstance;
  haveI : V âŠ§/![ORingStruc.numeral n] (T.yablo) â†” T.YabloSystem âŒœT.yabloâŒ (ORingStruc.numeral n) := yablo_diagonal_modeled _;
  simpa [models_iff, Matrix.constant_eq_singleton, Matrix.comp_vecCons'] using this;

lemma iff_neg_yablo_provable (n : â„•) : U âŠ¢ âˆ¼(T.yabloPred n) â†” U âŠ¢ â€œâˆƒ m, â†‘n < m âˆ§ âˆƒ nÏƒ, !ssnum nÏƒ âŒœT.yabloâŒ m âˆ§ !T.provable (nÏƒ)â€ := by
  suffices U âŠ¢ âˆ¼T.yablo/[n] â­¤ â€œâˆƒ m, â†‘n < m âˆ§ âˆƒ nÏƒ, !ssnum nÏƒ âŒœT.yabloâŒ m âˆ§ !T.provable (nÏƒ)â€ by
    constructor <;> . intro h; cl_prover [h, this];
  apply oRing_provable_of.{0};
  intro V _ _;
  haveI : V âŠ§â‚˜* ğ—œğšºâ‚ := ModelsTheory.of_provably_subtheory V ğ—œğšºâ‚ U inferInstance;
  haveI : Â¬V âŠ§/![ORingStruc.numeral n] (T.yablo) â†” âˆƒ m, ORingStruc.numeral n < m âˆ§ Provable T (substNumeral âŒœT.yabloâŒ m) := yablo_diagonal_neg_modeled _;
  simpa [models_iff, Matrix.constant_eq_singleton, Matrix.comp_vecCons'] using this;

lemma provable_greater_yablo {n m : â„•} (hnm : n < m) : U âŠ¢ T.yabloPred n â T.yabloPred m := by
  apply oRing_provable_of.{0};
  intro V _ _;
  haveI : V âŠ§â‚˜* ğ—œğšºâ‚ := ModelsTheory.of_provably_subtheory V ğ—œğšºâ‚ U inferInstance;
  suffices
    (âˆ€ m, ORingStruc.numeral n < m â†’ Â¬Provable T (substNumeral âŒœyablo TâŒ m)) â†’
    (âˆ€ k, ORingStruc.numeral m < k â†’ Â¬Provable T (substNumeral âŒœyablo TâŒ k))
    by simpa [models_iff, Matrix.constant_eq_singleton, Matrix.comp_vecCons', yablo_diagonal_modeled] using this;
  intro h k hmk;
  apply h;
  apply PeanoMinus.lt_trans _ _ _ (by simpa) hmk;

end

-- Main Results
section

variable [ğ—œğšºâ‚ âª¯ T] {n : â„•}

theorem yablo_unprovable [Entailment.Consistent T] : T âŠ¬ (T.yabloPred n) := by
  by_contra! hC;
  have Hâ‚ : T âŠ¢ T.provabilityPred (T.yabloPred (n + 1)) := by
    apply Entailment.WeakerThan.pbl $ provable_D1 (T := T) ?_;
    apply provable_greater_yablo (show n < n + 1 by omega) â¨€ hC;
  have Hâ‚‚ : T âŠ¢ âˆ¼T.provabilityPred (T.yabloPred (n + 1)) := by
    apply oRing_provable_of.{0};
    intro V _ _;
    haveI : V âŠ§â‚˜* ğ—œğšºâ‚ := ModelsTheory.of_provably_subtheory V ğ—œğšºâ‚ T inferInstance;
    suffices Â¬T.Provable (substNumeral âŒœT.yabloâŒ (n + 1 : V)) by
      simpa [provabilityPred, models_iff, â†substNumeral_app_quote_nat_model];
    have : âˆ€ (x : V), ORingStruc.numeral n < x â†’ Â¬T.Provable (substNumeral âŒœT.yabloâŒ x) := by
      have : V âŠ§â‚˜ â€œâˆ€ m, â†‘n < m â†’ âˆ€ nÏƒ, !ssnum nÏƒ âŒœT.yabloâŒ m â†’ Â¬!T.provable (nÏƒ)â€ :=
        models_of_provable (T := T) (by assumption) $ (iff_yablo_provable n |>.mp hC);
      simpa [models_iff, Matrix.comp_vecCons'] using this;
    apply this (n + 1) (by simp [numeral_eq_natCast]);
  apply Entailment.Consistent.not_bot (ğ“¢ := T);
  . infer_instance;
  . cl_prover [Hâ‚, Hâ‚‚];

theorem yablo_unrefutable [T.SoundOnHierarchy ğšº 1] : T âŠ¬ âˆ¼T.yabloPred n := by
  by_contra! hC;
  haveI := T.soundOnHierarchy ğšº 1 (iff_neg_yablo_provable n |>.mp hC) $ by simp;
  obtain âŸ¨k, _, hkâŸ© : âˆƒ k, n < k âˆ§ Provable T (substNumeral âŒœT.yabloâŒ k) := by simpa [models_iff, Matrix.comp_vecCons'] using this;
  rw [substNumeral_app_quote_nat_Nat, provable_iff_provable (T := T)] at hk;
  exact yablo_unprovable hk;

theorem yablo_independent [T.SoundOnHierarchy ğšº 1] : Entailment.Independent T (T.yabloPred n) := âŸ¨yablo_unprovable, yablo_unrefutableâŸ©

end

end Arithmetic


end LO.FirstOrder
