import Foundation.FirstOrder.Incompleteness.StandardProvability

/-!
# GÃ¶del's second incompleteness theorem over $\mathsf{I}\Sigma_1$

-/

namespace LO.ISigma1

open FirstOrder Arith PeanoMinus IOpen ISigma0 Metamath Arithmetization
  Entailment FiniteContext

variable (T : Theory â„’â‚’áµ£) [ğˆğšºâ‚ âª¯ T] [T.Delta1Definable]

local notation "ğ—š" => T.goedelâ‚

local notation "ğ—–ğ—¼ğ—»" => T.consistentâ‚

local prefix:max "â–¡" => T.bewâ‚

lemma goedel_iff_unprovable_goedel' : T âŠ¢! â†‘ğ—š â­¤ âˆ¼â†‘â–¡ğ—š := by
  simpa [Axiom.provable_iff] using goedel_iff_unprovable_goedel

lemma goedel_unprovable [Entailment.Consistent T] : T âŠ¬ â†‘ğ—š := by
  intro h
  have hp : T âŠ¢! â†‘â–¡ğ—š := (Axiom.provable_iff (T := T)).mp <| provableâ‚_D1 <| (Axiom.provable_iff (T := T)).mpr h
  have hn : T âŠ¢! âˆ¼â†‘â–¡ğ—š := by simpa [Axiom.provable_iff] using K!_left (goedel_iff_unprovable_goedel' T) â¨€ h
  exact not_consistent_iff_inconsistent.mpr (inconsistent_of_provable_of_unprovable hp hn) inferInstance

lemma not_goedel_unprovable [â„• âŠ§â‚˜* T] : T âŠ¬ âˆ¼â†‘ğ—š := fun h â†¦ by
  haveI : ğ‘â‚€ âª¯ T := Entailment.WeakerThan.trans (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  have : T âŠ¢!. â–¡ğ—š := Entailment.CN!_of_CN!_left (K!_right goedel_iff_unprovable_goedel) â¨€ (by simpa [Axiom.provable_iff] using h)
  have : T âŠ¢! â†‘ğ—š := (Axiom.provable_iff (T := T)).mp <| provableâ‚_sound this
  exact not_consistent_iff_inconsistent.mpr (inconsistent_of_provable_of_unprovable this h)
    (Sound.consistent_of_satisfiable âŸ¨_, (inferInstance : â„• âŠ§â‚˜* T)âŸ©)

lemma consistent_iff_goedel : T âŠ¢! â†‘ğ—–ğ—¼ğ—» â­¤ â†‘ğ—š := by
  apply E!_intro
  Â· have bew_G : [âˆ¼ğ—š] âŠ¢[T.toAxiom]! â–¡ğ—š := deductInv'! <| CN!_of_CN!_left <| K!_right goedel_iff_unprovable_goedel
    have bew_not_bew_G : [âˆ¼ğ—š] âŠ¢[T.toAxiom]! â–¡(âˆ¼â–¡ğ—š) := by
      have : T âŠ¢!. â–¡(ğ—š â âˆ¼â–¡ğ—š) := provableâ‚_D1 <| K!_left goedel_iff_unprovable_goedel
      exact provableâ‚_D2_context (of'! this) bew_G
    have bew_bew_G : [âˆ¼ğ—š] âŠ¢[T.toAxiom]! â–¡â–¡ğ—š := provableâ‚_D3_context bew_G
    have : [âˆ¼ğ—š] âŠ¢[T.toAxiom]! â–¡âŠ¥ :=
      provableâ‚_D2_context (provableâ‚_D2_context (of'! <| provableâ‚_D1 <| CNC!) bew_not_bew_G) bew_bew_G
    simpa [Axiom.provable_iff] using CN!_of_CN!_left (deduct'! this)
  Â· have : [â–¡âŠ¥] âŠ¢[T.toAxiom]! â–¡ğ—š := by
      have : T âŠ¢!. â–¡(âŠ¥ â ğ—š) := provableâ‚_D1 efq!
      exact provableâ‚_D2_context (of'! this) (by simp)
    have : [â–¡âŠ¥] âŠ¢[T.toAxiom]! âˆ¼ğ—š :=
      of'! (CN!_of_CN!_right <| K!_left <| goedel_iff_unprovable_goedel) â¨€ this
    simpa [Axiom.provable_iff] using  CN!_of_CN!_right (deduct'! this)

/-- GÃ¶del's Second Incompleteness Theorem-/
theorem goedel_second_incompleteness [Entailment.Consistent T] : T âŠ¬ â†‘ğ—–ğ—¼ğ—» := fun h â†¦
  goedel_unprovable T <| K!_left (consistent_iff_goedel T) â¨€ h

theorem inconsistent_unprovable [â„• âŠ§â‚˜* T] : T âŠ¬ âˆ¼â†‘ğ—–ğ—¼ğ—» := fun h â†¦
  not_goedel_unprovable T <| contra! (K!_right (consistent_iff_goedel T)) â¨€ h

theorem inconsistent_undecidable [â„• âŠ§â‚˜* T] : Entailment.Undecidable T â†‘ğ—–ğ—¼ğ—» := by
  haveI : Consistent T := Sound.consistent_of_satisfiable âŸ¨_, (inferInstance : â„• âŠ§â‚˜* T)âŸ©
  constructor
  Â· exact goedel_second_incompleteness T
  Â· exact inconsistent_unprovable T

instance [Entailment.Consistent T] : T âª± T + ğ‚ğ¨ğ§[T] :=
  Entailment.StrictlyWeakerThan.of_unprovable_provable (Ï† := â†‘ğ—–ğ—¼ğ—»)
    (goedel_second_incompleteness T) (Entailment.by_axm _ (by simp))

instance [Entailment.Consistent T] : â„• âŠ§â‚˜* ğ‚ğ¨ğ§[T] := by
  suffices â„• âŠ§â‚˜â‚€ T.consistentâ‚ by simpa [Modelsâ‚€] using this
  suffices Â¬T.Provableâ‚ âŒœâŠ¥âŒ by simpa [modelsâ‚€_iff] using  this
  intro H
  haveI : ğ‘â‚€ âª¯ T := Entailment.WeakerThan.trans (ğ“£ := ğˆğšºâ‚) inferInstance inferInstance
  have : T âŠ¢! âŠ¥ := (Axiom.provable_iff (T := T)).mp <| provableâ‚_iff_provableâ‚€.mp H
  have : Entailment.Inconsistent T := inconsistent_iff_provable_bot.mpr this
  exact Consistent.not_inconsistent this

instance [â„• âŠ§â‚˜* T] : â„• âŠ§â‚˜* T + ğ‚ğ¨ğ§[T] :=
  haveI : Entailment.Consistent T := Sound.consistent_of_satisfiable âŸ¨_, inferInstanceAs (â„• âŠ§â‚˜* T)âŸ©
  ModelsTheory.add_iff.mpr âŸ¨inferInstance, inferInstanceâŸ©

instance [â„• âŠ§â‚˜* T] : T âª± T + Â¬ğ‚ğ¨ğ§[T] :=
  Entailment.StrictlyWeakerThan.of_unprovable_provable (Ï† := âˆ¼â†‘ğ—–ğ—¼ğ—»)
    (inconsistent_unprovable T) (Entailment.by_axm _ (by simp))

end LO.ISigma1
