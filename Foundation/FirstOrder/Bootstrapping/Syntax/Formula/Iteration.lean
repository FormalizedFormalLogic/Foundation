import Foundation.FirstOrder.Bootstrapping.Syntax.Formula.Functions

namespace LO.FirstOrder.Semiformula

variable {L : Language} {Î¾ : Type*} {n : â„•}

def replicate (p : Semiformula L Î¾ n) : â„• â†’ Semiformula L Î¾ n
  |     0 => p
  | k + 1 => p â‹ p.replicate k

lemma replicate_zero (p : Semiformula L Î¾ n) : p.replicate 0 = p := by simp [replicate]

lemma replicate_succ (p : Semiformula L Î¾ n) (k : â„•) : p.replicate (k + 1) = p â‹ p.replicate k := by simp [replicate]

def weight (k : â„•) : Semiformula L Î¾ n := (List.replicate k âŠ¤).conj

end LO.FirstOrder.Semiformula

namespace LO.FirstOrder.Arithmetic.Bootstrapping

variable {V : Type*} [ORingStructure V] [V âŠ§â‚˜* ğ—œğšºâ‚]

variable {L : Language} [L.Encodable] [L.LORDefinable]

namespace QQConj

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy. !qqVerumDef yâ€
  adjoin := .mkSigma â€œy p ps ih. !qqAndDef y p ihâ€

noncomputable def construction : VecRec.Construction V blueprint where
  nil _ := ^âŠ¤
  adjoin _ p _ ih := p ^â‹ ih
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint]

end QQConj

section qqConj

open QQConj

noncomputable def qqConj (ps : V) : V := construction.result ![] ps

def qqConjGraph : ğšºâ‚.Semisentence 2 := blueprint.resultDef

scoped notation:65 "^â‹€ " ps:66 => qqConj ps

@[simp] lemma qqConj_nil : ^â‹€ (0 : V) = ^âŠ¤ := by simp [qqConj, construction]

@[simp] lemma qqConj_cons (p ps : V) : ^â‹€ (p âˆ· ps) = p ^â‹ (^â‹€ ps) := by simp [qqConj, construction]

section

instance qqConj.defined : ğšºâ‚-Functionâ‚[V] qqConj via qqConjGraph := construction.result_defined

instance qqConj.definable : ğšºâ‚-Functionâ‚ (qqConj : V â†’ V) := qqConj.defined.to_definable

instance qqConj.definable' : Î“-[m + 1]-Functionâ‚ (qqConj : V â†’ V) := .of_sigmaOne qqConj.definable

end

@[simp]
lemma qqConj_semiformula {n ps : V} :
    IsSemiformula L n (^â‹€ ps) â†” (âˆ€ i < len ps, IsSemiformula L n ps.[i]) := by
  induction ps using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin p ps ih =>
    simp only [qqConj_cons, IsSemiformula.and, ih, len_adjoin]
    constructor
    Â· rintro âŸ¨hp, hpsâŸ© i hi
      rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
      Â· simpa using hp
      Â· simpa using hps i (by simpa using hi)
    Â· intro h
      exact âŸ¨
        by simpa using h 0 (by simp),
        fun i hi â†¦ by simpa using h (i + 1) (by simpa using hi)âŸ©

@[simp] lemma len_le_conj (ps : V) : len ps â‰¤ ^â‹€ ps := by
  induction ps using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp [qqVerum]
  case adjoin p ps ih =>
    simp only [len_adjoin, qqConj_cons, succ_le_iff_lt]
    exact lt_of_le_of_lt ih (by simp)

end qqConj

namespace QQDisj

def blueprint : VecRec.Blueprint 0 where
  nil := .mkSigma â€œy. !qqFalsumDef yâ€
  adjoin := .mkSigma â€œy p ps ih. !qqOrDef y p ihâ€

noncomputable def construction : VecRec.Construction V blueprint where
  nil _ := ^âŠ¥
  adjoin _ p _ ih := p ^â‹ ih
  nil_defined := .mk fun v â†¦ by simp [blueprint]
  adjoin_defined := .mk fun v â†¦ by simp [blueprint]

end QQDisj

section qqDisj

open QQDisj

noncomputable def qqDisj (ps : V) : V := construction.result ![] ps

def qqDisjGraph : ğšºâ‚.Semisentence 2 := blueprint.resultDef

scoped notation:65 "^â‹ " ps:66 => qqDisj ps

@[simp] lemma qqDisj_nil : ^â‹ (0 : V) = ^âŠ¥ := by simp [qqDisj, construction]

@[simp] lemma qqDisj_cons (p ps : V) : ^â‹ (p âˆ· ps) = p ^â‹ (^â‹ ps) := by simp [qqDisj, construction]

section

instance qqDisj.defined : ğšºâ‚-Functionâ‚[V] qqDisj via qqDisjGraph := construction.result_defined

instance qqDisj.definable : ğšºâ‚-Functionâ‚[V] qqDisj := qqDisj.defined.to_definable

instance qqDisj.definable'  : Î“-[m + 1]-Functionâ‚[V] qqDisj := .of_sigmaOne qqDisj.definable

end

@[simp]
lemma qqDisj_semiformula {ps : V} :
    IsSemiformula L n (^â‹ ps) â†” (âˆ€ i < len ps, IsSemiformula L n ps.[i]) := by
  induction ps using adjoin_ISigma1.sigma1_succ_induction
  Â· definability
  case nil => simp
  case adjoin p ps ih =>
    simp only [qqDisj_cons, IsSemiformula.or, ih, len_adjoin]
    constructor
    Â· rintro âŸ¨hp, hpsâŸ© i hi
      rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
      Â· simpa using hp
      Â· simpa using hps i (by simpa using hi)
    Â· intro h
      exact âŸ¨
        by simpa using h 0 (by simp),
        fun i hi â†¦ by simpa using h (i + 1) (by simpa using hi)âŸ©

end qqDisj

namespace Arithmetic

/-! ### Disjunction of sequential substution

`disjSeqSubst w p k = subst (k âˆ· w) p ^â‹ â‹¯ ^â‹ subst (0 âˆ· w) p ^â‹ âŠ¥`

 -/

-- TOFO: remove
section disjSeqSubst

namespace DisjSeqSubst

noncomputable def blueprint : PR.Blueprint 2 where
  zero := .mkSigma â€œy w p. !qqFalsumDef yâ€
  succ := .mkSigma â€œy ih k w p. âˆƒ numeral, !numeralGraph numeral k âˆ§ âˆƒ v, !adjoinDef v numeral w âˆ§
    âˆƒ q, !(substsGraph â„’â‚’áµ£) q v p âˆ§ !qqOrDef y q ihâ€

noncomputable def construction : PR.Construction V blueprint where
  zero _ := ^âŠ¥
  succ param k ih := (subst â„’â‚’áµ£ (numeral k âˆ· param 0) (param 1)) ^â‹ ih
  zero_defined := .mk fun v â†¦ by simp [blueprint]
  succ_defined := .mk fun v â†¦ by simp [blueprint]

end DisjSeqSubst

open DisjSeqSubst

noncomputable def disjSeqSubst (w p k : V) : V := construction.result ![w, p] k

@[simp] lemma disjSeqSubst_zero (w p : V) : disjSeqSubst w p 0 = ^âŠ¥ := by simp [disjSeqSubst, construction]

@[simp] lemma disjSeqSubst_succ (w p k : V) :
    disjSeqSubst w p (k + 1) = subst â„’â‚’áµ£ (numeral k âˆ· w) p ^â‹ disjSeqSubst w p k := by simp [disjSeqSubst, construction]

noncomputable def disjSeqSubstGraph : ğšºâ‚.Semisentence 4 := blueprint.resultDef |>.rew (Rew.subst ![#0, #3, #1, #2])

section

instance disjSeqSubst.defined : ğšºâ‚-Functionâ‚ƒ[V] disjSeqSubst via disjSeqSubstGraph := .mk fun v â†¦ by
  simp [construction.result_defined_iff, disjSeqSubstGraph, disjSeqSubst, Matrix.comp_vecCons', Matrix.constant_eq_singleton]

instance disjSeqSubst.definable : ğšºâ‚-Functionâ‚ƒ[V] disjSeqSubst := disjSeqSubst.defined.to_definable

instance disjSeqSubst.definable' : Î“-[m + 1]-Functionâ‚ƒ[V] disjSeqSubst := .of_sigmaOne disjSeqSubst.definable

end

lemma _root_.LO.FirstOrder.Arithmetic.Bootstrapping.IsSemiformula.disjSeqSubst {n m w p : V} (hw : IsSemitermVec â„’â‚’áµ£ n m w) (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (k : V) :
    IsSemiformula â„’â‚’áµ£ m (disjSeqSubst w p k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    simpa [ih] using hp.subst <| hw.adjoin (numeral_semiterm m k)

lemma substs_conj_disjSeqSubst {n m l v w p : V}
    (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) (hv : IsSemitermVec â„’â‚’áµ£ m l v) (k : V) :
    subst â„’â‚’áµ£ v (disjSeqSubst w p k) = disjSeqSubst (termSubstVec â„’â‚’áµ£ n v w) p k := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    have hkw : IsSemitermVec â„’â‚’áµ£ (n + 1) m (numeral k âˆ· w) := hw.adjoin (numeral_semiterm m k)
    have ha : IsSemiformula â„’â‚’áµ£ m (disjSeqSubst w p k) := hp.disjSeqSubst hw k
    rw [disjSeqSubst_succ,
      substs_or (hp.subst hkw).isUFormula ha.isUFormula,
      substs_substs hp hv hkw,
      termSubstVec_cons (by simp) hw.isUTerm,
      numeral_substs hv]
    simp [ih]

end disjSeqSubst

section substItr

namespace SubstItr

noncomputable def blueprint : PR.Blueprint 2 where
  zero := .mkSigma â€œy w p. y = 0â€
  succ := .mkSigma â€œy ih k w p. âˆƒ numeral, !numeralGraph numeral k âˆ§ âˆƒ v, !adjoinDef v numeral w âˆ§
    âˆƒ sp, !(substsGraph â„’â‚’áµ£) sp v p âˆ§ !adjoinDef y sp ihâ€

noncomputable def construction : PR.Construction V blueprint where
  zero _ := 0
  succ param k ih := (subst â„’â‚’áµ£ (numeral k âˆ· param 0) (param 1)) âˆ· ih
  zero_defined := .mk fun v â†¦ by simp [blueprint]
  succ_defined := .mk fun v â†¦ by simp [blueprint]

end SubstItr

open SubstItr

noncomputable def substItr (w p k : V) : V := construction.result ![w, p] k

@[simp] lemma substItr_zero (w p : V) : substItr w p 0 = 0 := by simp [substItr, construction]

@[simp] lemma substItr_succ (w p k : V) : substItr w p (k + 1) = subst â„’â‚’áµ£ (numeral k âˆ· w) p âˆ· substItr w p k := by simp [substItr, construction]

section

noncomputable def substItrGraph : ğšºâ‚.Semisentence 4 := blueprint.resultDef |>.rew (Rew.subst ![#0, #3, #1, #2])

instance substItr.defined : ğšºâ‚-Functionâ‚ƒ[V] substItr via substItrGraph := .mk fun v â†¦ by
  simp [construction.result_defined_iff, substItrGraph, substItr, Matrix.comp_vecCons', Matrix.constant_eq_singleton]

instance substItr.definable : ğšºâ‚-Functionâ‚ƒ (substItr : V â†’ V â†’ V â†’ V) := substItr.defined.to_definable

instance substItr.definable' : Î“-[m + 1]-Functionâ‚ƒ (substItr : V â†’ V â†’ V â†’ V) := .of_sigmaOne substItr.definable

end

@[simp] lemma len_substItr (w p k : V) : len (substItr w p k) = k := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih => simp [ih]

@[simp] lemma substItr_nth (w p k : V) {i} (hi : i < k) :
    (substItr w p k).[i] = subst â„’â‚’áµ£ (numeral (k - (i + 1)) âˆ· w) p := by
  induction k using ISigma1.sigma1_succ_induction generalizing i
  Â· definability
  case zero => simp at hi
  case succ k ih =>
    simp only [substItr_succ]
    rcases zero_or_succ i with (rfl | âŸ¨i, rflâŸ©)
    Â· simp
    Â· simp [ih (by simpa using hi)]

lemma _root_.LO.FirstOrder.Arithmetic.Bootstrapping.IsSemiformula.substItrConj
    {m n w p : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) (k : V) :
    IsSemiformula â„’â‚’áµ£ m (^â‹€ substItr w p k) := by
  simp only [qqConj_semiformula, len_substItr]
  intro i hi
  simp only [hi, substItr_nth]
  apply hp.subst (by simp [hw])

lemma _root_.LO.FirstOrder.Arithmetic.Bootstrapping.IsSemiformula.substItrDisj
    {m n w p : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) (k : V) :
    IsSemiformula â„’â‚’áµ£ m (^â‹ substItr w p k) := by
  simp only [qqDisj_semiformula, len_substItr]
  intro i hi
  simp only [hi, substItr_nth]
  apply hp.subst (by simp [hw])

lemma neg_conj_substItr {n w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) :
    neg â„’â‚’áµ£ (^â‹€ (substItr w p k)) = ^â‹ (substItr w (neg â„’â‚’áµ£ p) k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    simp only [substItr_succ, qqConj_cons, qqDisj_cons]
    rw [neg_and (L := â„’â‚’áµ£), â†substs_neg hp (m := m), ih]
    Â· simp [hw]
    Â· exact IsSemiformula.isUFormula <| hp.subst (by simpa [hw])
    Â· exact IsSemiformula.isUFormula <| hp.substItrConj hw k

lemma neg_disj_substItr {n w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) :
    neg â„’â‚’áµ£ (^â‹ (substItr w p k)) = ^â‹€ (substItr w (neg â„’â‚’áµ£ p) k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    simp only [substItr_succ, qqDisj_cons, qqConj_cons]
    rw [neg_or (L := â„’â‚’áµ£), â†substs_neg hp (m := m), ih]
    Â· simp [hw]
    Â· apply IsSemiformula.isUFormula <| hp.subst (by simpa [hw])
    Â· apply IsSemiformula.isUFormula <| hp.substItrDisj hw k

lemma shift_conj_substItr {n w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) :
    shift â„’â‚’áµ£ (^â‹€ (substItr w p k)) = ^â‹€ (substItr (termShiftVec â„’â‚’áµ£ n w) (shift â„’â‚’áµ£ p) k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    simp only [substItr_succ, qqConj_cons]
    rw [shift_and (L := â„’â‚’áµ£), shift_substs hp (m := m), ih, termShiftVec_cons (L := â„’â‚’áµ£), numeral_shift]
    Â· simp
    Â· exact hw.isUTerm
    Â· exact hw.adjoin (numeral_semiterm m k)
    Â· exact IsSemiformula.isUFormula <| hp.subst (by simpa [hw])
    Â· exact IsSemiformula.isUFormula <| hp.substItrConj hw k

lemma shift_disj_substItr {n w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) :
    shift â„’â‚’áµ£ (^â‹ (substItr w p k)) = ^â‹ (substItr (termShiftVec â„’â‚’áµ£ n w) (shift â„’â‚’áµ£ p) k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    simp only [substItr_succ, qqDisj_cons]
    rw [shift_or (L := â„’â‚’áµ£), shift_substs hp (m := m), ih, termShiftVec_cons (L := â„’â‚’áµ£), numeral_shift]
    Â· simp
    Â· exact hw.isUTerm
    Â· exact hw.adjoin (numeral_semiterm m k)
    Â· exact IsSemiformula.isUFormula <| hp.subst (by simpa [hw])
    Â· exact IsSemiformula.isUFormula <| hp.substItrDisj hw k

lemma substs_conj_substItr {n m l w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) (hv : IsSemitermVec â„’â‚’áµ£ m l v) :
    subst â„’â‚’áµ£ v (^â‹€ (substItr w p k)) = ^â‹€ (substItr (termSubstVec â„’â‚’áµ£ n v w) p k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    have hkw : IsSemitermVec â„’â‚’áµ£ (n + 1) m (numeral k âˆ· w) := by simp [hw]
    have ha : IsSemiformula â„’â‚’áµ£ m (^â‹€ substItr w p k) := by
      simp only [qqConj_semiformula, len_substItr]
      intro i hi; simpa [hi] using hp.subst (hw.adjoin (by simp))
    simp only [substItr_succ, qqConj_cons]
    rw [substs_and (hp.subst hkw).isUFormula ha.isUFormula,
      substs_substs hp hv hkw,
      termSubstVec_cons (by simp) hw.isUTerm,
      numeral_substs hv]
    simp [ih]

lemma substs_disj_substItr {n m l w p k : V} (hp : IsSemiformula â„’â‚’áµ£ (n + 1) p) (hw : IsSemitermVec â„’â‚’áµ£ n m w) (hv : IsSemitermVec â„’â‚’áµ£ m l v) :
    subst â„’â‚’áµ£ v (^â‹ (substItr w p k)) = ^â‹ (substItr (termSubstVec â„’â‚’áµ£ n v w) p k) := by
  induction k using ISigma1.sigma1_succ_induction
  Â· definability
  case zero => simp
  case succ k ih =>
    have hkw : IsSemitermVec â„’â‚’áµ£ (n + 1) m (numeral k âˆ· w) := by simp [hw]
    have ha : IsSemiformula â„’â‚’áµ£ m (^â‹ substItr w p k) := by
      simp only [qqDisj_semiformula, len_substItr]
      intro i hi; simpa [hi] using hp.subst (hw.adjoin (by simp))
    simp only [substItr_succ, qqDisj_cons]
    rw [substs_or (hp.subst hkw).isUFormula ha.isUFormula,
      substs_substs hp hv hkw,
      termSubstVec_cons (by simp) hw.isUTerm,
      numeral_substs hv]
    simp [ih]

end substItr

end Arithmetic

section verums

noncomputable def qqVerums (k : V) : V := ^â‹€ repeatVec ^âŠ¤ k

def qqVerumsGraph : ğšºâ‚.Semisentence 2 := .mkSigma
  â€œy k. âˆƒ verum, !qqVerumDef verum âˆ§ âˆƒ vs, !repeatVecDef vs verum k âˆ§ !qqConjGraph y vsâ€

@[simp] lemma le_qqVerums (k : V) : k â‰¤ qqVerums k := by
  simpa [qqVerums] using len_le_conj (repeatVec ^âŠ¤ k)

section

instance qqVerums.defined : ğšºâ‚-Functionâ‚[V] qqVerums via qqVerumsGraph := .mk fun v â†¦ by simp [qqVerumsGraph]; rfl

instance qqVerums.definable : ğšºâ‚-Functionâ‚[V] qqVerums := qqVerums.defined.to_definable

instance qqVerums.definable' : Î“-[m + 1]-Functionâ‚[V] qqVerums := .of_sigmaOne qqVerums.definable

end

@[simp] protected lemma IsSemiformula.qqVerums (k : V) : IsSemiformula L n (qqVerums k) := by
  simp only [qqVerums, qqConj_semiformula, len_repeatVec]
  intro i hi; simp [nth_repeatVec _ _ hi]

@[simp] lemma qqVerums_zero : qqVerums (0 : V) = ^âŠ¤ := by simp [qqVerums]

@[simp] lemma qqVerums_succ (k : V) : qqVerums (k + 1) = ^âŠ¤ ^â‹ qqVerums k := by simp [qqVerums]

end verums

end LO.FirstOrder.Arithmetic.Bootstrapping
