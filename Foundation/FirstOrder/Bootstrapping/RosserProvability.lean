import Foundation.FirstOrder.Bootstrapping.WitnessComparison

/-!
# Rosser's provability predicate
-/

namespace LO.FirstOrder.Arithmetic.Bootstrapping

variable {V : Type*} [ORingStructure V] [V âŠ§â‚˜* ğ—œğšºâ‚]

variable {L : Language} [L.Encodable] [L.LORDefinable]

variable (T : Theory L) [T.Î”â‚]

def _root_.LO.FirstOrder.Theory.RosserProvable (Ï† : V) : Prop := T.ProvabilityComparison Ï† (neg L Ï†)

section

noncomputable def _root_.LO.FirstOrder.Theory.rosserProvable : ğšºâ‚.Semisentence 1 := .mkSigma
  â€œÏ†. âˆƒ nÏ†, !(negGraph L) nÏ† Ï† âˆ§ !T.provabilityComparison Ï† nÏ†â€

instance _root_.LO.FirstOrder.Theory.RosserProvable_defined :
    ğšºâ‚-Predicate (T.RosserProvable : V â†’ Prop) via T.rosserProvable := .mk fun v â†¦ by
  simp [Theory.rosserProvable, Theory.RosserProvable]

instance _root_.LO.FirstOrder.Theory.rosserProvable_definable :
    ğšºâ‚-Predicate (T.RosserProvable : V â†’ Prop) := T.RosserProvable_defined.to_definable

noncomputable abbrev _root_.LO.FirstOrder.Theory.rosserPred (Ïƒ : Sentence L) : Sentence â„’â‚’áµ£ := T.rosserProvable.val/[âŒœÏƒâŒ]

end

variable {T}

lemma rosser_quote {Ï† : SyntacticFormula L} : T.RosserProvable (V := V) âŒœÏ†âŒ â†” T.ProvabilityComparison (V := V) âŒœÏ†âŒ âŒœâˆ¼Ï†âŒ := by
  simp [Theory.RosserProvable, Semiformula.quote_def]

lemma rosser_quoteâ‚€ {Ï† : Sentence L} : T.RosserProvable (V := V) âŒœÏ†âŒ â†” T.ProvabilityComparison (V := V) âŒœÏ†âŒ âŒœâˆ¼Ï†âŒ := by
  simpa [Sentence.quote_def] using rosser_quote

lemma rosser_quote_def {Ï† : SyntacticFormula L} :
    T.RosserProvable (V := V) âŒœÏ†âŒ â†” âˆƒ b : V, T.Proof b âŒœÏ†âŒ âˆ§ âˆ€ b' < b, Â¬T.Proof b' âŒœâˆ¼Ï†âŒ := rosser_quote

lemma rosser_quote_defâ‚€ {Ï† : Sentence L} :
    T.RosserProvable (V := V) âŒœÏ†âŒ â†” âˆƒ b : V, T.Proof b âŒœÏ†âŒ âˆ§ âˆ€ b' < b, Â¬T.Proof b' âŒœâˆ¼Ï†âŒ := by simpa [Sentence.quote_def] using rosser_quote

def RosserProvable.to_provable {Ï† : V} : T.RosserProvable Ï† â†’ T.Provable Ï† := ProvabilityComparison.to_provable

lemma provable_of_standard_proof {n : â„•} {Ï† : Sentence L} : T.Proof (n : V) âŒœÏ†âŒ â†’ T âŠ¢ Ï† := fun h â†¦ by
  have : T.Proof n âŒœÏ†âŒ â†” T.Proof (â†‘n : V) âŒœÏ†âŒ := by
    simpa [Sentence.coe_quote_eq_quote] using
      Defined.shigmaOne_absolute V (Ï† := T.proof)
        (R := fun v â†¦ T.Proof (v 0) (v 1)) (R' := fun v â†¦ T.Proof (v 0) (v 1))
        Theory.Proof.defined Theory.Proof.defined ![n, âŒœÏ†âŒ]
  have : T.Provable (âŒœÏ†âŒ : â„•) := âŸ¨n, this.mpr hâŸ©
  exact provable_iff_provable.mp this

open Classical

def rosser_internalize [Entailment.Consistent T] {Ï† : Sentence L} : T âŠ¢ Ï† â†’ T.RosserProvable (âŒœÏ†âŒ : V) := by
  intro h
  let n : â„• := âŒœh.getâŒ
  have hn : T.Proof (â†‘n : V) âŒœÏ†âŒ := by simp [n, coe_quote_proof_eq]
  refine rosser_quote_defâ‚€.mpr âŸ¨n, hn, ?_âŸ©
  intro b hb Hb
  rcases eq_nat_of_lt_nat hb with âŸ¨b, rflâŸ©
  have : T âŠ¢ âˆ¼Ï† := provable_of_standard_proof (V := V) Hb
  have : Entailment.Inconsistent T := Entailment.inconsistent_of_provable_of_unprovable h this
  have : Â¬Entailment.Inconsistent T := Entailment.Consistent.not_inc inferInstance
  contradiction

def rosser_internalize_sentence [Entailment.Consistent T] {Ïƒ : Sentence L} : T âŠ¢ Ïƒ â†’ T.RosserProvable (âŒœÏƒâŒ : V) := fun h â†¦ by
  simpa [Sentence.quote_def] using rosser_internalize h

open Classical in
def not_rosserProvable [Entailment.Consistent T] {Ï† : Sentence L} : T âŠ¢ âˆ¼Ï† â†’ Â¬T.RosserProvable (âŒœÏ†âŒ : V) := by
  rintro h r
  let n : â„• := âŒœh.getâŒ
  have hn : T.Proof (â†‘n : V) âŒœâˆ¼Ï†âŒ := by simp [n, coe_quote_proof_eq]
  rcases rosser_quoteâ‚€.mp r with âŸ¨b, hb, HbâŸ©
  have : b â‰¤ n := by
    by_contra A
    have : Â¬T.Proof (â†‘n : V) âŒœâˆ¼Ï†âŒ := Hb n (lt_of_not_ge A)
    contradiction
  rcases eq_nat_of_le_nat this with âŸ¨b, rflâŸ©
  have : T âŠ¢ Ï† := provable_of_standard_proof hb
  have : Entailment.Inconsistent T := Entailment.inconsistent_of_provable_of_unprovable this h
  have : Â¬Entailment.Inconsistent T := Entailment.Consistent.not_inc inferInstance
  contradiction

def not_rosserProvable_sentence [Entailment.Consistent T] {Ïƒ : Sentence L} : T âŠ¢ âˆ¼Ïƒ â†’ Â¬T.RosserProvable (âŒœÏƒâŒ : V) := fun h â†¦ by
  simpa [Sentence.quote_def] using not_rosserProvable h

end LO.FirstOrder.Arithmetic.Bootstrapping

namespace LO.FirstOrder.Arithmetic

open Bootstrapping

section

variable {L : Language} [L.Encodable] [L.LORDefinable]

variable {T : Theory L} [T.Î”â‚] [Entailment.Consistent T]

local prefix:90 "ğ—¥" => T.rosserPred

theorem rosserProvable_D1 {Ïƒ} : T âŠ¢ Ïƒ â†’ ğ—œğšºâ‚ âŠ¢ ğ—¥Ïƒ := fun h â†¦
  provable_of_models _ _ fun (V : Type) _ _ â†¦ by
    simpa [models_iff] using rosser_internalize_sentence h

theorem rosserProvable_rosser {Ïƒ} : T âŠ¢ âˆ¼Ïƒ â†’ ğ—œğšºâ‚ âŠ¢ âˆ¼ğ—¥Ïƒ := fun h â†¦
  provable_of_models _ _ fun (V : Type) _ _ â†¦ by
    simpa [models_iff] using not_rosserProvable_sentence h

end

open ProvabilityAbstraction

variable {L : Language} [L.Encodable] [L.LORDefinable]

variable {T : Theory L} [T.Î”â‚] [Entailment.Consistent T]

variable (T)

noncomputable abbrev _root_.LO.FirstOrder.Theory.rosserProvability : Provability ğ—œğšºâ‚ T where
  prov := T.rosserProvable

instance : T.rosserProvability.HBL1 := âŸ¨rosserProvable_D1âŸ©
instance : T.rosserProvability.Rosser := âŸ¨rosserProvable_rosserâŸ©

lemma rosserProvability_def (Ïƒ : Sentence L) : T.rosserProvability Ïƒ = T.rosserPred Ïƒ := rfl

end LO.FirstOrder.Arithmetic
