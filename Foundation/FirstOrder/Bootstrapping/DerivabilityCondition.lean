module

public import Foundation.FirstOrder.Bootstrapping.DerivabilityCondition.D1
public import Foundation.FirstOrder.Bootstrapping.DerivabilityCondition.D2
public import Foundation.FirstOrder.Bootstrapping.DerivabilityCondition.D3
public import Foundation.FirstOrder.Bootstrapping.ProvabilityAbstraction.Basic
public import Foundation.FirstOrder.Bootstrapping.FixedPoint

@[expose] public section
/-!
# Derivability conditions of standard provability predicate
-/

namespace LO.FirstOrder.Arithmetic

open ISigma1 Bootstrapping ProvabilityAbstraction

noncomputable instance : Diagonalization ğ—œğšºâ‚ where
  fixedpoint := fixedpoint
  diag Î¸ := diagonal Î¸

section

variable {L : Language} [L.Encodable] [L.LORDefinable] {T : Theory L} [T.Î”â‚]

local prefix:90 "â–¡" => T.provabilityPred

/-- The derivability condition D1. -/
theorem provable_D1 {Ïƒ} : T âŠ¢ Ïƒ â†’ ğ—œğšºâ‚ âŠ¢ â–¡Ïƒ := fun h â†¦
  provable_of_models _ _ fun (V : Type) _ _ â†¦ by simpa [models_iff] using internalize_provability (V := V) h

/-- The derivability condition D2. -/
theorem provable_D2 {Ïƒ Ï€} : ğ—œğšºâ‚ âŠ¢ â–¡(Ïƒ â Ï€) â â–¡Ïƒ â â–¡Ï€ :=
  provable_of_models _ _ fun (V : Type) _ _ â†¦ by simpa [models_iff] using modus_ponens_sentence T

variable (T)

noncomputable abbrev _root_.LO.FirstOrder.Theory.standardProvability : Provability ğ—œğšºâ‚ T where
  prov := T.provable
  prov_def := provable_D1

variable {T}

instance : T.standardProvability.HBL2 := âŸ¨provable_D2âŸ©

lemma standardProvability_def (Ïƒ : Sentence L) : T.standardProvability Ïƒ = T.provabilityPred Ïƒ := rfl

instance [T.Î”â‚] : T.standardProvability.SoundOnModel â„• :=
  âŸ¨fun {Ïƒ} â†¦ by simp [Arithmetic.standardProvability_def, models_iff]âŸ©

end

section arithmetic

variable {T : Theory â„’â‚’áµ£} [T.Î”â‚]

local prefix:90 "â–¡" => T.provabilityPred

lemma provable_sigma_one_complete [ğ—£ğ—”â» âª¯ T] {Ïƒ : Sentence â„’â‚’áµ£} (hÏƒ : Hierarchy ğšº 1 Ïƒ) :
    ğ—œğšºâ‚ âŠ¢ Ïƒ â â–¡Ïƒ :=
  provable_of_models _ _ fun (V : Type) _ _ â†¦ by
    simpa [models_iff] using Bootstrapping.Arithmetic.sigma_one_complete (T := T) (V := V) hÏƒ

/-- The derivability condition D3. -/
theorem provable_D3 [ğ—£ğ—”â» âª¯ T] {Ïƒ : Sentence â„’â‚’áµ£} :
    ğ—œğšºâ‚ âŠ¢ â–¡Ïƒ â â–¡â–¡Ïƒ := provable_sigma_one_complete (by simp)

open LO.Entailment LO.Entailment.FiniteContext

variable {U : ArithmeticTheory}

lemma provable_D2_context [ğ—œğšºâ‚ âª¯ U] {Î“ Ïƒ Ï€} (hÏƒÏ€ : Î“ âŠ¢[U] â–¡(Ïƒ â Ï€)) (hÏƒ : Î“ âŠ¢[U] â–¡Ïƒ) :
    Î“ âŠ¢[U] â–¡Ï€ := FiniteContext.of'! (weakening inferInstance provable_D2) â¨€! hÏƒÏ€ â¨€! hÏƒ

lemma provable_D3_context [ğ—£ğ—”â» âª¯ T] [ğ—œğšºâ‚ âª¯ U] {Î“ Ïƒ} (hÏƒÏ€ : Î“ âŠ¢[U] â–¡Ïƒ) :
  Î“ âŠ¢[U] â–¡â–¡Ïƒ := FiniteContext.of'! (weakening inferInstance provable_D3) â¨€! hÏƒÏ€

lemma provable_sound [U.SoundOnHierarchy ğšº 1] {Ïƒ} : U âŠ¢ â–¡Ïƒ â†’ T âŠ¢ Ïƒ := fun h â†¦ by
  have : â„• âŠ§â‚˜ T.provabilityPred Ïƒ := ArithmeticTheory.SoundOn.sound (F := Arithmetic.Hierarchy ğšº 1) h (by simp)
  simpa [models_iff] using this

lemma provable_complete [U.SoundOnHierarchy ğšº 1] [ğ—œğšºâ‚ âª¯ U] {Ïƒ} : T âŠ¢ Ïƒ â†” U âŠ¢ â–¡Ïƒ :=
  âŸ¨fun h â†¦ weakening inferInstance (provable_D1 h), provable_soundâŸ©

instance [ğ—£ğ—”â» âª¯ T] : T.standardProvability.HBL3 := âŸ¨provable_D3âŸ©

instance [ğ—£ğ—”â» âª¯ T] : T.standardProvability.HBL where

instance [ArithmeticTheory.SoundOnHierarchy T ğšº 1] : T.standardProvability.SoundOnClass (Set.univ) := âŸ¨fun _ _ h â†¦ provable_sound hâŸ©

open LO.Entailment in
/--
  If `Ï€` is equivalent to some ğšºâ‚ sentence `Ïƒ`,
  then `Ï€ â â–¡Ï€` is provable in `T` (note: not `ğ—œğšºâ‚`, compare `provable_sigma_one_complete`)
-/
lemma provable_sigma_one_complete_of_E {Ïƒ Ï€} [ğ—œğšºâ‚ âª¯ T]
  (hÏƒ : Hierarchy ğšº 1 Ïƒ) (hÏƒÏ€ : T âŠ¢ Ïƒ â­¤ Ï€) : T âŠ¢ Ï€ â â–¡Ï€ := by
  apply C!_trans (Ïˆ := Ïƒ) ?_ $ C!_trans (Ïˆ := â–¡Ïƒ) ?_ ?_;
  . cl_prover [hÏƒÏ€];
  . apply WeakerThan.pbl $ provable_sigma_one_complete hÏƒ;
  . apply WeakerThan.pbl (ğ“¢ := ğ—œğšºâ‚);
    apply ProvabilityAbstraction.prov_distribute_imply (ğ”… := T.standardProvability);
    cl_prover [hÏƒÏ€];

end arithmetic

end LO.FirstOrder.Arithmetic
