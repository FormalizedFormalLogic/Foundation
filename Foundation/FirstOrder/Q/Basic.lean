import Foundation.FirstOrder.Arithmetic.Basic
import Foundation.FirstOrder.R0.Basic
import Mathlib.Data.ENat.Basic

/-!
# Robinson's theory $\mathsf{Q}$
-/

noncomputable section

namespace LO

open FirstOrder FirstOrder.Arithmetic

inductive RobinsonQ : ArithmeticTheory
  | equal : âˆ€ Ï† âˆˆ ğ„ğ, RobinsonQ Ï†
  | succNeZero : RobinsonQ â€œa | a + 1 â‰  0â€
  | succInj : RobinsonQ â€œa b | a + 1 = b + 1 â†’ a = bâ€
  | addZero : RobinsonQ â€œa | a + 0 = aâ€
  | addSucc : RobinsonQ â€œa b | a + (b + 1) = (a + b) + 1â€
  | mulZero : RobinsonQ â€œa | a * 0 = 0â€
  | mulSucc : RobinsonQ â€œa b | a * (b + 1) = a * b + aâ€
  | zeroAddOne : RobinsonQ â€œ0 + 1 = 1â€

notation "ğ" => RobinsonQ

namespace RobinsonQ

open ORingStruc

set_option linter.flexible false in
@[simp] instance : â„• âŠ§â‚˜* ğ := âŸ¨by
  intro Ïƒ h
  rcases h <;> simp [models_def, add_assoc, mul_add]
  case equal h =>
    have : â„• âŠ§â‚˜* (ğ„ğ : ArithmeticTheory) := inferInstance
    exact modelsTheory_iff.mp this hâŸ©

instance : ğ„ğ âª¯ ğ := Entailment.WeakerThan.ofSubset <| fun Ï† hp â†¦ equal Ï† hp

variable {M : Type*} [ORingStruc M] [M âŠ§â‚˜* ğ]

@[simp] protected lemma succ_ne_zero (a : M) : a + 1 â‰  0 := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.succNeZero (fun _ â†¦ a)

lemma succ_inj {a b : M} : a + 1 = b + 1 â†’ a = b := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.succInj (a :>â‚™ fun _ â†¦ b)

@[simp] protected lemma add_zero (a : M) : a + 0 = a := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.addZero (fun _ â†¦ a)

protected lemma add_succ (a b : M) : a + (b + 1) = a + b + 1 := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.addSucc (a :>â‚™ fun _ â†¦ b)

@[simp] protected lemma mul_zero (a : M) : a * 0 = 0 := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.mulZero (fun _ â†¦ a)

protected lemma mul_succ (a b : M) : a * (b + 1) = a * b + a := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.mulSucc (a :>â‚™ fun _ â†¦ b)

@[simp] protected lemma zero_add_one : (0 + 1 : M) = 1 := by
  simpa [models_iff] using ModelsTheory.models M RobinsonQ.zeroAddOne (fun _ â†¦ 0)

@[simp] lemma numeral_zero_add (n : â„•) : 0 + (numeral n : M) = numeral n := by
  match n with
  |     0 => simp
  |     1 => simp
  | n + 2 => simp [ORingStruc.numeral, RobinsonQ.add_succ, numeral_zero_add (n + 1)]

@[simp] lemma zero_mul_one : (0 : M) * 1 = 0 := calc
   (0 : M) * 1 = 0 * (0 + 1) := by simp
    _          = 0 * 0 + 0   := by rw [RobinsonQ.mul_succ]
    _          = 0           := by simp

@[simp] lemma numeral_zero_mul (n : â„•) : 0 * (numeral n : M) = 0 := by
  match n with
  |     0 => simp
  |     1 => simp
  | n + 2 => calc
    (0 : M) * numeral (n + 2) = 0 * (numeral (n + 1) + 1) := rfl
    _                         = 0 * numeral (n + 1) + 0   := by rw [RobinsonQ.mul_succ]
    _                         = 0                         := by simp [numeral_zero_mul (n + 1)]

@[simp] lemma one_mul_one : (1 : M) * 1 = 1 := calc
  (1 : M) * 1 = 1 * (0 + 1) := by simp
  _           = 1 * 0 + 1   := by rw [RobinsonQ.mul_succ]
  _           = 1           := by simp

@[simp] lemma mul_one (n : â„•) : (numeral n : M) * 1 = numeral n := calc
  (numeral n : M) * 1 = numeral n * (0 + 1)       := by simp
  _                   = numeral n * 0 + numeral n := by rw [RobinsonQ.mul_succ]
  _                   = numeral n                 := by simp

lemma numeral_add_one (n : â„•) : (numeral n : M) + 1 = numeral (n + 1) := by
  match n with
  |     0 => simp
  | n + 1 => rfl

lemma numeral_add (n m : â„•) : (numeral n : M) + numeral m = numeral (n + m) := by
  match m with
  |     0 => simp
  |     1 => simp [numeral_add_one]
  | m + 2 => calc
    (numeral n : M) + (numeral (m + 1) + 1) = (numeral n + numeral (m + 1)) + 1 := RobinsonQ.add_succ _ _
    _                                       = numeral (n + (m + 1)) + 1         := by rw [numeral_add n (m + 1)]
    _                                       = numeral (n + (m + 2))             := by simp [â†add_assoc]; rfl

lemma numeral_mul (n m : â„•) : (numeral n : M) * numeral m = numeral (n * m) := by
  match m with
  |     0 => simp
  |     1 => simp
  | m + 2 => calc
    (numeral n : M) * (numeral (m + 1) + 1) = numeral n * numeral (m + 1) + numeral n := by rw [RobinsonQ.mul_succ]
    _                                       = numeral (n * (m + 1)) + numeral n       := by rw [numeral_mul n (m + 1)]
    _                                       = numeral (n * (m + 2))                   := by simp [numeral_add, mul_add, mul_two, â†add_assoc]

@[simp] lemma zero_ne_one : (0 : M) â‰  1 := calc
  (0 : M) â‰  0 + 1 := Ne.symm <| RobinsonQ.succ_ne_zero 0
  _       = 1     := by simp

lemma numeral_succ_ne {n : â„•} : (numeral n : M) â‰  numeral (n + 1) := by
  match n with
  |     0 => simp
  |     1 => calc
    (1 : M) = 0 + 1 := by simp
    _       â‰  1 + 1 := fun e â†¦ zero_ne_one (RobinsonQ.succ_inj e)
  | n + 2 =>
    intro e
    exact numeral_succ_ne (RobinsonQ.succ_inj e)

/-
instance : M âŠ§â‚˜* ğ‘â‚€ := modelsTheory_iff.mpr <| by
  intro Ï† h
  rcases h
  case equal h =>
    have : M âŠ§â‚˜* (ğ„ğ : ArithmeticTheory) := inferInstance
    exact modelsTheory_iff.mp this h
  case Î©â‚ n m => simp [models_iff, numeral_add]
  case Î©â‚‚ n m => simp [models_iff, numeral_mul]
  case Î©â‚ƒ n m h =>
    simp [models_iff]
  case Î©â‚„ n =>
    suffices âˆ€ x : M, x < â†‘n â†” âˆƒ i < n, x = â†‘i by simpa [models_iff, numeral_eq_natCast];
    intro x
    constructor
    Â· intro hx; rcases eq_nat_of_lt_nat hx with âŸ¨x, rflâŸ©; exact âŸ¨x, by simpa using hx, by simpâŸ©
    Â· rintro âŸ¨i, hi, rflâŸ©; simp [hi]
-/



namespace Countermodel

namespace ENat

variable {a b : â„•âˆ} {m n : â„•}

local instance : ORingStruc ENat where
  add := (Â· + Â·)
  mul := (Â· * Â·)
  lt := (Â· < Â·)

end ENat

set_option linter.flexible false in
instance : â„•âˆ âŠ§â‚˜* ğ := âŸ¨by
  intro Ïƒ h;
  rcases h <;> simp [models_def];
  case equal h =>
    have : â„•âˆ âŠ§â‚˜* (ğ„ğ : ArithmeticTheory) := inferInstance
    exact modelsTheory_iff.mp this h
  case addSucc =>
    intro f;
    generalize f 0 = m;
    generalize f 1 = n;
    match m, n with
    | âŠ¤, _ | _, âŠ¤ => simp;
    | .some m, .some n => rfl;
  case mulSucc =>
    intro f;
    generalize f 0 = m;
    generalize f 1 = n;
    cases m <;> cases n;
    case coe.top m => match m with | 0 | m + 1 => simp;
    case coe.coe m n => rfl;
    all_goals simp;
  case succInj =>
    intro f;
    generalize f 0 = m;
    generalize f 1 = n;
    cases m <;> cases n;
    case coe.coe m n =>
      intro h;
      simpa using ENat.coe_inj.mp h
    all_goals tauto;
âŸ©

lemma unprovable_neSucc : ğ âŠ¬ â€œx | x + 1 â‰  xâ€ := unprovable_of_countermodel (M := â„•âˆ) (fun x â†¦ âŠ¤) _ (by simp)

end Countermodel

end RobinsonQ

end LO
