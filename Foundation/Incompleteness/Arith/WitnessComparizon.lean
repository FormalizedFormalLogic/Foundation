import Foundation.Incompleteness.Arith.D3
import Foundation.Logic.HilbertStyle.Supplemental
import Foundation.Incompleteness.ToFoundation.Basic

noncomputable section

open Classical
namespace LO.Arith

open LO.FirstOrder LO.FirstOrder.Arith

variable {V : Type*} [ORingStruc V] [V âŠ§â‚˜* ğˆğšºâ‚]

section WitnessComparisons

variable {L : Arith.Language V} {pL : LDef} [Arith.Language.Defined L pL]

variable (T : L.Theory) {pT : pL.TDef} [T.Defined pT]

def Language.Theory.DerivabilityComparison (sâ‚ sâ‚‚ : V) : Prop := âˆƒ dâ‚, T.DerivationOf dâ‚ sâ‚ âˆ§ âˆ€ dâ‚‚ < dâ‚, Â¬T.DerivationOf dâ‚‚ sâ‚‚

def Language.Theory.ProvabilityComparison (Ï† Ïˆ : V) : Prop := T.DerivabilityComparison {Ï†} {Ïˆ}

section

def _root_.LO.FirstOrder.Arith.LDef.TDef.derivabilityComparisonDef {pL : LDef} (pT : pL.TDef) : ğšºâ‚.Semisentence 2 := .mkSigma
  â€œsâ‚ sâ‚‚. âˆƒ dâ‚, !pT.derivationOfDef.sigma dâ‚ sâ‚ âˆ§ âˆ€ dâ‚‚ < dâ‚, Â¬!pT.derivationOfDef.pi dâ‚‚ sâ‚‚â€ (by simp)

lemma Language.Theory.derivabilityComparison_defined : ğšºâ‚-Relation T.DerivabilityComparison via pT.derivabilityComparisonDef := by
  intro v
  simp [LDef.TDef.derivabilityComparisonDef, HierarchySymbol.Semiformula.val_sigma,
    T.derivationOf_defined.df.iff, T.derivationOf_defined.proper.iff', Language.Theory.DerivabilityComparison]

instance Language.Theory.derivabilityComparison_definable : ğšºâ‚-Relation T.DerivabilityComparison := T.derivabilityComparison_defined.to_definable

def _root_.LO.FirstOrder.Arith.LDef.TDef.provabilityComparisonDef {pL : LDef} (pT : pL.TDef) : ğšºâ‚.Semisentence 2 := .mkSigma
  â€œÏ† Ïˆ. âˆƒ sÏ† sÏˆ, !insertDef sÏ† Ï† 0 âˆ§ !insertDef sÏˆ Ïˆ 0 âˆ§ !pT.derivabilityComparisonDef sÏ† sÏˆâ€ (by simp)

lemma Language.Theory.provabilityComparison_defined : ğšºâ‚-Relation T.ProvabilityComparison via pT.provabilityComparisonDef := by
  intro v; simp [LDef.TDef.provabilityComparisonDef, T.derivabilityComparison_defined.df.iff, Language.Theory.ProvabilityComparison, singleton_eq_insert, emptyset_def]

instance Language.Theory.provabilityComparison_definable : ğšºâ‚-Relation T.ProvabilityComparison := T.provabilityComparison_defined.to_definable

/-- instance for definability tactic-/
instance Language.Theory.provabilityComparison_definable' : ğšº-[0 + 1]-Relation T.ProvabilityComparison := T.provabilityComparison_definable

end

variable {T}

namespace Language.Theory.DerivabilityComparison

variable {Î“ Î” : V}

lemma refl_iff_derivable : T.DerivabilityComparison Î“ Î“ â†” T.Derivable Î“ := by
  constructor
  Â· rintro âŸ¨d, dd, hdâŸ©
    exact âŸ¨d, ddâŸ©
  Â· rintro âŸ¨d, ddâŸ©
    have : âˆƒ b, T.DerivationOf b Î“ âˆ§ âˆ€ z < b, Â¬T.DerivationOf z Î“ :=
      least_number_hh ğšº 1 (P := (T.DerivationOf Â· Î“)) (by definability) dd
    rcases this with âŸ¨b, bd, hâŸ©
    exact âŸ¨b, bd, hâŸ©

lemma antisymm : T.DerivabilityComparison Î“ Î” â†’ T.DerivabilityComparison Î” Î“ â†’ Î“ = Î” := by
  rintro âŸ¨dÎ“, dÎ“d, HÎ“âŸ© âŸ¨dÎ”, dÎ”d, HÎ”âŸ©
  have : dÎ“ = dÎ” := by
    by_contra ne
    wlog lt : dÎ“ < dÎ”
    Â· have : dÎ“ â‰¤ dÎ” := le_of_not_lt <| this dÎ” dÎ”d HÎ” dÎ“ dÎ“d HÎ“ (Ne.symm ne)
      have : dÎ” â‰¤ dÎ“ := le_of_not_lt lt
      have : dÎ“ = dÎ” := le_antisymm (by assumption) (by assumption)
      contradiction
    have : Â¬T.DerivationOf dÎ“ Î“ := HÎ” dÎ“ lt
    contradiction
  have : fstIdx dÎ” = Î” := dÎ”d.1
  have : fstIdx dÎ“ = Î“ := dÎ“d.1
  simp_all

lemma find_minimal_proof_fintype [Fintype Î¹] (Î“ : Î¹ â†’ V) (H : T.Derivable (Î“ i)) :
    âˆƒ j, âˆ€ k, T.DerivabilityComparison (Î“ j) (Î“ k) := by
  rcases show âˆƒ dáµ¢, T.DerivationOf dáµ¢ (Î“ i)from H with âŸ¨dáµ¢, Hdáµ¢âŸ©
  have : âˆƒ z, (âˆƒ j, T.DerivationOf z (Î“ j)) âˆ§ âˆ€ w < z, âˆ€ (x : Î¹), Â¬T.DerivationOf w (Î“ x) := by
    simpa using
      least_number_hh ğšº 1 (P := fun z â†¦ âˆƒ j, T.DerivationOf z (Î“ j))
        (HierarchySymbol.Boldface.fintype_ex fun j â†¦ by definability) (x := dáµ¢) âŸ¨i, Hdáµ¢âŸ©
  rcases this with âŸ¨z, âŸ¨j, hjâŸ©, HâŸ©
  exact âŸ¨j, fun k â†¦ âŸ¨z, hj, fun w hw â†¦ H w hw kâŸ©âŸ©

end Language.Theory.DerivabilityComparison

namespace Language.Theory.ProvabilityComparison

variable {Ï† Ïˆ : V}

lemma refl_iff_provable : T.ProvabilityComparison Ï† Ï† â†” T.Provable Ï† := Language.Theory.DerivabilityComparison.refl_iff_derivable

lemma antisymm : T.ProvabilityComparison Ï† Ïˆ â†’ T.ProvabilityComparison Ïˆ Ï† â†’ Ï† = Ïˆ :=
  fun hâ‚ hâ‚‚ â†¦ by
    simpa using mem_ext_iff.mp (Language.Theory.DerivabilityComparison.antisymm hâ‚ hâ‚‚) Ï†

lemma find_minimal_proof_fintype [Fintype Î¹] (Ï† : Î¹ â†’ V) (H : T.Provable (Ï† i)) :
    âˆƒ j, âˆ€ k, T.ProvabilityComparison (Ï† j) (Ï† k) := DerivabilityComparison.find_minimal_proof_fintype _ H

end Language.Theory.ProvabilityComparison

end WitnessComparisons

section ProvabilityComparisonOnArithmetic

variable (T : Theory â„’â‚’áµ£) [T.Delta1Definable]

/-- Provability predicate for arithmetic stronger than $\mathbf{R_0}$. -/
def _root_.LO.FirstOrder.Theory.ProvabilityComparisonâ‚ (Ï† Ïˆ : V) : Prop := ((T + ğ‘â‚€').codeIn V).ProvabilityComparison Ï† Ïˆ

def _root_.LO.FirstOrder.Theory.provabilityComparisonâ‚Def : ğšºâ‚.Semisentence 2 := .mkSigma
  â€œÏ† Ïˆ. !(T + ğ‘â‚€').tDef.provabilityComparisonDef Ï† Ïˆâ€ (by simp)

lemma provabilityComparisonâ‚_defined : ğšºâ‚-Relation (T.ProvabilityComparisonâ‚ : V â†’ V â†’ Prop) via T.provabilityComparisonâ‚Def := by
  intro v; simp [FirstOrder.Theory.provabilityComparisonâ‚Def, FirstOrder.Theory.ProvabilityComparisonâ‚, ((T + ğ‘â‚€').codeIn V).provabilityComparison_defined.df.iff]

@[simp] lemma eval_provabilityComparisonâ‚ (v) :
    Semiformula.Evalbm V v T.provabilityComparisonâ‚Def.val â†” T.ProvabilityComparisonâ‚ (v 0) (v 1) := (provabilityComparisonâ‚_defined T).df.iff v

instance provabilityComparisonâ‚_definable : ğšºâ‚-Relation (T.ProvabilityComparisonâ‚ : V â†’ V â†’ Prop) := (provabilityComparisonâ‚_defined T).to_definable

/-- instance for definability tactic-/
instance provabilityComparisonâ‚_definable' : ğšº-[0 + 1]-Relation (T.ProvabilityComparisonâ‚ : V â†’ V â†’ Prop) := provabilityComparisonâ‚_definable T

end ProvabilityComparisonOnArithmetic

end LO.Arith
