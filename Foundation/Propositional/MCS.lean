/-
  Maximal consistent set for propositional classical logic
-/
import Foundation.Logic.HilbertStyle.Supplemental
import Foundation.Meta.ClProver

section

open LO LO.Entailment LO.Entailment.Context

variable {F} [DecidableEq F] [LogicalConnective F]
         {S} [Entailment S F]
variable {ğ“¢ : S} {Î“ Î” : Set F} {Ï† Ïˆ : F}

namespace Set.LO

def Consistent (ğ“¢ : S) (Î“ : Set F) : Prop := Î“ *âŠ¬[ğ“¢] âŠ¥

def Inconsistent (ğ“¢ : S) (Î“ : Set F) : Prop := Â¬(Consistent ğ“¢ Î“)

def Maximal (ğ“¢ : S) (Î“ : Set F) : Prop := âˆ€ Î”, Î“ âŠ‚ Î” â†’ Inconsistent ğ“¢ Î”


lemma def_consistent [Entailment.Minimal ğ“¢] : Consistent ğ“¢ Î“ â†” âˆ€ Î” : Finset F, (â†‘Î” âŠ† Î“) â†’ Î” *âŠ¬[ğ“¢] âŠ¥ := by
  constructor;
  . intro h Î” hÎ”;
    have := Context.provable_iff_finset.not.mp h;
    push_neg at this;
    apply this;
    simpa;
  . intro h;
    apply Context.provable_iff_finset.not.mpr;
    push_neg;
    simpa using h;

lemma def_inconsistent [Entailment.Minimal ğ“¢] : Inconsistent ğ“¢ Î“ â†” âˆƒ Î” : Finset F, (â†‘Î” âŠ† Î“) âˆ§ (Î” *âŠ¢[ğ“¢] âŠ¥) := by
  rw [Inconsistent, def_consistent];
  push_neg;
  simp;

omit [DecidableEq F] in
@[grind]
lemma iff_maximal_no_proper_consistent_superset : Maximal ğ“¢ Î“ â†” (âˆ€ Î”, Consistent ğ“¢ Î” â†’ Î“ âŠ† Î” â†’ Î” = Î“) := by
  dsimp [Maximal, Inconsistent];
  grind;

section

variable [Entailment.Cl ğ“¢]

@[simp]
lemma empty_consistent [consis : Entailment.Consistent ğ“¢] : Consistent ğ“¢ âˆ… := by
  obtain âŸ¨Ï†, hÏ†âŸ© := consis.exists_unprovable;
  apply def_consistent.mpr;
  intro Î” hÎ”;
  rw [(show Î” = âˆ… by simpa [Set.subset_empty_iff, Finset.coe_eq_empty] using hÎ”)];
  contrapose! hÏ†;
  apply Context.emptyPrf!
  apply of_O!;
  simp_all;

@[grind]
lemma not_mem_falsum_of_consistent (Î“_consis : Consistent ğ“¢ Î“) : âŠ¥ âˆ‰ Î“ := by
  contrapose! Î“_consis;
  suffices Î“ *âŠ¢[ğ“¢] âŠ¥ by simpa [Consistent];
  apply Context.by_axm!;
  simpa;

@[grind]
lemma not_mem_neg_of_mem_of_consistent (Î“_consis : Consistent ğ“¢ Î“) (h : Ï† âˆˆ Î“) : âˆ¼Ï† âˆ‰ Î“ := by
  by_contra hC;
  apply def_consistent.mp Î“_consis {Ï†, âˆ¼Ï†} ?_;
  . apply Context.bot_of_mem_neg (Ï† := Ï†) <;> simp;
  . grind;

@[grind]
lemma not_mem_of_mem_neg_of_consistent (Î“_consis : Consistent ğ“¢ Î“) (h : âˆ¼Ï† âˆˆ Î“) : Ï† âˆ‰ Î“ := by
  by_contra hC;
  apply def_consistent.mp Î“_consis {Ï†, âˆ¼Ï†} ?_;
  . apply Context.bot_of_mem_neg (Ï† := Ï†) <;> simp;
  . grind;

lemma iff_insert_consistent : Consistent ğ“¢ (insert Ï† Î“) â†” âˆ€ Î” : Finset F, â†‘Î” âŠ† Î“ â†’ Î” *âŠ¬[ğ“¢] Ï† â âŠ¥ := by
  constructor;
  . intro h Î“ hÎ“;
    have := def_consistent.mp h (insert Ï† Î“) ?_;
    . contrapose! this;
      simp only [Finset.coe_insert];
      apply Context.deductInv! this;
    . grind;
  . intro h;
    apply def_consistent.mpr;
    intro Î“ hÎ“;
    have := @h (Î“.erase Ï†) (by grind);
    contrapose! this;
    apply Context.deduct!;
    apply Context.weakening! (Î“ := Î“);
    . simp;
    . assumption;

lemma iff_inconsistent_insert : Inconsistent ğ“¢ (insert Ï† Î“) â†” âˆƒ Î” : Finset F, â†‘Î” âŠ† Î“ âˆ§ Î” *âŠ¢[ğ“¢] Ï† â âŠ¥ := by
  rw [Inconsistent, iff_insert_consistent]
  push_neg;
  simp;

lemma iff_inconsistent_insert_provable_neg : Inconsistent ğ“¢ (insert Ï† Î“) â†” Î“ *âŠ¢[ğ“¢] âˆ¼Ï† := by
  apply Iff.trans iff_inconsistent_insert;
  constructor;
  . rintro âŸ¨Î”, hÎ“Î”, hÎ”âŸ©;
    apply N!_iff_CO!.mpr;
    apply weakening! hÎ“Î” hÎ”;
  . intro h;
    obtain âŸ¨Î”, hÎ”â‚, hÎ”â‚‚âŸ© := Context.provable_iff_finset.mp h;
    use Î”;
    constructor;
    . tauto;
    . apply N!_iff_CO!.mp;
      assumption;

lemma iff_inconsistent_insert_neg_provable : Inconsistent ğ“¢ (insert (âˆ¼Ï†) Î“) â†” Î“ *âŠ¢[ğ“¢] Ï† := by
  apply Iff.trans iff_inconsistent_insert_provable_neg;
  constructor <;> . intro h; cl_prover [h];

lemma iff_consistent_insert_neg_unprovable : Consistent ğ“¢ (insert (âˆ¼Ï†) Î“) â†” Î“ *âŠ¬[ğ“¢] Ï† := by
  simpa [Inconsistent] using iff_inconsistent_insert_neg_provable.not;

lemma iff_consistent_neg_singleton_unprovable : Consistent ğ“¢ ({âˆ¼Ï†}) â†” ğ“¢ âŠ¬ Ï† := by
  rw [(show {âˆ¼Ï†} = insert (âˆ¼Ï†) âˆ… by simp), iff_consistent_insert_neg_unprovable];
  apply Context.provable_iff_provable.not.symm;


lemma or_consistent_insert_consistent_insert_neg (T_consis : Consistent ğ“¢ T) (Ï†) :
  Consistent ğ“¢ (insert Ï† T) âˆ¨ Consistent ğ“¢ (insert (âˆ¼Ï†) T) := by
  by_contra! hC;
  obtain âŸ¨hCâ‚, hCâ‚‚âŸ© := hC;
  obtain âŸ¨Î”â‚, hÎ”â‚Î“, hÎ”â‚âŸ© := iff_inconsistent_insert.mp $ by simpa using hCâ‚;
  obtain âŸ¨Î”â‚‚, hÎ”â‚‚Î“, hÎ”â‚‚âŸ© := iff_inconsistent_insert.mp $ by simpa using hCâ‚‚;
  apply def_consistent.mp T_consis (Î”â‚ âˆª Î”â‚‚);
  . grind;
  . replace hÎ”â‚ : â†‘(Î”â‚ âˆª Î”â‚‚) *âŠ¢[ğ“¢] Ï† â âŠ¥ := Context.weakening! (by simp) hÎ”â‚;
    replace hÎ”â‚‚ : â†‘(Î”â‚ âˆª Î”â‚‚) *âŠ¢[ğ“¢] âˆ¼Ï† â âŠ¥ := Context.weakening! (by simp) hÎ”â‚‚;
    exact of_C!_of_C!_of_A! hÎ”â‚ hÎ”â‚‚ (by simp);

lemma exists_consistent_maximal_of_consistent (Î“_consis : Consistent ğ“¢ Î“)
  : âˆƒ Î”, Î“ âŠ† Î” âˆ§ Consistent ğ“¢ Î” âˆ§ âˆ€ U, Consistent ğ“¢ U â†’ Î” âŠ† U â†’ U = Î” := by
  obtain âŸ¨Î”, hâ‚, âŸ¨hâ‚‚, hâ‚ƒâŸ©âŸ© := zorn_subset_nonempty { Î“ | Consistent ğ“¢ Î“} (by
    intro c hc chain hnc;
    existsi (â‹ƒâ‚€ c);
    simp only [Set.mem_setOf_eq];
    constructor;
    . apply def_consistent.mpr;
      intro Î“ hÎ“;
      by_contra hC;
      obtain âŸ¨U, hUc, hUsâŸ© := Set.subset_mem_chain_of_finite c hnc chain (s := â†‘Î“.toSet) (by simp) $ by
        intro Ï† hÏ†;
        apply hÎ“ hÏ†;
      have : Consistent ğ“¢ U := hc hUc;
      have : Inconsistent ğ“¢ U := by
        apply def_inconsistent.mpr;
        use Î“;
      contradiction;
    . intro s a;
      exact Set.subset_sUnion_of_mem a;
  ) Î“ Î“_consis;
  use Î”;
  refine âŸ¨?_, ?_, ?_âŸ©;
  . assumption;
  . assumption;
  . intro U hU hZU;
    apply Set.eq_of_subset_of_subset;
    . exact hâ‚ƒ hU hZU;
    . assumption;

end

end Set.LO


namespace LO

open Set.LO

abbrev MaximalConsistentSet (ğ“¢ : S) := { Î“ // Consistent ğ“¢ Î“ âˆ§ Maximal ğ“¢ Î“ }

namespace MaximalConsistentSet

variable {Î“ Î” Î“â‚ Î“â‚‚ : MaximalConsistentSet ğ“¢}

instance : Membership F (MaximalConsistentSet ğ“¢) := âŸ¨Î» Î“ Ï† => Ï† âˆˆ Î“.1âŸ©

omit [DecidableEq F] in @[simp] lemma consistent (Î“ : MaximalConsistentSet ğ“¢) : Consistent ğ“¢ Î“ := Î“.2.1

omit [DecidableEq F] in lemma maximal (Î“ : MaximalConsistentSet ğ“¢) : Maximal ğ“¢ Î“ := Î“.2.2

omit [DecidableEq F] in
lemma no_proper_consistent_superset (Î“ : MaximalConsistentSet ğ“¢) : âˆ€ Î”, Consistent ğ“¢ Î” â†’ Î“.1 âŠ† Î” â†’ Î” = Î“.1 := by
  apply iff_maximal_no_proper_consistent_superset.mp;
  exact Î“.maximal;


variable [Entailment.Cl ğ“¢]

lemma exists_of_consistent {Î“ : Set F} (Î“_consis : Consistent ğ“¢ Î“) : âˆƒ Î” : MaximalConsistentSet ğ“¢, (Î“ âŠ† Î”.1) := by
  have âŸ¨Î“, _âŸ© := exists_consistent_maximal_of_consistent Î“_consis;
  use âŸ¨Î“, ?_âŸ© <;> grind;

alias lindenbaum := exists_of_consistent


instance [Entailment.Consistent ğ“¢] : Nonempty (MaximalConsistentSet ğ“¢) := âŸ¨lindenbaum empty_consistent |>.chooseâŸ©

@[grind]
lemma or_mem_mem_neg (Ï†) : Ï† âˆˆ Î“ âˆ¨ âˆ¼Ï† âˆˆ Î“ := by
  by_contra! hC;
  rcases or_consistent_insert_consistent_insert_neg Î“.consistent Ï† with h | h;
  . apply hC.1; simpa using Î“.no_proper_consistent_superset _ h (by simp);
  . apply hC.2; simpa using Î“.no_proper_consistent_superset _ h (by simp);

lemma iff_mem_provable : Ï† âˆˆ Î“ â†” Î“.1 *âŠ¢[ğ“¢] Ï† := by
  constructor;
  . intro h;
    apply Context.by_axm!;
    simpa;
  . intro hÏ†;
    suffices âˆ¼Ï† âˆ‰ Î“.1 by apply or_iff_not_imp_right.mp $ (or_mem_mem_neg Ï†); assumption;
    by_contra hnÏ†;
    apply Î“.consistent;
    replace hnÏ† : Î“.1 *âŠ¢[ğ“¢] âˆ¼Ï† := Context.by_axm! hnÏ†;
    cl_prover [hÏ†, hnÏ†];

@[simp, grind] lemma not_mem_falsum : âŠ¥ âˆ‰ Î“ := not_mem_falsum_of_consistent Î“.consistent
@[simp, grind] lemma mem_verum : âŠ¤ âˆˆ Î“ := by apply iff_mem_provable.mpr; cl_prover;

@[grind]
lemma iff_mem_neg : (âˆ¼Ï† âˆˆ Î“) â†” (Ï† âˆ‰ Î“) := by
  simp only [iff_mem_provable];
  constructor;
  . intro hnÏ† hÏ†;
    apply Î“.consistent;
    cl_prover [hÏ†, hnÏ†];
  . intro hÏ†;
    apply Context.by_axm!;
    simpa using Î“.no_proper_consistent_superset _ (iff_consistent_insert_neg_unprovable.mpr hÏ†) (by tauto);

lemma iff_forall_mem_provable : (âˆ€ Î“ : MaximalConsistentSet ğ“¢, Ï† âˆˆ Î“) â†” ğ“¢ âŠ¢ Ï† := by
  constructor;
  . contrapose!;
    intro h;
    obtain âŸ¨Î“, hÎ“âŸ© := lindenbaum $ iff_consistent_neg_singleton_unprovable.mpr h;
    use Î“;
    apply iff_mem_neg.mp;
    simpa using hÎ“;
  . intro h Î“;
    apply iff_mem_provable.mpr;
    exact Context.of! h;

@[grind] lemma mem_of_provable (h : ğ“¢ âŠ¢ Ï†) : Ï† âˆˆ Î“ := iff_forall_mem_provable.mpr h Î“

@[grind] lemma iff_mem_negneg : (âˆ¼âˆ¼Ï† âˆˆ Î“) â†” (Ï† âˆˆ Î“) := by grind

@[grind â‡’]
lemma iff_mem_imp : ((Ï† â Ïˆ) âˆˆ Î“) â†” ((Ï† âˆˆ Î“) â†’ (Ïˆ âˆˆ Î“)) := by
  constructor;
  . intro hÏ†Ïˆ hÏ†;
    simp_all only [iff_mem_provable]
    cl_prover [hÏ†Ïˆ, hÏ†];
  . intro h;
    rcases imp_iff_not_or.mp h with (h | h);
    . replace h := iff_mem_provable.mp $ iff_mem_neg.mpr h;
      apply iff_mem_provable.mpr;
      cl_prover [h];
    . replace h := iff_mem_provable.mp h;
      apply iff_mem_provable.mpr;
      cl_prover [h];


@[grind]
lemma mdp (hÏ†Ïˆ : (Ï† â Ïˆ) âˆˆ Î“) (hÏ† : Ï† âˆˆ Î“) : Ïˆ âˆˆ Î“ := iff_mem_imp.mp hÏ†Ïˆ hÏ†

@[grind â‡’]
lemma iff_mem_and : ((Ï† â‹ Ïˆ) âˆˆ Î“) â†” (Ï† âˆˆ Î“) âˆ§ (Ïˆ âˆˆ Î“) := by
  constructor;
  . intro hÏ†Ïˆ;
    simp_all only [iff_mem_provable];
    constructor <;> cl_prover [hÏ†Ïˆ];
  . simp_all only [iff_mem_provable];
    rintro âŸ¨hÏ†, hÏˆâŸ©;
    cl_prover [hÏ†, hÏˆ];

@[grind â‡’]
lemma iff_mem_or : ((Ï† â‹ Ïˆ) âˆˆ Î“) â†” (Ï† âˆˆ Î“) âˆ¨ (Ïˆ âˆˆ Î“) := by
  constructor;
  . intro hÏ†Ïˆ;
    replace hÏ†Ïˆ := iff_mem_provable.mp hÏ†Ïˆ;
    by_contra!;
    rcases this with âŸ¨hÏ†, hÏˆâŸ©;
    replace hÏ† := iff_mem_provable.mp $ iff_mem_neg.mpr hÏ†;
    replace hÏˆ := iff_mem_provable.mp $ iff_mem_neg.mpr hÏˆ;
    apply Î“.consistent;
    cl_prover [hÏ†Ïˆ, hÏ†, hÏˆ];
  . simp_all only [iff_mem_provable];
    rintro (h | h) <;> cl_prover [h];

@[grind]
lemma iff_mem_conj {l : List F} : (â‹€l âˆˆ Î“) â†” (âˆ€ Ï† âˆˆ l, Ï† âˆˆ Î“) := by simp [iff_mem_provable, Conjâ‚‚!_iff_forall_provable];

lemma imp_of_provable_C (hÏ†Ïˆ : Î“ *âŠ¢[ğ“¢] (Ï† â Ïˆ)) : (Ï† âˆˆ Î“) â†’ (Ïˆ âˆˆ Î“) := by
  apply iff_mem_imp.mp;
  apply iff_mem_provable.mpr;
  assumption;

lemma iff_of_provable_E (h : Î“ *âŠ¢[ğ“¢] (Ï† â­¤ Ïˆ)) : Ï† âˆˆ Î“ â†” Ïˆ âˆˆ Î“ := by
  constructor <;> . apply imp_of_provable_C; cl_prover [h];

@[grind â‡’] lemma neg_monotone (h : Ï† âˆˆ Î“ â†’ Ïˆ âˆˆ Î“) : (âˆ¼Ïˆ âˆˆ Î“) â†’ (âˆ¼Ï† âˆˆ Î“) := by simp only [iff_mem_neg]; grind;

@[grind â‡’] lemma neg_congruence (h : Ï† âˆˆ Î“ â†” Ïˆ âˆˆ Î“) : (âˆ¼Ï† âˆˆ Î“) â†” (âˆ¼Ïˆ âˆˆ Î“) := by grind;

end MaximalConsistentSet

end LO

end
