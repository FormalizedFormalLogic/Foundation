import Foundation.Logic.LogicSymbol
import Foundation.Logic.Semantics
import Foundation.Vorspiel.Collection

/-!
# Basic definitions and properties of proof system related notions

This file defines a characterization of the system/proof/provability/calculus of formulae.
Also defines soundness and completeness.

## Main Definitions
* `LO.System F S`: a general framework of deductive system `S` for formulae `F`.
* `LO.System.Inconsistent ğ“¢`: a proposition that states that all formulae in `F` is provable from `ğ“¢`.
* `LO.System.Consistent ğ“¢`: a proposition that states that `ğ“¢` is not inconsistent.
* `LO.System.Sound ğ“¢ ğ“œ`: provability from `ğ“¢` implies satisfiability on `ğ“œ`.
* `LO.System.Complete ğ“¢ ğ“œ`: satisfiability on `ğ“œ` implies provability from `ğ“¢`.

## Notation
* `ğ“¢ âŠ¢ Ï†`: a type of formalized proofs of `Ï† : F` from deductive system `ğ“¢ : S`.
* `ğ“¢ âŠ¢! Ï†`: a proposition that states there is a proof of `Ï†` from `ğ“¢`, i.e. `Ï†` is provable from `ğ“¢`.
* `ğ“¢ âŠ¬ Ï†`: a proposition that states `Ï†` is not provable from `ğ“¢`.
* `ğ“¢ âŠ¢* T`: a type of formalized proofs for each formulae in a set `T` from `ğ“¢`.
* `ğ“¢ âŠ¢!* T`: a proposition that states each formulae in `T` is provable from `ğ“¢`.

-/

namespace LO

class System (F : outParam Type*) (S : Type*) where
  Prf : S â†’ F â†’ Type*

infix:45 " âŠ¢ " => System.Prf

namespace System

variable {F : Type*} {S T U : Type*} [System F S] [System F T] [System F U]

section

variable (ğ“¢ : S)

def Provable (f : F) : Prop := Nonempty (ğ“¢ âŠ¢ f)

abbrev Unprovable (f : F) : Prop := Â¬Provable ğ“¢ f

infix:45 " âŠ¢! " => Provable

infix:45 " âŠ¬ " => Unprovable

def PrfSet (s : Set F) : Type _ := {f : F} â†’ f âˆˆ s â†’ ğ“¢ âŠ¢ f

def ProvableSet (s : Set F) : Prop := âˆ€ {f}, f âˆˆ s â†’ ğ“¢ âŠ¢! f

infix:45 " âŠ¢* " => PrfSet

infix:45 " âŠ¢!* " => ProvableSet

def theory : Set F := {f | ğ“¢ âŠ¢! f}

end

lemma unprovable_iff_isEmpty {ğ“¢ : S} {f : F} :
    ğ“¢ âŠ¬ f â†” IsEmpty (ğ“¢ âŠ¢ f) := by simp [Provable, Unprovable]

noncomputable def Provable.get {ğ“¢ : S} {f : F} (h : ğ“¢ âŠ¢! f) : ğ“¢ âŠ¢ f :=
  Classical.choice h

lemma provableSet_iff {ğ“¢ : S} {s : Set F} :
    ğ“¢ âŠ¢!* s â†” Nonempty (ğ“¢ âŠ¢* s) := by
  simp [ProvableSet, PrfSet, Provable, Classical.nonempty_pi, â†imp_iff_not_or]

noncomputable def ProvableSet.get {ğ“¢ : S} {s : Set F} (h : ğ“¢ âŠ¢!* s) : ğ“¢ âŠ¢* s :=
  Classical.choice (Î± := ğ“¢ âŠ¢* s) (provableSet_iff.mp h : Nonempty (ğ“¢ âŠ¢* s))

class WeakerThan (ğ“¢ : S) (ğ“£ : T) : Prop where
  subset : theory ğ“¢ âŠ† theory ğ“£

infix:40 " âª¯ " => WeakerThan

class StrictlyWeakerThan (ğ“¢ : S) (ğ“£ : T) : Prop where
   weakerThan : ğ“¢ âª¯ ğ“£
   notWT : Â¬ğ“£ âª¯ ğ“¢

infix:40 " âª± " => StrictlyWeakerThan

class Equiv (ğ“¢ : S) (ğ“£ : T) : Prop where
  eq : theory ğ“¢ = theory ğ“£

infix:40 " â‰Š " => Equiv

section WeakerThan

variable {ğ“¢ : S} {ğ“£ : T} {ğ“¤ : U}

@[instance, simp, refl] protected lemma WeakerThan.refl (ğ“¢ : S) : ğ“¢ âª¯ ğ“¢ := âŸ¨Set.Subset.refl _âŸ©

lemma WeakerThan.pbl [h : ğ“¢ âª¯ ğ“£] {Ï†} : ğ“¢ âŠ¢! Ï† â†’ ğ“£ âŠ¢! Ï† := @h.subset Ï†

@[trans] lemma WeakerThan.trans : ğ“¢ âª¯ ğ“£ â†’ ğ“£ âª¯ ğ“¤ â†’ ğ“¢ âª¯ ğ“¤ := fun wâ‚ wâ‚‚ â†¦ âŸ¨Set.Subset.trans wâ‚.subset wâ‚‚.subsetâŸ©

lemma weakerThan_iff : ğ“¢ âª¯ ğ“£ â†” (âˆ€ {f}, ğ“¢ âŠ¢! f â†’ ğ“£ âŠ¢! f) :=
  âŸ¨fun h _ hf â†¦ h.subset hf, fun h â†¦ âŸ¨fun _ hf â†¦ h hfâŸ©âŸ©

lemma not_weakerThan_iff : Â¬ğ“¢ âª¯ ğ“£ â†” (âˆƒ f, ğ“¢ âŠ¢! f âˆ§ ğ“£ âŠ¬ f) := by simp [weakerThan_iff, Unprovable];

lemma strictlyWeakerThan_iff : ğ“¢ âª± ğ“£ â†” (âˆ€ {f}, ğ“¢ âŠ¢! f â†’ ğ“£ âŠ¢! f) âˆ§ (âˆƒ f, ğ“¢ âŠ¬ f âˆ§ ğ“£ âŠ¢! f) := by
  constructor
  Â· rintro âŸ¨wt, nwtâŸ©
    exact âŸ¨weakerThan_iff.mp wt, by rcases not_weakerThan_iff.mp nwt with âŸ¨Ï†, ht, hsâŸ©; exact âŸ¨Ï†, hs, htâŸ©âŸ©
  Â· rintro âŸ¨h, Ï†, hs, htâŸ©
    exact âŸ¨weakerThan_iff.mpr h, not_weakerThan_iff.mpr âŸ¨Ï†, ht, hsâŸ©âŸ©

@[trans]
lemma strictlyWeakerThan.trans : ğ“¢ âª± ğ“£ â†’ ğ“£ âª± ğ“¤ â†’ ğ“¢ âª± ğ“¤ := by
  rintro âŸ¨hâ‚, nhâ‚âŸ© âŸ¨hâ‚‚, _âŸ©;
  constructor;
  . exact WeakerThan.trans hâ‚ hâ‚‚;
  . apply not_weakerThan_iff.mpr;
    obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := not_weakerThan_iff.mp nhâ‚;
    use f;
    constructor;
    . apply weakerThan_iff.mp hâ‚‚;
      assumption;
    . assumption;

lemma weakening (h : ğ“¢ âª¯ ğ“£) {f} : ğ“¢ âŠ¢! f â†’ ğ“£ âŠ¢! f := weakerThan_iff.mp h

lemma Equiv.iff : ğ“¢ â‰Š ğ“£ â†” (âˆ€ f, ğ“¢ âŠ¢! f â†” ğ“£ âŠ¢! f) :=
  âŸ¨fun e â†¦ by simpa [Set.ext_iff, theory] using e.eq, fun e â†¦ âŸ¨by simpa [Set.ext_iff, theory] using eâŸ©âŸ©

@[instance, simp, refl] protected lemma Equiv.refl (ğ“¢ : S) : ğ“¢ â‰Š ğ“¢ := âŸ¨rflâŸ©

@[symm] lemma Equiv.symm : ğ“¢ â‰Š ğ“£ â†’ ğ“£ â‰Š ğ“¢ := fun e â†¦ âŸ¨Eq.symm e.eqâŸ©

@[trans] lemma Equiv.trans : ğ“¢ â‰Š ğ“£ â†’ ğ“£ â‰Š ğ“¤ â†’ ğ“¢ â‰Š ğ“¤ := fun eâ‚ eâ‚‚ â†¦ âŸ¨Eq.trans eâ‚.eq eâ‚‚.eqâŸ©

lemma Equiv.antisymm_iff : ğ“¢ â‰Š ğ“£ â†” ğ“¢ âª¯ ğ“£ âˆ§ ğ“£ âª¯ ğ“¢ := by
  constructor
  Â· intro e
    exact âŸ¨âŸ¨Set.Subset.antisymm_iff.mp e.eq |>.1âŸ©, âŸ¨Set.Subset.antisymm_iff.mp e.eq |>.2âŸ©âŸ©
  Â· rintro âŸ¨wâ‚, wâ‚‚âŸ©
    exact âŸ¨Set.Subset.antisymm wâ‚.subset wâ‚‚.subsetâŸ©

alias âŸ¨_, Equiv.antisymmâŸ© := Equiv.antisymm_iff

lemma Equiv.le : ğ“¢ â‰Š ğ“£ â†’ ğ“¢ âª¯ ğ“£ := fun e â†¦ âŸ¨by rw [e.eq]âŸ©

end WeakerThan

@[simp] lemma provableSet_theory (ğ“¢ : S) : ğ“¢ âŠ¢!* theory ğ“¢ := fun hf â†¦ hf

def Inconsistent (ğ“¢ : S) : Prop := âˆ€ f, ğ“¢ âŠ¢! f

class Consistent (ğ“¢ : S) : Prop where
  not_inconsistent : Â¬Inconsistent ğ“¢

lemma inconsistent_def {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” âˆ€ f, ğ“¢ âŠ¢! f := by simp [Inconsistent]

lemma inconsistent_iff_theory_eq {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” theory ğ“¢ = Set.univ := by
  simp [Inconsistent, Set.ext_iff, theory]

lemma not_inconsistent_iff_consistent {ğ“¢ : S} :
    Â¬Inconsistent ğ“¢ â†” Consistent ğ“¢ :=
  âŸ¨fun h â†¦ âŸ¨hâŸ©, by rintro âŸ¨hâŸ©; exact hâŸ©

alias âŸ¨_, Consistent.not_incâŸ© := not_inconsistent_iff_consistent

lemma not_consistent_iff_inconsistent {ğ“¢ : S} :
    Â¬Consistent ğ“¢ â†” Inconsistent ğ“¢ := by simp [â†not_inconsistent_iff_consistent]

alias âŸ¨_, Inconsistent.not_conâŸ© := not_consistent_iff_inconsistent

lemma consistent_iff_exists_unprovable {ğ“¢ : S} :
    Consistent ğ“¢ â†” âˆƒ f, ğ“¢ âŠ¬ f := by
  simp [â†not_inconsistent_iff_consistent, inconsistent_def]

alias âŸ¨Consistent.exists_unprovable, _âŸ© := consistent_iff_exists_unprovable

lemma Consistent.of_unprovable {ğ“¢ : S} {f} (h : ğ“¢ âŠ¬ f) : Consistent ğ“¢ :=
  âŸ¨fun hp â†¦ h (hp f)âŸ©

lemma inconsistent_iff_theory_eq_univ {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” theory ğ“¢ = Set.univ := by simp [inconsistent_def, theory, Set.ext_iff]

alias âŸ¨Inconsistent.theory_eq, _âŸ© := inconsistent_iff_theory_eq_univ

lemma Inconsistent.of_ge {ğ“¢ : S} {ğ“£ : T} (hğ“¢ : Inconsistent ğ“¢) (h : ğ“¢ âª¯ ğ“£) : Inconsistent ğ“£ :=
  fun f â†¦ h.subset (hğ“¢ f)

lemma Consistent.of_le {ğ“¢ : S} {ğ“£ : T} (hğ“¢ : Consistent ğ“¢) (h : ğ“£ âª¯ ğ“¢) : Consistent ğ“£ :=
  âŸ¨fun H â†¦ not_consistent_iff_inconsistent.mpr (H.of_ge h) hğ“¢âŸ©

@[ext] structure Translation {S S' F F'} [System F S] [System F' S'] (ğ“¢ : S) (ğ“£ : S') where
  toFun : F â†’ F'
  prf {f} : ğ“¢ âŠ¢ f â†’ ğ“£ âŠ¢ toFun f

infix:40 " â† " => Translation

@[ext] structure Bitranslation {S S' F F'} [System F S] [System F' S'] (ğ“¢ : S) (ğ“£ : S') where
  r : ğ“¢ â† ğ“£
  l : ğ“£ â† ğ“¢
  r_l : r.toFun âˆ˜ l.toFun = id
  l_r : l.toFun âˆ˜ r.toFun = id

infix:40 " â†­ " => Bitranslation

@[ext] structure FaithfulTranslation {S S' F F'} [System F S] [System F' S'] (ğ“¢ : S) (ğ“£ : S') extends ğ“¢ â† ğ“£ where
  prfInv {f} : ğ“£ âŠ¢ toFun f â†’ ğ“¢ âŠ¢ f

infix:40 " â†Â¹ " => FaithfulTranslation

namespace Translation

variable {S S' S'' : Type*} {F F' F'' : Type*} [System F S] [System F' S'] [System F'' S'']

instance (ğ“¢ : S) (ğ“£ : S') : CoeFun (ğ“¢ â† ğ“£) (fun _ â†¦ F â†’ F') := âŸ¨Translation.toFunâŸ©

protected def id (ğ“¢ : S) : ğ“¢ â† ğ“¢ where
  toFun := id
  prf := id

@[simp] lemma id_app (ğ“¢ : S) (f : F) : Translation.id ğ“¢ f = f := rfl

def comp {ğ“¢ : S} {ğ“£ : S'} {ğ“¤ : S''} (Ï† : ğ“£ â† ğ“¤) (Ïˆ : ğ“¢ â† ğ“£) : ğ“¢ â† ğ“¤ where
  toFun := Ï†.toFun âˆ˜ Ïˆ.toFun
  prf := Ï†.prf âˆ˜ Ïˆ.prf

@[simp] lemma comp_app {ğ“¢ : S} {ğ“£ : S'} {ğ“¤ : S''} (Ï† : ğ“£ â† ğ“¤) (Ïˆ : ğ“¢ â† ğ“£) (f : F) :
    Ï†.comp Ïˆ f = Ï† (Ïˆ f) := rfl

lemma provable {ğ“¢ : S} {ğ“£ : S'} (f : ğ“¢ â† ğ“£) {Ï†} (h : ğ“¢ âŠ¢! Ï†) : ğ“£ âŠ¢! f Ï† := âŸ¨f.prf h.getâŸ©

end Translation

namespace Bitranslation

variable {S S' S'' : Type*} {F F' F'' : Type*} [System F S] [System F' S'] [System F'' S'']

@[simp] lemma r_l_app {ğ“¢ : S} {ğ“£ : S'} (f : ğ“¢ â†­ ğ“£) (Ï† : F') : f.r (f.l Ï†) = Ï† := congr_fun f.r_l Ï†

@[simp] lemma l_r_app {ğ“¢ : S} {ğ“£ : S'} (f : ğ“¢ â†­ ğ“£) (Ï† : F) : f.l (f.r Ï†) = Ï† := congr_fun f.l_r Ï†

protected def id (ğ“¢ : S) : ğ“¢ â†­ ğ“¢ where
  r := Translation.id ğ“¢
  l := Translation.id ğ“¢
  r_l := by ext; simp
  l_r := by ext; simp

protected def symm {ğ“¢ : S} {ğ“£ : S'} (Ï† : ğ“¢ â†­ ğ“£) : ğ“£ â†­ ğ“¢ where
  r := Ï†.l
  l := Ï†.r
  r_l := Ï†.l_r
  l_r := Ï†.r_l

def comp {ğ“¢ : S} {ğ“£ : S'} {ğ“¤ : S''} (Ï† : ğ“£ â†­ ğ“¤) (Ïˆ : ğ“¢ â†­ ğ“£) : ğ“¢ â†­ ğ“¤ where
  r := Ï†.r.comp Ïˆ.r
  l := Ïˆ.l.comp Ï†.l
  r_l := by ext; simp
  l_r := by ext; simp

end Bitranslation

namespace FaithfulTranslation

variable {S S' S'' : Type*} {F F' F'' : Type*} [System F S] [System F' S'] [System F'' S'']

instance (ğ“¢ : S) (ğ“£ : S') : CoeFun (ğ“¢ â†Â¹ ğ“£) (fun _ â†¦ F â†’ F') := âŸ¨fun t â†¦ t.toFunâŸ©

protected def id (ğ“¢ : S) : ğ“¢ â†Â¹ ğ“¢ where
  toFun := id
  prf := id
  prfInv := id

@[simp] lemma id_app (ğ“¢ : S) (f : F) : FaithfulTranslation.id ğ“¢ f = f := rfl

def comp {ğ“¢ : S} {ğ“£ : S'} {ğ“¤ : S''} (Ï† : ğ“£ â†Â¹ ğ“¤) (Ïˆ : ğ“¢ â†Â¹ ğ“£) : ğ“¢ â†Â¹ ğ“¤ where
  toFun := Ï†.toFun âˆ˜ Ïˆ.toFun
  prf := Ï†.prf âˆ˜ Ïˆ.prf
  prfInv := Ïˆ.prfInv âˆ˜ Ï†.prfInv

@[simp] lemma comp_app {ğ“¢ : S} {ğ“£ : S'} {ğ“¤ : S''} (Ï† : ğ“£ â†Â¹ ğ“¤) (Ïˆ : ğ“¢ â†Â¹ ğ“£) (f : F) :
    Ï†.comp Ïˆ f = Ï† (Ïˆ f) := rfl

lemma provable {ğ“¢ : S} {ğ“£ : S'} (f : ğ“¢ â†Â¹ ğ“£) {Ï†} (h : ğ“¢ âŠ¢! Ï†) : ğ“£ âŠ¢! f Ï† := âŸ¨f.prf h.getâŸ©

lemma provable_iff {ğ“¢ : S} {ğ“£ : S'} (f : ğ“¢ â†Â¹ ğ“£) {Ï†} : ğ“£ âŠ¢! f Ï† â†” ğ“¢ âŠ¢! Ï† :=
  âŸ¨fun h â†¦ âŸ¨f.prfInv h.getâŸ©, fun h â†¦ âŸ¨f.prf h.getâŸ©âŸ©

end FaithfulTranslation

section

variable [LogicalConnective F]

variable (ğ“¢ : S)

def Complete : Prop := âˆ€ f, ğ“¢ âŠ¢! f âˆ¨ ğ“¢ âŠ¢! âˆ¼f

def Undecidable (f : F) : Prop := ğ“¢ âŠ¬ f âˆ§ ğ“¢ âŠ¬ âˆ¼f

end

lemma incomplete_iff_exists_undecidable [LogicalConnective F] {ğ“¢ : S} :
    Â¬System.Complete ğ“¢ â†” âˆƒ f, Undecidable ğ“¢ f := by simp [Complete, Undecidable, not_or]

variable (S T)

class Axiomatized [Collection F S] where
  prfAxm {ğ“¢ : S} : ğ“¢ âŠ¢* Collection.set ğ“¢
  weakening {ğ“¢ ğ“£ : S} : ğ“¢ âŠ† ğ“£ â†’ ğ“¢ âŠ¢ f â†’ ğ“£ âŠ¢ f

alias byAxm := Axiomatized.prfAxm
alias wk := Axiomatized.weakening

class StrongCut [Collection F T] where
  cut {ğ“¢ : S} {ğ“£ : T} {Ï†} : ğ“¢ âŠ¢* Collection.set ğ“£ â†’ ğ“£ âŠ¢ Ï† â†’ ğ“¢ âŠ¢ Ï†

variable {S T}

section Axiomatized

namespace Axiomatized

variable [Collection F S] [Axiomatized S] {ğ“¢ ğ“£ : S}

@[simp] lemma provable_axm (ğ“¢ : S) : ğ“¢ âŠ¢!* Collection.set ğ“¢ := fun hf â†¦ âŸ¨prfAxm hfâŸ©

lemma axm_subset (ğ“¢ : S) : Collection.set ğ“¢ âŠ† theory ğ“¢ := fun _ hp â†¦ provable_axm ğ“¢ hp

lemma le_of_subset (h : ğ“¢ âŠ† ğ“£) : ğ“¢ âª¯ ğ“£ := âŸ¨by rintro f âŸ¨bâŸ©; exact âŸ¨weakening h bâŸ©âŸ©

lemma weakening! (h : ğ“¢ âŠ† ğ“£) {f} : ğ“¢ âŠ¢! f â†’ ğ“£ âŠ¢! f := by rintro âŸ¨bâŸ©; exact âŸ¨weakening h bâŸ©

def weakerThanOfSubset (h : ğ“¢ âŠ† ğ“£) : ğ“¢ âª¯ ğ“£ := âŸ¨fun _ â†¦ weakening! hâŸ©

def translation (h : ğ“¢ âŠ† ğ“£) : ğ“¢ â† ğ“£ where
  toFun := id
  prf := weakening h

end Axiomatized

alias by_axm := Axiomatized.provable_axm
alias wk! := Axiomatized.weakening!

section axiomatized

variable [Collection F S] [Collection F T] [Axiomatized S]

def FiniteAxiomatizable (ğ“¢ : S) : Prop := âˆƒ ğ“• : S, Collection.Finite ğ“• âˆ§ ğ“• â‰Š ğ“¢

lemma Consistent.of_subset {ğ“¢ ğ“£ : S} (hğ“¢ : Consistent ğ“¢) (h : ğ“£ âŠ† ğ“¢) : Consistent ğ“£ :=
  hğ“¢.of_le (Axiomatized.le_of_subset h)

lemma Inconsistent.of_supset {ğ“¢ ğ“£ : S} (hğ“¢ : Inconsistent ğ“¢) (h : ğ“¢ âŠ† ğ“£) : Inconsistent ğ“£ :=
  hğ“¢.of_ge (Axiomatized.le_of_subset h)

end axiomatized

namespace StrongCut

variable [Collection F T] [StrongCut S T]

lemma cut! {ğ“¢ : S} {ğ“£ : T} {Ï† : F} (H : ğ“¢ âŠ¢!* Collection.set ğ“£) (hp : ğ“£ âŠ¢! Ï†) : ğ“¢ âŠ¢! Ï† := by
  rcases hp with âŸ¨bâŸ©; exact âŸ¨StrongCut.cut H.get bâŸ©

def translation {ğ“¢ : S} {ğ“£ : T} (B : ğ“¢ âŠ¢* Collection.set ğ“£) : ğ“£ â† ğ“¢ where
  toFun := id
  prf := StrongCut.cut B

end StrongCut

noncomputable def WeakerThan.ofAxm! [Collection F S] [StrongCut S S] {ğ“¢â‚ ğ“¢â‚‚ : S} (B : ğ“¢â‚‚ âŠ¢!* Collection.set ğ“¢â‚) :
    ğ“¢â‚ âª¯ ğ“¢â‚‚ := âŸ¨fun _ b â†¦ StrongCut.cut! B bâŸ©

def WeakerThan.ofSubset [Collection F S] [Axiomatized S] {ğ“¢ ğ“£ : S} (h : ğ“¢ âŠ† ğ“£) : ğ“¢ âª¯ ğ“£ := âŸ¨fun _ â†¦ wk! hâŸ©

variable (S)

class Compact [Collection F S] where
  Ï† {ğ“¢ : S} {f : F} : ğ“¢ âŠ¢ f â†’ S
  Ï†Prf {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Ï† b âŠ¢ f
  Ï†_subset {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Ï† b âŠ† ğ“¢
  Ï†_finite {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Collection.Finite (Ï† b)

variable {S}

namespace Compact

variable [Collection F S] [Compact S]

lemma finite_provable {ğ“¢ : S} (h : ğ“¢ âŠ¢! f) : âˆƒ ğ“• : S, ğ“• âŠ† ğ“¢ âˆ§ Collection.Finite ğ“• âˆ§ ğ“• âŠ¢! f := by
  rcases h with âŸ¨bâŸ©
  exact âŸ¨Ï† b, Ï†_subset b, Ï†_finite b, âŸ¨Ï†Prf bâŸ©âŸ©

end Compact

end Axiomatized

end System

namespace System

variable {S : Type*} {F : Type*} [LogicalConnective F] [System F S]

variable (S)

class DeductiveExplosion where
  dexp {ğ“¢ : S} : ğ“¢ âŠ¢ âŠ¥ â†’ (Ï† : F) â†’ ğ“¢ âŠ¢ Ï†

variable {S}

section

variable [DeductiveExplosion S]

def DeductiveExplosion.dexp! {ğ“¢ : S} (h : ğ“¢ âŠ¢! âŠ¥) (f : F) : ğ“¢ âŠ¢! f := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨dexp b fâŸ©

lemma inconsistent_iff_provable_bot {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” ğ“¢ âŠ¢! âŠ¥ := âŸ¨fun h â†¦ h âŠ¥, fun h f â†¦ DeductiveExplosion.dexp! h fâŸ©

alias âŸ¨_, inconsistent_of_provableâŸ© := inconsistent_iff_provable_bot

lemma consistent_iff_unprovable_bot {ğ“¢ : S} :
    Consistent ğ“¢ â†” ğ“¢ âŠ¬ âŠ¥ := by
  simp [inconsistent_iff_provable_bot, â†not_inconsistent_iff_consistent]

alias âŸ¨Consistent.not_bot, _âŸ© := consistent_iff_unprovable_bot

variable [Collection F S] [Axiomatized S] [Compact S]

lemma inconsistent_compact {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” âˆƒ ğ“• : S, ğ“• âŠ† ğ“¢ âˆ§ Collection.Finite ğ“• âˆ§ Inconsistent ğ“• :=
  âŸ¨fun H â†¦ by rcases Compact.finite_provable (H âŠ¥) with âŸ¨ğ“•, hğ“•, fin, hâŸ©; exact âŸ¨ğ“•, hğ“•, fin, inconsistent_of_provable hâŸ©, by
    rintro âŸ¨ğ“•, hğ“•, _, HâŸ©; exact H.of_supset hğ“•âŸ©

lemma consistent_compact {ğ“¢ : S} :
    Consistent ğ“¢ â†” âˆ€ ğ“• : S, ğ“• âŠ† ğ“¢ â†’ Collection.Finite ğ“• â†’ Consistent ğ“• := by
  simp [â†not_inconsistent_iff_consistent, inconsistent_compact (ğ“¢ := ğ“¢)]

end

variable (S)

class Deduction [Cons F S] where
  ofInsert {Ï† Ïˆ : F} {ğ“¢ : S} : cons Ï† ğ“¢ âŠ¢ Ïˆ â†’ ğ“¢ âŠ¢ Ï† â Ïˆ
  inv {Ï† Ïˆ : F} {ğ“¢ : S} : ğ“¢ âŠ¢ Ï† â Ïˆ â†’ cons Ï† ğ“¢ âŠ¢ Ïˆ

variable {S}

section deduction

variable [Cons F S] [Deduction S] {ğ“¢ : S} {Ï† Ïˆ : F}

alias deduction := Deduction.ofInsert

lemma Deduction.of_insert! (h : cons Ï† ğ“¢ âŠ¢! Ïˆ) : ğ“¢ âŠ¢! Ï† â Ïˆ := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨Deduction.ofInsert bâŸ©

alias deduction! := Deduction.of_insert!

lemma Deduction.inv! (h : ğ“¢ âŠ¢! Ï† â Ïˆ) : cons Ï† ğ“¢ âŠ¢! Ïˆ := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨Deduction.inv bâŸ©

def Deduction.translation (Ï† : F) (ğ“¢ : S) : cons Ï† ğ“¢ â† ğ“¢ where
  toFun := fun Ïˆ â†¦ Ï† â Ïˆ
  prf := deduction

lemma deduction_iff : cons Ï† ğ“¢ âŠ¢! Ïˆ â†” ğ“¢ âŠ¢! Ï† â Ïˆ := âŸ¨deduction!, Deduction.inv!âŸ©

end deduction

end System

section

variable {S : Type*} {F : Type*} [System F S] {M : Type*} [Semantics F M]

class Sound (ğ“¢ : S) (ğ“œ : M) : Prop where
  sound : âˆ€ {f : F}, ğ“¢ âŠ¢! f â†’ ğ“œ âŠ§ f

class Complete (ğ“¢ : S) (ğ“œ : M) : Prop where
  complete : âˆ€ {f : F}, ğ“œ âŠ§ f â†’ ğ“¢ âŠ¢! f

namespace Sound

section

variable {ğ“¢ ğ“£ : S} {ğ“œ ğ“ : M} [Sound ğ“¢ ğ“œ] [Sound ğ“£ ğ“]

lemma not_provable_of_countermodel {Ï† : F} (hp : Â¬ğ“œ âŠ§ Ï†) : ğ“¢ âŠ¬ Ï† :=
  fun b â†¦ hp (Sound.sound b)

lemma consistent_of_meaningful : Semantics.Meaningful ğ“œ â†’ System.Consistent ğ“¢ :=
  fun H â†¦ âŸ¨fun h â†¦ by rcases H with âŸ¨f, hfâŸ©; exact hf (Sound.sound (h f))âŸ©

lemma consistent_of_model [LogicalConnective F] [Semantics.Bot M] (ğ“œ : M) [Sound ğ“¢ ğ“œ] : System.Consistent ğ“¢ :=
  consistent_of_meaningful (ğ“œ := ğ“œ) inferInstance

lemma realizeSet_of_prfSet {T : Set F} (b : ğ“¢ âŠ¢!* T) : ğ“œ âŠ§* T :=
  âŸ¨fun _ hf => sound (b hf)âŸ©

end

section

variable {ğ“¢ : S} {T : Set F} [Sound ğ“¢ (Semantics.models M T)]

lemma consequence_of_provable {f : F} : ğ“¢ âŠ¢! f â†’ T âŠ¨[M] f := sound

lemma consistent_of_satisfiable [LogicalConnective F] [âˆ€ ğ“œ : M, Semantics.Meaningful ğ“œ] : Semantics.Satisfiable M T â†’ System.Consistent ğ“¢ :=
  fun H â†¦ consistent_of_meaningful (Semantics.meaningful_iff_satisfiableSet.mp H)

end

end Sound

namespace Complete

section

variable {ğ“¢ : S} {ğ“œ : M} [Complete ğ“¢ ğ“œ]

lemma meaningful_of_consistent : System.Consistent ğ“¢ â†’ Semantics.Meaningful ğ“œ := by
  contrapose
  suffices (âˆ€ (f : F), ğ“œ âŠ§ f) â†’ System.Inconsistent ğ“¢ by
    simpa [Semantics.not_meaningful_iff, System.not_consistent_iff_inconsistent]
  exact fun h f â†¦ Complete.complete (h f)

end

section

variable {ğ“¢ : S} {T : Set F} [Complete ğ“¢ (Semantics.models M T)]

lemma provable_of_consequence {f : F} : T âŠ¨[M] f â†’ ğ“¢ âŠ¢! f := complete

lemma provable_iff_consequence [Sound ğ“¢ (Semantics.models M T)] {f : F} : T âŠ¨[M] f â†” ğ“¢ âŠ¢! f := âŸ¨complete, Sound.soundâŸ©

variable [LogicalConnective F] [âˆ€ ğ“œ : M, Semantics.Meaningful ğ“œ]

lemma satisfiable_of_consistent :
    System.Consistent ğ“¢ â†’ Semantics.Satisfiable M T :=
  fun H â†¦ Semantics.meaningful_iff_satisfiableSet.mpr (meaningful_of_consistent H)

lemma inconsistent_of_unsatisfiable :
    Â¬Semantics.Satisfiable M T â†’ System.Inconsistent ğ“¢ := by
  contrapose; simpa [â†System.not_consistent_iff_inconsistent] using satisfiable_of_consistent

lemma consistent_iff_satisfiable [Sound ğ“¢ (Semantics.models M T)] : System.Consistent ğ“¢ â†” Semantics.Satisfiable M T :=
  âŸ¨satisfiable_of_consistent, Sound.consistent_of_satisfiableâŸ©

end

end Complete

end

end LO
