module

public import Foundation.LinearLogic.FirstOrder.Rew

/-! # One-sided sequent calculus for first-Order linear logic -/

@[expose] public section

namespace LO.FirstOrder.LinearLogic

abbrev Sequent (L : Language) := List (Statement L)

abbrev Sequent.IsQuest (Î“ : Sequent L) : Prop := âˆ€ Ï† âˆˆ Î“, Ï†.IsQuest

variable {L : Language}

/-- Derivation of first-Order linear logic -/
inductive Derivation : Sequent L â†’ Type _ where
  | protected id (r : L.Rel k) (v) : Derivation [.rel r v, .nrel r v]
  | cut : Derivation (Ï† :: Î“) â†’ Derivation (âˆ¼Ï† :: Î”) â†’ Derivation (Î“ ++ Î”)
  | exchange : Derivation Î“ â†’ Î“.Perm Î” â†’ Derivation Î”
  | one : Derivation [1]
  | falsum : Derivation Î“ â†’ Derivation (âŠ¥ :: Î“)
  | tensor : Derivation (Ï† :: Î“) â†’ Derivation (Ïˆ :: Î”) â†’ Derivation (Ï† â¨‚ Ïˆ :: Î“ ++ Î”)
  | par : Derivation (Ï† :: Ïˆ :: Î“) â†’ Derivation (Ï† â…‹ Ïˆ :: Î“)
  | verum (Î“) : Derivation (âŠ¤ :: Î“)
  | with : Derivation (Ï† :: Î“) â†’ Derivation (Ïˆ :: Î“) â†’ Derivation (Ï† ï¼† Ïˆ :: Î“)
  | plusLeft : Derivation (Ïˆ :: Î“) â†’ (Ï† : Statement L) â†’ Derivation (Ï† â¨ Ïˆ :: Î“)
  | plusRight : Derivation (Ï† :: Î“) â†’ (Ïˆ : Statement L) â†’ Derivation (Ï† â¨ Ïˆ :: Î“)
  | ofCourse : Derivation (Ï† :: Î“) â†’ Sequent.IsQuest Î“ â†’ Derivation (ï¼Ï† :: Î“)
  | weakening : Derivation Î“ â†’ Derivation (ï¼ŸÏ† :: Î“)
  | dereliction : Derivation (Ï† :: Î“) â†’ Derivation (ï¼ŸÏ† :: Î“)
  | contraction : Derivation (ï¼ŸÏ† :: ï¼ŸÏ† :: Î“) â†’ Derivation (ï¼ŸÏ† :: Î“)
  | all : Derivation (Ï†.free :: Î“âº) â†’ Derivation ((âˆ€' Ï†) :: Î“)
  | ex (t) : Derivation (Ï†/[t] :: Î“) â†’ Derivation ((âˆƒ' Ï†) :: Î“)

abbrev Statement.Proof (Ï† : Statement L) : Type _ := Derivation [Ï†]

abbrev Sentence.Proof (Ïƒ : Sentence L) : Type _ := Derivation [(Ïƒ : Statement L)]

inductive SymbolFV (L : Language) where
  | symbol : SymbolFV L

abbrev SymbolFV.symbol' (L : Language) : SymbolFV L := .symbol

notation "ð‹ð‹ð•" => SymbolFV.symbol'

instance : Entailment (SymbolFV L) (Statement L) := âŸ¨fun _ â†¦ Statement.ProofâŸ©

inductive Symbol (L : Language) where
  | symbol : Symbol L

abbrev Symbol.symbol' (L : Language) : Symbol L := .symbol

notation "ð‹ð‹" => Symbol.symbol'

instance : Entailment (Symbol L) (Sentence L) := âŸ¨fun _ â†¦ Sentence.ProofâŸ©

scoped prefix:45 "âŠ¢! " => Derivation

abbrev Derivable (Î“ : Sequent L) : Prop := Nonempty (âŠ¢! Î“)

scoped prefix:45 "âŠ¢ " => Derivable

namespace Derivation

variable {Î“ Î” : Sequent L}

def cast (d : âŠ¢! Î“) (e : Î“ = Î”) : âŠ¢! Î” := e â–¸ d

def rotate (d : âŠ¢! Ï† :: Î“) : âŠ¢! Î“ ++ [Ï†] :=
  d.exchange (by grind only [List.perm_comm, List.perm_append_singleton])

def height {Î“ : Sequent L} : âŠ¢! Î“ â†’ â„•
  |       .id _ _ => 0
  |     cut dâ‚ dâ‚‚ => max (height dâ‚) (height dâ‚‚) + 1
  |  exchange d _ => height d
  |           one => 0
  |      falsum d => height d + 1
  |  tensor dâ‚ dâ‚‚ => max (height dâ‚) (height dâ‚‚) + 1
  |         par d => height d + 1
  |       verum _ => 0
  |   .with dâ‚ dâ‚‚ => max (height dâ‚) (height dâ‚‚) + 1
  |  plusLeft d _ => height d + 1
  | plusRight d _ => height d + 1
  |  ofCourse d _ => height d + 1
  |   weakening d => height d + 1
  | dereliction d => height d + 1
  | contraction d => height d + 1
  |         all d => height d + 1
  |        ex _ d => height d + 1

section height

@[simp] lemma height_id (r : L.Rel k) (v) :
    (Derivation.id r v).height = 0 := rfl

@[simp] lemma height_cut (dâ‚ : âŠ¢! Ï† :: Î“) (dâ‚‚ : âŠ¢! âˆ¼Ï† :: Î”) :
    (dâ‚.cut dâ‚‚).height = max dâ‚.height dâ‚‚.height + 1 := rfl

@[simp] lemma height_exchange (d : âŠ¢! Î“) (p : Î“.Perm Î”) :
    (d.exchange p).height = d.height := rfl

@[simp] lemma height_one :
    (one (L := L)).height = 0 := rfl

@[simp] lemma height_falsum (d : âŠ¢! Î“) :
    d.falsum.height = d.height + 1 := rfl

@[simp] lemma height_tensor (dâ‚ : âŠ¢! Ï† :: Î“) (dâ‚‚ : âŠ¢! Ïˆ :: Î”) :
    (dâ‚.tensor dâ‚‚).height = max dâ‚.height dâ‚‚.height + 1 := rfl

@[simp] lemma height_par (d : âŠ¢! Ï† :: Ïˆ :: Î“) :
    d.par.height = d.height + 1 := rfl

@[simp] lemma height_verum (Î“ : Sequent L) :
    (verum Î“).height = 0 := rfl

@[simp] lemma height_with (dâ‚ : âŠ¢! Ï† :: Î“) (dâ‚‚ : âŠ¢! Ïˆ :: Î“) :
    (dâ‚.with dâ‚‚).height = max dâ‚.height dâ‚‚.height + 1 := rfl

@[simp] lemma height_plusLeft (d : âŠ¢! Ï† :: Î“) (Ïˆ) :
    (d.plusLeft Ïˆ).height = d.height + 1 := rfl

@[simp] lemma height_plusRight (d : âŠ¢! Ïˆ :: Î“) (Ï†) :
    (d.plusRight Ï†).height = d.height + 1 := rfl

@[simp] lemma height_ofCourse (d : âŠ¢! Ï† :: Î“) (hÎ“ : Sequent.IsQuest Î“) :
    (d.ofCourse hÎ“).height = d.height + 1 := rfl

@[simp] lemma height_weakening (d : âŠ¢! Î“) (Ï†) :
    (d.weakening (Ï† := Ï†)).height = d.height + 1 := rfl

@[simp] lemma height_dereliction (d : âŠ¢! Ï† :: Î“) :
    d.dereliction.height = d.height + 1 := rfl

@[simp] lemma height_contraction (d : âŠ¢! ï¼ŸÏ† :: ï¼ŸÏ† :: Î“) :
    d.contraction.height = d.height + 1 := rfl

@[simp] lemma height_all {Ï† : Semistatement L 1} (d : âŠ¢! Ï†.free :: Î“âº) :
    d.all.height = d.height + 1 := rfl

@[simp] lemma height_ex {Ï† : Semistatement L 1} {t} (d : âŠ¢! Ï†/[t] :: Î“) :
    (d.ex t).height = d.height + 1 := rfl

@[simp] lemma height_cast (d : âŠ¢! Î“) (e : Î“ = Î”) :
    (d.cast e).height = d.height := by rcases e; rfl

end height

def identity : (Ï† : Statement L) â†’ âŠ¢! [Ï†, âˆ¼Ï†]
  |  .rel r v => Derivation.id r v
  | .nrel r v => (Derivation.id r v).rotate
  |         1 => one.falsum.rotate
  |         âŠ¥ => one.falsum
  |     Ï† â¨‚ Ïˆ => ((identity Ï†).tensor (identity Ïˆ)).rotate.par.rotate
  |     Ï† â…‹ Ïˆ => ((identity Ï†).rotate.tensor (identity Ïˆ).rotate).rotate.par
  |         âŠ¤ => verum _
  |         0 => (verum [0]).rotate
  |     Ï† ï¼† Ïˆ => ((identity Ï†).rotate.plusRight (âˆ¼Ïˆ)).rotate.with ((identity Ïˆ).rotate.plusLeft (âˆ¼Ï†)).rotate
  |     Ï† â¨ Ïˆ => (((identity Ï†).plusRight Ïˆ).rotate.with ((identity Ïˆ).plusLeft Ï†).rotate).rotate
  |        ï¼Ï† => (identity Ï†).rotate.dereliction.rotate.ofCourse (by simp [Sequent.IsQuest])
  |        ï¼ŸÏ† => (identity Ï†).dereliction.rotate.ofCourse (by simp [Sequent.IsQuest]) |>.rotate
  |      âˆ€' Ï† =>
    have : âŠ¢! [(âˆ¼Ï†.shift)/[&0], Ï†.free] := (identity Ï†.free).rotate.cast (by simp)
    have : âŠ¢! Ï†.free :: [âˆƒ' âˆ¼Ï†]âº := (this.ex _).rotate.cast (by simp)
    this.all
  |      âˆƒ' Ï† =>
    have : âŠ¢! [Ï†.shift/[&0], âˆ¼Ï†.free] := (identity Ï†.free).cast (by simp)
    have : âŠ¢! (âˆ¼Ï†).free :: [âˆƒ' Ï†]âº := (this.ex _).rotate.cast (by simp)
    this.all.rotate
  termination_by Ï† => Ï†.complexity

end Derivation

end LO.FirstOrder.LinearLogic
