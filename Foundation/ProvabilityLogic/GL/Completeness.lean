import Foundation.ProvabilityLogic.SolovaySentences
import Foundation.Modal.Kripke.Logic.GL.Tree
import Foundation.Modal.Kripke.ExtendRoot


/-!
# Solovay's arithmetical completeness of $\mathsf{GL}$
-/

namespace LO.ProvabilityLogic

open Entailment Entailment.FiniteContext
open FirstOrder Arithmetic
open Modal
open Modal.Kripke

variable {T : ArithmeticTheory} [T.Œî‚ÇÅ] [ùêàùö∫‚ÇÅ ‚™Ø T] {A : Modal.Formula _}

theorem unprovable_realization_exists
    (M‚ÇÅ : Model) [Fintype M‚ÇÅ] {r‚ÇÅ : M‚ÇÅ} [M‚ÇÅ.IsFiniteTree r‚ÇÅ]
    (hA : ¬¨r‚ÇÅ ‚äß A) (h : M‚ÇÅ.finHeight < T.standardProvability.height) :
    ‚àÉ f : T.PLRealization, T ‚ä¨. f A := by
  let M‚ÇÄ := M‚ÇÅ.extendRoot 1
  let r‚ÇÄ : M‚ÇÄ := Frame.extendRoot.root
  have hdnA : r‚ÇÄ ‚äß ‚óá(‚àºA) := by
    suffices ‚àÉ i, r‚ÇÄ ‚â∫ i ‚àß ¬¨i ‚äß A by simpa [Formula.Kripke.Satisfies]
    refine ‚ü®.inr r‚ÇÅ, ?_, ?_‚ü©
    ¬∑ simpa [r‚ÇÄ] using Frame.extendRoot.rooted_original
    ¬∑ exact Model.extendRoot.inr_satisfies_iff |>.not.mpr hA
  let S : SolovaySentences T.standardProvability M‚ÇÄ.toFrame r‚ÇÄ :=
    SolovaySentences.standard T M‚ÇÄ.toFrame
  use S.realization
  intro hC
  have : T.standardProvability.height ‚â§ M‚ÇÅ.finHeight := by
    apply PartENat.le_of_lt_add_one
    calc
      (Theory.standardProvability T).height < M‚ÇÄ.finHeight := S.theory_height hdnA hC
      _                                     = M‚ÇÅ.finHeight + 1 := by simp [M‚ÇÄ]
  exact not_lt_of_ge this h

/-- Arithmetical completeness of GL-/
theorem GL.arithmetical_completeness (height : T.standardProvability.height = ‚ä§) :
    (‚àÄ f : T.PLRealization, T ‚ä¢!. f A) ‚Üí Modal.GL ‚ä¢! A := by
  suffices ¬¨Hilbert.GL ‚ä¢! A ‚Üí ‚àÉ f : T.PLRealization, T ‚ä¨. f A by
    contrapose
    intro hA
    simpa using this <| Hilbert.Normal.iff_logic_provable_provable |>.not.mp hA
  intro hA
  obtain ‚ü®M‚ÇÅ, r‚ÇÅ, _, hA‚ÇÅ‚ü© :=
    Logic.GL.Kripke.iff_unprovable_exists_unsatisfies_FiniteTransitiveTree.mp hA
  have : Fintype M‚ÇÅ := Fintype.ofFinite _
  exact unprovable_realization_exists M‚ÇÅ hA‚ÇÅ <| by simp [height]

theorem GLBoxBot'.arithmetical_completeness {n : ‚Ñï} (height : n ‚â§ T.standardProvability.height) :
    (‚àÄ f : T.PLRealization, T ‚ä¢!. f A) ‚Üí Modal.GL ‚ä¢! ‚ñ°^[n] ‚ä• ‚ûù A := by
  suffices ¬¨Hilbert.GL ‚ä¢! ‚ñ°^[n]‚ä• ‚ûù A ‚Üí ‚àÉ f : T.PLRealization, T ‚ä¨. f A by
    contrapose
    intro hA
    simpa using this <| Hilbert.Normal.iff_logic_provable_provable |>.not.mp hA
  intro hA
  obtain ‚ü®M‚ÇÅ, r‚ÇÅ, _, hA‚ÇÅ‚ü© :=
    Logic.GL.Kripke.iff_unprovable_exists_unsatisfies_FiniteTransitiveTree.mp hA
  have : Fintype M‚ÇÅ := Fintype.ofFinite _
  have hA‚ÇÅ : r‚ÇÅ ‚äß ‚ñ°^[n]‚ä• ‚àß ¬¨r‚ÇÅ ‚äß A := by
    simpa [Formula.Kripke.Satisfies] using hA‚ÇÅ
  have M‚ÇÅ_height : M‚ÇÅ.finHeight < n :=
    finHeight_lt_iff_satisfies_boxbot.mpr hA‚ÇÅ.1
  exact unprovable_realization_exists M‚ÇÅ hA‚ÇÅ.2 <| lt_of_lt_of_le (by simp [M‚ÇÅ_height]) height

theorem GL.arithmetical_completeness_iff (height : T.standardProvability.height = ‚ä§) {A} :
    (‚àÄ f : T.PLRealization, T ‚ä¢!. f A) ‚Üî Modal.GL ‚ä¢! A :=
  ‚ü®GL.arithmetical_completeness height, GL.arithmetical_soundness‚ü©

theorem GL.arithmetical_completeness_sound_iff [T.SoundOnHierarchy ùö∫ 1] {A} :
    (‚àÄ f : T.PLRealization, T ‚ä¢!. f A) ‚Üî Modal.GL ‚ä¢! A :=
  GL.arithmetical_completeness_iff (Provability.hight_eq_top_of_sigma1_sound T)

end LO.ProvabilityLogic
