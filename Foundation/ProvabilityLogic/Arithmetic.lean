module

public import Foundation.ProvabilityLogic.GL.Soundness
/-!
# Provability logic of arithmetic theory
-/


@[expose] public section

namespace LO

open FirstOrder

namespace Modal

namespace Logic

/-- `L` is provability logic of `T` relative to metatheory `U` -/
def IsProvabilityLogic (L : Modal.Logic â„•) (T U : ArithmeticTheory) [T.Î”â‚] := âˆ€ A, L âŠ¢ A â†” âˆ€ f : T.StandardRealization, U âŠ¢ f A

variable {T U : ArithmeticTheory} [T.Î”â‚] {L : Modal.Logic â„•}

/-- `L` is Åukasiewicz if `L` is provability logic. -/
def inst_Åukasiewiicz_of_isProvabilityLogic (hPL : L.IsProvabilityLogic T U) : Entailment.Åukasiewicz L where
  mdp := by
    rintro A B âŸ¨hAâŸ© âŸ¨hBâŸ©;
    constructor;
    simp only [â†Modal.Logic.iff_provable, hPL A, hPL B, hPL (A â B)] at hA hB âŠ¢;
    intro f;
    replace hA : U âŠ¢ f A â f B := hA f;
    replace hB : U âŠ¢ f A := hB f;
    cl_prover [hA, hB];
  implyK {_ _} := by
    constructor;
    apply Modal.Logic.iff_provable.mp;
    apply hPL _ |>.mpr;
    simp;
  implyS {_ _ _} := by
    constructor;
    apply Modal.Logic.iff_provable.mp;
    apply hPL _ |>.mpr;
    simp;
  elimContra {_ _} := by
    constructor;
    apply Modal.Logic.iff_provable.mp;
    apply hPL _ |>.mpr;
    intro f;
    dsimp [ProvabilityLogic.Realization.interpret];
    cl_prover;

def inst_Cl_of_isProvabilityLogic (hPL : L.IsProvabilityLogic T U) : Entailment.Cl L := by
  have := inst_Åukasiewiicz_of_isProvabilityLogic hPL;
  infer_instance;

lemma subset_GL_of_isProvabilityLogic [ğ—œğšºâ‚ âª¯ T] [ğ—œğšºâ‚ âª¯ U] [T âª¯ U] (hPL : L.IsProvabilityLogic T U) : Modal.GL âŠ† L := by
  intro A hA;
  simp only [â†Modal.Logic.iff_provable] at âŠ¢ hA;
  apply hPL A |>.mpr;
  intro f;
  apply Entailment.WeakerThan.pbl (ğ“¢ := T);
  apply ProvabilityLogic.GL.arithmetical_soundness (ğ”… := T.standardProvability) hA;

lemma provable_GL_of_isProvabilityLogic [ğ—œğšºâ‚ âª¯ T] [ğ—œğšºâ‚ âª¯ U] [T âª¯ U] (hPL : L.IsProvabilityLogic T U) : Modal.GL âŠ¢ A â†’ L âŠ¢ A := by
  simp only [Modal.Logic.iff_provable];
  apply subset_GL_of_isProvabilityLogic hPL;

end Logic

end Modal


namespace FirstOrder

namespace ArithmeticTheory

variable {T U : ArithmeticTheory} [T.Î”â‚] {A : Modal.Formula â„•}

/-- Provability Logic of `T` relative to metatheory `U` -/
def provabilityLogicOn (T U : ArithmeticTheory) [T.Î”â‚] : Modal.Logic â„• := {A | âˆ€ f : T.StandardRealization, U âŠ¢ f A}

@[simp, grind .]
lemma isProvabilityLogic_provabilityLogicOn : (T.provabilityLogicOn U).IsProvabilityLogic T U := by
  simp [Modal.Logic.IsProvabilityLogic, provabilityLogicOn];
  grind;

@[grind =]
lemma provabilityLogicOn.provable_iff : (T.provabilityLogicOn U) âŠ¢ A â†” âˆ€ f : T.StandardRealization, U âŠ¢ f A := by
  simp [Modal.Logic.iff_provable, provabilityLogicOn]

instance : Entailment.Cl (T.provabilityLogicOn U) := Modal.Logic.inst_Cl_of_isProvabilityLogic isProvabilityLogic_provabilityLogicOn

end ArithmeticTheory

end FirstOrder

end LO

end
