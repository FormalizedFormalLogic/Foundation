import Foundation.Modal.Kripke.Completeness

namespace LO.Modal

namespace Kripke


section definability

open Formula.Kripke

protected abbrev FrameClass.multiGeachean (G : Set Geachean.Taple) : FrameClass := { F | (MultiGeachean G) F.Rel }

@[simp]
lemma FrameClass.multiGeachean.nonempty : (FrameClass.multiGeachean G).Nonempty := by
  use âŸ¨Unit, Î» _ _ => TrueâŸ©;
  intros t ht x y z h;
  use x;
  constructor <;> { apply Rel.iterate.true_any; tauto; }

instance FrameClass.multiGeachean.definability (G) : (FrameClass.multiGeachean G).DefinedBy (G.image (Î» t => Axioms.Geach t (.atom 0))) := by
  unfold FrameClass.multiGeachean MultiGeachean Axioms.Geach;
  constructor;
  intro F;
  constructor;
  . rintro hF Ï† âŸ¨g, âŸ¨hg, rflâŸ©âŸ© V x h;
    obtain âŸ¨y, Rxy, hbpâŸ© := Satisfies.multidia_def.mp h;
    apply Satisfies.multibox_def.mpr;
    intro z Rxz;
    apply Satisfies.multidia_def.mpr;
    obtain âŸ¨u, Ryu, RzuâŸ© := hF g hg âŸ¨Rxy, RxzâŸ©;
    use u;
    constructor;
    . assumption;
    . exact (Satisfies.multibox_def.mp hbp) Ryu;
  . rintro h g hg x y z âŸ¨Rxy, RxzâŸ©;
    let V : Kripke.Valuation F := Î» v _ => y â‰º^[g.m] v;
    have : Satisfies âŸ¨F, VâŸ© x (â—‡^[g.i](â–¡^[g.m](.atom 0))) := by
      apply Satisfies.multidia_def.mpr;
      use y;
      constructor;
      . assumption;
      . apply Satisfies.multibox_def.mpr;
        aesop;
    have : Satisfies âŸ¨F, VâŸ© x (â–¡^[g.j](â—‡^[g.n]Formula.atom 0)) := h (Axioms.Geach g (.atom 0)) (by tauto) V x this;
    have : Satisfies âŸ¨F, VâŸ© z (â—‡^[g.n]Formula.atom 0) := Satisfies.multibox_def.mp this Rxz;
    obtain âŸ¨u, Rzu, RyuâŸ© := Satisfies.multidia_def.mp this;
    exact âŸ¨u, Ryu, RzuâŸ©;


section

variable {F : Frame}

lemma reflexive_of_validate_AxiomT (h : F âŠ§ (Axioms.T (.atom 0))) : Reflexive F.Rel := by
  have : ValidOnFrame F (Axioms.T (.atom 0)) â†’ Reflexive F.Rel := by
    simpa [Axioms.Geach, MultiGeachean, â†Geachean.reflexive_def] using
    FrameClass.multiGeachean.definability {âŸ¨0, 0, 1, 0âŸ©} |>.defines F |>.mpr;
  exact this h;

lemma transitive_of_validate_AxiomFour (h : F âŠ§ (Axioms.Four (.atom 0))) : Transitive F.Rel := by
  have : ValidOnFrame F (Axioms.Four (.atom 0)) â†’ Transitive F.Rel := by
    simpa [Axioms.Geach, MultiGeachean, â†Geachean.transitive_def] using
    FrameClass.multiGeachean.definability {âŸ¨0, 2, 1, 0âŸ©} |>.defines F |>.mpr;
  exact this h;

end

end definability


section canonicality

variable {S} [Entailment (Formula â„•) S]
variable {ğ“¢ : S} [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢]

open Formula.Kripke
open Entailment
open MaximalConsistentTableau
open canonicalModel

namespace Canonical

protected lemma geachean
  (hG : âˆ€ {Ï†}, ğ“¢ âŠ¢! â—‡^[g.i](â–¡^[g.m]Ï†) â â–¡^[g.j](â—‡^[g.n]Ï†))
  : Geachean g (canonicalFrame ğ“¢).Rel := by
  rintro x y z âŸ¨Rxy, RxzâŸ©;
  have âŸ¨u, huâŸ© := lindenbaum (ğ“¢ := ğ“¢) (tâ‚€ := âŸ¨â–¡''â»Â¹^[g.m]y.1.1, â—‡''â»Â¹^[g.n]z.1.2âŸ©) $ by
    rintro Î“ Î” hÎ“ hÎ”;
    by_contra hC;

    replace hÎ“ : âˆ€ Ï† âˆˆ â–¡'^[g.m]Î“, Ï† âˆˆ y.1.1 := by
      intro Ï† hÏ†;
      obtain âŸ¨Ïˆ, hÏˆ, rflâŸ© := List.exists_of_multibox hÏ†;
      exact hÎ“ _ hÏˆ;
    have hÎ³ : â–¡^[g.m](â‹€Î“) âˆˆ y.1.1 := mdp_memâ‚_provable collect_multibox_conj! $ iff_memâ‚_conj.mpr hÎ“
    generalize â‹€Î“ = Î³ at hÎ³ hC;

    replace hÎ” : âˆ€ Ï† âˆˆ â—‡'^[g.n]Î”, Ï† âˆˆ z.1.2 := by
      intro Ï† hÏ†;
      obtain âŸ¨Ïˆ, hÏˆ, rflâŸ© := List.exists_of_multidia hÏ†;
      exact hÎ” _ hÏˆ;
    have hÎ´ : â—‡^[g.n](â‹Î”) âˆˆ z.1.2 := mdp_memâ‚‚_provable distribute_multidia_disj! $ iff_memâ‚‚_disj.mpr hÎ”;
    generalize â‹Î” = Î´ at hÎ´ hC;

    have : ğ“¢ âŠ¢! â–¡^[g.m]Î³ â â–¡^[g.m]Î´ := imply_multibox_distribute'! hC;
    have : â–¡^[g.m]Î´ âˆˆ y.1.1 := mdp_memâ‚_provable this hÎ³;
    have : â—‡^[g.i](â–¡^[g.m]Î´) âˆˆ x.1.1 := def_multirel_multidia_memâ‚.mp Rxy this;
    have : â–¡^[g.j](â—‡^[g.n]Î´) âˆˆ x.1.1 := mdp_memâ‚_provable hG this;
    have : â—‡^[g.n]Î´ âˆˆ z.1.1 := def_multirel_multibox_memâ‚.mp Rxz this;
    have : â—‡^[g.n]Î´ âˆ‰ z.1.2 := iff_not_memâ‚‚_memâ‚.mpr this;
    contradiction;
  use u;
  constructor;
  . apply def_multirel_multibox_memâ‚.mpr;
    intro Ï† hÏ†;
    exact hu.1 hÏ†;
  . apply def_multirel_multidia_memâ‚‚.mpr;
    intro Ï† hÏ†;
    exact hu.2 hÏ†;

protected lemma reflexive [Entailment.HasAxiomT ğ“¢] : Reflexive (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.reflexive_def]; apply Canonical.geachean; simp [axiomT!];

protected lemma transitive [Entailment.HasAxiomFour ğ“¢] : Transitive (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.transitive_def]; apply Canonical.geachean; simp [axiomFour!];

protected lemma euclidean [Entailment.HasAxiomFive ğ“¢] : Euclidean (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.euclidean_def]; apply Canonical.geachean; simp [axiomFive!];

protected lemma serial [Entailment.HasAxiomD ğ“¢] : Serial (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.serial_def]; apply Canonical.geachean; simp [axiomD!];

protected lemma symmetric [Entailment.HasAxiomB ğ“¢] : Symmetric (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.symmetric_def]; apply Canonical.geachean; simp [axiomB!];

protected lemma coreflexive [Entailment.HasAxiomTc ğ“¢] : Coreflexive (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.coreflexive_def]; apply Canonical.geachean; simp [axiomTc!];

protected lemma confluent [Entailment.HasAxiomPoint2 ğ“¢] : Confluent (canonicalFrame ğ“¢).Rel := by
  rw [Geachean.confluent_def]; apply Canonical.geachean; simp [axiomPoint2!];

end Canonical

end canonicality


end Kripke

end LO.Modal
