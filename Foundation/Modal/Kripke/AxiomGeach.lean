import Foundation.Modal.Kripke.Completeness
import Foundation.Vorspiel.HRel.Euclidean
import Foundation.Vorspiel.HRel.Coreflexive
import Foundation.Vorspiel.HRel.Convergent

namespace LO.Modal

namespace Kripke

variable {F : Frame}

namespace Frame

class IsGeachConvergent (F : Frame) (g : Axioms.Geach.Taple) where
  gconv : âˆ€ â¦ƒx y z : Fâ¦„, x â‰º^[g.i] y â†’ x â‰º^[g.j] z â†’ âˆƒ u, y â‰º^[g.m] u âˆ§ z â‰º^[g.n] u


protected abbrev IsReflexive (F : Frame) := _root_.IsRefl _ F

@[simp] lemma refl [F.IsReflexive] : âˆ€ {x : F.World}, x â‰º x := by apply IsRefl.refl

@[simp]
instance [F.IsGeachConvergent âŸ¨0, 0, 1, 0âŸ©] : F.IsReflexive where
  refl := by simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨0, 0, 1, 0âŸ©);
instance [F.IsReflexive] : F.IsGeachConvergent âŸ¨0, 0, 1, 0âŸ© where
  gconv x y z Rxy Rxz := by simp_all;

protected abbrev IsSerial (F : Frame) := _root_.IsSerial F.Rel

lemma serial [F.IsSerial] : âˆ€ x : F, âˆƒ y, x â‰º y := IsSerial.serial

@[simp]
instance [F.IsGeachConvergent âŸ¨0, 0, 1, 1âŸ©] : F.IsSerial where
  serial := by simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨0, 0, 1, 1âŸ©);
instance [F.IsSerial] : F.IsGeachConvergent âŸ¨0, 0, 1, 1âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_zero, HRel.Iterate.iff_succ, exists_eq_right, and_self];
    subst Rxz;
    apply _root_.IsSerial.serial


protected abbrev IsTransitive (F : Frame) := _root_.IsTrans _ F.Rel

lemma trans [F.IsTransitive] : âˆ€ {x y z : F.World}, x â‰º y â†’ y â‰º z â†’ x â‰º z := by apply IsTrans.trans

@[simp]
instance [F.IsGeachConvergent âŸ¨0, 2, 1, 0âŸ©] : F.IsTransitive where
  trans := by
    rintro x y z;
    have : âˆ€ x y z : F, x = y â†’ âˆ€ (u : F.World), x â‰º u â†’ u â‰º z â†’ y â‰º z := by
      simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨0, 2, 1, 0âŸ©);
    apply this x x z rfl y;
instance [F.IsTransitive] : F.IsGeachConvergent âŸ¨0, 2, 1, 0âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_zero, HRel.Iterate.iff_succ, exists_eq_right, exists_eq_right'];
    subst Rxy;
    obtain âŸ¨y, Rxy, RyzâŸ© := Rxz;
    exact IsTrans.trans _ _ _ Rxy Ryz


protected abbrev IsSymmetric (F : Frame) := _root_.IsSymm _ F.Rel

lemma symm [F.IsSymmetric] : âˆ€ {x y : F.World}, x â‰º y â†’ y â‰º x := by apply IsSymm.symm

@[simp]
instance [F.IsGeachConvergent âŸ¨0, 1, 0, 1âŸ©] : F.IsSymmetric where
  symm x y := by
    have : âˆ€ x y z : F, x = y â†’ x â‰º z â†’ z â‰º y := by
      simpa using IsGeachConvergent.gconv (g := âŸ¨0, 1, 0, 1âŸ©) (F := F);
    apply @this x x y rfl;
instance [F.IsSymmetric] : F.IsGeachConvergent âŸ¨0, 1, 0, 1âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_zero, HRel.Iterate.iff_succ, exists_eq_right, exists_eq_left'];
    subst Rxy;
    exact _root_.IsSymm.symm _ _ Rxz;


protected abbrev IsEuclidean (F : Frame) := _root_.IsRightEuclidean F.Rel

lemma eucl [F.IsEuclidean] : âˆ€ {x y z : F.World}, x â‰º y â†’ x â‰º z â†’ y â‰º z := by apply IsRightEuclidean.reucl

@[simp]
instance [F.IsGeachConvergent âŸ¨1, 1, 0, 1âŸ©] : F.IsEuclidean where
  reucl x y z Rxy Rxz := by
    have : âˆ€ x y z : F, x â‰º y â†’ x â‰º z â†’ z â‰º y := by
      simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨1, 1, 0, 1âŸ©);
    apply this x z y Rxz Rxy;
instance [F.IsEuclidean] : F.IsGeachConvergent âŸ¨1, 1, 0, 1âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_succ, HRel.Iterate.iff_zero, exists_eq_right, exists_eq_left'];
    exact IsRightEuclidean.reucl Rxz Rxy


protected abbrev IsPiecewiseStronglyConvergent (F : Frame) := _root_.IsPiecewiseStronglyConvergent F.Rel

lemma ps_convergent [F.IsPiecewiseStronglyConvergent] : âˆ€ {x y z : F.World}, x â‰º y â†’ x â‰º z â†’ âˆƒ u, y â‰º u âˆ§ z â‰º u := by
  apply IsPiecewiseStronglyConvergent.ps_convergent

@[simp]
instance [F.IsGeachConvergent âŸ¨1, 1, 1, 1âŸ©] : F.IsPiecewiseStronglyConvergent where
  ps_convergent := by simpa using IsGeachConvergent.gconv (g := âŸ¨1, 1, 1, 1âŸ©) (F := F);
instance [F.IsPiecewiseStronglyConvergent] : F.IsGeachConvergent âŸ¨1, 1, 1, 1âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_succ, HRel.Iterate.iff_zero, exists_eq_right];
    obtain âŸ¨u, Ryu, RzuâŸ© := IsPiecewiseStronglyConvergent.ps_convergent Rxy Rxz;
    use u;


protected abbrev IsCoreflexive (F : Frame) := _root_.IsCoreflexive F.Rel

lemma corefl [F.IsCoreflexive] : âˆ€ {x y : F.World}, x â‰º y â†’ x = y := by apply IsCoreflexive.corefl

@[simp]
instance [F.IsGeachConvergent âŸ¨0, 1, 0, 0âŸ©] : F.IsCoreflexive where
  corefl x y Rxy := by
    have : âˆ€ x y z : F, x = y â†’ x â‰º z â†’ z = y := by
      simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨0, 1, 0, 0âŸ©);
    apply this x x y rfl Rxy |>.symm;
instance [F.IsCoreflexive] : F.IsGeachConvergent âŸ¨0, 1, 0, 0âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_zero, HRel.Iterate.iff_succ, exists_eq_right, exists_eq_left'];
    subst Rxy;
    exact F.corefl Rxz |>.symm;


protected class IsFunctional (F : Frame) where
  functional : âˆ€ â¦ƒx y z : F.Worldâ¦„, x â‰º y â†’ x â‰º z â†’ y = z

lemma functional [F.IsFunctional] : âˆ€ {x y z : F.World}, x â‰º y â†’ x â‰º z â†’ y = z := by apply IsFunctional.functional

instance [F.IsGeachConvergent âŸ¨1, 1, 0, 0âŸ©] : F.IsFunctional where
  functional x y z Rxy Rxz := by
    have : âˆ€ x y z : F, x â‰º y â†’ x â‰º z â†’ z = y := by
      simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨1, 1, 0, 0âŸ©);
    exact this x y z Rxy Rxz |>.symm;
instance [F.IsFunctional] : F.IsGeachConvergent âŸ¨1, 1, 0, 0âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_succ, HRel.Iterate.iff_zero, exists_eq_right, exists_eq_left'];
    apply IsFunctional.functional Rxy Rxz |>.symm;


protected class IsDense (F : Frame) where
  dense : âˆ€ â¦ƒx y : F.Worldâ¦„, x â‰º y â†’ âˆƒ u, x â‰º u âˆ§ u â‰º y

lemma dense [F.IsDense] : âˆ€ {x y : F.World}, x â‰º y â†’ âˆƒ u, x â‰º u âˆ§ u â‰º y := by apply IsDense.dense

instance [F.IsGeachConvergent âŸ¨0, 1, 2, 0âŸ©] : F.IsDense where
  dense x y Rxy := by
    have : âˆ€ x y z : F, x = y â†’ x â‰º z â†’ âˆƒ u, y â‰º u âˆ§ u â‰º z := by
      simpa using IsGeachConvergent.gconv (F := F) (g := âŸ¨0, 1, 2, 0âŸ©);
    apply this x x y rfl Rxy;
instance [F.IsDense] : F.IsGeachConvergent âŸ¨0, 1, 2, 0âŸ© where
  gconv x y z Rxy Rxz := by
    simp_all only [HRel.Iterate.iff_zero, HRel.Iterate.iff_succ, exists_eq_right, exists_eq_right'];
    subst Rxy;
    obtain âŸ¨u, Ryu, RzuâŸ© := IsDense.dense Rxz;
    use u;


protected class IsPreorder (F : Frame) extends F.IsReflexive, F.IsTransitive

protected class IsEquivalence (F : Frame) extends F.IsReflexive, F.IsTransitive, F.IsSymmetric
instance [F.IsEquivalence] : F.IsPreorder where

end Frame


instance : whitepoint.IsGeachConvergent g := âŸ¨by
  rintro x y z Rxy Rxz;
  use ();
  constructor <;> . apply HRel.Iterate.true_any; tauto;
âŸ©
instance : whitepoint.IsPreorder where


section definability

open Formula (atom)
open Formula.Kripke

lemma validate_axiomGeach_of_isGeachConvergent (g) [F.IsGeachConvergent g] : F âŠ§ (Axioms.Geach g (.atom 0)) := by
  rintro V x h;
  apply Satisfies.multibox_def.mpr;
  obtain âŸ¨y, Rxy, hbpâŸ© := Satisfies.multidia_def.mp h;
  intro z Rxz;
  apply Satisfies.multidia_def.mpr;
  obtain âŸ¨u, Ryu, RzuâŸ© := Frame.IsGeachConvergent.gconv Rxy Rxz;
  use u;
  constructor;
  . assumption;
  . exact (Satisfies.multibox_def.mp hbp) Ryu;

lemma validate_AxiomT_of_reflexive [refl : F.IsReflexive] : F âŠ§ (Axioms.T (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨0, 0, 1, 0âŸ©
lemma validate_AxiomD_of_serial [ser : F.IsSerial] : F âŠ§ (Axioms.D (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨0, 0, 1, 1âŸ©
lemma validate_AxiomB_of_symmetric [sym : F.IsSymmetric] : F âŠ§ (Axioms.B (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨0, 1, 0, 1âŸ©
lemma validate_AxiomFour_of_transitive [trans : F.IsTransitive] : F âŠ§ (Axioms.Four (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨0, 2, 1, 0âŸ©
lemma validate_AxiomFive_of_euclidean [eucl : F.IsEuclidean] : F âŠ§ (Axioms.Five (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨1, 1, 0, 1âŸ©
lemma validate_AxiomPoint2_of_confluent [conf : F.IsPiecewiseStronglyConvergent] : F âŠ§ (Axioms.Point2 (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨1, 1, 1, 1âŸ©
lemma validate_AxiomTc_of_coreflexive [corefl : F.IsCoreflexive] : F âŠ§ (Axioms.Tc (.atom 0)) := validate_axiomGeach_of_isGeachConvergent âŸ¨0, 1, 0, 0âŸ©


lemma isGeachConvergent_of_validate_axiomGeach {g} (h : F âŠ§ (Axioms.Geach g (.atom 0))) : F.IsGeachConvergent g := âŸ¨by
  rintro x y z Rxy Rxz;
  let V : Kripke.Valuation F := Î» v _ => y â‰º^[g.m] v;
  have : Satisfies âŸ¨F, VâŸ© x (â–¡^[g.j](â—‡^[g.n](.atom 0)))  := h V x $ by
    apply Satisfies.multidia_def.mpr;
    use y;
    constructor;
    . assumption;
    . apply Satisfies.multibox_def.mpr;
      aesop;
  replace : Satisfies âŸ¨F, VâŸ© z (â—‡^[g.n]Formula.atom 0) := Satisfies.multibox_def.mp this Rxz;
  obtain âŸ¨u, Rzu, RyuâŸ© := Satisfies.multidia_def.mp this;
  exact âŸ¨u, Ryu, RzuâŸ©;
âŸ©

lemma reflexive_of_validate_AxiomT (h : F âŠ§ (Axioms.T (.atom 0))) : F.IsReflexive := by
  suffices F.IsGeachConvergent âŸ¨0, 0, 1, 0âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma transitive_of_validate_AxiomFour (h : F âŠ§ (Axioms.Four (.atom 0))) : F.IsTransitive := by
  suffices F.IsGeachConvergent âŸ¨0, 2, 1, 0âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma euclidean_of_validate_AxiomFive (h : F âŠ§ (Axioms.Five (.atom 0))) : F.IsEuclidean := by
  suffices F.IsGeachConvergent âŸ¨1, 1, 0, 1âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma symmetric_of_validate_AxiomB (h : F âŠ§ (Axioms.B (.atom 0))) : F.IsSymmetric := by
  suffices F.IsGeachConvergent âŸ¨0, 1, 0, 1âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma serial_of_validate_AxiomD (h : F âŠ§ (Axioms.D (.atom 0))) : F.IsSerial := by
  suffices F.IsGeachConvergent âŸ¨0, 0, 1, 1âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma coreflexive_of_validate_AxiomTc (h : F âŠ§ (Axioms.Tc (.atom 0))) : F.IsCoreflexive := by
  suffices F.IsGeachConvergent âŸ¨0, 1, 0, 0âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

lemma confluent_of_validate_AxiomPoint2 (h : F âŠ§ (Axioms.Point2 (.atom 0))) : F.IsPiecewiseStronglyConvergent := by
  suffices F.IsGeachConvergent âŸ¨1, 1, 1, 1âŸ© by infer_instance;
  apply isGeachConvergent_of_validate_axiomGeach;
  simpa;

end definability


section canonicality

variable [Entailment S (Formula â„•)]
variable {ğ“¢ : S} [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢]

open Formula.Kripke
open Entailment
open MaximalConsistentTableau
open canonicalModel

instance [Entailment.HasAxiomGeach g ğ“¢] : (canonicalFrame ğ“¢).IsGeachConvergent g := âŸ¨by
  rintro x y z Rxy Rxz;
  have âŸ¨u, huâŸ© := lindenbaum (ğ“¢ := ğ“¢) (tâ‚€ := âŸ¨y.1.1.premultibox g.m, z.1.2.premultidia g.nâŸ©) $ by
    rintro Î“ Î” hÎ“ hÎ”;
    by_contra! hC;
    have hÎ³ : â–¡^[g.m](Î“.conj) âˆˆ y.1.1 := y.mdp_memâ‚_provable collect_multibox_fconj! $ iff_memâ‚_fconj.mpr (by simpa using hÎ“);
    have hÎ´ : â—‡^[g.n](Î”.disj) âˆˆ z.1.2 := mdp_memâ‚‚_provable distribute_multidia_fdisj! $ iff_memâ‚‚_fdisj.mpr (by simpa using hÎ”);
    generalize Î“.conj = Î³ at hÎ³ hC;
    generalize Î”.disj = Î´ at hÎ´ hC;
    have : ğ“¢ âŠ¢ â–¡^[g.m]Î³ â â–¡^[g.m]Î´ := imply_multibox_distribute'! hC;
    have : â–¡^[g.m]Î´ âˆˆ y.1.1 := mdp_memâ‚_provable this hÎ³;
    have : â—‡^[g.i](â–¡^[g.m]Î´) âˆˆ x.1.1 := def_multirel_multidia_memâ‚.mp Rxy this;
    have : â–¡^[g.j](â—‡^[g.n]Î´) âˆˆ x.1.1 := mdp_memâ‚_provable axiomGeach! this;
    have : â—‡^[g.n]Î´ âˆˆ z.1.1 := def_multirel_multibox_memâ‚.mp Rxz this;
    have : â—‡^[g.n]Î´ âˆ‰ z.1.2 := iff_not_memâ‚‚_memâ‚.mpr this;
    contradiction;
  use u;
  constructor;
  . apply def_multirel_multibox_memâ‚.mpr; apply hu.1;
  . apply def_multirel_multidia_memâ‚‚.mpr; apply hu.2;
âŸ©

instance [Entailment.HasAxiomT ğ“¢] : (canonicalFrame ğ“¢).IsReflexive := by simp
instance [Entailment.HasAxiomD ğ“¢] : (canonicalFrame ğ“¢).IsSerial := by simp
instance [Entailment.HasAxiomB ğ“¢] : (canonicalFrame ğ“¢).IsSymmetric := by simp
instance [Entailment.HasAxiomFour ğ“¢] : (canonicalFrame ğ“¢).IsTransitive := by simp
instance [Entailment.HasAxiomFive ğ“¢] :(canonicalFrame ğ“¢).IsEuclidean := by simp
instance [Entailment.HasAxiomTc ğ“¢] : (canonicalFrame ğ“¢).IsCoreflexive := by simp
instance [Entailment.HasAxiomPoint2 ğ“¢] : (canonicalFrame ğ“¢).IsPiecewiseStronglyConvergent := by simp

end canonicality

end Kripke

end LO.Modal
