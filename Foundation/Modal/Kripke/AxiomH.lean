module

public import Foundation.Modal.Kripke.Root

@[expose] public section

namespace LO.Modal

open Formula (atom)
open Formula.Kripke

namespace Kripke

variable {F : Frame}

section

/--
  There is no detour in `x < y`,
  i.e. there is no `u` such that `x < u < y` and `u â‰  x` and `u â‰  y`.
-/
class Frame.IsDetourFree (F : Frame) : Prop where
  detour_free : âˆ€ {x u y : F}, x â‰º u â†’ u â‰º y â†’ (u = x âˆ¨ u = y)

lemma Frame.detour_free [F.IsDetourFree] : âˆ€ {x u y : F}, x â‰º u â†’ u â‰º y â†’ (u = x âˆ¨ u = y) := IsDetourFree.detour_free

lemma Frame.not_exists_detour [F.IsDetourFree] {x y : F} : Â¬âˆƒ u, u â‰  x âˆ§ u â‰  y âˆ§ x â‰º u âˆ§ u â‰º y := by
  by_contra! hC;
  obtain âŸ¨u, nexy, neuy, Rxy, RuyâŸ© := hC;
  rcases F.detour_free Rxy Ruy with (rfl | rfl) <;> contradiction;

lemma Frame.IsDetourFree.of_not_exists_detour (h : âˆ€ {x y : F}, Â¬âˆƒ u, u â‰  x âˆ§ u â‰  y âˆ§ x â‰º u âˆ§ u â‰º y) : Frame.IsDetourFree F := by
  constructor;
  rintro x y u Rxu Ruy;
  contrapose! h;
  use x, u, y;
  tauto;

instance [F.IsFinite] [F.IsDetourFree] : F.IsAntisymmetric := by
  constructor;
  intro x y Rxy Ryx;
  rcases F.detour_free Rxy Ryx with rfl | rfl <;> trivial;

instance {r : F.World} [F.IsDetourFree] : (Fâ†¾r).IsDetourFree := âŸ¨by
  rintro âŸ¨x, (rfl | Rrx)âŸ© âŸ¨u, (rfl | Rru)âŸ© âŸ¨y, (rfl | Rry)âŸ© Rxu Ruy <;>
  rcases F.detour_free Rxu Ruy with h | h <;> simp_all;
âŸ©

end

section definability

instance : whitepoint.IsDetourFree := âŸ¨by tautoâŸ©

lemma validate_axiomH_of_isDetourFree [F.IsDetourFree] : F âŠ§ (Axioms.H (.atom 0)) := by
  have := @F.detour_free _;
  contrapose! this;

  obtain âŸ¨V, x, hâŸ© := ValidOnFrame.exists_valuation_world_of_not this;
  replace h := Satisfies.not_imp_def.mp h;
  have âŸ¨hâ‚, hâ‚‚âŸ© := h;

  replace hâ‚‚ := Satisfies.not_box_def.mp hâ‚‚;
  obtain âŸ¨u, Rxu, hâ‚‚âŸ© := hâ‚‚;
  replace âŸ¨hâ‚‚, hâ‚ƒâŸ© := Satisfies.not_imp_def.mp hâ‚‚;
  obtain âŸ¨y, Ruy, hâ‚‚âŸ© := Satisfies.dia_def.mp hâ‚‚;

  use x, u, y;
  refine âŸ¨Rxu, Ruy, ?_, ?_âŸ© <;>
  . by_contra hC;
    subst hC;
    tauto;

lemma isDetourFree_of_validate_axiomH (h : F âŠ§ (Axioms.H (.atom 0))) : F.IsDetourFree := by
  constructor;
  contrapose! h;
  rcases h with âŸ¨x, u, y, Rxu, Ruy, neux, neuyâŸ©;
  apply ValidOnFrame.not_of_exists_valuation_world;
  use Î» _ w => w â‰  u, x;
  simp [Satisfies];
  tauto;

end definability


section canonicality

variable {S} [Entailment S (Formula â„•)]
variable {ğ“¢ : S} [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢]

open Formula.Kripke
open LO.Entailment
     LO.Entailment.FiniteContext
     Modal.Entailment
open canonicalModel
open MaximalConsistentTableau

namespace Canonical

instance [Entailment.HasAxiomH ğ“¢] : (canonicalFrame ğ“¢).IsDetourFree := âŸ¨by
  rintro x u y Rxu Ruy;
  by_contra! hC;
  obtain âŸ¨neux, neuyâŸ© := hC;

  obtain âŸ¨Ï†, hÏ†u, hÏ†xâŸ© := existsâ‚‚â‚_of_ne neux;
  obtain âŸ¨Ïˆ, hÏˆu, hÏˆyâŸ© := existsâ‚‚â‚_of_ne neuy;

  suffices Ï† â‹ Ïˆ âˆˆ u.1.1 by
    apply neither âŸ¨this, iff_memâ‚‚_or.mpr $ ?_âŸ©;
    tauto;

  have : â–¡(â—‡(Ï† â‹ Ïˆ) â Ï† â‹ Ïˆ) âˆˆ x.1.1 := mdp_memâ‚_provable axiomH! $ by
    apply iff_memâ‚_or.mpr;
    tauto;
  apply iff_memâ‚_imp'.mp $ def_rel_box_memâ‚.mp Rxu this;
  apply def_rel_dia_memâ‚.mp Ruy;
  apply iff_memâ‚_or.mpr;
  tauto;
âŸ©

end Canonical

end canonicality

end Kripke

end LO.Modal
end
