import Foundation.Modal.Kripke.Rooted
import Foundation.Modal.Kripke.Logic.KTB
import Foundation.Modal.Kripke.Logic.KD45
import Foundation.Modal.Kripke.Logic.KB4
import Foundation.Modal.Kripke.Logic.S4
import Foundation.Modal.Kripke.Logic.S4Point4
import Foundation.Vorspiel.HRel.Universal

namespace LO.Modal

open Kripke
open Hilbert.Kripke


namespace Kripke

variable {F : Frame}

class Frame.IsUniversal (F : Frame) extends _root_.IsUniversal F.Rel
@[simp] lemma universal [F.IsUniversal] : âˆ€ {x y : F.World}, x â‰º y := by apply IsUniversal.universal;

instance [F.IsUniversal] : F.IsEuclidean := by simp
instance [F.IsUniversal] : F.IsPreorder where

protected class Frame.IsS5 (F : Frame) extends F.IsReflexive, F.IsEuclidean
protected class Frame.IsFiniteS5 (F : Frame) extends F.IsFinite, F.IsS5

instance [F.IsS5] : F.IsKD45 where
instance [F.IsS5] : F.IsKB4 where
instance [F.IsS5] : F.IsKTB where
instance [F.IsS5] : F.IsS4Point4 where

protected abbrev FrameClass.S5 : FrameClass := { F | F.IsS5 }
protected abbrev FrameClass.finite_S5: FrameClass := { F | F.IsFiniteS5 }
protected abbrev FrameClass.universal : FrameClass := { F | F.IsUniversal }

instance Frame.pointGenerate.isUniversal (F : Frame) (r : F.World) (_ : F.IsS5) : (Fâ†¾r).IsUniversal where
  universal := by
    rintro âŸ¨x, (rfl | hx)âŸ© âŸ¨y, (rfl | hy)âŸ©;
    . simp;
    . exact hy.unwrap;
    . suffices x â‰º y by simpa;
      exact IsSymm.symm _ _ hx.unwrap;
    . suffices x â‰º y by simpa;
      apply F.eucl hx.unwrap hy.unwrap ;

lemma iff_validOnUniversalFrameClass_validOnReflexiveEuclideanFrameClass : FrameClass.universal âŠ§ Ï† â†” FrameClass.S5 âŠ§ Ï† := by
  constructor;
  . rintro h F hF V r;
    apply @Model.pointGenerate.modal_equivalent_at_root _ _ |>.mp;
    apply h;
    apply Frame.pointGenerate.isUniversal F r hF;
  . rintro h F F_univ;
    apply h;
    simp_all;
    constructor;

end Kripke


namespace Hilbert.S5.Kripke

instance : Sound Hilbert.S5 FrameClass.S5 := instSound_of_validates_axioms $ by
  apply FrameClass.Validates.withAxiomK;
  rintro F âŸ¨_, _âŸ© _ (rfl | rfl);
  . exact validate_AxiomT_of_reflexive;
  . exact validate_AxiomFive_of_euclidean;

instance : Sound Hilbert.S5 FrameClass.universal := âŸ¨by
  intro Ï† hF;
  apply iff_validOnUniversalFrameClass_validOnReflexiveEuclideanFrameClass.mpr;
  exact Sound.sound (ð“œ := FrameClass.S5) hF;
âŸ©

instance : Entailment.Consistent Hilbert.S5 := consistent_of_sound_frameclass FrameClass.S5 $ by
  use whitepoint;
  constructor;

instance : Canonical Hilbert.S5 FrameClass.S5 := âŸ¨by constructorâŸ©

instance : Complete Hilbert.S5 FrameClass.S5 := inferInstance

instance : Complete Hilbert.S5 FrameClass.universal := âŸ¨by
  intro Ï† hF;
  apply Complete.complete (ð“œ := FrameClass.S5);
  apply iff_validOnUniversalFrameClass_validOnReflexiveEuclideanFrameClass.mp;
  exact hF;
âŸ©

end Hilbert.S5.Kripke


namespace Logic

open Formula
open Entailment
open Kripke

lemma S5.Kripke.refl_eucl : Modal.S5 = FrameClass.S5.logic := eq_hilbert_logic_frameClass_logic
lemma S5.Kripke.universal : Modal.S5 = FrameClass.universal.logic := eq_hilbert_logic_frameClass_logic

instance : Hilbert.KTB âª± Hilbert.S5 := by
  constructor;
  . apply Hilbert.Kripke.weakerThan_of_subset_frameClass (FrameClass.KTB) (FrameClass.S5);
    intro F hF;
    simp_all only [Set.mem_setOf_eq];
    infer_instance;
  . apply Entailment.not_weakerThan_iff.mpr;
    use Axioms.Five (.atom 0);
    constructor;
    . exact axiomFive!;
    . apply Sound.not_provable_of_countermodel (ð“œ := FrameClass.KTB)
      apply Kripke.not_validOnFrameClass_of_exists_model_world;
      let M : Model := âŸ¨âŸ¨Fin 3, Î» x y => (x = 0) âˆ¨ (x = 1 âˆ§ y â‰  2) âˆ¨ (x = 2 âˆ§ y â‰  1)âŸ©, Î» x _ => x = 1âŸ©;
      use M, 0;
      constructor;
      . refine { refl := by omega, symm := by omega };
      . suffices (0 : M.World) â‰º 1 âˆ§ âˆƒ x : M.World, (0 : M.World) â‰º x âˆ§ Â¬x â‰º 1 by
          simpa [M, Semantics.Realize, Satisfies];
        constructor;
        . omega;
        . use 2;
          constructor <;> omega;

instance : Hilbert.KD45 âª± Hilbert.S5 := by
  constructor;
  . apply Hilbert.Kripke.weakerThan_of_subset_frameClass (FrameClass.KD45) (FrameClass.S5);
    intro F hF;
    simp_all only [Set.mem_setOf_eq];
    infer_instance;
  . apply Entailment.not_weakerThan_iff.mpr;
    use (Axioms.T (.atom 0));
    constructor;
    . exact axiomT!;
    . apply Sound.not_provable_of_countermodel (ð“œ := FrameClass.KD45)
      apply Kripke.not_validOnFrameClass_of_exists_model_world;
      let M : Model := âŸ¨âŸ¨Fin 2, Î» x y => (x = 0 âˆ§ y = 1) âˆ¨ (x = 1 âˆ§ y = 1)âŸ©, Î» x _ => x = 1âŸ©;
      use M, 0;
      constructor;
      . refine {
          serial := by intro x; use 1; omega;,
          trans := by omega,
          reucl := by simp [RightEuclidean]; omega
        }
      . simp [Semantics.Realize, Satisfies, M];
        tauto;

instance : Hilbert.KB4 âª± Hilbert.S5 := by
  constructor;
  . apply Hilbert.Kripke.weakerThan_of_subset_frameClass (FrameClass.KB4) (FrameClass.S5);
    intro F hF;
    simp_all only [Set.mem_setOf_eq];
    infer_instance;
  . apply Entailment.not_weakerThan_iff.mpr;
    use (Axioms.T (.atom 0));
    constructor;
    . exact axiomT!;
    . apply Sound.not_provable_of_countermodel (ð“œ := FrameClass.KB4)
      apply Kripke.not_validOnFrameClass_of_exists_model_world;
      use âŸ¨âŸ¨Fin 1, Î» x y => FalseâŸ©, Î» x _ => FalseâŸ©, 0;
      constructor;
      . refine { symm := by tauto, trans := by tauto };
      . simp [Semantics.Realize, Satisfies];

instance : Hilbert.S4Point4 âª± Hilbert.S5 := by
  constructor;
  . apply Hilbert.Kripke.weakerThan_of_subset_frameClass (FrameClass.S4Point4) (FrameClass.S5);
    intro F hF;
    simp_all only [Set.mem_setOf_eq];
    infer_instance;
  . apply Entailment.not_weakerThan_iff.mpr;
    use Axioms.Five (.atom 0);
    constructor;
    . simp;
    . apply Sound.not_provable_of_countermodel (ð“œ := FrameClass.S4Point4)
      apply Kripke.not_validOnFrameClass_of_exists_model_world;
      let M : Model := âŸ¨âŸ¨Fin 2, Î» x y => x â‰¤ yâŸ©, Î» w a => w = 0âŸ©;
      use M, 0;
      constructor;
      . refine {
          sobocinski := by
            intro x y z _ _;
            match x, y with
            | 0, 0 => contradiction;
            | 0, 1 => omega;
            | 1, 0 => contradiction;
            | 1, 1 => contradiction;
        };
      . suffices (0 : M.World) â‰º 0 âˆ§ âˆƒ x : M.World, (0 : M) â‰º x âˆ§ Â¬x â‰º 0 by
          simpa [M, Semantics.Realize, Satisfies];
        constructor;
        . omega;
        . use 1;
          constructor <;> omega;

instance : Hilbert.S4 âª± Hilbert.S5 := calc
  Hilbert.S4 âª± Hilbert.S4Point2 := by infer_instance
  _          âª± Hilbert.S4Point3 := by infer_instance
  _          âª± Hilbert.S4Point4 := by infer_instance
  _          âª± Hilbert.S5       := by infer_instance

end Logic

instance : Modal.KTB âª± Modal.S5 := inferInstance

instance : Modal.KD45 âª± Modal.S5 := inferInstance

instance : Modal.KB4 âª± Modal.S5 := inferInstance

instance : Modal.S4Point4 âª± Modal.S5 := inferInstance

instance : Modal.S4 âª± Modal.S5 := inferInstance

end LO.Modal
