import Foundation.Modal.Entailment.KT
import Foundation.Modal.Hilbert.WellKnown
import Foundation.Modal.Kripke.AxiomGeach
import Foundation.Modal.Kripke.AxiomMk
import Foundation.Modal.Logic.Basic
import Foundation.Vorspiel.List.Chain

namespace LO.Modal

namespace Kripke

protected abbrev FrameClass.refl_makinson : FrameClass := { F | IsRefl _ F âˆ§ SatisfiesMakinsonCondition _ F.Rel }

end Kripke



open Kripke
open Hilbert.Kripke
open Geachean

namespace Hilbert.KTMk.Kripke

instance sound : Sound (Hilbert.KTMk) Kripke.FrameClass.refl_makinson := instSound_of_validates_axioms $ by
  apply FrameClass.Validates.withAxiomK;
  rintro F âŸ¨_, _âŸ© _ (rfl | rfl);
  . exact validate_AxiomT_of_reflexive;
  . exact validate_axiomMk_of_satisfiesMakinsonCondition;

instance consistent : Entailment.Consistent (Hilbert.KTMk) := consistent_of_sound_frameclass Kripke.FrameClass.refl_makinson $ by
  use whitepoint;
  constructor;
  . infer_instance;
  . constructor;
    intro x;
    use x;
    tauto;

instance canonical : Canonical (Hilbert.KTMk) Kripke.FrameClass.refl_makinson := âŸ¨by
  apply Set.mem_setOf_eq.mpr;
  constructor;
  . infer_instance;
  . infer_instance;
âŸ©

instance complete : Complete (Hilbert.KTMk) Kripke.FrameClass.refl_makinson := inferInstance


section

open Formula.Kripke
open Entailment

lemma validate_axiomFour_of_model_finitely {M : Kripke.Model} (hM : M âŠ§* Hilbert.KTMk.logic)
  : Finite M â†’ âˆ€ Ï† : Formula â„•, M âŠ§ Axioms.Four Ï† := by
  contrapose!;
  rintro âŸ¨Ï†, hÏ†âŸ©;
  apply not_finite_iff_infinite.mpr;
  apply List.Nodup.infinite_of_infinite;
  have H : âˆ€ n : â„•+, âˆƒ l : List M.World, l.Nodup âˆ§ l.length = n âˆ§ List.Chain' (Â· â‰º Â·) l âˆ§ (âˆ€ i : Fin l.length, l[i] âŠ§ â–¡^[(i + 1)]Ï† â‹ âˆ¼â–¡^[(i + 2)]Ï†) := by
    intro n;
    induction n with
    | one =>
      replace âŸ¨xâ‚€, hÏ†âŸ© := ValidOnModel.exists_world_of_not hÏ†;
      use [xâ‚€];
      simpa using hÏ†;
    | succ n ih =>
      obtain âŸ¨l, hl_nodup, hl_len, hl_chain, hlâŸ© := ih;
      let m : Fin l.length := âŸ¨n - 1, by simp [hl_len]âŸ©;
      have : l[m] âŠ§ â—‡(â–¡^[(m + 2)]Ï† â‹ âˆ¼â–¡^[(m + 3)]Ï†) := Satisfies.mdp ?_ $ hl m;
      obtain âŸ¨y, Rmy, hyâ‚‚âŸ© := Satisfies.dia_def.mp this;
      let l' := l.concat y;
      use l';
      have hl' : âˆ€ (i : Fin l'.length), l'[i] âŠ§ â–¡^[(i + 1)]Ï† â‹ âˆ¼â–¡^[(i + 2)]Ï† := by
        rintro âŸ¨i, hi'âŸ©;
        replace hi : i < l.length âˆ¨ i = l.length := by
          simp [l'] at hi';
          omega;
        rcases hi with (hi | rfl);
        . let i : Fin (l.length) := âŸ¨i, by omegaâŸ©;
          generalize ei' : (âŸ¨i, hi'âŸ© : Fin l'.length) = i';
          simpa [
            show l[i] = l'[i'] by simp [â†ei', l'],
            show â–¡^[(i + 1)]Ï† = â–¡^[(i' + 1)]Ï† by simp [â†ei'],
            show âˆ¼â–¡^[(i + 2)]Ï† = âˆ¼â–¡^[(i' + 2)]Ï† by simp [â†ei']
          ] using @hl i;
        . simpa [l', hl_len, m] using hyâ‚‚;
      refine âŸ¨?_, by simpa [l'], ?_, hl'âŸ©;
      . apply List.nodup_iff_get_ne_get.mpr;
        rintro âŸ¨i, hiâŸ© âŸ¨j, hjâŸ© hij eij;
        replace hij : i < j := hij;
        apply Satisfies.not_def.mp $ Satisfies.and_def.mp (hl' âŸ¨i, hiâŸ©) |>.2;
        apply Satisfies.mdp ?_ $ eij â–¸ Satisfies.and_def.mp (hl' âŸ¨j, hjâŸ©) |>.1;
        apply hM.realize;
        obtain âŸ¨c, hc, rflâŸ© := lt_iff_exists_add.mp hij;
        match c with
        | 0 => contradiction;
        | n + 1 =>
          suffices Hilbert.KTMk âŠ¢! â–¡^[((i + 2) + n)]Ï† â â–¡^[(i + 2)]Ï† by
            simp_all [
              show (i + (n + 1)) = (i + n) + 1 by omega,
              show (i + 2) + n = (i + n) + 2 by omega
            ];
          apply reduce_box_in_CAnt!;
      . apply List.chain'_concat_of_not_nil (List.length_pos_iff_ne_nil.mp (by simp [hl_len])) |>.mpr;
        constructor;
        . assumption;
        . convert Rmy;
          trans l[l.length - 1]'(by simp [hl_len]);
          . apply List.getLast_eq_getElem;
          . simp [m, hl_len];
      . intro h;
        have : l[m] âŠ§ â–¡^[(m + 1)]Ï† â‹ âˆ¼â–¡^[(m + 2)]Ï† â â—‡(â–¡^[(m + 2)]Ï† â‹ â—‡(âˆ¼â–¡^[(m + 2)]Ï†)) := by
          apply hM.realize;
          simp;
        replace : l[m] âŠ§ â—‡(â–¡^[(m + 2)]Ï† â‹ â—‡(âˆ¼â–¡^[(m + 2)]Ï†)) := this h;
        obtain âŸ¨y, hyâ‚, hyâ‚‚âŸ© := Satisfies.dia_def.mp this;
        apply Satisfies.dia_def.mpr;
        use y;
        constructor;
        . assumption;
        . apply Satisfies.and_def.mpr;
          constructor;
          . exact Satisfies.and_def.mp hyâ‚‚ |>.1;
          . apply Satisfies.not_def.mpr;
            simpa using Satisfies.box_dn.not.mp $ Satisfies.not_def.mp $ Satisfies.dia_dual.mp $ Satisfies.and_def.mp hyâ‚‚ |>.2;
  apply Infinite.of_injective (Î² := â„•+) (Î» n => âŸ¨H n |>.choose, H n |>.choose_spec.1âŸ©);
  intro i j;
  simp only [Subtype.mk.injEq];
  contrapose!;
  suffices i â‰  j â†’ (H i).choose.length â‰  (H j).choose.length by tauto;
  rw [H i |>.choose_spec.2.1, H j |>.choose_spec.2.1];
  simp;

lemma model_infinitity_of_not_validate_axiomFour {M : Kripke.Model} (hM : M âŠ§* Hilbert.KTMk.logic)
  : (âˆƒ Ï† : Formula â„•, Â¬M âŠ§ Axioms.Four Ï†) â†’ Infinite M := by
  contrapose!;
  intro h;
  apply validate_axiomFour_of_model_finitely hM;
  simpa using h;

abbrev recessionFrame : Kripke.Frame where
  World := â„•
  Rel i j := i â‰¤ j + 1


namespace recessionFrame

instance : IsRefl _ recessionFrame := âŸ¨by tautoâŸ©
instance : SatisfiesMakinsonCondition _ recessionFrame := âŸ¨by
  intro i;
  use i + 1;
  refine âŸ¨by omega, by omega, by simp_all; omegaâŸ©;
âŸ©

lemma not_transitive : Â¬Transitive recessionFrame := by
  by_contra h_trans;
  have := @h_trans 2 1 0;
  simp [recessionFrame] at this;

lemma exists_not_validate_axiomFour : âˆƒ Ï† : Formula â„•, Â¬recessionFrame âŠ§ Axioms.Four Ï† := by
  use (.atom 0);
  exact not_imp_not.mpr transitive_of_validate_AxiomFour not_transitive;

end recessionFrame

lemma exists_not_provable_axiomFour : âˆƒ Ï† : Formula â„•, Hilbert.KTMk âŠ¬ Axioms.Four Ï† := by
  obtain âŸ¨Ï†, hÏ†âŸ© := recessionFrame.exists_not_validate_axiomFour;
  use! Ï†;
  apply Sound.not_provable_of_countermodel (ğ“œ := Kripke.FrameClass.refl_makinson);
  apply iff_not_validOnFrameClass_exists_frame.mpr;
  use recessionFrame;
  constructor;
  . constructor <;> infer_instance;
  . assumption;

lemma no_finite_model_property : Â¬(âˆ€ Ï†, Hilbert.KTMk âŠ¬ Ï† â†’ âˆƒ M : Kripke.Model, Finite M âˆ§ M âŠ§* Hilbert.KTMk.logic âˆ§ Â¬M âŠ§ Ï†)  := by
  by_contra! hC;
  obtain âŸ¨Ï†, hÏ†âŸ© := exists_not_provable_axiomFour;
  obtain âŸ¨M, hMâ‚, hMâ‚‚, hMâ‚ƒâŸ© := @hC (Axioms.Four Ï†) hÏ†;
  apply not_finite_iff_infinite.mpr $ @model_infinitity_of_not_validate_axiomFour M ?_ âŸ¨Ï†, hMâ‚ƒâŸ©;
  . assumption;
  . assumption;

example : âˆƒ Ï†, Hilbert.KTMk âŠ¬ Ï† âˆ§ (âˆ€ M : Kripke.Model, Finite M â†’ M âŠ§* Hilbert.KTMk.logic â†’ M âŠ§ Ï†) := by
  simpa using no_finite_model_property;

end

end Hilbert.KTMk.Kripke

end LO.Modal
