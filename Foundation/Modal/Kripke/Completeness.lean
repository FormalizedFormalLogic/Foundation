import Foundation.Modal.Tableau
import Foundation.Modal.Kripke.Basic

namespace LO.Modal

open Entailment
open Formula
open Kripke
open MaximalConsistentTableau

variable {S} [Entailment S (Formula â„•)]
variable {ğ“¢ : S} [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢]

namespace Kripke


section

abbrev canonicalFrame (ğ“¢ : S) [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢] : Kripke.Frame where
  World := MaximalConsistentTableau ğ“¢
  Rel tâ‚ tâ‚‚ := â–¡'â»Â¹tâ‚.1.1 âŠ† tâ‚‚.1.1

abbrev canonicalModel (ğ“¢ : S) [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢] : Model where
  toFrame := canonicalFrame ğ“¢
  Val t a := (atom a) âˆˆ t.1.1

@[reducible]
instance : Semantics (canonicalModel ğ“¢) (Formula â„•) := Formula.Kripke.Satisfies.semantics (M := canonicalModel ğ“¢)

end


section lemmata

variable {Ï† Ïˆ : Formula â„•}
variable {t : (canonicalModel ğ“¢).World}

lemma truthlemma : ((Ï† âˆˆ t.1.1) â†” t âŠ§ Ï†) âˆ§ ((Ï† âˆˆ t.1.2) â†” Â¬t âŠ§ Ï†) := by
  induction Ï† generalizing t with
  | hatom =>
    simp_all only [Semantics.Models, Satisfies, true_and];
    exact iff_not_memâ‚_memâ‚‚.symm;
  | hfalsum => simp [Semantics.Models, Satisfies];
  | himp Ï† Ïˆ ihÏ† ihÏˆ =>
    constructor;
    . constructor;
      . intro hÏ†Ïˆ hÏ†;
        rcases iff_memâ‚_imp.mp hÏ†Ïˆ with (hÏ† | hÏˆ);
        . have := ihÏ†.2.1 hÏ†; contradiction;
        . exact ihÏˆ.1.1 hÏˆ;
      . intro hÏ†Ïˆ;
        rcases Satisfies.imp_defâ‚‚.mp hÏ†Ïˆ with (hÏ† | hÏˆ);
        . apply iff_memâ‚_imp.mpr;
          left;
          exact ihÏ†.2.2 hÏ†;
        . apply iff_memâ‚_imp.mpr;
          right;
          exact ihÏˆ.1.2 hÏˆ;
    . constructor;
      . intro hÏ†Ïˆ;
        rcases iff_memâ‚‚_imp.mp hÏ†Ïˆ with âŸ¨hÏ†, hÏˆâŸ©;
        apply Satisfies.imp_defâ‚‚.not.mpr;
        push_neg;
        constructor;
        . exact ihÏ†.1.mp hÏ†;
        . exact ihÏˆ.2.mp hÏˆ;
      . intro hÏ†Ïˆ;
        apply iff_memâ‚‚_imp.mpr;
        replace hÏ†Ïˆ := Satisfies.imp_defâ‚‚.not.mp hÏ†Ïˆ;
        push_neg at hÏ†Ïˆ;
        rcases hÏ†Ïˆ with âŸ¨hÏ†, hÏˆâŸ©;
        constructor;
        . exact ihÏ†.1.mpr hÏ†;
        . exact ihÏˆ.2.mpr hÏˆ;
  | hbox Ï† ihÏ† =>
    constructor;
    . constructor;
      . intro h t' Rtt';
        apply ihÏ†.1.1;
        exact iff_memâ‚_box.mp h Rtt';
      . intro h;
        apply iff_memâ‚_box.mpr;
        intro t' Rtt';
        apply ihÏ†.1.2;
        exact h t' Rtt';
    . constructor;
      . intro h;
        apply Satisfies.box_def.not.mpr;
        push_neg;
        obtain âŸ¨t', Rtt', ht'âŸ© := iff_memâ‚‚_box.mp h;
        use t';
        constructor;
        . exact Rtt';
        . exact ihÏ†.2.mp ht';
      . intro h;
        apply iff_memâ‚‚_box.mpr;
        replace h := Satisfies.box_def.not.mp h;
        push_neg at h;
        obtain âŸ¨t', Rtt', ht'âŸ© := h;
        use t';
        constructor;
        . exact Rtt';
        . exact ihÏ†.2.mpr ht';

lemma truthlemmaâ‚ : (Ï† âˆˆ t.1.1) â†” t âŠ§ Ï† := truthlemma.1

lemma truthlemmaâ‚‚ : (Ï† âˆˆ t.1.2) â†” Â¬t âŠ§ Ï† := truthlemma.2

lemma iff_valid_on_canonicalModel_deducible : (canonicalModel ğ“¢) âŠ§ Ï† â†” ğ“¢ âŠ¢ Ï† := by
  constructor;
  . contrapose;
    intro h;
    have : Tableau.Consistent ğ“¢ (âˆ…, {Ï†}) := by
      apply Tableau.iff_consistent_empty_singletonâ‚‚ (ğ“¢ := ğ“¢) (Ï† := Ï†) |>.mpr;
      exact h;
    obtain âŸ¨t, htâŸ© := lindenbaum this;
    apply ValidOnModel.not_of_exists_world;
    use t;
    apply truthlemmaâ‚‚.mp;
    apply ht.2;
    tauto_set;
  . intro h t;
    exact truthlemmaâ‚.mp $ MaximalConsistentTableau.iff_provable_memâ‚.mp h t;

end lemmata


class Canonical (ğ“¢ : S) [Entailment.Consistent ğ“¢] [Entailment.K ğ“¢] (C : FrameClass) : Prop where
  canonical : (Kripke.canonicalFrame ğ“¢) âˆˆ C

instance [Canonical ğ“¢ C] : Complete ğ“¢ C := âŸ¨by
  contrapose;
  intro h;
  apply not_validOnFrameClass_of_exists_model;
  use (canonicalModel ğ“¢);
  constructor;
  . exact Canonical.canonical;
  . exact iff_valid_on_canonicalModel_deducible.not.mpr h;
âŸ©


namespace canonicalModel

open Formula.Kripke.Satisfies

variable {x y : (canonicalModel ğ“¢).World}

lemma def_rel_box_memâ‚ : x â‰º y â†” â–¡'â»Â¹x.1.1 âŠ† y.1.1 := by simp [Frame.Rel'];

lemma def_rel_box_satisfies : x â‰º y â†” âˆ€ {Ï†}, x âŠ§ â–¡Ï† â†’ y âŠ§ Ï† := by
  constructor;
  . intro h Ï† hÏ†;
    exact truthlemmaâ‚.mp $  def_rel_box_memâ‚.mp h (truthlemmaâ‚.mpr hÏ†);
  . intro h;
    apply def_rel_box_memâ‚.mpr;
    intro Ï† hÏ†;
    exact truthlemmaâ‚.mpr $ h $ truthlemmaâ‚.mp hÏ†

lemma def_multirel_boxItr_satisfies : x â‰º^[n] y â†” (âˆ€ {Ï†}, x âŠ§ â–¡^[n]Ï† â†’ y âŠ§ Ï†) := by
  constructor;
  . intro h Ï† hÏ†;
    exact Satisfies.boxItr_def.mp hÏ† h;
  . induction n generalizing x y with
    | zero =>
      suffices (âˆ€ {Ï† : Formula â„•}, x âŠ§ Ï† â†’ y âŠ§ Ï†) â†’ x = y by simpa;
      intro h;
      apply intro_equality;
      . intro Ï† hÏ†; exact truthlemmaâ‚.mpr $ h $ truthlemmaâ‚.mp hÏ†;
      . intro Ï† hÏ†; exact truthlemmaâ‚‚.mpr $ h $ Satisfies.not_def.mpr $ truthlemmaâ‚‚.mp hÏ†;
    | succ n ih =>
      intro h;
      obtain âŸ¨t, htâŸ© := lindenbaum (ğ“¢ := ğ“¢) (tâ‚€ := âŸ¨{ Ï† | x âŠ§ â–¡Ï† }, â–¡'^[n]{ Ï† | Â¬y âŠ§ Ï† }âŸ©) $ by
        intro Î“ Î” hÎ“ hÎ”;
        by_contra! hC;
        have : ğ“¢ âŠ¢ â–¡Î“.conj â â–¡Î”.disj := imply_box_distribute'! hC;
        have : â–¡Î”.disj âˆˆ x.1.1 := mdp_memâ‚_provable this $ by
          apply truthlemmaâ‚.mpr;
          intro y Rxy;
          apply Satisfies.fconj_def.mpr;
          intro Ï† hÏ†;
          apply hÎ“ hÏ† y Rxy;
        have : x âŠ§ â–¡Î”.disj := truthlemmaâ‚.mp this;
        have : x âŠ§ â–¡^[(n + 1)](â–¡'â»Â¹^[n]Î”).disj := by
          suffices x âŠ§ â–¡â–¡^[n](â–¡'â»Â¹^[n]Î”).disj by simpa;
          intro y Rxy;
          apply boxItr_def.mpr;
          intro z Ryz;
          obtain âŸ¨Ïˆ, hÏˆâ‚, hÏˆâ‚‚âŸ© := Satisfies.fdisj_def.mp $ this y Rxy;
          obtain âŸ¨Î¾, _, rflâŸ© := by simpa using hÎ” hÏˆâ‚;
          apply Satisfies.fdisj_def.mpr;
          use Î¾;
          constructor;
          . simpa;
          . exact Satisfies.boxItr_def.mp hÏˆâ‚‚ Ryz;
        have : y âŠ§ (â–¡'â»Â¹^[n]Î”).disj := h this;
        obtain âŸ¨Ïˆ, hÏˆâ‚, hÏˆâ‚‚âŸ© := fdisj_def.mp this;
        have : Â¬y âŠ§ Ïˆ := by simpa using @hÎ” (â–¡^[n]Ïˆ) (by simpa using hÏˆâ‚);
        contradiction;
      use t;
      constructor;
      . intro Ï† hÏ†;
        apply ht.1;
        exact truthlemmaâ‚.mp hÏ†;
      . apply ih;
        intro Ï† hÏ†;
        have := Set.compl_subset_compl.mpr ht.2 $ iff_not_memâ‚‚_memâ‚.mpr $ truthlemmaâ‚.mpr hÏ†;
        grind;

lemma def_multirel_boxItr_memâ‚ : x â‰º^[n] y â†” ((â–¡'â»Â¹^[n]x.1.1) âŠ† y.1.1) := âŸ¨
  fun h _ hÏ† => truthlemmaâ‚.mpr $ def_multirel_boxItr_satisfies.mp h $ truthlemmaâ‚.mp hÏ†,
  fun h => def_multirel_boxItr_satisfies.mpr fun hÏ† => truthlemmaâ‚.mp (h $ truthlemmaâ‚.mpr hÏ†)
âŸ©

lemma def_multirel_boxItr_memâ‚‚ : x â‰º^[n] y â†” (y.1.2 âŠ† (â–¡'â»Â¹^[n]x.1.2)) := by
  apply Iff.trans def_multirel_boxItr_memâ‚;
  constructor;
  . intro h Ï†;
    contrapose!;
    intro hÏ†;
    apply iff_not_memâ‚‚_memâ‚.mpr;
    apply h;
    apply iff_not_memâ‚‚_memâ‚.mp;
    assumption;
  . intro h Ï†;
    contrapose!;
    intro hÏ†;
    apply iff_not_memâ‚_memâ‚‚.mpr;
    apply h;
    apply iff_not_memâ‚_memâ‚‚.mp;
    assumption;

lemma def_rel_box_memâ‚‚ : x â‰º y â†” (y.1.2 âŠ† (â–¡'â»Â¹ x.1.2)) := by
  simpa using def_multirel_boxItr_memâ‚‚ (n := 1);

lemma def_multirel_diaItr_satisfies : x â‰º^[n] y â†” (âˆ€ {Ï†}, y âŠ§ Ï† â†’ x âŠ§ â—‡^[n]Ï†) := by
  constructor;
  . intro h Ï† hÏ†;
    apply Formula.Kripke.Satisfies.diaItr_def.mpr;
    use y;
  . intro h;
    apply def_multirel_boxItr_satisfies.mpr;
    intro Ï†;
    contrapose;
    intro hÏ†;
    apply Satisfies.not_def.mp;
    have : x âŠ§ âˆ¼â–¡^[n](âˆ¼âˆ¼Ï†) := diaItr_dual.mp $ h (Ï† := âˆ¼Ï†) (Satisfies.not_def.mp hÏ†);
    revert this;
    apply intro_neg_semiequiv;
    apply intro_boxItr_semiequiv;
    intro _ _;
    apply negneg_def.mpr;

lemma def_multirel_diaItr_memâ‚ : x â‰º^[n] y â†” (y.1.1 âŠ† (â–¡'â»Â¹^[n]x.1.1)) := âŸ¨
  fun h _ hÏ† => truthlemmaâ‚.mpr $ def_multirel_diaItr_satisfies.mp h (truthlemmaâ‚.mp hÏ†),
  fun h => def_multirel_diaItr_satisfies.mpr fun hÏ† => truthlemmaâ‚.mp $ h (truthlemmaâ‚.mpr hÏ†)
âŸ©

lemma def_rel_dia_memâ‚ : x â‰º y â†” (y.1.1 âŠ† (â–¡'â»Â¹ x.1.1)) := by
  simpa using def_multirel_diaItr_memâ‚ (n := 1);

lemma def_multirel_diaItr_memâ‚‚ : x â‰º^[n] y â†” ((â—‡'â»Â¹^[n]x.1.2) âŠ† y.1.2) := by
  constructor;
  . intro Rxy Ï†;
    contrapose;
    intro hÏ†;
    apply iff_not_memâ‚‚_memâ‚.mpr;
    apply def_multirel_diaItr_memâ‚.mp Rxy;
    exact iff_not_memâ‚‚_memâ‚.mp hÏ†;
  . intro H;
    apply def_multirel_diaItr_memâ‚.mpr;
    intro Ï†;
    contrapose;
    intro hÏ†;
    exact iff_not_memâ‚_memâ‚‚.mpr $ @H Ï† (iff_not_memâ‚_memâ‚‚.mp hÏ†);

lemma def_rel_dia_memâ‚‚ : x â‰º y â†” ((â—‡'â»Â¹x.1.2) âŠ† y.1.2) := by
  simpa using def_multirel_diaItr_memâ‚‚ (n := 1);

end canonicalModel

end Kripke

end LO.Modal
