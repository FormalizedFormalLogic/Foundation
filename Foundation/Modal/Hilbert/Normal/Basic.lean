import Foundation.Modal.Entailment.GL
import Foundation.Modal.Entailment.Grz
import Foundation.Modal.Entailment.S5Grz
import Foundation.Modal.Entailment.K4Hen
import Foundation.Modal.Logic.Basic


namespace LO.Modal

open LO.Entailment LO.Modal.Entailment

namespace Hilbert

protected structure Normal (Œ±) where
  axioms : Set (Formula Œ±)

namespace Normal

variable {H H‚ÇÅ H‚ÇÇ : Hilbert.Normal Œ±} {œÜ œà : Formula Œ±}

abbrev axiomInstances (H : Hilbert.Normal Œ±) : Set (Formula Œ±) := { œÜ‚ü¶s‚üß | (œÜ ‚àà H.axioms) (s : Substitution Œ±) }

lemma mem_axiomInstances_of_mem_axioms {œÜ} (h : œÜ ‚àà H.axioms) : œÜ ‚àà H.axiomInstances := by
  use œÜ;
  constructor;
  . assumption;
  . use Substitution.id;
    simp;

inductive Deduction (H : Hilbert.Normal Œ±) : (Formula Œ±) ‚Üí Type u
  | axm {œÜ} (s : Substitution _) : œÜ ‚àà H.axioms ‚Üí Deduction H (œÜ‚ü¶s‚üß)
  | mdp {œÜ œà}     : Deduction H (œÜ ‚ûù œà) ‚Üí Deduction H œÜ ‚Üí Deduction H œà
  | nec {œÜ}       : Deduction H œÜ ‚Üí Deduction H (‚ñ°œÜ)
  | imply‚ÇÅ œÜ œà    : Deduction H $ Axioms.Imply‚ÇÅ œÜ œà
  | imply‚ÇÇ œÜ œà œá  : Deduction H $ Axioms.Imply‚ÇÇ œÜ œà œá
  | ec œÜ œà        : Deduction H $ Axioms.ElimContra œÜ œà

instance : Entailment (Formula Œ±) (Hilbert.Normal Œ±) := ‚ü®Deduction‚ü©

def Deduction.axm' {H : Hilbert.Normal Œ±} {œÜ} (h : œÜ ‚àà H.axioms) : Deduction H œÜ := by
  rw [(show œÜ = œÜ‚ü¶.id‚üß by simp)]
  apply Deduction.axm _ h;

section

variable {H : Hilbert.Normal Œ±}

instance : Entailment.Lukasiewicz H where
  mdp := .mdp
  imply‚ÇÅ := .imply‚ÇÅ
  imply‚ÇÇ := .imply‚ÇÇ
  elimContra := .ec
instance : Entailment.Necessitation H where
  nec := .nec

instance : Entailment.DeductiveExplosion (Hilbert.Normal Œ±) where
  dexp := fun h _ ‚Ü¶ of_O h

protected lemma rec!
  {motive   : (œÜ : Formula Œ±) ‚Üí (H ‚ä¢! œÜ) ‚Üí Sort}
  (axm      : ‚àÄ {œÜ : Formula Œ±} (s), (h : œÜ ‚àà H.axioms) ‚Üí motive (œÜ‚ü¶s‚üß) ‚ü®.axm s h‚ü©)
  (mdp      : ‚àÄ {œÜ œà : Formula Œ±}, {hœÜœà : H ‚ä¢! œÜ ‚ûù œà} ‚Üí {hœÜ : H ‚ä¢! œÜ} ‚Üí motive (œÜ ‚ûù œà) hœÜœà ‚Üí motive œÜ hœÜ ‚Üí motive œà (hœÜœà ‚®Ä hœÜ))
  (nec      : ‚àÄ {œÜ}, {hœÜœà : H ‚ä¢! œÜ} ‚Üí motive (œÜ) hœÜœà ‚Üí motive (‚ñ°œÜ) (nec! hœÜœà))
  (imply‚ÇÅ   : ‚àÄ {œÜ œà}, motive (Axioms.Imply‚ÇÅ œÜ œà) $ by simp)
  (imply‚ÇÇ   : ‚àÄ {œÜ œà œá}, motive (Axioms.Imply‚ÇÇ œÜ œà œá) $ by simp)
  (ec       : ‚àÄ {œÜ œà}, motive (Axioms.ElimContra œÜ œà) $ by simp)
  : ‚àÄ {œÜ}, (d : H ‚ä¢! œÜ) ‚Üí motive œÜ d := by
  rintro œÜ ‚ü®d‚ü©;
  induction d with
  | axm s h => apply axm s h;
  | mdp hœÜœà hœÜ ihœÜœà ihœÜ => apply mdp ihœÜœà ihœÜ;
  | nec hœÜœà ihœÜ => apply nec ihœÜ
  | imply‚ÇÅ œÜ œà => apply imply‚ÇÅ
  | imply‚ÇÇ œÜ œà œá => apply imply‚ÇÇ
  | ec œÜ œà => apply ec;

lemma axm! {œÜ} (s) (h : œÜ ‚àà H.axioms) : H ‚ä¢! (œÜ‚ü¶s‚üß) := ‚ü®.axm s h‚ü©

lemma axm'! {œÜ} (h : œÜ ‚àà H.axioms) : H ‚ä¢! œÜ := by simpa using axm! Substitution.id h

lemma subst! {œÜ} (s) (h : H ‚ä¢! œÜ) : H ‚ä¢! (œÜ‚ü¶s‚üß) := by
  induction h using Normal.rec! with
  | mdp ihœÜœà ihœÜ => apply ihœÜœà ‚®Ä ihœÜ;
  | @axm œÜ s' h => rw [(show œÜ‚ü¶s'‚üß‚ü¶s‚üß = œÜ‚ü¶s' ‚àò s‚üß by simp)]; apply axm!; exact h;
  | @nec œÜ h => apply nec!; simpa;
  | _ => simp;

lemma weakerThan_of_provable_axioms (hs : H‚ÇÇ ‚ä¢!* H‚ÇÅ.axioms) : H‚ÇÅ ‚™Ø H‚ÇÇ := by
  apply weakerThan_iff.mpr;
  intro œÜ h;
  induction h using Normal.rec! with
  | @axm œÜ s h => apply subst!; apply @hs œÜ h;
  | @nec œÜ h => apply nec!; simpa;
  | mdp ih‚ÇÅ ih‚ÇÇ => exact ih‚ÇÅ ‚®Ä ih‚ÇÇ;
  | _ => simp;

lemma weakerThan_of_subset_axioms (hs : H‚ÇÅ.axioms ‚äÜ H‚ÇÇ.axioms) : H‚ÇÅ ‚™Ø H‚ÇÇ := by
  apply weakerThan_of_provable_axioms;
  intro œÜ h;
  apply axm'!;
  exact hs h;

end


section

abbrev logic (H : Hilbert.Normal Œ±) : Logic Œ± := Entailment.theory H

@[simp high]
lemma iff_logic_provable_provable : H.logic ‚ä¢! œÜ ‚Üî H ‚ä¢! œÜ := by simp [Entailment.theory, Logic.iff_provable];

instance : Entailment.Lukasiewicz H.logic where
  mdp hœÜœà hœÜ := by
    replace hœÜœà := hœÜœà.1;
    replace hœÜ := hœÜ.1;
    simp_all only [theory, Set.mem_setOf_eq];
    constructor;
    exact hœÜœà ‚®Ä hœÜ;
  imply‚ÇÅ _ _ := by constructor; simp [Entailment.theory];
  imply‚ÇÇ _ _ _ := by constructor; simp [Entailment.theory];
  elimContra _ _ := by constructor; simp [Entailment.theory];

instance : Entailment.Necessitation H.logic where
  nec hœÜ := by
    replace hœÜ := hœÜ.1;
    simp only [theory, Set.mem_setOf_eq];
    constructor;
    exact nec! hœÜ;

instance [H‚ÇÅ ‚™Ø H‚ÇÇ] : H‚ÇÅ.logic ‚™Ø H‚ÇÇ.logic := by
  apply weakerThan_iff.mpr;
  simp only [theory, Logic.iff_provable, Set.mem_setOf_eq];
  apply WeakerThan.wk;
  infer_instance;

instance [H‚ÇÅ ‚™± H‚ÇÇ] : H‚ÇÅ.logic ‚™± H‚ÇÇ.logic := by
  apply strictlyWeakerThan_iff.mpr;
  simp only [theory, Logic.iff_provable, Set.mem_setOf_eq, Logic.iff_unprovable];
  apply strictlyWeakerThan_iff.mp;
  infer_instance;

instance [H‚ÇÅ ‚âä H‚ÇÇ] : H‚ÇÅ.logic ‚âä H‚ÇÇ.logic := by
  apply Equiv.iff.mpr;
  simp only [theory, Logic.iff_provable, Set.mem_setOf_eq];
  apply Equiv.iff.mp;
  infer_instance;

instance [Entailment.Consistent H] : Entailment.Consistent H.logic where
  not_inconsistent := by
    simp only [Inconsistent, iff_logic_provable_provable, not_forall]
    use ‚ä•;
    apply Entailment.Consistent.not_bot;
    infer_instance;

end


section

variable [DecidableEq Œ±]

class HasK (H : Hilbert.Normal Œ±) where
  p : Œ±
  q : Œ±
  ne_pq : p ‚â† q := by trivial;
  mem_K : Axioms.K (.atom p) (.atom q) ‚àà H.axioms := by tauto;

instance [H.HasK] : Entailment.HasAxiomK H where
  K œÜ œà := by
    simpa [HasK.ne_pq] using Deduction.axm
      (œÜ := Axioms.K (.atom (HasK.p H)) (.atom (HasK.q H)))
      (s := Œª b =>
        if (HasK.p H) = b then œÜ
        else if (HasK.q H) = b then œà
        else (.atom b))
      HasK.mem_K;

instance [H.HasK] : H.logic.IsNormal where
  K œÜ œà := by constructor; simp [Entailment.theory];
  subst s hœÜ := by
    replace hœÜ := hœÜ.1;
    constructor;
    simp_all only [theory, Set.mem_setOf_eq];
    apply subst! s hœÜ;


class HasT (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_T : Axioms.T (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasT] : Entailment.HasAxiomT H where
  T œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.T (.atom (HasT.p H)))
      (s := Œª b => if (HasT.p H) = b then œÜ else (.atom b))
      HasT.mem_T;

class HasB (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_B : Axioms.B (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasB] : Entailment.HasAxiomB H where
  B œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.B (.atom (HasB.p H)))
      (s := Œª b => if (HasB.p H) = b then œÜ else (.atom b))
      HasB.mem_B;


class HasD (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_D : Axioms.D (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasD] : Entailment.HasAxiomD H where
  D œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.D (.atom (HasD.p H)))
      (s := Œª b => if (HasD.p H) = b then œÜ else (.atom b))
      HasD.mem_D;


class HasP (H : Hilbert.Normal Œ±) where
  mem_P : Axioms.P ‚àà H.axioms := by tauto;

instance [DecidableEq Œ±] [H.HasP] : Entailment.HasAxiomP H where
  P := by
    simpa using Deduction.axm
      (œÜ := Axioms.P)
      (s := .id)
      HasP.mem_P;


class HasFour (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Four : Axioms.Four (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasFour] : Entailment.HasAxiomFour H where
  Four œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Four (.atom (HasFour.p H)))
      (s := Œª b => if (HasFour.p H) = b then œÜ else (.atom b))
      HasFour.mem_Four;


class HasFive (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Five : Axioms.Five (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasFive] : Entailment.HasAxiomFive H where
  Five œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Five (.atom (HasFive.p H)))
      (s := Œª b => if (HasFive.p H) = b then œÜ else (.atom b))
      HasFive.mem_Five;


class HasPoint2 (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Point2 : Axioms.Point2 (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasPoint2] : Entailment.HasAxiomPoint2 H where
  Point2 œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Point2 (.atom (HasPoint2.p H)))
      (s := Œª b => if (HasPoint2.p H) = b then œÜ else (.atom b))
      HasPoint2.mem_Point2;


class HasWeakPoint2 (H : Hilbert.Normal Œ±) where
  p : Œ±
  q : Œ±
  ne_pq : p ‚â† q := by trivial;
  mem_WeakPoint2 : Axioms.WeakPoint2 (.atom p) (.atom q) ‚àà H.axioms := by tauto;

instance [H.HasWeakPoint2] : Entailment.HasAxiomWeakPoint2 H where
  WeakPoint2 œÜ œà := by
    simpa [HasWeakPoint2.ne_pq] using Deduction.axm
      (œÜ := Axioms.WeakPoint2 (.atom (HasWeakPoint2.p H)) (.atom (HasWeakPoint2.q H)))
      (s := Œª b =>
        if (HasWeakPoint2.p H) = b then œÜ
        else if (HasWeakPoint2.q H) = b then œà
        else (.atom b))
      HasWeakPoint2.mem_WeakPoint2;


class HasPoint3 (H : Hilbert.Normal Œ±) where
  p : Œ±
  q : Œ±
  ne_pq : p ‚â† q := by trivial;
  mem_Point3 : Axioms.Point3 (.atom p) (.atom q) ‚àà H.axioms := by tauto;

instance [H.HasPoint3] : Entailment.HasAxiomPoint3 H where
  Point3 œÜ œà := by
    simpa [HasPoint3.ne_pq] using Deduction.axm
      (œÜ := Axioms.Point3 (.atom (HasPoint3.p H)) (.atom (HasPoint3.q H)))
      (s := Œª b =>
        if (HasPoint3.p H) = b then œÜ
        else if (HasPoint3.q H) = b then œà
        else (.atom b))
      HasPoint3.mem_Point3;


class HasWeakPoint3 (H : Hilbert.Normal Œ±) where
  p : Œ±
  q : Œ±
  ne_pq : p ‚â† q := by trivial;
  mem_WeakPoint3 : Axioms.WeakPoint3 (.atom p) (.atom q) ‚àà H.axioms := by tauto;

instance [H.HasWeakPoint3] : Entailment.HasAxiomWeakPoint3 H where
  WeakPoint3 œÜ œà := by
    simpa [HasWeakPoint3.ne_pq] using Deduction.axm
      (œÜ := Axioms.WeakPoint3 (.atom (HasWeakPoint3.p H)) (.atom (HasWeakPoint3.q H)))
      (s := Œª b =>
        if (HasWeakPoint3.p H) = b then œÜ
        else if (HasWeakPoint3.q H) = b then œà
        else (.atom b))
      HasWeakPoint3.mem_WeakPoint3;


class HasPoint4 (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Point4 : Axioms.Point4 (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasPoint4] : Entailment.HasAxiomPoint4 H where
  Point4 œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Point4 (.atom (HasPoint4.p H)))
      (s := Œª b => if (HasPoint4.p H) = b then œÜ else (.atom b))
      HasPoint4.mem_Point4;


class HasL (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_L : Axioms.L (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasL] : Entailment.HasAxiomL H where
  L œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.L (.atom (HasL.p H)))
      (s := Œª b => if (HasL.p H) = b then œÜ else (.atom b))
      HasL.mem_L;


class HasZ (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Z : Axioms.Z (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasZ] : Entailment.HasAxiomZ H where
  Z œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Z (.atom (HasZ.p H)))
      (s := Œª b => if (HasZ.p H) = b then œÜ else (.atom b))
      HasZ.mem_Z;


class HasGrz (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Grz : Axioms.Grz (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasGrz] : Entailment.HasAxiomGrz H where
  Grz œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Grz (.atom (HasGrz.p H)))
      (s := Œª b => if (HasGrz.p H) = b then œÜ else (.atom b))
      HasGrz.mem_Grz;


class HasDum (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Dum : Axioms.Dum (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasDum] : Entailment.HasAxiomDum H where
  Dum œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Dum (.atom (HasDum.p H)))
      (s := Œª b => if (HasDum.p H) = b then œÜ else (.atom b))
      HasDum.mem_Dum;


class HasTc (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Tc : Axioms.Tc (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasTc] : Entailment.HasAxiomTc H where
  Tc œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Tc (.atom (HasTc.p H)))
      (s := Œª b => if (HasTc.p H) = b then œÜ else (.atom b))
      HasTc.mem_Tc;


class HasVer (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Ver : Axioms.Ver (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasVer] : Entailment.HasAxiomVer H where
  Ver œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Ver (.atom (HasVer.p H)))
      (s := Œª b => if (HasVer.p H) = b then œÜ else (.atom b))
      HasVer.mem_Ver;


class HasHen (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Hen : Axioms.Hen (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasHen] : Entailment.HasAxiomHen H where
  Hen œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Hen (.atom (HasHen.p H)))
      (s := Œª b => if (HasHen.p H) = b then œÜ else (.atom b))
      HasHen.mem_Hen;



class HasMcK (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_M : Axioms.McK (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasMcK] : Entailment.HasAxiomMcK H where
  McK œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.McK (.atom (HasMcK.p H)))
      (s := Œª b => if (HasMcK.p H) = b then œÜ else (.atom b))
      HasMcK.mem_M;


class HasMk (H : Hilbert.Normal Œ±) where
  p : Œ±
  q : Œ±
  ne_pq : p ‚â† q := by trivial;
  mem_Mk : Axioms.Mk (.atom p) (.atom q) ‚àà H.axioms := by tauto;

instance [H.HasMk] : Entailment.HasAxiomMk H where
  Mk œÜ œà := by
    simpa [HasMk.ne_pq] using Deduction.axm
      (œÜ := Axioms.Mk (.atom (HasMk.p H)) (.atom (HasMk.q H)))
      (s := Œª b =>
        if (HasMk.p H) = b then œÜ
        else if (HasMk.q H) = b then œà
        else (.atom b))
      HasMk.mem_Mk;


class HasGeach (g) (H : Hilbert.Normal Œ±) where
  p : Œ±
  mem_Geach : Axioms.Geach g (.atom p) ‚àà H.axioms := by tauto;

instance [H.HasGeach g] : Entailment.HasAxiomGeach g H where
  Geach œÜ := by
    simpa using Deduction.axm
      (œÜ := Axioms.Geach g (.atom (HasGeach.p g H)))
      (s := Œª b => if (HasGeach.p g H) = b then œÜ else (.atom b))
      HasGeach.mem_Geach;

end

end Normal

end Hilbert


section

variable {H : Hilbert.Normal ‚Ñï} {L : Logic ‚Ñï}

open Formula (atom)
open Hilbert.Normal (weakerThan_of_subset_axioms weakerThan_of_provable_axioms)

protected abbrev Hilbert.K : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1)}‚ü©
protected abbrev K := Hilbert.K.logic
instance : Hilbert.K.HasK where p := 0; q := 1
instance : Entailment.K (Hilbert.K) where

instance [L.IsNormal] : Modal.K ‚™Ø L := by
  constructor;
  intro œÜ;
  suffices Hilbert.K ‚ä¢! œÜ ‚Üí L ‚ä¢! œÜ by simpa [theory, Set.mem_setOf_eq, Set.setOf_mem_eq];
  intro hœÜ;
  induction hœÜ using Hilbert.Normal.rec! with
  | axm s h => rcases h with rfl; simp;
  | nec hœÜ => apply nec! hœÜ;
  | mdp hœÜœà hœÜ => exact mdp! hœÜœà hœÜ
  | imply‚ÇÅ | imply‚ÇÇ | ec => simp;


protected abbrev Hilbert.KT : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0)}‚ü©
protected abbrev KT := Hilbert.KT.logic
instance : (Hilbert.KT).HasK where p := 0; q := 1;
instance : (Hilbert.KT).HasT where p := 0
instance : Entailment.KT (Hilbert.KT) where


protected abbrev Hilbert.KD : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0)}‚ü©
protected abbrev KD := Hilbert.KD.logic
instance : (Hilbert.KD).HasK where p := 0; q := 1;
instance : (Hilbert.KD).HasD where p := 0
instance : Entailment.KD (Hilbert.KD) where


protected abbrev Hilbert.KP : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.P}‚ü©
protected abbrev KP : Logic ‚Ñï := Hilbert.KP.logic
instance : Hilbert.KP.HasK where p := 0; q := 1
instance : Hilbert.KP.HasP where
instance : Entailment.KP (Hilbert.KP) where


instance : Hilbert.KP ‚âä Hilbert.KD := by
  apply Entailment.Equiv.antisymm_iff.mpr;
  constructor;
  . apply weakerThan_of_provable_axioms; rintro œÜ (rfl | rfl) <;> simp;
  . apply weakerThan_of_provable_axioms; rintro œÜ (rfl | rfl) <;> simp;

instance : Modal.KP ‚âä Modal.KD := inferInstance


protected abbrev Hilbert.KB : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.B (.atom 0)}‚ü©
protected abbrev KB := Hilbert.KB.logic
instance : (Hilbert.KB).HasK where p := 0; q := 1;
instance : (Hilbert.KB).HasB where p := 0
instance : Entailment.KB (Hilbert.KB) where


protected abbrev Hilbert.KDB : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0), Axioms.B (.atom 0)}‚ü©
protected abbrev KDB := Hilbert.KDB.logic
instance : (Hilbert.KDB).HasK where p := 0; q := 1;
instance : (Hilbert.KDB).HasD where p := 0
instance : (Hilbert.KDB).HasB where p := 0
instance : Entailment.KDB (Hilbert.KDB) where


protected abbrev Hilbert.KTB : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.B (.atom 0)}‚ü©
protected abbrev KTB := Hilbert.KTB.logic
instance : (Hilbert.KTB).HasK where p := 0; q := 1;
instance : (Hilbert.KTB).HasT where p := 0
instance : (Hilbert.KTB).HasB where p := 0
instance : Entailment.KTB (Hilbert.KTB) where


protected abbrev Hilbert.KMcK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.McK (.atom 0)}‚ü©
protected abbrev KMcK := Hilbert.KMcK.logic
instance : (Hilbert.KMcK).HasK where p := 0; q := 1;
instance : (Hilbert.KMcK).HasMcK where p := 0
instance : Entailment.KMcK (Hilbert.KMcK) where
instance : Hilbert.K ‚™Ø Hilbert.KMcK := weakerThan_of_subset_axioms $ by simp;


protected abbrev Hilbert.K4 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0)}‚ü©
protected abbrev K4 := Hilbert.K4.logic
instance : (Hilbert.K4).HasK where p := 0; q := 1;
instance : (Hilbert.K4).HasFour where p := 0
instance : Entailment.K4 (Hilbert.K4) where


protected abbrev Hilbert.K4McK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.McK (.atom 0)}‚ü©
protected abbrev K4McK := Hilbert.K4McK.logic
instance : (Hilbert.K4McK).HasK where p := 0; q := 1;
instance : (Hilbert.K4McK).HasFour where p := 0
instance : (Hilbert.K4McK).HasMcK where p := 0
instance : Entailment.K4McK (Hilbert.K4McK) where

instance : Hilbert.K ‚™Ø Hilbert.K4McK := weakerThan_of_subset_axioms $ by simp;

noncomputable instance [Entailment.K H] [Hilbert.K4McK ‚™Ø H] : Entailment.K4McK H where
  Four _ := Entailment.WeakerThan.pbl (ùì¢ := Hilbert.K4McK) (by simp) |>.some
  McK _ := Entailment.WeakerThan.pbl (ùì¢ := Hilbert.K4McK) (by simp) |>.some



protected abbrev Hilbert.K4Point2 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.WeakPoint2 (.atom 0) (.atom 1)}‚ü©
protected abbrev K4Point2 := Hilbert.K4Point2.logic
instance : (Hilbert.K4Point2).HasK where p := 0; q := 1;
instance : (Hilbert.K4Point2).HasFour where p := 0
instance : (Hilbert.K4Point2).HasWeakPoint2 where p := 0; q := 1;
instance : Entailment.K4Point2 (Hilbert.K4Point2) where

protected abbrev Hilbert.K4Point3 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.WeakPoint3 (.atom 0) (.atom 1)}‚ü©
protected abbrev K4Point3 := Hilbert.K4Point3.logic
instance : (Hilbert.K4Point3).HasK where p := 0; q := 1;
instance : (Hilbert.K4Point3).HasFour where p := 0
instance : (Hilbert.K4Point3).HasWeakPoint3 where p := 0; q := 1;
instance : Entailment.K4Point3 (Hilbert.K4Point3) where


protected abbrev Hilbert.KT4B : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.B (.atom 0)}‚ü©
protected abbrev KT4B := Hilbert.KT4B.logic
instance : (Hilbert.KT4B).HasK where p := 0; q := 1;
instance : (Hilbert.KT4B).HasT where p := 0
instance : (Hilbert.KT4B).HasFour where p := 0
instance : (Hilbert.KT4B).HasB where p := 0
instance : Entailment.KT4B (Hilbert.KT4B) where


protected abbrev Hilbert.K45 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.Five (.atom 0)}‚ü©
protected abbrev K45 := Hilbert.K45.logic
instance : (Hilbert.K45).HasK where p := 0; q := 1;
instance : (Hilbert.K45).HasFour where p := 0
instance : (Hilbert.K45).HasFive where p := 0
instance : Entailment.K45 (Hilbert.K45) where


protected abbrev Hilbert.KD4 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0), Axioms.Four (.atom 0)}‚ü©
protected abbrev KD4 := Hilbert.KD4.logic
instance : (Hilbert.KD4).HasK where p := 0; q := 1;
instance : (Hilbert.KD4).HasD where p := 0
instance : (Hilbert.KD4).HasFour where p := 0
instance : Entailment.KD4 (Hilbert.KD4) where


protected abbrev Hilbert.KD5 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0), Axioms.Five (.atom 0)}‚ü©
protected abbrev KD5 := Hilbert.KD5.logic
instance : (Hilbert.KD5).HasK where p := 0; q := 1;
instance : (Hilbert.KD5).HasD where p := 0
instance : (Hilbert.KD5).HasFive where p := 0
instance : Entailment.KD5 (Hilbert.KD5) where


protected abbrev Hilbert.KD45 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0), Axioms.Four (.atom 0), Axioms.Five (.atom 0)}‚ü©
protected abbrev KD45 := Hilbert.KD45.logic
instance : (Hilbert.KD45).HasK where p := 0; q := 1;
instance : (Hilbert.KD45).HasD where p := 0
instance : (Hilbert.KD45).HasFour where p := 0
instance : (Hilbert.KD45).HasFive where p := 0
instance : Entailment.KD45 (Hilbert.KD45) where


protected abbrev Hilbert.KB4 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.B (.atom 0), Axioms.Four (.atom 0)}‚ü©
protected abbrev KB4 := Hilbert.KB4.logic
instance : (Hilbert.KB4).HasK where p := 0; q := 1;
instance : (Hilbert.KB4).HasB where p := 0
instance : (Hilbert.KB4).HasFour where p := 0
instance : Entailment.KB4 (Hilbert.KB4) where


protected abbrev Hilbert.KB5 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.B (.atom 0), Axioms.Five (.atom 0)}‚ü©
protected abbrev KB5 := Hilbert.KB5.logic
instance : (Hilbert.KB5).HasK where p := 0; q := 1;
instance : (Hilbert.KB5).HasB where p := 0
instance : (Hilbert.KB5).HasFive where p := 0
instance : Entailment.KB5 (Hilbert.KB5) where


protected abbrev Hilbert.S4 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0)}‚ü©
protected abbrev S4 := Hilbert.S4.logic
instance : (Hilbert.S4).HasK where p := 0; q := 1;
instance : (Hilbert.S4).HasT where p := 0
instance : (Hilbert.S4).HasFour where p := 0
instance : Entailment.S4 (Hilbert.S4) where
instance : Hilbert.K4 ‚™Ø Hilbert.S4 := weakerThan_of_subset_axioms $ by simp;


protected abbrev Hilbert.S4McK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.McK (.atom 0)}‚ü©
protected abbrev S4McK := Hilbert.S4McK.logic
instance : (Hilbert.S4McK).HasK where p := 0; q := 1;
instance : (Hilbert.S4McK).HasT where p := 0
instance : (Hilbert.S4McK).HasFour where p := 0
instance : (Hilbert.S4McK).HasMcK where p := 0
instance : Entailment.S4McK (Hilbert.S4McK) where
instance : Hilbert.K4McK ‚™Ø Hilbert.S4McK := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.S4Point2McK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.McK (.atom 0), Axioms.Point2 (.atom 0)}‚ü©
protected abbrev S4Point2McK := Hilbert.S4Point2McK.logic
instance : (Hilbert.S4Point2McK).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point2McK).HasT where p := 0
instance : (Hilbert.S4Point2McK).HasFour where p := 0
instance : (Hilbert.S4Point2McK).HasMcK where p := 0
instance : (Hilbert.S4Point2McK).HasPoint2 where p := 0
instance : Entailment.S4Point2McK (Hilbert.S4Point2McK) where
instance : Hilbert.K4McK ‚™Ø Hilbert.S4Point2McK := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.S4Point3McK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.McK (.atom 0), Axioms.Point3 (.atom 0) (.atom 1)}‚ü©
protected abbrev S4Point3McK := Hilbert.S4Point3McK.logic
instance : (Hilbert.S4Point3McK).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point3McK).HasT where p := 0
instance : (Hilbert.S4Point3McK).HasFour where p := 0
instance : (Hilbert.S4Point3McK).HasMcK where p := 0
instance : (Hilbert.S4Point3McK).HasPoint3 where p := 0; q := 1;
instance : Entailment.S4Point3McK (Hilbert.S4Point3McK) where
instance : Hilbert.K4McK ‚™Ø Hilbert.S4Point3McK := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.S4Point4McK : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.McK (.atom 0), Axioms.Point4 (.atom 0)}‚ü©
protected abbrev S4Point4McK := Hilbert.S4Point4McK.logic
instance : (Hilbert.S4Point4McK).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point4McK).HasT where p := 0
instance : (Hilbert.S4Point4McK).HasFour where p := 0
instance : (Hilbert.S4Point4McK).HasMcK where p := 0
instance : (Hilbert.S4Point4McK).HasPoint4 where p := 0
instance : Entailment.S4Point4McK (Hilbert.S4Point4McK) where
instance : Hilbert.K4McK ‚™Ø Hilbert.S4Point4McK := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.S4Point2 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Point2 (.atom 0)}‚ü©
protected abbrev S4Point2 := Hilbert.S4Point2.logic
instance : (Hilbert.S4Point2).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point2).HasT where p := 0
instance : (Hilbert.S4Point2).HasFour where p := 0
instance : (Hilbert.S4Point2).HasPoint2 where p := 0
instance : Entailment.S4Point2 (Hilbert.S4Point2) where


protected abbrev Hilbert.S4Point3 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Point3 (.atom 0) (.atom 1)}‚ü©
protected abbrev S4Point3 := Hilbert.S4Point3.logic
instance : (Hilbert.S4Point3).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point3).HasT where p := 0
instance : (Hilbert.S4Point3).HasFour where p := 0
instance : (Hilbert.S4Point3).HasPoint3 where p := 0; q := 1;
instance : Entailment.S4Point3 (Hilbert.S4Point3) where


protected abbrev Hilbert.S4Point4 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Point4 (.atom 0)}‚ü©
protected abbrev S4Point4 := Hilbert.S4Point4.logic
instance : (Hilbert.S4Point4).HasK where p := 0; q := 1;
instance : (Hilbert.S4Point4).HasT where p := 0
instance : (Hilbert.S4Point4).HasFour where p := 0
instance : (Hilbert.S4Point4).HasPoint4 where p := 0
instance : Entailment.S4Point4 (Hilbert.S4Point4) where


protected abbrev Hilbert.K5 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Five (.atom 0)}‚ü©
protected abbrev K5 := Hilbert.K5.logic
instance : (Hilbert.K5).HasK where p := 0; q := 1;
instance : (Hilbert.K5).HasFive where p := 0
instance : Entailment.K5 (Hilbert.K5) where


protected abbrev Hilbert.S5 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Five (.atom 0)}‚ü©
protected abbrev S5 := Hilbert.S5.logic
instance : (Hilbert.S5).HasK where p := 0; q := 1;
instance : (Hilbert.S5).HasT where p := 0
instance : (Hilbert.S5).HasFive where p := 0
instance : Entailment.S5 (Hilbert.S5) where


protected abbrev Hilbert.GL : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.L (.atom 0)}‚ü©
protected abbrev GL := Hilbert.GL.logic
instance : (Hilbert.GL).HasK where p := 0; q := 1;
instance : (Hilbert.GL).HasL where p := 0;
instance : Entailment.GL (Hilbert.GL) where


protected abbrev Hilbert.GLPoint2 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.L (.atom 0), Axioms.WeakPoint2 (.atom 0) (.atom 1)}‚ü©
protected abbrev GLPoint2 := Hilbert.GLPoint2.logic
instance : (Hilbert.GLPoint2).HasK where p := 0; q := 1;
instance : (Hilbert.GLPoint2).HasL where p := 0
instance : (Hilbert.GLPoint2).HasWeakPoint2 where p := 0; q := 1;
instance : Entailment.GLPoint2 (Hilbert.GLPoint2) where
instance : Hilbert.GL ‚™Ø Hilbert.GLPoint2 := weakerThan_of_subset_axioms $ by simp


protected abbrev Hilbert.GLPoint3 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.L (.atom 0), Axioms.WeakPoint3 (.atom 0) (.atom 1)}‚ü©
protected abbrev GLPoint3 := Hilbert.GLPoint3.logic
instance : (Hilbert.GLPoint3).HasK where p := 0; q := 1;
instance : (Hilbert.GLPoint3).HasL where p := 0
instance : (Hilbert.GLPoint3).HasWeakPoint3 where p := 0; q := 1;
instance : Entailment.GLPoint3 (Hilbert.GLPoint3) where


protected abbrev Hilbert.K4Z : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.Z (.atom 0)}‚ü©
protected abbrev K4Z := Hilbert.K4Z.logic
instance : (Hilbert.K4Z).HasK where p := 0; q := 1;
instance : (Hilbert.K4Z).HasFour where p := 0
instance : (Hilbert.K4Z).HasZ where p := 0
instance : Entailment.K4Z (Hilbert.K4Z) where
instance : Hilbert.K4 ‚™Ø Hilbert.K4Z := weakerThan_of_subset_axioms $ by simp
instance : Hilbert.K4Z ‚™Ø Hilbert.GL := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.K4Point2Z : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.Z (.atom 0), Axioms.WeakPoint2 (.atom 0) (.atom 1)}‚ü©
protected abbrev K4Point2Z := Hilbert.K4Point2Z.logic
instance : (Hilbert.K4Point2Z).HasK where p := 0; q := 1;
instance : (Hilbert.K4Point2Z).HasFour where p := 0
instance : (Hilbert.K4Point2Z).HasZ where p := 0
instance : (Hilbert.K4Point2Z).HasWeakPoint2 where p := 0; q := 1;
instance : Entailment.K4Point2Z (Hilbert.K4Point2Z) where
instance : Hilbert.K4Point2 ‚™Ø Hilbert.K4Point2Z := weakerThan_of_subset_axioms (by simp)
instance : Hilbert.K4Z ‚™Ø Hilbert.K4Point2Z := weakerThan_of_subset_axioms (by simp)
instance : Hilbert.K4Point2Z ‚™Ø Hilbert.GLPoint2 := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.K4Point3Z : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.Z (.atom 0), Axioms.WeakPoint3 (.atom 0) (.atom 1)}‚ü©
protected abbrev K4Point3Z := Hilbert.K4Point3Z.logic
instance : (Hilbert.K4Point3Z).HasK where p := 0; q := 1;
instance : (Hilbert.K4Point3Z).HasFour where p := 0
instance : (Hilbert.K4Point3Z).HasZ where p := 0
instance : (Hilbert.K4Point3Z).HasWeakPoint3 where p := 0; q := 1;
instance : Entailment.K4Point3Z (Hilbert.K4Point3Z) where
instance : Hilbert.K4Point3 ‚™Ø Hilbert.K4Point3Z := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;
instance : Hilbert.K4Z ‚™Ø Hilbert.K4Point3Z := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;
instance : Hilbert.K4Point3Z ‚™Ø Hilbert.GLPoint3 := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.KHen : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Hen (.atom 0)}‚ü©
protected abbrev KHen := Hilbert.KHen.logic
instance : (Hilbert.KHen).HasK where p := 0; q := 1;
instance : (Hilbert.KHen).HasHen where p := 0;


protected abbrev Hilbert.K4Hen : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Four (.atom 0), Axioms.Hen (.atom 0)}‚ü©
protected abbrev Logic.K4Hen := Hilbert.K4Hen.logic
instance : (Hilbert.K4Hen).HasK where p := 0; q := 1;
instance : (Hilbert.K4Hen).HasFour where p := 0
instance : (Hilbert.K4Hen).HasHen where p := 0
instance : Entailment.K4Hen (Hilbert.K4Hen) where


protected abbrev Hilbert.Grz : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Grz (.atom 0)}‚ü©
protected abbrev Grz := Hilbert.Grz.logic
instance : (Hilbert.Grz).HasK where p := 0; q := 1;
instance : (Hilbert.Grz).HasGrz where p := 0
instance : Entailment.Grz (Hilbert.Grz) where
instance : Hilbert.KT ‚™Ø Hilbert.Grz := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl) <;> simp;


protected abbrev Hilbert.GrzPoint2 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Grz (.atom 0), Axioms.Point2 (.atom 0)}‚ü©
protected abbrev GrzPoint2 := Hilbert.GrzPoint2.logic
instance : (Hilbert.GrzPoint2).HasK where p := 0; q := 1;
instance : (Hilbert.GrzPoint2).HasGrz where p := 0
instance : (Hilbert.GrzPoint2).HasPoint2 where p := 0
instance : Entailment.GrzPoint2 (Hilbert.GrzPoint2) where


protected abbrev Hilbert.GrzPoint3 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Grz (.atom 0), Axioms.Point3 (.atom 0) (.atom 1)}‚ü©
protected abbrev GrzPoint3 := Hilbert.GrzPoint3.logic
instance : (Hilbert.GrzPoint3).HasK where p := 0; q := 1;
instance : (Hilbert.GrzPoint3).HasGrz where p := 0
instance : (Hilbert.GrzPoint3).HasPoint3 where p := 0; q := 1;
instance : Entailment.GrzPoint3 (Hilbert.GrzPoint3) where


protected abbrev Hilbert.Dum : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Dum (.atom 0)}‚ü©
protected abbrev Dum := Hilbert.Dum.logic
instance : (Hilbert.Dum).HasK where p := 0; q := 1;
instance : (Hilbert.Dum).HasT where p := 0
instance : (Hilbert.Dum).HasFour where p := 0
instance : (Hilbert.Dum).HasDum where p := 0
instance : Entailment.Dum (Hilbert.Dum) where
instance : Hilbert.S4 ‚™Ø Hilbert.Dum := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl) <;> simp;
instance : Hilbert.Dum ‚™Ø Hilbert.Grz := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.DumPoint2 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Dum (.atom 0), Axioms.Point2 (.atom 0)}‚ü©
protected abbrev DumPoint2 := Hilbert.DumPoint2.logic
instance : (Hilbert.DumPoint2).HasK where p := 0; q := 1;
instance : (Hilbert.DumPoint2).HasT where p := 0
instance : (Hilbert.DumPoint2).HasFour where p := 0
instance : (Hilbert.DumPoint2).HasDum where p := 0
instance : (Hilbert.DumPoint2).HasPoint2 where p := 0
instance : Entailment.DumPoint2 (Hilbert.DumPoint2) where
instance : Hilbert.Dum ‚™Ø Hilbert.DumPoint2 := weakerThan_of_subset_axioms (by simp)
instance : Hilbert.S4Point2 ‚™Ø Hilbert.DumPoint2 := weakerThan_of_subset_axioms (by simp)
instance : Hilbert.DumPoint2 ‚™Ø Hilbert.GrzPoint2 := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.DumPoint3 : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Four (.atom 0), Axioms.Dum (.atom 0), Axioms.Point3 (.atom 0) (.atom 1)}‚ü©
protected abbrev DumPoint3 := Hilbert.DumPoint3.logic
instance : (Hilbert.DumPoint3).HasK where p := 0; q := 1;
instance : (Hilbert.DumPoint3).HasT where p := 0
instance : (Hilbert.DumPoint3).HasFour where p := 0
instance : (Hilbert.DumPoint3).HasDum where p := 0
instance : (Hilbert.DumPoint3).HasPoint3 where p := 0; q := 1;
instance : Entailment.DumPoint3 (Hilbert.DumPoint3) where
instance : Hilbert.Dum ‚™Ø Hilbert.DumPoint3 := weakerThan_of_subset_axioms (by simp)
instance : Hilbert.S4Point3 ‚™Ø Hilbert.DumPoint3 := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl | rfl) <;> simp;
instance : Hilbert.DumPoint3 ‚™Ø Hilbert.GrzPoint3 := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl | rfl | rfl | rfl) <;> simp;


protected abbrev Hilbert.KTc : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Tc (.atom 0)}‚ü©
protected abbrev KTc := Hilbert.KTc.logic
instance : (Hilbert.KTc).HasK where p := 0; q := 1;
instance : (Hilbert.KTc).HasTc where p := 0
instance : Entailment.KTc (Hilbert.KTc) where


protected abbrev Hilbert.KD4Point3Z : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.D (.atom 0), Axioms.Four (.atom 0), Axioms.WeakPoint3 (.atom 0) (.atom 1), Axioms.Z (.atom 0)}‚ü©
protected abbrev KD4Point3Z := Hilbert.KD4Point3Z.logic
instance : (Hilbert.KD4Point3Z).HasK where p := 0; q := 1;
instance : (Hilbert.KD4Point3Z).HasD where p := 0
instance : (Hilbert.KD4Point3Z).HasFour where p := 0
instance : (Hilbert.KD4Point3Z).HasWeakPoint3 where p := 0; q := 1;
instance : (Hilbert.KD4Point3Z).HasZ where p := 0
instance : Entailment.KD4Point3Z (Hilbert.KD4Point3Z) where


protected abbrev Hilbert.KTMk : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Mk (.atom 0) (.atom 1)}‚ü©
protected abbrev KTMk := Hilbert.KTMk.logic
instance : (Hilbert.KTMk).HasK where p := 0; q := 1;
instance : (Hilbert.KTMk).HasT where p := 0
instance : (Hilbert.KTMk).HasMk where p := 0; q := 1
instance : Entailment.KTMk (Hilbert.KTMk) where


protected abbrev Hilbert.N : Hilbert.Normal ‚Ñï := ‚ü®{}‚ü©
protected abbrev N := Hilbert.N.logic


protected abbrev Hilbert.Ver : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.Ver (.atom 0)}‚ü©
protected abbrev Ver := Hilbert.Ver.logic
instance : (Hilbert.Ver).HasK where p := 0; q := 1;
instance : (Hilbert.Ver).HasVer where p := 0
instance : Entailment.Ver (Hilbert.Ver) where


protected abbrev Hilbert.Triv : Hilbert.Normal ‚Ñï := ‚ü®{ Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Tc (.atom 0)}‚ü©
protected abbrev Triv := Hilbert.Triv.logic
instance : (Hilbert.Triv).HasK where p := 0; q := 1;
instance : (Hilbert.Triv).HasT where p := 0
instance : (Hilbert.Triv).HasTc where p := 0
instance : Entailment.Triv (Hilbert.Triv) where
instance : Hilbert.K4 ‚™Ø Hilbert.Triv := weakerThan_of_provable_axioms $ by rintro œÜ (rfl | rfl) <;> simp;


protected abbrev Hilbert.S5Grz : Hilbert.Normal ‚Ñï := ‚ü®{Axioms.K (.atom 0) (.atom 1), Axioms.T (.atom 0), Axioms.Five (.atom 0), Axioms.Grz (.atom 0)}‚ü©
protected abbrev S5Grz : Logic ‚Ñï := Hilbert.S5Grz.logic
instance : (Hilbert.S5Grz).HasK where p := 0; q := 1;
instance : (Hilbert.S5Grz).HasT where p := 0
instance : (Hilbert.S5Grz).HasFive where p := 0
instance : (Hilbert.S5Grz).HasGrz where p := 0
instance : Entailment.S5Grz (Hilbert.S5Grz) where

instance : Hilbert.S5Grz ‚âä Hilbert.Triv := by
  apply Entailment.Equiv.antisymm_iff.mpr;
  constructor;
  . apply weakerThan_of_provable_axioms; rintro œÜ (rfl | rfl | rfl | rfl) <;> simp;
  . apply weakerThan_of_provable_axioms; rintro œÜ (rfl | rfl | rfl) <;> simp;
instance : Modal.S5Grz ‚âä Modal.Triv := inferInstance

end

end LO.Modal
