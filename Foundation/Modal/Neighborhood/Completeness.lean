import Foundation.Modal.MaximalConsistentSet
import Foundation.Modal.Neighborhood.Basic

namespace LO.Modal

open LO.Entailment LO.Modal.Entailment

section

variable {Î± : Type*} [DecidableEq Î±]
variable {S} [Entailment (Formula Î±) S]
variable {ğ“¢ : S} [Entailment.Cl ğ“¢]

def MaximalConsistentSet.proofset (ğ“¢ : S) (Ï† : Formula Î±) : Set (MaximalConsistentSet ğ“¢) := { Î“ : MaximalConsistentSet ğ“¢ | Ï† âˆˆ Î“ }

namespace MaximalConsistentSet.proofset

local notation "â€–" Ï† "â€–" => MaximalConsistentSet.proofset ğ“¢ Ï†

variable {Ï† Ïˆ : Formula Î±}

lemma eq_top : â€–âŠ¤â€– = Set.univ := by simp [proofset];

lemma eq_bot : â€–âŠ¥â€– = âˆ… := by simp [proofset];

lemma eq_neg : â€–âˆ¼Ï†â€– = â€–Ï†â€–á¶œ := by simp [proofset]; tauto;

lemma eq_imp : â€–Ï† â Ïˆâ€– = (â€–Ï†â€–á¶œ âˆª â€–Ïˆâ€–) := by
  ext;
  simp [proofset];
  tauto;

lemma eq_and : â€–Ï† â‹ Ïˆâ€– = â€–Ï†â€– âˆ© â€–Ïˆâ€– := by simp [proofset]; tauto;

lemma eq_or : â€–Ï† â‹ Ïˆâ€– = â€–Ï†â€– âˆª â€–Ïˆâ€– := by simp [proofset]; tauto;

attribute [simp, grind]
  eq_top
  eq_bot
  eq_neg
  eq_imp
  eq_and
  eq_or

@[grind]
lemma imp_subset : ğ“¢ âŠ¢! Ï† â Ïˆ â†” â€–Ï†â€– âŠ† â€–Ïˆâ€– := by
  constructor;
  . intro h Î“;
    apply iff_mem_imp.mp $ iff_forall_mem_provable.mpr h Î“;
  . intro h;
    apply iff_forall_mem_provable.mp;
    intro Î“;
    apply iff_mem_imp.mpr $ @h Î“;

@[grind]
lemma iff_subset : ğ“¢ âŠ¢! Ï† â­¤ Ïˆ â†” â€–Ï†â€– = â€–Ïˆâ€– := by
  constructor;
  . intro h;
    apply Set.eq_of_subset_of_subset <;>
    . apply imp_subset.mp;
      cl_prover [h];
  . intro h;
    have âŸ¨hâ‚, hâ‚‚âŸ© := Set.Subset.antisymm_iff.mp h;
    replace hâ‚ := imp_subset.mpr hâ‚;
    replace hâ‚‚ := imp_subset.mpr hâ‚‚;
    cl_prover [hâ‚, hâ‚‚];

lemma eq_boxed_of_eq [Entailment.E ğ“¢] : â€–Ï†â€– = â€–Ïˆâ€– â†’ â€–â–¡Ï†â€– = â€–â–¡Ïˆâ€– := by
  intro h;
  apply iff_subset.mp;
  apply re!;
  apply iff_subset.mpr;
  assumption;

end MaximalConsistentSet.proofset

end


section

namespace Neighborhood

open Formula (atom)
open Formula.Neighborhood
open MaximalConsistentSet

variable {S} [Entailment (Formula â„•) S]
variable {ğ“¢ : S} [Entailment.Cl ğ“¢] [Entailment.Consistent ğ“¢]
variable {Ï† Ïˆ Î¾ : Formula â„•}

structure CanonicalBox (ğ“¢ : S) where
  box : Set (MaximalConsistentSet ğ“¢) â†’ Set (MaximalConsistentSet ğ“¢)
  canonicity : âˆ€ Ï†, box (proofset ğ“¢ Ï†) = proofset ğ“¢ (â–¡Ï†)

instance : CoeFun (CanonicalBox ğ“¢) (fun _ => Set (MaximalConsistentSet ğ“¢) â†’ Set (MaximalConsistentSet ğ“¢)) := âŸ¨CanonicalBox.boxâŸ©

def mkCanonicalFrame
  (ğ“¢ : S) [Entailment.Consistent ğ“¢] [Entailment.Cl ğ“¢]
  (box : CanonicalBox ğ“¢)
  : Frame := Frame.mk_â„¬ (MaximalConsistentSet ğ“¢) box

def mkCanonicalModel
  (ğ“¢ : S) [Entailment.Consistent ğ“¢] [Entailment.Cl ğ“¢]
  (box : CanonicalBox ğ“¢)
  : Model where
  toFrame := mkCanonicalFrame ğ“¢ box
  Val a := proofset ğ“¢ (.atom a)

@[simp] lemma mkCanonicalModel.eq_â„¬_self : (mkCanonicalModel ğ“¢ box).box = box := by tauto;

lemma truthlemma : â†‘(proofset ğ“¢ Ï†) = ((mkCanonicalModel ğ“¢ box).truthset Ï†) := by
  induction Ï† with
  | hatom =>
    simp [mkCanonicalModel]
  | hfalsum =>
    simp [mkCanonicalModel]
  | himp Ï† Ïˆ ihÏ† ihÏˆ =>
    simp_all [MaximalConsistentSet.proofset.eq_imp, â†ihÏ†, â†ihÏˆ];
  | hbox Ï† ihÏ† =>
    rw [Model.truthset.eq_box, â†ihÏ†, mkCanonicalModel.eq_â„¬_self, (@box.canonicity Ï†)];

lemma complete_of_canonical_frame
  (C : FrameClass) (box)
  (hC : (mkCanonicalFrame ğ“¢ box) âˆˆ C)
  : LO.Complete ğ“¢ C := by
  constructor;
  intro Ï†;
  contrapose!;
  intro hÏ†;
  have := FormulaSet.unprovable_iff_singleton_neg_consistent.mpr hÏ†;
  obtain âŸ¨Î“, hÎ“âŸ© := lindenbaum this;
  apply not_validOnFrameClass_of_exists_model_world;
  use (mkCanonicalModel ğ“¢ box), Î“;
  constructor;
  . assumption;
  . simp only [Semantics.Realize, Satisfies, â†truthlemma];
    suffices Î“ âˆˆ (proofset ğ“¢ (âˆ¼Ï†)) by simpa;
    apply hÎ“;
    tauto;

open Classical in
def minimal_canonical_box (ğ“¢ : S) [Entailment.E ğ“¢] : CanonicalBox ğ“¢ where
  box Î“ := if h : âˆƒ Ï†, Î“ = (proofset ğ“¢ Ï†) then (proofset ğ“¢ (â–¡(h.choose))) else âˆ…
  canonicity := by
    intro Ï†;
    split;
    . rename_i h;
      apply proofset.eq_boxed_of_eq;
      apply h.choose_spec.symm;
    . tauto;

namespace minimal_canonical_box

variable {ğ“¢ : S} [Entailment.E ğ“¢] [Consistent ğ“¢]

lemma exists_box (X) (Î“ : (mkCanonicalFrame ğ“¢ (minimal_canonical_box ğ“¢)).World) (hÎ“ : Î“ âˆˆ â„¬ X)
  : âˆƒ Ï†, X = proofset ğ“¢ Ï† âˆ§ â„¬ X = proofset ğ“¢ (â–¡Ï†)
  := by
    simp [mkCanonicalFrame, Frame.mk_â„¬, minimal_canonical_box] at hÎ“;
    split at hÎ“;
    . rename_i h;
      obtain âŸ¨Ï†, hÏ†âŸ© := h;
      use Ï†;
      constructor;
      . assumption;
      . convert minimal_canonical_box ğ“¢ |>.canonicity Ï†;
    . contradiction;

lemma exists_dia (X) (Î“ : (mkCanonicalFrame ğ“¢ (minimal_canonical_box ğ“¢)).World) (hÎ“ : Î“ âˆˆ â„¬ X)
  : âˆƒ Ï†, X = proofset ğ“¢ Ï† âˆ§ ğ’Ÿ X = proofset ğ“¢ (â—‡Ï†)
  := by
    obtain âŸ¨Ï†, hÏ†, hÎ“âŸ© := exists_box X Î“ hÎ“;
    use Ï†;
    constructor;
    . assumption;
    . ext Î“;
      rw [(show â—‡Ï† = âˆ¼â–¡(âˆ¼Ï†) by rfl)];
      simp only [
        minimal_canonical_box, mkCanonicalFrame, Frame.mk_â„¬, Set.mem_compl_iff,
        Set.mem_setOf_eq, proofset.eq_neg
      ];
      constructor;
      . intro h;
        split at h;
        . rename_i hâ‚‚;
          suffices proofset ğ“¢ (â–¡hâ‚‚.choose) = proofset ğ“¢ (â–¡(âˆ¼Ï†)) by rw [this] at h; simpa;
          apply proofset.eq_boxed_of_eq;
          rw [â†hâ‚‚.choose_spec, hÏ†];
          simp;
        . exfalso;
          rename_i hâ‚‚;
          push_neg at hâ‚‚;
          apply @hâ‚‚ (âˆ¼Ï†);
          simpa;
      . intro h;
        split;
        . rename_i hâ‚‚;
          suffices proofset ğ“¢ (â–¡hâ‚‚.choose) = proofset ğ“¢ (â–¡(âˆ¼Ï†)) by rw [â†this] at h; simpa;
          apply proofset.eq_boxed_of_eq;
          rw [â†hâ‚‚.choose_spec, hÏ†];
          simp;
        . tauto;

end minimal_canonical_box


end Neighborhood

end

end LO.Modal
