import Foundation.Vorspiel.HRel.Basic
import Foundation.Modal.Axioms
import Foundation.Modal.Formula
import Foundation.Modal.Logic.Basic

namespace LO.Modal

open Formula (atom)

namespace Neighborhood

structure Frame where
  World : Type
  [world_nonempty : Nonempty World]
  ð’© : World â†’ Set (Set World)
attribute [simp] Frame.world_nonempty

instance : CoeSort Frame Type := âŸ¨Frame.WorldâŸ©
instance {F : Frame} : Nonempty F.World := F.world_nonempty

@[reducible] def Frame.â„¬ (F : Frame) : Set F.World â†’ Set F.World := Î» X => { w | X âˆˆ F.ð’© w }

@[simp]
lemma Frame.eq_â„¬_ð’© {F : Frame} {X Y : Set F.World} : (F.â„¬ X) = Y â†” (âˆ€ x, X âˆˆ F.ð’© x â†” x âˆˆ Y) := by
  constructor;
  . rintro rfl;
    tauto;
  . rintro h;
    ext x;
    apply h;

def Frame.mk_B (World : Type) [Nonempty World] (B : Set World â†’ Set World) : Frame where
  World := World
  ð’© x := { X | x âˆˆ B X }

section

abbrev Frame.simple_blackhole : Frame := âŸ¨Unit, Î» _ => { Set.univ }âŸ©

end


abbrev FrameClass := Set Frame


abbrev Valuation (F : Frame) := â„• â†’ Set F.World

structure Model extends Frame where
  Val : Valuation toFrame
instance : CoeSort Model Type := âŸ¨Î» M => M.toFrame.WorldâŸ©
--instance : CoeFun Model (Î» M => M.World â†’ Set (Set M.World)) := âŸ¨Î» M => Frame.Î½ M.toFrameâŸ©

@[simp]
lemma Model.nonempty_univ_world {M : Model} : Set.Nonempty (Set.univ (Î± := M.World)) := by
  use M.world_nonempty.some;
  tauto;

@[grind]
def Model.truthset (M : Model) : Formula â„• â†’ Set M.World
| .atom n => M.Val n
| âŠ¥       => âˆ…
| Ï† âž Ïˆ  => (truthset M Ï†)á¶œ âˆª truthset M Ïˆ
| â–¡Ï†      => M.â„¬ (truthset M Ï†)

namespace Model.truthset

variable {M : Model} {n : â„•} {Ï† Ïˆ : Formula â„•}

instance : CoeFun Model (Î» M => Formula â„• â†’ Set M.World) := âŸ¨Î» M => truthset MâŸ©

@[simp, grind] lemma eq_atom : M (.atom n) = M.Val n := rfl
@[simp, grind] lemma eq_bot  : M âŠ¥ = âˆ… := rfl
@[simp, grind] lemma eq_top  : M âŠ¤ = Set.univ := by simp [truthset]
@[simp, grind] lemma eq_imp  : M (Ï† âž Ïˆ) = (M Ï†)á¶œ âˆª (M Ïˆ) := rfl
@[simp, grind] lemma eq_or   : M (Ï† â‹Ž Ïˆ) = (M Ï†) âˆª (M Ïˆ) := by simp [truthset];
@[simp, grind] lemma eq_and  : M (Ï† â‹ Ïˆ) = (M Ï†) âˆ© (M Ïˆ) := by simp [truthset];
@[simp, grind] lemma eq_neg  : M (âˆ¼Ï†) = (M Ï†)á¶œ := by simp [truthset]
@[simp, grind] lemma eq_iff  : M (Ï† â­¤ Ïˆ) = (M Ï† âˆ© M Ïˆ) âˆª ((M Ï†)á¶œ âˆ© (M Ïˆ)á¶œ) := calc
  M (Ï† â­¤ Ïˆ) = M (Ï† âž Ïˆ) âˆ© (M (Ïˆ âž Ï†))             := by simp [LogicalConnective.iff];
  _         = ((M Ï†)á¶œ âˆª (M Ïˆ)) âˆ© ((M Ïˆ)á¶œ âˆª (M Ï†)) := by simp;
  _         = (M Ï† âˆ© M Ïˆ) âˆª ((M Ï†)á¶œ âˆ© (M Ïˆ)á¶œ)     := by tauto_set;
@[simp, grind] lemma eq_box  : M (â–¡Ï†) = M.â„¬ (M Ï†) := rfl
@[simp, grind] lemma eq_dia  : M (â—‡Ï†) = (M.â„¬ (M Ï†)á¶œ)á¶œ := by simp [truthset]

@[grind]
lemma eq_subst :
  letI U : Valuation M.toFrame := Î» a => M ((atom a)âŸ¦sâŸ§)
  M (Ï†âŸ¦sâŸ§) = (âŸ¨M.toFrame, UâŸ© : Model) Ï† := by
  induction Ï† <;> simp_all;

end Model.truthset


end Neighborhood


namespace Formula.Neighborhood

open Modal.Neighborhood

def Satisfies (M : Model) (x : M.World) (Ï† : Formula â„•) : Prop := x âˆˆ M Ï†

namespace Satisfies

protected instance semantics {M : Model} : Semantics (Formula â„•) M.World := âŸ¨Î» x => Formula.Neighborhood.Satisfies M xâŸ©

variable {M : Model} {x : M.World} {Ï† Ïˆ Î¾ : Formula â„•}

@[grind] lemma def_top : x âŠ§ âŠ¤ := by simp [Semantics.Realize, Satisfies];
@[grind] lemma def_bot : Â¬x âŠ§ âŠ¥ := by simp [Semantics.Realize, Satisfies];
@[grind] lemma def_neg : x âŠ§ âˆ¼Ï† â†” Â¬x âŠ§ Ï† := by simp [Semantics.Realize, Satisfies];
@[grind] lemma def_imp : x âŠ§ Ï† âž Ïˆ â†” (x âŠ§ Ï† â†’ x âŠ§ Ïˆ) := by simp [Semantics.Realize, Satisfies]; tauto;
@[grind] lemma def_and : x âŠ§ Ï† â‹ Ïˆ â†” (x âŠ§ Ï† âˆ§ x âŠ§ Ïˆ) := by simp [Semantics.Realize, Satisfies];
@[grind] lemma def_or  : x âŠ§ Ï† â‹Ž Ïˆ â†” (x âŠ§ Ï† âˆ¨ x âŠ§ Ïˆ) := by simp [Semantics.Realize, Satisfies];

@[grind] lemma def_box : x âŠ§ â–¡Ï† â†” M Ï† âˆˆ (M.ð’© x) := by simp [Semantics.Realize, Satisfies];
@[grind] lemma def_dia : x âŠ§ â—‡Ï† â†” (M Ï†)á¶œ âˆˆ (M.ð’© x)á¶œ := by simp [Semantics.Realize, Satisfies];

protected instance : Semantics.Tarski (M.World) where
  realize_top := by grind
  realize_bot := by grind
  realize_imp := by grind
  realize_not := by grind
  realize_or  := by grind
  realize_and := by grind

@[simp] protected lemma implyâ‚ : x âŠ§ Axioms.Implyâ‚ Ï† Ïˆ := by grind
@[simp] protected lemma implyâ‚‚ : x âŠ§ Axioms.Implyâ‚‚ Ï† Ïˆ Î¾ := by grind
@[simp] protected lemma elimContra : x âŠ§ Axioms.ElimContra Ï† Ïˆ := by grind
protected lemma mdp (hÏ†Ïˆ : x âŠ§ Ï† âž Ïˆ) (hÏˆ : x âŠ§ Ï†) : x âŠ§ Ïˆ := by grind

lemma dia_dual : x âŠ§ â—‡Ï† â†” x âŠ§ âˆ¼â–¡(âˆ¼Ï†) := by simp [Semantics.Realize, Satisfies];
lemma box_dual : x âŠ§ â–¡Ï† â†” x âŠ§ âˆ¼â—‡(âˆ¼Ï†) := by simp [Semantics.Realize, Satisfies];

lemma iff_subst_self {M : Model} {x : M.World} (s) :
  letI U : Valuation M.toFrame := Î» a => M ((atom a)âŸ¦sâŸ§)
  Satisfies âŸ¨M.toFrame, UâŸ© x Ï† â†” Satisfies M x (Ï†âŸ¦sâŸ§) := by
  simp [Satisfies, Model.truthset.eq_subst];

end Satisfies


def ValidOnModel (M : Model) (Ï† : Formula â„•) : Prop := âˆ€ x, Satisfies M x Ï†

namespace ValidOnModel

variable {M : Model} {Ï† Ïˆ Î¾ : Formula â„•}

protected instance semantics : Semantics (Formula â„•) Model := âŸ¨fun M â†¦ ValidOnModel MâŸ©

@[simp]
lemma iff_eq_truthset_univ : M âŠ§ Ï† â†” (M Ï† = Set.univ) := by
  constructor;
  . intro hÏ†;
    ext x;
    simp only [Set.mem_univ, iff_true];
    apply hÏ†;
  . intro h x;
    simp [Satisfies, h]

instance : Semantics.Bot Model where
  realize_bot M := by
    simp only [iff_eq_truthset_univ, Neighborhood.Model.truthset.eq_bot];
    apply Set.nonempty_iff_ne_empty.mp ?_ |>.symm;
    simp;

instance : Semantics.Top Model where
  realize_top M := by simp;

lemma valid_iff : M âŠ§ Ï† â­¤ Ïˆ â†” (M Ï† = M Ïˆ) := by
  constructor;
  . intro h;
    ext x;
    have := @h x;
    simp [Satisfies] at this;
    tauto;
  . intro h x;
    simp [Satisfies, Set.mem_union, Set.mem_inter_iff, Set.mem_compl_iff, h];

@[simp] protected lemma implyâ‚ : M âŠ§ Axioms.Implyâ‚ Ï† Ïˆ := Î» _ â†¦ Satisfies.implyâ‚

@[simp] protected lemma implyâ‚‚ : M âŠ§ Axioms.Implyâ‚‚ Ï† Ïˆ Î¾ := Î» _ â†¦ Satisfies.implyâ‚‚

@[simp] protected lemma elimContra : M âŠ§ Axioms.ElimContra Ï† Ïˆ := Î» _ â†¦ Satisfies.elimContra

protected lemma mdp (hÏ†Ïˆ : M âŠ§ Ï† âž Ïˆ) (hÏˆ : M âŠ§ Ï†) : M âŠ§ Ïˆ := by
  intro x;
  apply Satisfies.mdp;
  . exact hÏ†Ïˆ x;
  . exact hÏˆ x;

protected lemma re (hÏ† : M âŠ§ Ï† â­¤ Ïˆ) : M âŠ§ â–¡Ï† â­¤ â–¡Ïˆ := by
  rw [valid_iff] at âŠ¢ hÏ†;
  ext x;
  simp [Satisfies, hÏ†];

end ValidOnModel



def ValidOnFrame (F : Neighborhood.Frame) (Ï† : Formula â„•) : Prop := âˆ€ V, (âŸ¨F, VâŸ© : Model) âŠ§ Ï†

namespace ValidOnFrame

variable {F : Frame} {Ï† Ïˆ Î¾ : Formula â„•}

protected instance semantics : Semantics (Formula â„•) Frame := âŸ¨fun F â†¦ ValidOnFrame FâŸ©

instance : Semantics.Bot Frame where
  realize_bot F := by
    by_contra! hC;
    simpa using hC (Î» _ => {});

instance : Semantics.Top Frame where
  realize_top F := by intro; simp;

protected lemma mdp (hÏ†Ïˆ : F âŠ§ Ï† âž Ïˆ) (hÏ† : F âŠ§ Ï†) : F âŠ§ Ïˆ := by
  intro V;
  apply ValidOnModel.mdp;
  . exact hÏ†Ïˆ V;
  . exact hÏ† V;

protected lemma re (hÏ† : F âŠ§ Ï† â­¤ Ïˆ) : F âŠ§ â–¡Ï† â­¤ â–¡Ïˆ := by
  intro V;
  apply ValidOnModel.re;
  exact hÏ† V;

@[simp] protected lemma implyâ‚ : F âŠ§ Axioms.Implyâ‚ Ï† Ïˆ := Î» _ â†¦ ValidOnModel.implyâ‚

@[simp] protected lemma implyâ‚‚ : F âŠ§ Axioms.Implyâ‚‚ Ï† Ïˆ Î¾ := Î» _ â†¦ ValidOnModel.implyâ‚‚

@[simp] protected lemma elimContra : F âŠ§ Axioms.ElimContra Ï† Ïˆ := Î» _ â†¦ ValidOnModel.elimContra


lemma iff_not_exists_valuation_world : (Â¬F âŠ§ Ï†) â†” (âˆƒ V : Valuation F, âˆƒ x : (âŸ¨F, VâŸ© : Model).World, Â¬Satisfies _ x Ï†) := by
  simp [ValidOnFrame, Satisfies, ValidOnModel, Semantics.Realize];

alias âŸ¨exists_valuation_world_of_not, not_of_exists_valuation_worldâŸ© := iff_not_exists_valuation_world

protected lemma subst (s) (h : F âŠ§ Ï†) : F âŠ§ Ï†âŸ¦sâŸ§ := by
  by_contra hC;
  obtain âŸ¨V, x, hxâŸ© := exists_valuation_world_of_not hC;
  apply Satisfies.iff_subst_self s |>.not.mpr hx;
  exact h (Î» a => Model.truthset âŸ¨F, VâŸ© (atom aâŸ¦sâŸ§)) x;

end ValidOnFrame

section

variable {C : FrameClass} {Ï† Ïˆ Ï‡ : Formula â„•}

lemma iff_not_validOnFrameClass_exists_model_world : (Â¬C âŠ§ Ï†) â†” (âˆƒ M : Model, âˆƒ x : M.World, M.toFrame âˆˆ C âˆ§ Â¬(x âŠ§ Ï†)) := by
  apply not_iff_not.mp;
  push_neg;
  tauto;
alias âŸ¨exists_model_world_of_not_validOnFrameClass, not_validOnFrameClass_of_exists_model_worldâŸ© := iff_not_validOnFrameClass_exists_model_world

end

end Formula.Neighborhood


namespace Neighborhood

abbrev Frame.logic (F : Frame) : Logic â„• := { Ï† | F âŠ§ Ï† }
abbrev FrameClass.logic (C : FrameClass) : Logic â„• := { Ï† | C âŠ§ Ï† }

end Neighborhood



end LO.Modal
