import Foundation.Modal.Boxdot.Basic
import Foundation.Modal.Kripke.Logic.KTB
import Foundation.Modal.Kripke.Logic.S4
import Foundation.Modal.Kripke.Logic.S5
import Foundation.Modal.Logic.Global

namespace LO.Modal

namespace Kripke

variable {F : Frame}

def Frame.twice (F : Frame) : Frame where
  World := F.World Ã— Bool
  Rel := Î» (x, _) (y, _) => x â‰º y

local postfix:100 "Ã—2" => Frame.twice

instance [F.IsReflexive] : FÃ—2.IsReflexive where
  refl := by rintro âŸ¨x, iâŸ©; simp [Frame.twice];

instance [F.IsTransitive] : FÃ—2.IsTransitive where
  trans := by
    rintro âŸ¨x, iâŸ© âŸ¨y, jâŸ© âŸ¨z, kâŸ© Rxy Ryj;
    simp only [Frame.twice] at Rxy Ryj;
    exact F.trans Rxy Ryj;

instance [F.IsSymmetric] : FÃ—2.IsSymmetric where
  symm := by
    rintro âŸ¨x, iâŸ© âŸ¨y, jâŸ© Rxy;
    simp only [Frame.twice] at Rxy;
    exact F.symm Rxy;

def Frame.twice.PMorphism (F : Frame) : FÃ—2 â†’â‚š F where
  toFun := Prod.fst
  forth := by
    rintro âŸ¨x, _âŸ© âŸ¨y, _âŸ© h;
    simpa using h;
  back := by
    intro âŸ¨x, iâŸ© y Rxy;
    use âŸ¨y, trueâŸ©;
    constructor;
    . simp;
    . tauto;

class FrameClass.JerabekAssumption (C : FrameClass) where
  twice : âˆ€ F âˆˆ C, FÃ—2 âˆˆ C

instance : FrameClass.KT.JerabekAssumption := by
  constructor;
  intro F hF;
  simp_all only [Set.mem_setOf_eq];
  infer_instance;

instance : FrameClass.KTB.JerabekAssumption := by
  constructor;
  intro F hF;
  simp_all only [Set.mem_setOf_eq];
  constructor;

instance : FrameClass.S4.JerabekAssumption := by
  constructor;
  intro F hF;
  simp_all only [Set.mem_setOf_eq];
  constructor;

instance : FrameClass.S5.JerabekAssumption := by
  constructor;
  intro F hF;
  simp_all only [Set.mem_setOf_eq];
  constructor;

end Kripke


namespace Formula

variable {Î± : Type*} {Ï† : Formula _}

def flag (Ï† : Formula Î±) : Bool â†’ Formula Î±
  | true  => Ï†
  | false => âˆ¼Ï†

@[simp]
lemma atom_flag_boxdotTranslated : (flag (.atom a) b)áµ‡ = (flag (.atom a) b) := by
  match b with | true | false => rfl;

end Formula


namespace Logic

variable {Î±} {Lâ‚€ L : Logic Î±}

def boxdot_preimage (L : Logic Î±) := { Ï† âˆˆ L | L âŠ¢! Ï†áµ‡ }
local postfix:100 "áµ‡â»Â¹" => boxdot_preimage

def BoxdotProperty (Lâ‚€ : Logic Î±) := âˆ€ {L : Logic _}, L.IsNormal â†’ Láµ‡â»Â¹ = Lâ‚€ â†’ L âŠ† Lâ‚€

def StrongBoxdotProperty (Lâ‚€ : Logic Î±) := âˆ€ {L : Logic _}, L.IsNormal â†’ Láµ‡â»Â¹ âŠ† Lâ‚€ â†’ L âŠ† Lâ‚€

lemma BDP_of_SBDP : StrongBoxdotProperty Lâ‚€ â†’ BoxdotProperty Lâ‚€ := by
  intro hSBDP;
  intro L _ hL;
  apply hSBDP;
  . assumption;
  . rw [hL];

end Logic

section



open LO.Entailment LO.Modal.Entailment
open GlobalConsequence
open Formula (atom flag boxdotTranslate)
open Formula.Kripke
open Kripke


def Formula.Kripke.Satisfies.neither_flag {M : Model} {x : M} {Ï† : Formula â„•} : Â¬(x âŠ§ Ï†.flag b âˆ§ x âŠ§ Ï†.flag !b) := by
  match b with
  | true  => simp [Formula.flag];
  | false => simp [Formula.flag];

section

variable {L : Logic â„•} [L.IsNormal] {n : â„•} {Î“ Î” : Finset (Formula â„•)} {Ï† : Formula â„•} {p} {b}

private lemma jerabek_SBDP.lemmaâ‚ : Hilbert.K âŠ¢! (flag (.atom p) b) â‹ â–¡Ï†áµ‡ âž âŠ¡((flag (.atom p) !b) âž Ï†áµ‡) := by
  apply Complete.complete (ð“œ := Kripke.FrameClass.all);
  intro F hF V x hx;
  replace hF := Set.mem_setOf_eq.mp hF;
  apply Satisfies.and_def.mpr;
  constructor;
  . intro hxâ‚;
    by_contra hC;
    apply Satisfies.neither_flag;
    constructor;
    . exact Satisfies.and_def.mp hx |>.1;
    . assumption;
  . replace hx := Satisfies.and_def.mp hx |>.2;
    intro y Rxy h;
    apply hx;
    assumption;

private lemma jerabek_SBDP.lemmaâ‚‚ : L âŠ¢! (flag (.atom p) b) â‹ â–¡Ï†áµ‡ âž âŠ¡((flag (.atom p) !b) âž Ï†áµ‡) := by
  apply normal_provable_of_K_provable;
  simpa using lemmaâ‚;

private lemma jerabek_SBDP.lemmaâ‚ƒ : L âŠ¢! (â–¡^[n]Î“.conj)áµ‡ âž â–¡^â‰¤[n](Î“.image (Â·áµ‡)).conj := by
  apply normal_provable_of_K_provable;
  apply Complete.complete (ð“œ := Kripke.FrameClass.all);
  intro F hF V x h;
  apply Satisfies.fconj_def.mpr;
  simp only [Finset.mem_image, Finset.mem_range, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚];
  intro k hk;
  apply Satisfies.multibox_def.mpr;
  intro y Rxy;
  apply Satisfies.fconj_def.mpr;
  simp only [Finset.mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚];
  intro Î¾ hÎ¾;
  replace h : Satisfies _ x (â–¡^[k]Î“.conjáµ‡) := Satisfies.fconj_def.mp (Satisfies.iff_boxdotTranslateMultibox_boxdotTranslateBoxlt.mp h) _ ?_;
  . apply Satisfies.fconj_def.mp (Satisfies.boxdotTranslate_fconjâ‚‚.mp $ Satisfies.multibox_def.mp h Rxy) _;
    simp only [Finset.mem_image];
    use Î¾;
  . simp only [Finset.mem_image, Finset.mem_range];
    use k;

end

/--
  Every Logic `Lâ‚€` which is `Modal.KT âª¯ Lâ‚€` and sound and complete to frame class `C` satisfies JeÅ™Ã¡bek's assumption, has strong boxdot property.
-/
theorem jerabek_SBDP
  (Lâ‚€ : Logic â„•) [hKT : Modal.KT âª¯ Lâ‚€]
  (C : Kripke.FrameClass) [C.JerabekAssumption]
  [sound : Sound Lâ‚€ C] [complete : Complete Lâ‚€ C]
  : Lâ‚€.StrongBoxdotProperty := by
  intro L _;
  contrapose!;
  intro hL;
  obtain âŸ¨Ï†, hÏ†L, hÏ†Lâ‚€âŸ© := Set.not_subset.mp hL;
  apply Set.not_subset.mpr;

  let q := Formula.freshAtom Ï†;
  let Xâ‚€ := Ï†.subformulas.prebox.image (Î» Ïˆ => â–¡((.atom q) âž Ïˆ) âž Ïˆ);
  let Xâ‚ := Ï†.subformulas.prebox.image (Î» Ïˆ => â–¡(âˆ¼(.atom q) âž Ïˆ) âž Ïˆ);
  let X := Xâ‚€ âˆª Xâ‚;
  let XB := X.image (Â·áµ‡);

  have Claim1 : âˆ€ Ïˆ âˆˆ Ï†.subformulas.prebox, (L, XB.toSet) âŠ¢! â–¡Ïˆáµ‡ âž Ïˆáµ‡ := by
    intro Ïˆ hÏˆ;
    have Hâ‚ : âˆ€ b, (L, XB.toSet) âŠ¢! (flag (.atom q) b) â‹ â–¡Ïˆáµ‡ âž âŠ¡((flag (.atom q) !b) âž Ïˆáµ‡) := by
      intro b;
      apply GlobalConsequence.thm!;
      apply jerabek_SBDP.lemmaâ‚‚;
    have Hâ‚‚ : âˆ€ b, (L, XB.toSet) âŠ¢! âŠ¡((flag (.atom q) b) âž Ïˆáµ‡) âž Ïˆáµ‡ := by
      intro b;
      suffices (L, XB.toSet) âŠ¢! (â–¡((flag (.atom q) b) âž Ïˆ) âž Ïˆ)áµ‡ by
        simpa only [Formula.boxdotTranslate, Formula.atom_flag_boxdotTranslated] using this;
      apply GlobalConsequence.ctx!;
      simp only [Finset.coe_image, Set.mem_image, Finset.mem_coe, XB];
      use (â–¡((flag (atom q) b) âž Ïˆ) âž Ïˆ);
      constructor;
      . match b with | true | false => simpa [X, Xâ‚€, Xâ‚, flag] using hÏˆ;
      . rfl;
    have Hâ‚ƒ : âˆ€ b, (L, XB.toSet) âŠ¢! (flag (.atom q) b) âž (â–¡Ïˆáµ‡ âž Ïˆáµ‡) := by
      intro b;
      cl_prover [(Hâ‚ b), (Hâ‚‚ !b)];
    have Hâ‚„ : (L, XB.toSet) âŠ¢!  atom q âž â–¡Ïˆáµ‡ âž Ïˆáµ‡ := Hâ‚ƒ true;
    have Hâ‚… : (L, XB.toSet) âŠ¢! âˆ¼atom q âž â–¡Ïˆáµ‡ âž Ïˆáµ‡ := Hâ‚ƒ false;
    cl_prover [Hâ‚„, Hâ‚…];
  have Claim2 : âˆ€ Ïˆ âˆˆ Ï†.subformulas, (L, XB.toSet) âŠ¢! Ïˆ â­¤ Ïˆáµ‡ := by
    intro Ïˆ hÏˆ;
    induction Ïˆ with
    | hfalsum => simp [Formula.boxdotTranslate];
    | hatom a => simp [Formula.boxdotTranslate];
    | himp Ïˆâ‚ Ïˆâ‚‚ ihÏˆâ‚ ihÏˆâ‚‚ =>
      replace ihÏˆâ‚ := ihÏˆâ‚ (by grind);
      replace ihÏˆâ‚‚ := ihÏˆâ‚‚ (by grind);
      dsimp [Formula.boxdotTranslate];
      cl_prover [ihÏˆâ‚, ihÏˆâ‚‚];
    | hbox Ïˆ ihÏˆ =>
      replace ihÏˆ : (L, XB.toSet) âŠ¢! Ïˆ â­¤ Ïˆáµ‡ := ihÏˆ (by grind);
      have Hâ‚ : (L, XB.toSet) âŠ¢! â–¡Ïˆ â­¤ â–¡Ïˆáµ‡ := box_congruence! ihÏˆ;
      have Hâ‚‚ : (L, XB.toSet) âŠ¢! â–¡Ïˆáµ‡ â­¤ âŠ¡Ïˆáµ‡ := by
        apply Entailment.E!_intro;
        . have : (L, XB.toSet) âŠ¢! â–¡Ïˆáµ‡ âž Ïˆáµ‡ := Claim1 Ïˆ (by simpa);
          cl_prover [this];
        . cl_prover;
      cl_prover [Hâ‚, Hâ‚‚];
  have : (L, XB.toSet) âŠ¢! Ï†áµ‡ := by
    have hâ‚ : (L, XB.toSet) âŠ¢! Ï† âž Ï†áµ‡ := C_of_E_mp! $ Claim2 Ï† (by grind);
    have hâ‚‚ : (L, XB.toSet) âŠ¢! Ï† := by
      apply GlobalConsequence.thm!;
      simpa using hÏ†L;
    exact hâ‚ â¨€ hâ‚‚;
  obtain âŸ¨Î“, n, hÎ“, hÏ†âŸ© := GlobalConsequence.iff_finite_boxlt_provable.mp this;
  replace hÏ† : L âŠ¢! (â–¡^â‰¤[n]XB.conj) âž Ï†áµ‡ := C!_trans (boxlt_fconj_regularity_of_subset hÎ“) hÏ†;
  let Ï‡ := (â–¡^[n](X.conj) âž Ï†);
  have hÏ‡ : L âŠ¢! Ï‡áµ‡ := by apply C!_trans jerabek_SBDP.lemmaâ‚ƒ hÏ†;
  use Ï‡;
  constructor;
  . constructor;
    . suffices L âŠ¢! Ï‡ by simpa;
      apply dhyp!;
      simpa using hÏ†L;
    . assumption;
  . suffices Lâ‚€ âŠ¬ (â–¡^[n]X.conj) âž Ï† by simpa;
    apply sound.not_provable_of_countermodel;
    apply not_validOnFrameClass_of_exists_model_world;

    have : Â¬C âŠ§ Ï† := complete.exists_countermodel_of_not_provable (by simpa using hÏ†Lâ‚€);
    obtain âŸ¨M, x, hMC, hFâŸ© := Kripke.exists_model_world_of_not_validOnFrameClass this;
    let Mâ‚‚ : Kripke.Model := {
      toFrame := M.toFrame.twice
      Val := Î» âŸ¨w, iâŸ© a =>
        if   a = q then i = true
        else M.Val w a
    }
    have : M.IsReflexive := by
      apply reflexive_of_validate_AxiomT;
      apply sound.sound;
      . apply hKT.pbl;
        simp;
      . assumption;
    have H2 : âˆ€ Ïˆ âˆˆ Ï†.subformulas, âˆ€ w : M.World, âˆ€ b : Bool, Satisfies Mâ‚‚ (w, b) Ïˆ â†” Satisfies M w Ïˆ := by
      intro Ïˆ hÏˆ w b;
      induction Ïˆ generalizing w b with
      | hfalsum => simp [Satisfies];
      | hatom a =>
        have : a â‰  q := Formula.ne_freshAtom_of_mem_subformulas hÏˆ |>.symm;
        simp [Satisfies, Mâ‚‚, this];
      | himp Ïˆâ‚ Ïˆâ‚‚ ihÏˆâ‚ ihÏˆâ‚‚ =>
        replace ihÏˆâ‚ := ihÏˆâ‚ (by grind);
        replace ihÏˆâ‚‚ := ihÏˆâ‚‚ (by grind);
        simp [Satisfies, ihÏˆâ‚, ihÏˆâ‚‚]
      | hbox Ïˆ ihÏˆ =>
        replace ihÏˆ := ihÏˆ (by grind);
        constructor;
        . intro h v Rwv;
          apply ihÏˆ v _ |>.mp;
          apply h (v, b);
          simpa [Frame.Rel', Mâ‚‚, Frame.twice]
        . intro h v Rwv;
          apply ihÏˆ v.1 v.2 |>.mpr;
          apply h;
          simpa [Frame.Rel', Mâ‚‚, Frame.twice] using Rwv;

    use Mâ‚‚, (x, true);
    constructor;
    . exact Kripke.FrameClass.JerabekAssumption.twice (C := C) _ hMC;
    . apply Satisfies.not_imp_def.mpr;
      constructor;
      . have : Mâ‚‚ âŠ§ X.conj := by
          intro x;
          apply Satisfies.fconj_def.mpr;
          rintro Ïˆ hÏˆ;
          rcases (by simpa [X, Xâ‚€, Xâ‚] using hÏˆ) with (âŸ¨Ïˆ, _, rflâŸ© | âŸ¨Ïˆ, _, rflâŸ©);
          . intro hÏˆâ‚;
            by_cases hÏˆ : Satisfies M x.1 Ïˆ;
            . exact H2 _ (by grind) _ _ |>.mpr hÏˆ;
            . exfalso;
              apply hÏˆ;
              apply H2 _ (by grind) _ true |>.mp;
              apply hÏˆâ‚ (x.1, true);
              . simp [Frame.Rel', Mâ‚‚, Frame.twice];
              . simp [Satisfies, Mâ‚‚];
          . intro hÏˆâ‚;
            by_cases hÏˆ : Satisfies M x.1 Ïˆ;
            . exact H2 _ (by grind) _ _ |>.mpr hÏˆ;
            . exfalso;
              apply hÏˆ;
              apply H2 _ (by grind) _ false |>.mp;
              apply hÏˆâ‚ (x.1, false);
              . simp [Frame.Rel', Mâ‚‚, Frame.twice];
              . simp [Satisfies, Mâ‚‚];
        apply ValidOnModel.multinec n this;
      . exact H2 Ï† (by grind) x _ |>.not.mpr hF;

/--
  Every Logic `Lâ‚€` which is `Modal.KT âª¯ Lâ‚€` and sound and complete to frame class `C` satisfies JeÅ™Ã¡bek's assumption, has boxdot property.
-/
theorem jerabek_BDP
  (Lâ‚€ : Logic â„•) [Modal.KT âª¯ Lâ‚€]
  (C : Kripke.FrameClass) [C.JerabekAssumption]
  [Sound Lâ‚€ C] [Complete Lâ‚€ C]
  : Lâ‚€.BoxdotProperty := Logic.BDP_of_SBDP $ jerabek_SBDP Lâ‚€ C

/-- `Modal.KT` has boxdot property. This is originally boxdot conjecture stated. -/
theorem KT.BDP : Modal.KT.BoxdotProperty := jerabek_BDP Modal.KT Kripke.FrameClass.KT
alias boxdot_conjecture := KT.BDP

/-- `Modal.KTB` has boxdot property. -/
theorem KTB.BDP : Modal.KTB.BoxdotProperty := jerabek_BDP Modal.KTB Kripke.FrameClass.KTB

/-- `Modal.S4` has boxdot property. -/
theorem S4.BDP : Modal.S4.BoxdotProperty := jerabek_BDP Modal.S4 Kripke.FrameClass.S4

/-- `Modal.S5` has boxdot property. -/
theorem S5.BDP : Modal.S5.BoxdotProperty := jerabek_BDP Modal.S5 Kripke.FrameClass.S5

end

end LO.Modal
