import Foundation.IntFO.Basic.Deduction

namespace LO.FirstOrder

open Frame

structure PreorderFrame where
  World : Type u
  Rel : World â†’ World â†’ Prop
  [world_nonempty : Nonempty World]
  [preorder : IsPreorder World Rel]

namespace PreorderFrame

instance : CoeSort PreorderFrame (Type u) := âŸ¨fun F â†¦ PreorderFrame.World FâŸ©

scoped infix:45 " â‰º " => Rel _

instance (F : PreorderFrame) : IsPreorder F (Â· â‰º Â·) := F.preorder

variable {F : PreorderFrame}

@[refl, simp] lemma rel_refl (w : F) : w â‰º w := IsRefl.refl w

@[trans] lemma rel_trans {w v z : F} : w â‰º v â†’ v â‰º z â†’ w â‰º z := IsTrans.trans w v z

end PreorderFrame

open PreorderFrame

structure KripkeModel (L : Language) where
  Frame : PreorderFrame
  Dom : Frame â†’ Struc L
  wire (w v : Frame) : w â‰º v â†’ Dom w â†ª Dom v
  wire_refl (w : Frame) : wire w w (IsRefl.refl _) = Function.Embedding.refl _
  wire_trans (w v z : Frame) (hxy : w â‰º v) (hyz : v â‰º z) :
    wire v z hyz âˆ˜ wire w v hxy = wire w z (IsTrans.trans w v z hxy hyz)
  monotone {w v : Frame} {k} (R : L.Rel k) (a : Fin k â†’ Dom w) :
    (hxy : w â‰º v) â†’ (Dom w).struc.rel R a â†’ (Dom v).struc.rel R fun i â†¦ wire w v hxy (a i)
  wire_func {w v : Frame} {k} (f : L.Func k) (a : Fin k â†’ Dom w) (hwv : w â‰º v) :
    wire w v hwv ((Dom w).struc.func f a) = (Dom v).struc.func f fun i â†¦ wire w v hwv (a i)

instance : CoeSort (KripkeModel L) (Type _) := âŸ¨fun ğ“š â†¦ ğ“š.FrameâŸ©

attribute [simp] KripkeModel.wire_refl

namespace KripkeModel

variable {L : Language} {ğ“š : KripkeModel L}

abbrev Domain (w : ğ“š) : Struc L := ğ“š.Dom w

def Val {n} (w : ğ“š) (bv : Fin n â†’ Domain w) (fv : Î¾ â†’ Domain w) : Semiformulaáµ¢ L Î¾ n â†’ Prop
  | .rel R t => (Domain w).struc.rel R fun i â†¦ Semiterm.val (Domain w).struc bv fv (t i)
  | âŠ¤        => True
  | âŠ¥        => False
  | Ï† â‹ Ïˆ    => Val w bv fv Ï† âˆ§ Val w bv fv Ïˆ
  | Ï† â‹ Ïˆ    => Val w bv fv Ï† âˆ¨ Val w bv fv Ïˆ
  | Ï† â Ïˆ    => âˆ€ v, (hwv : w â‰º v) â†’
    Val v (fun x â†¦ ğ“š.wire w v hwv (bv x)) (fun x â†¦ ğ“š.wire w v hwv (fv x)) Ï† â†’
    Val v (fun x â†¦ ğ“š.wire w v hwv (bv x)) (fun x â†¦ ğ“š.wire w v hwv (fv x)) Ïˆ
  | âˆ€' Ï†     => âˆ€ v, (hwv : w â‰º v) â†’
    âˆ€ x : Domain v, Val v (x :> fun x â†¦ ğ“š.wire w v hwv (bv x)) (fun x â†¦ ğ“š.wire w v hwv (fv x)) Ï†
  | âˆƒ' Ï†     => âˆƒ x : Domain w, Val w (x :> bv) fv Ï†

scoped notation:45 w " âŠ©[" bv "|" fv "] " Ï†:46 => Val w bv fv Ï†

variable (w : ğ“š) (bv : Fin n â†’ Domain w) (fv : Î¾ â†’ Domain w)

@[simp] lemma val_verum : w âŠ©[bv|fv] âŠ¤ := by trivial

@[simp] lemma val_falsum : Â¬w âŠ©[bv|fv] âŠ¥ := by rintro âŸ¨âŸ©

variable {w bv fv}

@[simp] lemma val_rel {k} {R : L.Rel k} {t} :
    w âŠ©[bv|fv] .rel R t â†” (Domain w).struc.rel R fun i â†¦ Semiterm.val (Domain w).struc bv fv (t i) := by rfl

@[simp] lemma val_and {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} : w âŠ©[bv|fv] Ï† â‹ Ïˆ â†” w âŠ©[bv|fv] Ï† âˆ§ w âŠ©[bv|fv] Ïˆ := by rfl

@[simp] lemma val_or {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} : w âŠ©[bv|fv] Ï† â‹ Ïˆ â†” w âŠ©[bv|fv] Ï† âˆ¨ w âŠ©[bv|fv] Ïˆ := by rfl

@[simp] lemma val_imply {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} :
    w âŠ©[bv|fv] Ï† â Ïˆ â†”
    âˆ€ v, (hwv : w â‰º v) â†’
      v âŠ©[fun x â†¦ ğ“š.wire w v hwv (bv x)|fun x â†¦ ğ“š.wire w v hwv (fv x)] Ï† â†’
      v âŠ©[fun x â†¦ ğ“š.wire w v hwv (bv x)|fun x â†¦ ğ“š.wire w v hwv (fv x)] Ïˆ := by rfl

@[simp] lemma val_all {Ï† : Semiformulaáµ¢ L Î¾ (n + 1)} :
    w âŠ©[bv|fv] âˆ€' Ï† â†”
    âˆ€ v, (hwv : w â‰º v) â†’
      âˆ€ x : Domain v, v âŠ©[x :> fun x â†¦ ğ“š.wire w v hwv (bv x)|fun x â†¦ ğ“š.wire w v hwv (fv x)] Ï† := by rfl

@[simp] lemma val_ex {Ï† : Semiformulaáµ¢ L Î¾ (n + 1)} :
    w âŠ©[bv|fv] âˆƒ' Ï† â†” âˆƒ x : Domain w, w âŠ©[x :> bv|fv] Ï† := by rfl

@[simp] lemma val_neg {Ï† : Semiformulaáµ¢ L Î¾ n} :
    w âŠ©[bv|fv] âˆ¼Ï† â†”
    âˆ€ v, (hwv : w â‰º v) â†’ Â¬v âŠ©[fun x â†¦ ğ“š.wire w v hwv (bv x)|fun x â†¦ ğ“š.wire w v hwv (fv x)] Ï† := by rfl

lemma wire_val (t : Semiterm L Î¾ n) {v : ğ“š} (hwv : w â‰º v) :
    ğ“š.wire w v hwv (t.val (Domain w).struc bv fv) =
    t.val (Domain v).struc (fun x â†¦ ğ“š.wire w v hwv (bv x)) (fun x â†¦ ğ“š.wire w v hwv (fv x)) := by
  induction t <;> simp [Semiterm.val_func, wire_func, *]

@[simp] lemma val_rew {bv : Fin nâ‚‚ â†’ Domain w} {fv : Î¾â‚‚ â†’ Domain w} {Ï‰ : Rew L Î¾â‚ nâ‚ Î¾â‚‚ nâ‚‚} {Ï† : Semiformulaáµ¢ L Î¾â‚ nâ‚} :
    w âŠ©[bv|fv] (Ï‰ â–¹ Ï†) â†”
    w âŠ©[fun x â†¦ (Ï‰ #x).val (Domain w).struc bv fv|fun x â†¦ (Ï‰ &x).val (Domain w).struc bv fv] Ï† := by
  induction Ï† using Semiformulaáµ¢.rec' generalizing nâ‚‚ w
  case hRel k R t =>
    simp only [Semiformulaáµ¢.rew_rel, val_rel]
    apply iff_of_eq; congr; funext x
    simp [Semiterm.val_rew Ï‰ (t x), Function.comp_def]
  case hImp Ï† Ïˆ ihÏ† ihÏˆ =>
    simp only [val_imply, Function.comp_apply, wire_val]
    constructor
    Â· intro h v hwv hÏ†
      simpa [Function.comp_def] using ihÏˆ.mp <| h v hwv (ihÏ†.mpr <| by simpa [Function.comp_def, wire_val] using hÏ†)
    Â· intro h v hwv hÏ†
      exact ihÏˆ.mpr <| h v hwv <| by simpa [Function.comp_def] using ihÏ†.mp hÏ†
  case hAnd Ï† Ïˆ ihÏ† ihÏˆ => simp [ihÏ†, ihÏˆ]
  case hOr Ï† Ïˆ ihÏ† ihÏˆ => simp [ihÏ†, ihÏˆ]
  case hVerum => simp
  case hFalsum => simp
  case hAll Ï† ih =>
    constructor
    Â· simp only [val_all, Nat.succ_eq_add_one, wire_val]
      intro h v hwv x
      exact cast (by congr; { funext x; cases x using Fin.cases <;> simp }; { simp }) <| ih.mp <| h v hwv x
    Â· simp only [val_all, Nat.succ_eq_add_one, wire_val]
      intro h v hwv x
      apply ih.mpr
      exact cast (by congr; { funext x; cases x using Fin.cases <;> simp }; { simp }) <| h v hwv x
  case hEx Ï† ih =>
    constructor
    Â· simp only [Rewriting.app_ex, val_ex, Nat.succ_eq_add_one, forall_exists_index]
      intro x h
      exact âŸ¨x, cast (by congr; { funext x; cases x using Fin.cases <;> simp }; { simp }) (ih.mp h)âŸ©
    Â· simp only [val_ex, Nat.succ_eq_add_one, forall_exists_index]
      intro x h
      exact âŸ¨x, ih.mpr <| cast (by congr; { funext x; cases x using Fin.cases <;> simp }; { simp }) hâŸ©

variable (ğ“š)

def Force (Ï† : Semiformulaáµ¢ L Î¾ n) : Prop := âˆ€ (w : ğ“š) bv fv, w âŠ©[bv|fv] Ï†

scoped infix:45 " âŠ© " => Force

instance : Semantics (SyntacticFormulaáµ¢ L) (KripkeModel L) := âŸ¨fun ğ“š Ï† â†¦ ğ“š.Force Ï†âŸ©

variable {ğ“š}

variable {Î› : Hilbertáµ¢ L}

open HilbertProofáµ¢ Semantics

/-
theorem sound (H : ğ“š âŠ§* Î›) : Î› âŠ¢! Ï† â†’ ğ“š âŠ§ Ï†
  | eaxm h => RealizeSet.realize ğ“š h
  | @mdp _ _ Ï† Ïˆ bÏ†Ïˆ bÏ† => fun w bv fv â†¦ by simpa using sound H bÏ†Ïˆ w bv fv w (by simp) (sound H bÏ† w _ _)
  | @gen _ _ Ï† b        => fun w bv fv v hwv x â†¦ by { have := sound H b v ![] }
-/

end KripkeModel

end LO.FirstOrder
