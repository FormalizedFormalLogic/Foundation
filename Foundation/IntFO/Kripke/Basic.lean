import Foundation.IntFO.Basic.Deduction

namespace LO.FirstOrder

namespace Semiterm

variable {L : Language} [L.Relational]

lemma bvar_or_fvar_of_relational (t : Semiterm L Î¾ n) : (âˆƒ x, t = #x) âˆ¨ (âˆƒ x, t = &x) :=
  match t with
  |        #x => by simp
  |        &x => by simp
  | .func f _ => Language.Relational.func_empty _ |>.elim' f

lemma fvar_of_relational (t : Term L Î¾) : âˆƒ x, t = &x := by
  rcases bvar_or_fvar_of_relational t with (âŸ¨x, rflâŸ© | âŸ¨x, rflâŸ©)
  Â· exact finZeroElim (Î± := fun _ â†¦ _) x
  Â· exact âŸ¨x, rflâŸ©

variable {M : Type*} (bv : Fin n â†’ M) (fv : Î¾ â†’ M)

def relationalVal : Semiterm L Î¾ n â†’ M
  |        #x => bv x
  |        &x => fv x
  | .func f _ => Language.Relational.func_empty _ |>.elim' f

variable {bv fv}

@[simp] lemma relationalVal_bvar : (#x : Semiterm L Î¾ n).relationalVal bv fv = bv x := rfl

@[simp] lemma relationalVal_fvar : (&x : Semiterm L Î¾ n).relationalVal bv fv = fv x := rfl

lemma relationalVal_rew {bv : Fin nâ‚‚ â†’ M} {fv : Î¾â‚‚ â†’ M} (Ï‰ : Rew L Î¾â‚ nâ‚ Î¾â‚‚ nâ‚‚) (t : Semiterm L Î¾â‚ nâ‚) :
    relationalVal bv fv (Ï‰ t) = relationalVal (relationalVal bv fv âˆ˜ Ï‰ âˆ˜ bvar) (relationalVal bv fv âˆ˜ Ï‰ âˆ˜ fvar) t := by
  rcases bvar_or_fvar_of_relational t with (âŸ¨x, rflâŸ© | âŸ¨x, rflâŸ©) <;> simp

@[simp] lemma relationalVal_bShift (x : M) (t : Semiterm L Î¾ n) :
    relationalVal (x :> bv) fv (Rew.bShift t) = relationalVal bv fv t := by
  simp [relationalVal_rew, Function.comp_def]

end Semiterm

open Frame

structure RelationalKripkeModel (L : Language) [L.Relational] where
  World : Type*
  [preorder : Preorder World]
  Carrier : Type*
  Domain : World â†’ Set Carrier
  domain_nonempty : âˆ€ w, âˆƒ x, x âˆˆ Domain w
  domain_antimonotone : w â‰¥ v â†’ Domain w âŠ† Domain v
  Rel (w : World) {k : â„•} (R : L.Rel k) : (Fin k â†’ Carrier) â†’ Prop
  rel_monotone : w â‰¥ v â†’ Rel w R t â†’ Rel v R t

instance (L : Language) [L.Relational] : CoeSort (RelationalKripkeModel L) (Type _) := âŸ¨fun ğ“š â†¦ ğ“š.WorldâŸ©

instance {L : Language} [L.Relational] (ğ“š : RelationalKripkeModel L) : CoeSort ğ“š.World (Type _) := âŸ¨fun w â†¦ ğ“š.Domain wâŸ©

instance {L : Language} [L.Relational] (ğ“š : RelationalKripkeModel L) : Preorder ğ“š.World := ğ“š.preorder

namespace RelationalKripkeModel

variable {L : Language} [L.Relational] {ğ“š : RelationalKripkeModel L}

def Val {n} (w : ğ“š) (bv : Fin n â†’ Carrier ğ“š) (fv : Î¾ â†’ Carrier ğ“š) : Semiformulaáµ¢ L Î¾ n â†’ Prop
  | .rel R t => ğ“š.Rel w R fun i â†¦ (t i).relationalVal bv fv
  |        âŠ¤ => True
  |        âŠ¥ => False
  |    Ï† â‹ Ïˆ => Val w bv fv Ï† âˆ§ Val w bv fv Ïˆ
  |    Ï† â‹ Ïˆ => Val w bv fv Ï† âˆ¨ Val w bv fv Ïˆ
  |    Ï† â Ïˆ => âˆ€ v â‰¤ w, Val v bv fv Ï† â†’ Val v bv fv Ïˆ
  |     âˆ€' Ï† => âˆ€ v â‰¤ w, âˆ€ x : v, Val v (x.val :> bv) fv Ï†
  |     âˆƒ' Ï† => âˆƒ x : w, Val w (x.val :> bv) fv Ï†

local notation:45 w " âŠ©[" bv "|" fv "] " Ï†:46 => Val w bv fv Ï†

abbrev Valb {n} (w : ğ“š) (bv : Fin n â†’ Carrier ğ“š) : Semisentenceáµ¢ L n â†’ Prop := ğ“š.Val w bv Empty.elim

scoped notation:45 w " âŠ©/" bv Ï†:46 => Valb w bv Ï†

variable (w : ğ“š) (bv : Fin n â†’ Carrier ğ“š) (fv : Î¾ â†’ Carrier ğ“š)

@[simp] lemma val_verum : w âŠ©[bv|fv] âŠ¤ := by trivial

@[simp] lemma val_falsum : Â¬w âŠ©[bv|fv] âŠ¥ := by rintro âŸ¨âŸ©

variable {w bv fv}

@[simp] lemma val_rel {k} {R : L.Rel k} {t} :
    w âŠ©[bv|fv] .rel R t â†” ğ“š.Rel w R fun i â†¦ (t i).relationalVal bv fv := by rfl

@[simp] lemma val_and {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} : w âŠ©[bv|fv] Ï† â‹ Ïˆ â†” w âŠ©[bv|fv] Ï† âˆ§ w âŠ©[bv|fv] Ïˆ := by rfl

@[simp] lemma val_or {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} : w âŠ©[bv|fv] Ï† â‹ Ïˆ â†” w âŠ©[bv|fv] Ï† âˆ¨ w âŠ©[bv|fv] Ïˆ := by rfl

@[simp] lemma val_imply {Ï† Ïˆ : Semiformulaáµ¢ L Î¾ n} :
    w âŠ©[bv|fv] Ï† â Ïˆ â†” âˆ€ v â‰¤ w, Val v bv fv Ï† â†’ Val v bv fv Ïˆ := by rfl

@[simp] lemma val_all {Ï† : Semiformulaáµ¢ L Î¾ (n + 1)} :
    w âŠ©[bv|fv] âˆ€' Ï† â†” âˆ€ v â‰¤ w, âˆ€ x : v, Val v (x.val :> bv) fv Ï† := by rfl

@[simp] lemma val_ex {Ï† : Semiformulaáµ¢ L Î¾ (n + 1)} :
    w âŠ©[bv|fv] âˆƒ' Ï† â†” âˆƒ x : w, w âŠ©[x :> bv|fv] Ï† := by rfl

@[simp] lemma val_neg {Ï† : Semiformulaáµ¢ L Î¾ n} :
    w âŠ©[bv|fv] âˆ¼Ï† â†” âˆ€ v â‰¤ w, Â¬v âŠ©[bv|fv] Ï† := by rfl

@[simp] lemma val_conj {Î“ : List (Semiformulaáµ¢ L Î¾ n)} :
    w âŠ©[bv|fv] â‹€Î“ â†” âˆ€ Ï† âˆˆ Î“, w âŠ©[bv|fv] Ï† :=
  match Î“ with
  |          [] => by simp
  |         [Ï†] => by simp
  | Ï† :: Ïˆ :: Î“ => by simp [val_conj (Î“ := Ïˆ :: Î“)]

@[simp] lemma val_disj {Î“ : List (Semiformulaáµ¢ L Î¾ n)} :
    w âŠ©[bv|fv] â‹Î“ â†” âˆƒ Ï† âˆˆ Î“, w âŠ©[bv|fv] Ï† :=
  match Î“ with
  |          [] => by simp
  |         [Ï†] => by simp
  | Ï† :: Ïˆ :: Î“ => by simp [val_disj (Î“ := Ïˆ :: Î“)]

lemma val_rew {bv : Fin nâ‚‚ â†’ Carrier ğ“š} {fv : Î¾â‚‚ â†’ Carrier ğ“š} {Ï‰ : Rew L Î¾â‚ nâ‚ Î¾â‚‚ nâ‚‚} {Ï† : Semiformulaáµ¢ L Î¾â‚ nâ‚} :
    w âŠ©[bv|fv] (Ï‰ â–¹ Ï†) â†”
    w âŠ©[fun x â†¦ (Ï‰ #x).relationalVal bv fv|fun x â†¦ (Ï‰ &x).relationalVal bv fv] Ï† := by
  induction Ï† using Semiformulaáµ¢.rec' generalizing nâ‚‚ w
  case hRel k R t =>
    simp only [Semiformulaáµ¢.rew_rel, val_rel]
    apply iff_of_eq; congr; funext x
    simp [Semiterm.relationalVal_rew Ï‰ (t x), Function.comp_def]
  case hImp Ï† Ïˆ ihÏ† ihÏˆ =>
    simp [*]
  case hAnd Ï† Ïˆ ihÏ† ihÏˆ => simp [ihÏ†, ihÏˆ]
  case hOr Ï† Ïˆ ihÏ† ihÏˆ => simp [ihÏ†, ihÏˆ]
  case hVerum => simp
  case hFalsum => simp
  case hAll Ï† ih =>
    have (x : Carrier ğ“š) : (fun i â†¦ (Ï‰.q #i).relationalVal (x :> bv) fv) = (x :> fun i â†¦ (Ï‰ #i).relationalVal bv fv) := by
      funext i; cases i using Fin.cases <;> simp
    simp [ih, this]
  case hEx Ï† ih =>
    have (x : Carrier ğ“š) : (fun i â†¦ (Ï‰.q #i).relationalVal (x :> bv) fv) = (x :> fun i â†¦ (Ï‰ #i).relationalVal bv fv) := by
      funext i; cases i using Fin.cases <;> simp
    simp [ih, this]

@[simp] lemma val_free {fv : â„• â†’ ğ“š.Carrier} {Ï† : SyntacticSemiformulaáµ¢ L (n + 1)} :
    v âŠ©[bv|â†‘x :>â‚™ fv] Rewriting.free Ï† â†” v âŠ©[bv <: x|fv] Ï† := by
  have : (fun i â†¦ Semiterm.relationalVal (L := L) bv (x :>â‚™ fv) (Rew.free #i)) = (bv <: x) := by
    ext i; cases i using Fin.lastCases <;> simp
  simp [Rewriting.free, val_rew, this]

lemma val_subst (w : Fin k â†’ Semiterm L Î¾ n) (Ï† : Semiformulaáµ¢ L Î¾ k) :
    v âŠ©[bv|fv] (Ï† â‡œ w) â†” v âŠ©[fun i â†¦ (w i).relationalVal bv fv|fv] Ï† := by
  simp [Rewriting.subst, val_rew]

@[simp] lemma val_substâ‚€ (Ï† : Formulaáµ¢ L Î¾) :
    v âŠ©[bv|fv] Ï†/[] â†” v âŠ©[![]|fv] Ï† := by
  simp [val_subst, Matrix.empty_eq]

@[simp] lemma val_substâ‚ (t : Semiterm L Î¾ n) (Ï† : Semiformulaáµ¢ L Î¾ 1) :
    v âŠ©[bv|fv] Ï†/[t] â†” v âŠ©[![t.relationalVal bv fv]|fv] Ï† := by
  simp [val_subst, Matrix.constant_eq_singleton]

@[simp] lemma val_emb {Ï† : Semisentenceáµ¢ L n} :
    v âŠ©[bv|fv] (Rewriting.emb Ï†) â†” v âŠ©[bv|Empty.elim] Ï† := by
  simp [Rewriting.emb, val_rew, Empty.eq_elim]

lemma Val.monotone
    {n} {bv : Fin n â†’ Carrier ğ“š} {fv : Î¾ â†’ Carrier ğ“š}
    (h : v â‰¤ w) {Ï†} : w âŠ©[bv|fv] Ï† â†’ v âŠ©[bv|fv] Ï† :=
  match Ï† with
  | .rel R v => ğ“š.rel_monotone h
  |        âŠ¤ => id
  |        âŠ¥ => id
  |    Ï† â‹ Ïˆ => by
    rintro âŸ¨hl, hrâŸ©
    exact âŸ¨hl.monotone h, hr.monotone hâŸ©
  |    Ï† â‹ Ïˆ => by
    rintro (hl | hr)
    Â· left; exact hl.monotone h
    Â· right; exact hr.monotone h
  |    Ï† â Ïˆ => fun Hw v' hvv' Hv' â†¦
    Hw v' (le_trans hvv' h) Hv'
  |    âˆ€' Ï† => fun Hw v' hvv' x â†¦ Hw v' (le_trans hvv' h) x
  |    âˆƒ' Ï† => by
    rintro âŸ¨x, HwâŸ©
    exact âŸ¨âŸ¨x, ğ“š.domain_antimonotone h x.propâŸ©, Hw.monotone hâŸ©

variable (ğ“š)

def Force (Ï† : Sentenceáµ¢ L) : Prop := âˆ€ w : ğ“š, w âŠ©[![]|Empty.elim] Ï†

instance : Semantics (Sentenceáµ¢ L) (RelationalKripkeModel L) := âŸ¨fun ğ“š Ï† â†¦ ğ“š.Force Ï†âŸ©

variable {ğ“š}

variable {Î› : Hilbertáµ¢ L}

open HilbertProofáµ¢ Semantics

lemma sound!_forces (w : ğ“š) (fv : â„• â†’ ğ“š.Carrier) (hfv : âˆ€ i, fv i âˆˆ ğ“š.Domain w) {Ï†} : ğ—œğ—»ğ˜Â¹ âŠ¢! Ï† â†’ w âŠ©[![]|fv] Ï†
  |     eaxm h => by
    have : âˆƒ Ïˆ, Axioms.EFQ Ïˆ = Ï† := by simpa [Hilbertáµ¢.Intuitionistic] using h
    rcases this with âŸ¨Ïˆ, rflâŸ©
    rintro v hvw âŸ¨âŸ©
  | mdp bÏ†Ïˆ bÏ† => by simpa using sound!_forces w fv hfv bÏ†Ïˆ w (by simp) (sound!_forces w fv hfv bÏ†)
  |      gen b => fun v hwv x â†¦ by
    simpa using sound!_forces v (x :>â‚™ fv) (by rintro (i | i) <;> simp [fun i â†¦ ğ“š.domain_antimonotone hwv (hfv i)]) b
  | verum => by simp
  | implyâ‚ Ï† Ïˆ => by
    intro wâ‚ hwâ‚wâ‚€ hwâ‚Ï† wâ‚‚ hwâ‚wâ‚‚ hwâ‚‚Ï†
    exact hwâ‚Ï†.monotone hwâ‚wâ‚‚
  | implyâ‚‚ Ï† Ïˆ Ï‡ => by
    intro wâ‚ hwâ‚wâ‚€ hwâ‚ wâ‚‚ hwâ‚‚wâ‚ hwâ‚‚ wâ‚ƒ hwâ‚ƒwâ‚‚ hwâ‚ƒ
    have : wâ‚ƒ âŠ©[![]|fv] Ïˆ := hwâ‚‚ wâ‚ƒ hwâ‚ƒwâ‚‚ hwâ‚ƒ
    exact hwâ‚ wâ‚ƒ (le_trans hwâ‚ƒwâ‚‚ hwâ‚‚wâ‚) hwâ‚ƒ wâ‚ƒ (by rfl) this
  | andâ‚ Ï† Ïˆ => by
    rintro v hvw âŸ¨hÏ†, hÏˆâŸ©
    exact hÏ†
  | andâ‚‚ Ï† Ïˆ => by
    rintro v hvw âŸ¨hÏ†, hÏˆâŸ©
    exact hÏˆ
  | andâ‚ƒ Ï† Ïˆ => by
    intro vâ‚ hvâ‚w hÏ† vâ‚‚ hvâ‚‚vâ‚ hÏˆ
    exact âŸ¨hÏ†.monotone hvâ‚‚vâ‚, hÏˆâŸ©
  | orâ‚ Ï† Ïˆ => by
    intro v hvw hÏ†
    left; exact hÏ†
  | orâ‚‚ Ï† Ïˆ => by
    intro v hvw hÏˆ
    right; exact hÏˆ
  | orâ‚ƒ Ï† Ïˆ Ï‡ => by
    rintro wâ‚ hwâ‚w hÏ†Ï‡ wâ‚‚ hwâ‚‚wâ‚ hÏˆÏ‡ wâ‚ƒ hwâ‚ƒwâ‚‚ (hÏ† | hÏˆ)
    Â· exact hÏ†Ï‡ wâ‚ƒ (le_trans hwâ‚ƒwâ‚‚ hwâ‚‚wâ‚) hÏ†
    Â· exact hÏˆÏ‡ wâ‚ƒ hwâ‚ƒwâ‚‚ hÏˆ
  | allâ‚ Ï† t => by
    rcases t.fvar_of_relational with âŸ¨x, rflâŸ©
    intro v hvw hÏ†
    suffices v âŠ©[![fv x]|fv] Ï† by simpa [val_subst, Matrix.constant_eq_singleton]
    simpa using hÏ† v (by rfl) âŸ¨fv x, ğ“š.domain_antimonotone hvw (hfv x)âŸ©
  | allâ‚‚ Ï† Ïˆ => by
    intro wâ‚ hwâ‚ H wâ‚‚ hwâ‚‚â‚ hÏ† wâ‚ƒ hwâ‚ƒâ‚‚ x
    exact H wâ‚ƒ (le_trans hwâ‚ƒâ‚‚ hwâ‚‚â‚) x wâ‚ƒ (by rfl) (by simpa using hÏ†.monotone hwâ‚ƒâ‚‚)
  | exâ‚ t Ï† => by
    rcases t.fvar_of_relational with âŸ¨x, rflâŸ©
    intro v hvw hÏ†
    have : v âŠ©[![fv x]|fv] Ï† := by simpa using hÏ†
    exact âŸ¨âŸ¨fv x, ğ“š.domain_antimonotone hvw (hfv x)âŸ©, by simpa using thisâŸ©
  | exâ‚‚ Ï† Ïˆ => by
    rintro wâ‚ hwâ‚ H wâ‚‚ hwâ‚‚â‚ âŸ¨x, hÏ†âŸ©
    simpa using H wâ‚‚ hwâ‚‚â‚ x wâ‚‚ (by rfl) hÏ†

lemma sound {T : Theoryáµ¢ (ğ—œğ—»ğ˜Â¹ : Hilbertáµ¢ L)} (b : T âŠ¢ Ï†) : ğ“š âŠ§* T â†’ ğ“š âŠ§ Ï† := fun H w â†¦ by
  rcases ğ“š.domain_nonempty w with âŸ¨x, hxâŸ©
  have : (Rewriting.emb '' T.theory) *âŠ¢[ğ—œğ—»ğ˜Â¹] â†‘Ï† := b
  rcases Entailment.Context.provable_iff.mp this with âŸ¨Î“, HÎ“, bâŸ©
  have : w âŠ©[![]|fun _ â†¦ x] â‹€Î“ â â†‘Ï† := sound!_forces (L := L) w (fun _ â†¦ x) (by simp [hx]) b.get
  have : w âŠ©[![]|fun _ : â„• â†¦ x] â†‘Ï† := by
    apply this w (by rfl)
    suffices âˆ€ Ï† âˆˆ Î“, w âŠ©[![]|fun _ â†¦ x] Ï† by simpa
    intro Ï† hÏ†
    rcases show âˆƒ x âˆˆ T.theory, â†‘x = Ï† by simpa using HÎ“ Ï† hÏ† with âŸ¨Ï†, hÏ†', rflâŸ©
    simpa using H.RealizeSet hÏ†' w
  simpa using this

instance (T : Theoryáµ¢ (ğ—œğ—»ğ˜Â¹ : Hilbertáµ¢ L)) : Sound T (Semantics.models (RelationalKripkeModel L) T) := âŸ¨fun b _ H â†¦ sound b HâŸ©

end RelationalKripkeModel

end LO.FirstOrder
