import Mathlib.Data.Fintype.List
import Logic.Vorspiel.BinaryRelations
import Logic.Modal.Standard.Kripke.Soundness
import Logic.Modal.Standard.Kripke.Filteration
import Logic.Modal.Standard.Kripke.Preservation

namespace LO.Modal.Standard

namespace Kripke

open System
open Kripke
open Formula

variable {Î± : Type u} [Inhabited Î±]

section Definability_and_Soundness

variable {F : Kripke.Frame}

abbrev TransitiveCWFFrameClass : FrameClass := { F | Transitive F âˆ§ ConverseWellFounded F }

private lemma trans_of_L : F# âŠ§* (ğ—Ÿ : AxiomSet Î±) â†’ Transitive F.Rel := by
  contrapose;
  intro hT; simp [Transitive] at hT;
  obtain âŸ¨wâ‚, wâ‚‚, râ‚â‚‚, wâ‚ƒ, râ‚‚â‚ƒ, nrâ‚â‚ƒâŸ© := hT;
  apply iff_not_validOnFrame.mpr;
  use (Axioms.L (atom default));
  constructor;
  . simp;
  . use (Î» w' _ => w' â‰  wâ‚‚ âˆ§ w' â‰  wâ‚ƒ), wâ‚;
    simp only [Kripke.Satisfies]; push_neg;
    constructor;
    . intro x hx h;
      by_cases hxâ‚‚ : x = wâ‚‚;
      . subst hxâ‚‚; simpa [Kripke.Satisfies] using h râ‚‚â‚ƒ;
      . by_cases hxâ‚ƒ : x = wâ‚ƒ <;> simp_all [Kripke.Satisfies, hxâ‚ƒ];
    . existsi wâ‚‚; simpa [Kripke.Satisfies];

private lemma cwf_of_L  : F# âŠ§* (ğ—Ÿ : AxiomSet Î±) â†’ ConverseWellFounded F.Rel := by
  contrapose;
  intro hCF;
  obtain âŸ¨X, âŸ¨x, _âŸ©, hXâ‚‚âŸ© := by simpa using ConverseWellFounded.iff_has_max.not.mp hCF;
  apply iff_not_validOnFrame.mpr;
  use (Axioms.L (atom default));
  constructor;
  . simp;
  . use (Î» w _ => w âˆ‰ X), x;
    simp only [Kripke.Satisfies]; push_neg;
    constructor;
    . intro y _;
      by_cases hys : y âˆˆ X
      . obtain âŸ¨z, _, RxzâŸ© := hXâ‚‚ y hys;
        simp_all;
        use z;
      . aesop;
    . obtain âŸ¨y, _, _âŸ© := hXâ‚‚ x (by assumption);
      use y;

private lemma L_of_trans_and_cwf : (Transitive F.Rel âˆ§ ConverseWellFounded F.Rel) â†’ F# âŠ§* (ğ—Ÿ : AxiomSet Î±) := by
  rintro âŸ¨hTrans, hWFâŸ©;
  simp [Axioms.L];
  intro p V w;
  simp only [Kripke.Satisfies.iff_models, Kripke.Satisfies];
  contrapose; push_neg;
  intro h;
  obtain âŸ¨z, rwz, hzâŸ© := h;
  obtain âŸ¨m, âŸ¨âŸ¨rwm, hmâŸ©, hmâ‚‚âŸ©âŸ© := hWF.has_min ({ x | (F.Rel w x) âˆ§ Â¬(Kripke.Satisfies âŸ¨F, VâŸ© x p) }) (by use z; simp_all)
  use m;
  constructor;
  . exact rwm;
  . constructor;
    . simp [flip] at hmâ‚‚;
      intro n rmn;
      apply not_imp_not.mp $ hmâ‚‚ n (hTrans rwm rmn);
      exact rmn;
    . exact hm;

lemma axiomL_defines : AxiomSet.DefinesKripkeFrameClass (Î± := Î±) ğ—Ÿ (TransitiveCWFFrameClass) := by
  intro F;
  constructor;
  . intro h;
    constructor;
    . exact trans_of_L h;
    . exact cwf_of_L h;
  . exact L_of_trans_and_cwf;


abbrev TransitiveIrreflexiveFrameClass : FrameClass := { F | Transitive F âˆ§ Irreflexive F }

/-
lemma TransitiveIrreflexiveFiniteFrameClass.nonempty : TransitiveIrreflexiveFrameClass.Nonempty.{0} := by
  use PointFrame;
  simp [Transitive, Irreflexive];
-/

lemma axiomL_finite_defines : AxiomSet.FinitelyDefinesKripkeFrameClass (Î± := Î±) ğ—Ÿ â†‘TransitiveIrreflexiveFrameClass := by
  intro F;
  constructor;
  . intro h;
    obtain âŸ¨hTrans, hCWFâŸ© := axiomL_defines.mp h;
    refine âŸ¨hTrans, ?irreflexiveâŸ©;
    . intro w;
      simpa using ConverseWellFounded.iff_has_max.mp hCWF {w} (by simp);
  . intro d;
    have âŸ¨hTrans, hIrreflâŸ© := d;
    apply axiomL_defines.mpr;
    constructor;
    . exact hTrans;
    . exact Finite.converseWellFounded_of_trans_irrefl' F.World_finite hTrans hIrrefl;

instance GL_sound : Sound (ğ†ğ‹ : DeductionParameter Î±) TransitiveIrreflexiveFrameClassêŸ³# := sound_of_finitely_defines axiomL_finite_defines

instance : System.Consistent (ğ†ğ‹ : DeductionParameter Î±) := consistent_of_finitely_defines.{u} axiomL_finite_defines $ by
  use PointFrame;
  simp [Transitive, Irreflexive];

end Definability_and_Soundness


section Completeness

open Formula (atom)
open Formula.Kripke
open MaximalConsistentTheory

variable [DecidableEq Î±]

noncomputable abbrev GLCanonicalFrame := CanonicalFrame (Î± := Î±) ğ†ğ‹

noncomputable abbrev GLCanonicalModel := CanonicalModel (Î± := Î±) ğ†ğ‹

lemma filter_truthlemma
  [Inhabited (Î›)-MCT] [Î›.IsNormal]
  {T : Theory Î±} [T.SubformulaClosed]
  {X Y : (CanonicalModel Î›).World} (hXY : filterEquiv _ T X Y := by aesop)
  {p : Formula Î±} (hp : p âˆˆ T := by aesop) : p âˆˆ X.theory â†” p âˆˆ Y.theory := by
  constructor;
  . intro h; exact truthlemma.mp $ hXY p hp |>.mp $ truthlemma.mpr h;
  . intro h; exact truthlemma.mp $ hXY p hp |>.mpr $ truthlemma.mpr h;

noncomputable abbrev GLFilteredFrame (p : Formula Î±) : Kripke.FiniteFrame where
  World := FilterEqvQuotient GLCanonicalModel ((ğ’® p).toSet)
  World_finite := by apply FilterEqvQuotient.finite; simp;
  Rel := Quotient.liftâ‚‚
    (Î» X Y =>
      (âˆ€ q âˆˆ â–¡''â»Â¹(ğ’® p), â–¡q âˆˆ X.theory â†’ q â‹ â–¡q âˆˆ Y.theory) âˆ§
      (âˆƒ r âˆˆ â–¡''â»Â¹(ğ’® p), â–¡r âˆ‰ X.theory âˆ§ â–¡r âˆˆ Y.theory)
    )
    (by
      intro Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ hX hY; simp;
      constructor;
      . rintro âŸ¨hâ‚, âŸ¨r, br_mem_sub, br_nmem_Xâ‚, br_mem_Yâ‚âŸ©âŸ©;
        constructor;
        . intro q bq_mem_sub bq_mem_Xâ‚‚;
          have bq_mem_Xâ‚ : â–¡q âˆˆ Xâ‚.theory := filter_truthlemma (by simpa) |>.mpr bq_mem_Xâ‚‚;
          have âŸ¨q_mem_Yâ‚, bq_mem_Yâ‚âŸ© := hâ‚ q bq_mem_sub bq_mem_Xâ‚;
          constructor;
          . exact filter_truthlemma (by simpa) |>.mp q_mem_Yâ‚;
          . exact filter_truthlemma (by simpa) |>.mp bq_mem_Yâ‚;
        . use r;
          refine âŸ¨br_mem_sub, ?br_nmem_Xâ‚‚, ?br_mem_Yâ‚‚âŸ©;
          . exact filter_truthlemma (by simpa) |>.not.mp br_nmem_Xâ‚;
          . exact filter_truthlemma (by simpa) |>.mp br_mem_Yâ‚;
      . rintro âŸ¨hâ‚, âŸ¨r, br_mem_sub, br_nmem_Xâ‚‚, br_mem_Yâ‚‚âŸ©âŸ©;
        constructor;
        . intro q bq_mem_sub bq_mem_Xâ‚‚;
          have bq_mem_Xâ‚‚ : â–¡q âˆˆ Xâ‚‚.theory := filter_truthlemma (by simpa) |>.mp bq_mem_Xâ‚‚;
          have âŸ¨q_mem_Yâ‚‚, bq_mem_Yâ‚‚âŸ© := hâ‚ q bq_mem_sub bq_mem_Xâ‚‚;
          constructor;
          . exact filter_truthlemma (by simpa) |>.mpr q_mem_Yâ‚‚;
          . exact filter_truthlemma (by simpa) |>.mpr bq_mem_Yâ‚‚;
        . use r;
          refine âŸ¨br_mem_sub, ?m, ?meâŸ©;
          . exact filter_truthlemma (by simpa) |>.not.mpr br_nmem_Xâ‚‚;
          . exact filter_truthlemma (by simpa) |>.mpr br_mem_Yâ‚‚;
    )

lemma GLFilteredFrame.def_rel {p : Formula Î±} {X Y : GLCanonicalFrame.World} :
  ((GLFilteredFrame p).Rel âŸ¦XâŸ§ âŸ¦YâŸ§) â†”
  (âˆ€ q âˆˆ â–¡''â»Â¹(ğ’® p), â–¡q âˆˆ X.theory â†’ q â‹ â–¡q âˆˆ Y.theory) âˆ§
  (âˆƒ r âˆˆ â–¡''â»Â¹(ğ’® p), â–¡r âˆ‰ X.theory âˆ§ â–¡r âˆˆ Y.theory) := by
  simp;

noncomputable abbrev GLFilteredModel (p : Formula Î±) : Kripke.Model Î± where
  Frame := GLFilteredFrame p
  Valuation := StandardFilterationValuation GLCanonicalModel ((ğ’® p).toSet)

variable {T : Theory Î±} [T.SubformulaClosed]
variable {p : Formula Î±}

lemma irreflexive_GLFilteredFrame : Irreflexive (GLFilteredFrame p).Rel := by
  intro QX;
  obtain âŸ¨X, hXâŸ© := Quotient.exists_rep QX; subst hX;
  simp_all;

lemma transitive_GLFilteredFrame : Transitive (GLFilteredFrame p).Rel := by
  intro QX QY QZ hXY hYZ;
  obtain âŸ¨X, hXâŸ© := Quotient.exists_rep QX; subst hX;
  obtain âŸ¨Y, hYâŸ© := Quotient.exists_rep QY; subst hY;
  obtain âŸ¨Z, hZâŸ© := Quotient.exists_rep QZ; subst hZ;
  have âŸ¨hXYâ‚, hXYâ‚‚âŸ© := hXY;
  have âŸ¨hYZâ‚, _âŸ© := hYZ;
  constructor;
  . intro q hq bq_mem_X;
    have âŸ¨_, bq_mem_YâŸ© := MaximalConsistentTheory.iff_mem_and.mp $ hXYâ‚ q hq bq_mem_X;
    exact hYZâ‚ q hq bq_mem_Y;
  . obtain âŸ¨r, hr, br_nmem_X, br_mem_YâŸ© := hXYâ‚‚;
    use r;
    constructor;
    . assumption;
    . constructor;
      . assumption;
      . have âŸ¨_, bq_mem_YâŸ© := MaximalConsistentTheory.iff_mem_and.mp $ hYZâ‚ r hr br_mem_Y;
        assumption;

open System System.FiniteContext in
private lemma GL_truthlemma.lemma1
  {q : Formula Î±}
  {X : (CanonicalModel ğ†ğ‹).World} (h : â–¡q âˆ‰ X.theory) : (ğ†ğ‹)-Consistent ({â–¡q, ~q} âˆª (â–¡''â»Â¹X.theory âˆª â–¡''â–¡''â»Â¹X.theory)) := by
  by_contra hC;
  obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp hC;
  have := toâ‚›! hÎ“â‚‚;
  have : ğ†ğ‹ âŠ¢! â‹€(Î“.remove (~q)) â‹ ~q âŸ¶ âŠ¥ := imply_left_remove_conj! (p := ~q) this;
  have : ğ†ğ‹ âŠ¢! â‹€(Î“.remove (~q)) âŸ¶ ~q âŸ¶ âŠ¥ := and_imply_iff_imply_imply'!.mp this;
  have : ğ†ğ‹ âŠ¢! â‹€(Î“.remove (~q)) âŸ¶ q := imp_trans''! this $ imp_trans''! (andâ‚‚'! neg_equiv!) dne!
  have : ğ†ğ‹ âŠ¢! â‹€((Î“.remove (~q)).remove (â–¡q)) â‹ â–¡q âŸ¶ q := imply_left_remove_conj! (p := â–¡q) this;
  have : ğ†ğ‹ âŠ¢! â‹€((Î“.remove (~q)).remove (â–¡q)) âŸ¶ (â–¡q âŸ¶ q) := and_imply_iff_imply_imply'!.mp this;
  have : ğ†ğ‹ âŠ¢! â–¡(â‹€(Î“.remove (~q)).remove (â–¡q)) âŸ¶ â–¡(â–¡q âŸ¶ q) := imply_box_distribute'! this;
  have : ğ†ğ‹ âŠ¢! â–¡(â‹€(Î“.remove (~q)).remove (â–¡q)) âŸ¶ â–¡q := imp_trans''! this axiomL!;
  have : ğ†ğ‹ âŠ¢! â‹€â–¡'(Î“.remove (~q)).remove (â–¡q) âŸ¶ â–¡q := imp_trans''! collect_box_conj! this;
  have : (â–¡'(Î“.remove (~q)).remove (â–¡q)) âŠ¢[ğ†ğ‹]! â–¡q := provable_iff.mpr this;
  have : X.theory *âŠ¢[ğ†ğ‹]! â–¡q := by
    apply Context.provable_iff.mpr;
    use (â–¡'List.remove (â–¡q) (List.remove (~q) Î“));
    constructor;
    . intro r hr; simp at hr;
      obtain âŸ¨s, hs, esâŸ© := hr; subst es;
      have âŸ¨s_mem', hsâ‚âŸ© := List.mem_remove_iff.mp hs;
      have âŸ¨s_mem, hsâ‚‚âŸ© := List.mem_remove_iff.mp s_mem';
      clear hs s_mem';
      have := hÎ“â‚ s s_mem;
      simp at this;
      rcases this with ((nsâ‚ | hsâ‚‚) | bs_mem | b);
      . contradiction;
      . contradiction;
      . assumption;
      . obtain âŸ¨t, ht, etâŸ© := b; subst et;
        apply membership_iff.mpr;
        apply axiomFour'!;
        apply membership_iff.mp;
        assumption;
    . assumption;
  have : â–¡q âˆˆ X.theory := membership_iff.mpr this;
  contradiction;

open Formula MaximalConsistentTheory in
lemma GL_truthlemma
  {p : Formula Î±} {X : (CanonicalModel ğ†ğ‹).World} {q : Formula Î±} (hq : q âˆˆ ğ’® p) :
  Satisfies (GLFilteredModel p) âŸ¦XâŸ§ q â†” q âˆˆ X.theory := by
  induction q using Formula.rec' generalizing X with
  | hbox q ih =>
    constructor;
    . contrapose;
      intro h;
      obtain âŸ¨Y, hYâŸ© := lindenbaum (Î› := ğ†ğ‹) (T := {â–¡q, ~q} âˆª (â–¡''â»Â¹X.theory âˆª â–¡''â–¡''â»Â¹X.theory)) $ GL_truthlemma.lemma1 h;
      simp [Satisfies];
      use âŸ¦YâŸ§;
      constructor;
      . apply GLFilteredFrame.def_rel.mpr;
        simp at hY;
        have âŸ¨hYâ‚, âŸ¨hYâ‚‚, hYâ‚ƒâŸ©âŸ© := hY;
        simp_all;
        constructor;
        . intro q _ bq_mem_X;
          constructor;
          . apply hYâ‚‚; simpa;
          . apply hYâ‚ƒ; simpa;
        . use q;
          simp_all;
          tauto;
      . apply ih (by aesop) |>.not.mpr;
        apply iff_mem_neg.mp;
        apply hY;
        simp;
    . intro bq_mem_X QY RXY;
      obtain âŸ¨Y, hYâŸ© := Quotient.exists_rep QY; subst hY;
      have âŸ¨hâ‚, _âŸ© := GLFilteredFrame.def_rel.mp RXY; simp at hâ‚;
      have âŸ¨q_mem_Y, _âŸ© := hâ‚ q hq bq_mem_X;
      exact ih (by aesop) |>.mpr q_mem_Y;
  | _ =>
    simp_all [Satisfies, StandardFilterationValuation];
    try aesop;

lemma exists_finite_frame : Â¬ğ”½êŸ³# âŠ§ p â†” âˆƒ F âˆˆ ğ”½.toFiniteFrameClass, Â¬F# âŠ§ p := by
  constructor;
  . simp;
  . rintro âŸ¨F, hFâ‚, hFâ‚‚âŸ©;
    simp; use F;
    constructor;
    . simp_all;
    . assumption;

private lemma GL_completeAux {p : Formula Î±} : TransitiveIrreflexiveFrameClass.{u}êŸ³# âŠ§ p â†’ ğ†ğ‹ âŠ¢! p := by
  contrapose;
  intro h;
  apply exists_finite_frame.mpr;
  use (GLFilteredFrame p);
  constructor;
  . exact âŸ¨transitive_GLFilteredFrame, irreflexive_GLFilteredFrameâŸ©;
  . simp [Formula.Kripke.ValidOnFrame, Formula.Kripke.ValidOnModel];
    obtain âŸ¨X, hXâŸ© := lindenbaum (Î› := ğ†ğ‹) (T := {~p}) (Theory.unprovable_iff_singleton_neg_consistent.mp h);
    use (GLFilteredModel p).Valuation, âŸ¦XâŸ§;
    apply GL_truthlemma (by simp) |>.not.mpr;
    apply MaximalConsistentTheory.iff_mem_neg.mp;
    simpa using hX;

instance GL_complete : Complete (ğ†ğ‹ : DeductionParameter Î±) TransitiveIrreflexiveFrameClass.{u}êŸ³# := âŸ¨GL_completeAuxâŸ©

instance : FiniteFrameProperty (Î± := Î±) ğ†ğ‹ TransitiveIrreflexiveFrameClass where

end Completeness

end Kripke

end LO.Modal.Standard
