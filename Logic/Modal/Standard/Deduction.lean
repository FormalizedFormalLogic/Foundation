import Logic.Modal.Standard.Formula
import Logic.Modal.Standard.HilbertStyle

namespace LO.Modal.Standard

variable {Î± : Type*} [DecidableEq Î±]

structure DeductionParameterRules where
  nec : Bool
  loeb : Bool
  henkin : Bool

namespace DeductionParameterRules

instance : LE DeductionParameterRules where
  le Râ‚ Râ‚‚ :=
    Râ‚.nec â‰¤ Râ‚‚.nec âˆ§
    Râ‚.loeb â‰¤ Râ‚‚.loeb âˆ§
    Râ‚.henkin â‰¤ Râ‚‚.henkin


variable {Râ‚ Râ‚‚ : DeductionParameterRules} (h : Râ‚ â‰¤ Râ‚‚ := by simpa)

@[simp] lemma nec_le (hNec : Râ‚.nec = true) : Râ‚‚.nec = true := by apply h.1; assumption;
@[simp] lemma loeb_le (hLoeb : Râ‚.loeb = true) : Râ‚‚.loeb = true := by apply h.2.1; assumption;
@[simp] lemma henkin_le (hHenkin : Râ‚.henkin = true) : Râ‚‚.henkin = true := by apply h.2.2; assumption;

end DeductionParameterRules

/--
  Parameter for deduction system.
-/
structure DeductionParameter (Î±) where
  axiomSet : AxiomSet Î±
  rules : DeductionParameterRules
notation "Ax(" ğ““ ")" => DeductionParameter.axiomSet ğ““

namespace DeductionParameter

variable (ğ““ ğ““â‚ ğ““â‚‚ : DeductionParameter Î±)

class HasNec where
  has_nec : ğ““.rules.nec = true := by rfl

class HasLoebRule where
  has_loeb : ğ““.rules.loeb = true := by rfl

class HasHenkinRule where
  has_henkin : ğ““.rules.henkin = true := by rfl

class HasNecOnly extends HasNec ğ““ where
  not_has_loeb : ğ““.rules.loeb = false := by rfl
  not_has_henkin : ğ““.rules.henkin = false := by rfl

class IncludeK where
  include_K : ğ— âŠ† Ax(ğ““) := by intro; aesop;

/--
  Deduction system of `L` is normal modal ğ““ogic.
-/
class Normal extends HasNecOnly ğ““, IncludeK ğ““ where

variable {ğ““}

@[simp] lemma normal_has_nec [ğ““.Normal] : ğ““.rules.nec = true := HasNec.has_nec
@[simp] lemma notmal_not_has_loeb [ğ““.Normal] : ğ““.rules.loeb = false := HasNecOnly.not_has_loeb
@[simp] lemma notmal_not_has_henkin [ğ““.Normal] : ğ““.rules.henkin = false := HasNecOnly.not_has_henkin

@[simp] -- TODO: more simple proof
lemma normal_rule [ğ““.Normal] : ğ““.rules = âŸ¨true, false, falseâŸ© := by
  nth_rw 1 [â†(normal_has_nec (ğ““ := ğ““))];
  nth_rw 1 [â†(notmal_not_has_loeb (ğ““ := ğ““))];
  nth_rw 1 [â†(notmal_not_has_henkin (ğ““ := ğ““))];

@[simp] lemma normal_rules [ğ““â‚.Normal] [ğ““â‚‚.Normal] : ğ““â‚.rules = ğ““â‚‚.rules := by simp;

def union (ğ““â‚ ğ““â‚‚ : DeductionParameter Î±) (_ : ğ““â‚.rules = ğ““â‚‚.rules := by first | assumption | simp) : DeductionParameter Î± where
  axiomSet := ğ““â‚.axiomSet âˆª ğ““â‚‚.axiomSet
  rules := ğ““â‚.rules
notation:50 ğ““â‚ " âŠ” " ğ““â‚‚ => DeductionParameter.union ğ““â‚ ğ““â‚‚

variable {ğ““â‚ ğ““â‚‚}

lemma union_left_rules (h : ğ““â‚.rules = ğ““â‚‚.rules) : (ğ““â‚ âŠ” ğ““â‚‚).rules = ğ““â‚.rules := by cases ğ““â‚; rfl

lemma union_right_rules (h : ğ““â‚.rules = ğ““â‚‚.rules) : (ğ““â‚ âŠ” ğ““â‚‚).rules = ğ““â‚‚.rules := by cases ğ““â‚‚; exact h;

lemma union_left_rules_normal [ğ““â‚.Normal] [ğ““â‚‚.Normal] : (ğ““â‚ âŠ” ğ““â‚‚).rules = ğ““â‚.rules := by apply union_left_rules;

lemma union_right_rules_normal [ğ““â‚.Normal] [ğ““â‚‚.Normal] : (ğ““â‚ âŠ” ğ““â‚‚).rules = ğ““â‚‚.rules := by apply union_right_rules;

end DeductionParameter


inductive Deduction (ğ““ : DeductionParameter Î±) : (Formula Î±) â†’ Type _
  | maxm {p}     : p âˆˆ Ax(ğ““) â†’ Deduction ğ““ p
  | mdp {p q}    : Deduction ğ““ (p âŸ¶ q) â†’ Deduction ğ““ p â†’ Deduction ğ““ q
  | nec {p}      : (ğ““.rules.nec = true) â†’ Deduction ğ““ p â†’ Deduction ğ““ (â–¡p)
  | loeb {p}     : (ğ““.rules.loeb = true) â†’ Deduction ğ““ (â–¡p âŸ¶ p) â†’ Deduction ğ““ p
  | henkin {p}   : (ğ““.rules.henkin = true) â†’ Deduction ğ““ (â–¡p âŸ· p) â†’ Deduction ğ““ p
  | verum        : Deduction ğ““ âŠ¤
  | implyâ‚ p q   : Deduction ğ““ (p âŸ¶ q âŸ¶ p)
  | implyâ‚‚ p q r : Deduction ğ““ ((p âŸ¶ q âŸ¶ r) âŸ¶ (p âŸ¶ q) âŸ¶ p âŸ¶ r)
  | andâ‚ p q     : Deduction ğ““ (p â‹ q âŸ¶ p)
  | andâ‚‚ p q     : Deduction ğ““ (p â‹ q âŸ¶ q)
  | andâ‚ƒ p q     : Deduction ğ““ (p âŸ¶ q âŸ¶ p â‹ q)
  | orâ‚ p q      : Deduction ğ““ (p âŸ¶ p â‹ q)
  | orâ‚‚ p q      : Deduction ğ““ (q âŸ¶ p â‹ q)
  | orâ‚ƒ p q r    : Deduction ğ““ ((p âŸ¶ r) âŸ¶ (q âŸ¶ r) âŸ¶ (p â‹ q âŸ¶ r))
  | dne p        : Deduction ğ““ (~~p âŸ¶ p)

namespace Deduction

open DeductionParameter

instance : System (Formula Î±) (DeductionParameter Î±) := âŸ¨DeductionâŸ©

variable {ğ““ ğ““â‚ ğ““â‚‚ : DeductionParameter Î±}

instance : System.Classical ğ““ where
  mdp := mdp
  verum := verum
  implyâ‚ := implyâ‚
  implyâ‚‚ := implyâ‚‚
  andâ‚ := andâ‚
  andâ‚‚ := andâ‚‚
  andâ‚ƒ := andâ‚ƒ
  orâ‚ := orâ‚
  orâ‚‚ := orâ‚‚
  orâ‚ƒ := orâ‚ƒ
  dne := dne

def maxm_subset
  (hRules : ğ““â‚.rules â‰¤ ğ““â‚‚.rules)
  (hAx : Ax(ğ““â‚) âŠ† Ax(ğ““â‚‚)) : (ğ““â‚ âŠ¢ p) â†’ (ğ““â‚‚ âŠ¢ p)
  | maxm h => maxm (hAx h)
  | mdp ihâ‚ ihâ‚‚  => mdp (maxm_subset hRules hAx ihâ‚) (maxm_subset hRules hAx ihâ‚‚)
  | nec b h      => nec (by apply hRules.1; assumption) $ maxm_subset hRules hAx h
  | loeb b h     => loeb (by apply hRules.2.1; assumption) $ maxm_subset hRules hAx h
  | henkin b h   => henkin (by apply hRules.2.2; assumption) $ maxm_subset hRules hAx h
  | verum        => verum
  | implyâ‚ _ _   => implyâ‚ _ _
  | implyâ‚‚ _ _ _ => implyâ‚‚ _ _ _
  | andâ‚ _ _    => andâ‚ _ _
  | andâ‚‚ _ _    => andâ‚‚ _ _
  | andâ‚ƒ _ _    => andâ‚ƒ _ _
  | orâ‚ _ _    => orâ‚ _ _
  | orâ‚‚ _ _    => orâ‚‚ _ _
  | orâ‚ƒ _ _ _  => orâ‚ƒ _ _ _
  | dne _        => dne _

lemma maxm_subset! (hRules : ğ““â‚.rules â‰¤ ğ““â‚‚.rules) (hAx : Ax(ğ““â‚) âŠ† Ax(ğ““â‚‚)) (h : ğ““â‚ âŠ¢! p) : ğ““â‚‚ âŠ¢! p := âŸ¨maxm_subset hRules hAx h.someâŸ©

@[simp]
lemma reducible_of_subset (hNec : ğ““â‚.rules â‰¤ ğ““â‚‚.rules) (hAx : Ax(ğ““â‚) âŠ† Ax(ğ““â‚‚) := by intro; aesop) : ğ““â‚ â‰¤â‚› ğ““â‚‚ := by
  intro p hp;
  apply maxm_subset! hNec hAx hp;

instance [HasNec ğ““] : System.Necessitation ğ““ where
  nec := nec HasNec.has_nec

instance [HasLoebRule ğ““] : System.LoebRule ğ““ where
  loeb := loeb HasLoebRule.has_loeb

instance [HasHenkinRule ğ““] : System.HenkinRule ğ““ where
  henkin := henkin HasHenkinRule.has_henkin

instance [IncludeK ğ““] : System.HasAxiomK ğ““ where
  K _ _ := maxm $ Set.mem_of_subset_of_mem (IncludeK.include_K) (by simp);

instance [Normal ğ““] : System.K ğ““ where

noncomputable def inducition!
  {motive  : (p : Formula Î±) â†’ ğ““ âŠ¢! p â†’ Sort*}
  (hMaxm   : âˆ€ {p}, (h : p âˆˆ Ax(ğ““)) â†’ motive p âŸ¨maxm hâŸ©)
  (hMdp    : âˆ€ {p q}, {hpq : ğ““ âŠ¢! p âŸ¶ q} â†’ {hp : ğ““ âŠ¢! p} â†’ motive (p âŸ¶ q) hpq â†’ motive p hp â†’ motive q âŸ¨mdp hpq.some hp.someâŸ©)
  (hNec    : (hasNec : ğ““.rules.nec) â†’ âˆ€ {p}, {hp : ğ““ âŠ¢! p} â†’ motive p hp â†’ motive (â–¡p) âŸ¨(nec hasNec hp.some)âŸ©)
  (hLoeb   : (hasLoeb : ğ““.rules.loeb) â†’ âˆ€ {p}, {hp : ğ““ âŠ¢! â–¡p âŸ¶ p} â†’ motive (â–¡p âŸ¶ p) hp â†’ motive p âŸ¨loeb hasLoeb hp.someâŸ©)
  (hHenkin : (hasHenkin : ğ““.rules.henkin) â†’ âˆ€ {p}, {hp : ğ““ âŠ¢! â–¡p âŸ· p} â†’ motive (â–¡p âŸ· p) hp â†’ motive p âŸ¨henkin hasHenkin hp.someâŸ©)
  (hVerum  : motive âŠ¤ âŸ¨verumâŸ©)
  (hImplyâ‚ : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p) $ âŸ¨implyâ‚ p qâŸ©)
  (hImplyâ‚‚ : âˆ€ {p q r}, motive ((p âŸ¶ q âŸ¶ r) âŸ¶ (p âŸ¶ q) âŸ¶ p âŸ¶ r) $ âŸ¨implyâ‚‚ p q râŸ©)
  (hConjâ‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ p) $ âŸ¨andâ‚ p qâŸ©)
  (hConjâ‚‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ q) $ âŸ¨andâ‚‚ p qâŸ©)
  (hConjâ‚ƒ  : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p â‹ q) $ âŸ¨andâ‚ƒ p qâŸ©)
  (hDisjâ‚  : âˆ€ {p q}, motive (p âŸ¶ p â‹ q) $ âŸ¨orâ‚ p qâŸ©)
  (hDisjâ‚‚  : âˆ€ {p q}, motive (q âŸ¶ p â‹ q) $ âŸ¨orâ‚‚ p qâŸ©)
  (hDisjâ‚ƒ  : âˆ€ {p q r}, motive ((p âŸ¶ r) âŸ¶ (q âŸ¶ r) âŸ¶ (p â‹ q âŸ¶ r)) $ âŸ¨orâ‚ƒ p q râŸ©)
  (hDne    : âˆ€ {p}, motive (~~p âŸ¶ p) $ âŸ¨dne pâŸ©)
  : âˆ€ {p}, (d : ğ““ âŠ¢! p) â†’ motive p d := by
  intro p d;
  induction d.some with
  | maxm h => exact hMaxm h
  | mdp hpq hp ihpq ihp => exact hMdp (ihpq âŸ¨hpqâŸ©) (ihp âŸ¨hpâŸ©)
  | nec has hp ihp => exact hNec has (ihp âŸ¨hpâŸ©)
  | loeb has hp ihp => exact hLoeb has (ihp âŸ¨hpâŸ©)
  | henkin has hp ihp => exact hHenkin has (ihp âŸ¨hpâŸ©)
  | _ => aesop

noncomputable def inducition_with_nec [HasNecOnly ğ““]
  {motive  : (p : Formula Î±) â†’ ğ““ âŠ¢ p â†’ Sort*}
  (hMaxm   : âˆ€ {p}, (h : p âˆˆ Ax(ğ““)) â†’ motive p (maxm h))
  (hMdp    : âˆ€ {p q}, (hpq : ğ““ âŠ¢ p âŸ¶ q) â†’ (hp : ğ““ âŠ¢ p) â†’ motive (p âŸ¶ q) hpq â†’ motive p hp â†’ motive q (mdp hpq hp))
  (hNec    : âˆ€ {p}, (hp : ğ““ âŠ¢ p) â†’ motive p hp â†’ motive (â–¡p) (nec HasNec.has_nec hp))
  (hVerum  : motive âŠ¤ verum)
  (hImplyâ‚ : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p) $ implyâ‚ p q)
  (hImplyâ‚‚ : âˆ€ {p q r}, motive ((p âŸ¶ q âŸ¶ r) âŸ¶ (p âŸ¶ q) âŸ¶ p âŸ¶ r) $ implyâ‚‚ p q r)
  (hConjâ‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ p) $ andâ‚ p q)
  (hConjâ‚‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ q) $ andâ‚‚ p q)
  (hConjâ‚ƒ  : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p â‹ q) $ andâ‚ƒ p q)
  (hDisjâ‚  : âˆ€ {p q}, motive (p âŸ¶ p â‹ q) $ orâ‚ p q)
  (hDisjâ‚‚  : âˆ€ {p q}, motive (q âŸ¶ p â‹ q) $ orâ‚‚ p q)
  (hDisjâ‚ƒ  : âˆ€ {p q r}, motive ((p âŸ¶ r) âŸ¶ (q âŸ¶ r) âŸ¶ (p â‹ q âŸ¶ r)) $ orâ‚ƒ p q r)
  (hDne    : âˆ€ {p}, motive (~~p âŸ¶ p) $ dne p)
  : âˆ€ {p}, (d : ğ““ âŠ¢ p) â†’ motive p d := by
  intro p d;
  induction d with
  | maxm h => exact hMaxm h
  | mdp hpq hp ihpq ihp => exact hMdp hpq hp ihpq ihp
  | nec _ hp ihp => exact hNec hp ihp
  | loeb => have : ğ““.rules.loeb = false := HasNecOnly.not_has_loeb; simp_all;
  | henkin => have : ğ““.rules.henkin = false := HasNecOnly.not_has_henkin; simp_all;
  | _ => aesop

noncomputable def inducition_with_nec! [HasNecOnly ğ““]
  {motive  : (p : Formula Î±) â†’ ğ““ âŠ¢! p â†’ Sort*}
  (hMaxm   : âˆ€ {p}, (h : p âˆˆ Ax(ğ““)) â†’ motive p âŸ¨maxm hâŸ©)
  (hMdp    : âˆ€ {p q}, {hpq : ğ““ âŠ¢! p âŸ¶ q} â†’ {hp : ğ““ âŠ¢! p} â†’ motive (p âŸ¶ q) hpq â†’ motive p hp â†’ motive q (hpq â¨€ hp))
  (hNec    : âˆ€ {p}, {hp : ğ““ âŠ¢! p} â†’ motive p hp â†’ motive (â–¡p) âŸ¨(nec HasNec.has_nec hp.some)âŸ©)
  (hVerum  : motive âŠ¤ âŸ¨verumâŸ©)
  (hImplyâ‚ : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p) $ âŸ¨implyâ‚ p qâŸ©)
  (hImplyâ‚‚ : âˆ€ {p q r}, motive ((p âŸ¶ q âŸ¶ r) âŸ¶ (p âŸ¶ q) âŸ¶ p âŸ¶ r) $ âŸ¨implyâ‚‚ p q râŸ©)
  (hConjâ‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ p) $ âŸ¨andâ‚ p qâŸ©)
  (hConjâ‚‚  : âˆ€ {p q}, motive (p â‹ q âŸ¶ q) $ âŸ¨andâ‚‚ p qâŸ©)
  (hConjâ‚ƒ  : âˆ€ {p q}, motive (p âŸ¶ q âŸ¶ p â‹ q) $ âŸ¨andâ‚ƒ p qâŸ©)
  (hDisjâ‚  : âˆ€ {p q}, motive (p âŸ¶ p â‹ q) $ âŸ¨orâ‚ p qâŸ©)
  (hDisjâ‚‚  : âˆ€ {p q}, motive (q âŸ¶ p â‹ q) $ âŸ¨orâ‚‚ p qâŸ©)
  (hDisjâ‚ƒ  : âˆ€ {p q r}, motive ((p âŸ¶ r) âŸ¶ (q âŸ¶ r) âŸ¶ (p â‹ q âŸ¶ r)) $ âŸ¨orâ‚ƒ p q râŸ©)
  (hDne    : âˆ€ {p}, motive (~~p âŸ¶ p) $ âŸ¨dne pâŸ©)
  : âˆ€ {p}, (d : ğ““ âŠ¢! p) â†’ motive p d := by
  intro p d;
  induction d using Deduction.inducition! with
  | hMaxm h => exact hMaxm h
  | hMdp ihpq ihp => exact hMdp ihpq ihp
  | hNec _ ihp => exact hNec ihp
  | hLoeb => have : ğ““.rules.loeb = false := HasNecOnly.not_has_loeb; simp_all;
  | hHenkin => have : ğ““.rules.henkin = false := HasNecOnly.not_has_henkin; simp_all;
  | _ => aesop

/-
instance : System.K (ğŠ : AxiomSet Î±) := K_of_subset_K (by rfl)

instance : System.K (ğŠ âˆª Î› : AxiomSet Î±) := K_of_subset_K

instance S4_of_subset_S4 (hS4 : ğ’ğŸ’ âŠ† Î› := by simp) : System.S4 (Î› : AxiomSet Î±) where
  K _ _   := Deduction.maxm $ Set.mem_of_subset_of_mem hS4 (by simp);
  T _     := Deduction.maxm $ Set.mem_of_subset_of_mem hS4 (by simp);
  Four _  := Deduction.maxm $ Set.mem_of_subset_of_mem hS4 (by simp);

instance : System.S4 (ğ’ğŸ’ : AxiomSet Î±) := S4_of_subset_S4 (by rfl)
-/

end Deduction


namespace DeductionParameter

open DeductionParameter

private abbrev NecOnly (Ax : AxiomSet Î±) : DeductionParameter Î± where
  axiomSet := Ax
  rules := âŸ¨true, false, falseâŸ©

protected abbrev K : DeductionParameter Î± := NecOnly ğ—
notation "ğŠ" => DeductionParameter.K
instance : Normal (Î± := Î±) ğŠ where


protected abbrev KT : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§)
notation "ğŠğ“" => DeductionParameter.KT
instance : Normal (Î± := Î±) ğŠğ“ where


protected abbrev KD : DeductionParameter Î± := NecOnly (ğ— âˆª ğ——)
notation "ğŠğƒ" => DeductionParameter.KD
instance : Normal (Î± := Î±) ğŠğƒ where


protected abbrev K4 : DeductionParameter Î± := NecOnly (ğ— âˆª ğŸ°)
notation "ğŠğŸ’" => DeductionParameter.K4
instance : Normal (Î± := Î±) ğŠğŸ’ where
instance : System.K4 (ğŠğŸ’ : DeductionParameter Î±) where
  Four _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev K5 : DeductionParameter Î± := NecOnly (ğ— âˆª ğŸ±)
notation "ğŠğŸ“" => DeductionParameter.K5
instance : Normal (Î± := Î±) ğŠğŸ“ where


protected abbrev S4 : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ°)
notation "ğ’ğŸ’" => DeductionParameter.S4
instance : Normal (Î± := Î±) ğ’ğŸ’ where
instance : System.S4 (ğ’ğŸ’ : DeductionParameter Î±) where
  T _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)
  Four _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev S5 : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ±)
notation "ğ’ğŸ“" => DeductionParameter.S5
instance : Normal (Î± := Î±) ğ’ğŸ“ where


protected abbrev KT4B : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ° âˆª ğ—•)
notation "ğŠğ“ğŸ’ğ" => DeductionParameter.KT4B
instance : Normal (Î± := Î±) ğŠğ“ğŸ’ğ where


protected abbrev S4Dot2 : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ° âˆª .ğŸ®)
notation "ğ’ğŸ’.ğŸ" => DeductionParameter.S4Dot2
instance : Normal (Î± := Î±) ğ’ğŸ’.ğŸ where


protected abbrev S4Dot3 : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ° âˆª .ğŸ¯)
notation "ğ’ğŸ’.ğŸ‘" => DeductionParameter.S4Dot3
instance : Normal (Î± := Î±) ğ’ğŸ’.ğŸ‘ where


protected abbrev S4Grz : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğŸ° âˆª ğ—šğ—¿ğ˜‡)
notation "ğ’ğŸ’ğ†ğ«ğ³" => DeductionParameter.S4Grz
instance : Normal (Î± := Î±) ğ’ğŸ’ğ†ğ«ğ³ where


protected abbrev Triv : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—§ âˆª ğ—§ğ—°)
notation "ğ“ğ«ğ¢ğ¯" => DeductionParameter.Triv
instance : Normal (Î± := Î±) ğ“ğ«ğ¢ğ¯ where
instance : System.Triv (ğ“ğ«ğ¢ğ¯ : DeductionParameter Î±) where
  T _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)
  Tc _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)

protected abbrev Ver : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—©ğ—²ğ—¿)
notation "ğ•ğğ«" => DeductionParameter.Ver
instance : Normal (Î± := Î±) ğ•ğğ« where
instance : System.Ver (ğ•ğğ« : DeductionParameter Î±) where
  Ver _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev GL : DeductionParameter Î± := NecOnly (ğ— âˆª ğ—Ÿ)
notation "ğ†ğ‹" => DeductionParameter.GL
instance : Normal (Î± := Î±) ğ†ğ‹ where
instance : System.GL (ğ†ğ‹ : DeductionParameter Î±) where
  L _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev K4H : DeductionParameter Î± := NecOnly (ğ— âˆª ğŸ° âˆª ğ—›)
notation "ğŠğŸ’ğ‡" => DeductionParameter.K4H
instance : Normal (Î± := Î±) ğŠğŸ’ğ‡ where
instance : System.K4H (ğŠğŸ’ğ‡ : DeductionParameter Î±) where
  Four _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)
  H _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev K4Loeb : DeductionParameter Î± where
  axiomSet := ğ— âˆª ğŸ°
  rules := âŸ¨true, true, falseâŸ©
notation "ğŠğŸ’(ğ‹)" => DeductionParameter.K4Loeb
instance : IncludeK (Î± := Î±) ğŠğŸ’(ğ‹) where
instance : HasNec (Î± := Î±) ğŠğŸ’(ğ‹) where
instance : HasLoebRule (Î± := Î±) ğŠğŸ’(ğ‹) where
instance : System.K4Loeb (ğŠğŸ’(ğ‹) : DeductionParameter Î±) where
  Four _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


protected abbrev K4Henkin : DeductionParameter Î± where
  axiomSet := ğ— âˆª ğŸ°
  rules := âŸ¨true, false, trueâŸ©
notation "ğŠğŸ’(ğ‡)" => DeductionParameter.K4Henkin
instance : IncludeK (Î± := Î±) ğŠğŸ’(ğ‡) where
instance : HasNec (Î± := Î±) ğŠğŸ’(ğ‡) where
instance : HasHenkinRule (Î± := Î±) ğŠğŸ’(ğ‡) where
instance : System.K4Henkin (ğŠğŸ’(ğ‡) : DeductionParameter Î±) where
  Four _ := Deduction.maxm $ Set.mem_of_subset_of_mem (by rfl) (by simp)


/-- Logic of Pure Necessitation -/
protected abbrev N : DeductionParameter Î± := NecOnly âˆ…
notation "ğ" => DeductionParameter.N

end DeductionParameter

open System

macro_rules | `(tactic| trivial) => `(tactic|
    first
    | apply verum!
    | apply implyâ‚!
    | apply implyâ‚!
    | apply implyâ‚‚!
    | apply andâ‚!
    | apply andâ‚‚!
    | apply andâ‚ƒ!
    | apply orâ‚!
    | apply orâ‚‚!
    | apply orâ‚ƒ!
  )

macro_rules | `(tactic| trivial) => `(tactic | apply dne!)

section Reducible

lemma normal_reducible
  {ğ““â‚ ğ““â‚‚ : DeductionParameter Î±} [ğ““â‚.Normal] [ğ““â‚‚.Normal]
  (hMaxm : âˆ€ {p : Formula Î±}, p âˆˆ Ax(ğ““â‚) â†’ ğ““â‚‚ âŠ¢! p) : (ğ““â‚ : DeductionParameter Î±) â‰¤â‚› ğ““â‚‚ := by
  apply System.reducible_iff.mpr;
  intro p h;
  induction h using Deduction.inducition_with_nec! with
  | hMaxm hp => exact hMaxm hp;
  | hMdp ihpq ihp => exact ihpq â¨€ ihp;
  | hNec ihp => exact nec! ihp;
  | _ => trivial;

lemma normal_reducible_subset {ğ““â‚ ğ““â‚‚ : DeductionParameter Î±} [ğ““â‚.Normal] [ğ““â‚‚.Normal]
  (hSubset : Ax(ğ““â‚) âŠ† Ax(ğ““â‚‚)) : (ğ““â‚ : DeductionParameter Î±) â‰¤â‚› ğ““â‚‚ := by
  apply normal_reducible;
  intro p hp;
  exact âŸ¨Deduction.maxm $ hSubset hpâŸ©;

lemma reducible_K_KT : (ğŠ : DeductionParameter Î±) â‰¤â‚› ğŠğ“ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_K_KD : (ğŠ : DeductionParameter Î±) â‰¤â‚› ğŠğƒ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_KT_S4 : (ğŠğ“ : DeductionParameter Î±) â‰¤â‚› ğ’ğŸ’ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_K4_S4 : (ğŠğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ’ğŸ’ := by apply normal_reducible_subset; intro; aesop;

lemma reducible_S4_S4Dot2 : (ğ’ğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ’ğŸ’.ğŸ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_S4_S4Dot3 : (ğ’ğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ’ğŸ’.ğŸ‘ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_S4_S4Grz : (ğ’ğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ’ğŸ’ğ†ğ«ğ³ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_K_GL : (ğŠ : DeductionParameter Î±) â‰¤â‚› ğ†ğ‹ := by apply normal_reducible_subset; simp only [Set.subset_union_left];

lemma reducible_K4_Triv : (ğŠğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ“ğ«ğ¢ğ¯ := by
  apply normal_reducible;
  intro p hp;
  rcases hp with (hK | hFour)
  . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
  . obtain âŸ¨_, _, eâŸ© := hFour; subst_vars; exact axiomFour!;

lemma reducible_K4_GL : (ğŠğŸ’ : DeductionParameter Î±) â‰¤â‚› ğ†ğ‹ := by
  apply normal_reducible;
  intro p hp;
  rcases hp with (hK | hFour)
  . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
  . obtain âŸ¨_, _, eâŸ© := hFour; subst_vars; exact axiomFour!;

-- Macintyre & Simmons (1973)
-- ğ†ğ‹ =â‚› ğŠğŸ’(ğ‹) =â‚› ğŠğŸ’(ğ‡) =â‚› ğŠğŸ’ğ‡
section GL

lemma reducible_GL_K4Loeb : (ğ†ğ‹ : DeductionParameter Î±) â‰¤â‚› ğŠğŸ’(ğ‹) := by
  apply System.reducible_iff.mpr;
  intro p h;
  induction h using Deduction.inducition! with
  | hMaxm hp =>
    rcases hp with (hK | hL)
    . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
    . obtain âŸ¨_, eâŸ© := hL; subst_vars; exact axiomL!;
  | hMdp ihpq ihp => exact ihpq â¨€ ihp;
  | hNec _ ihp => exact nec! ihp;
  | hLoeb _ ihp => exact loeb! ihp;
  | hHenkin => simp_all only [Bool.false_eq_true];
  | _ => trivial;

lemma reducible_K4Loeb_K4Henkin : (ğŠğŸ’(ğ‹) : DeductionParameter Î±) â‰¤â‚› ğŠğŸ’(ğ‡) := by
  apply System.reducible_iff.mpr;
  intro p h;
  induction h using Deduction.inducition! with
  | hMaxm hp =>
    rcases hp with (hK | hFour)
    . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
    . obtain âŸ¨_, eâŸ© := hFour; subst_vars; exact axiomFour!;
  | hMdp ihpq ihp => exact ihpq â¨€ ihp;
  | hNec _ ihp => exact nec! ihp;
  | hLoeb _ ihp => exact loeb! ihp;
  | hHenkin => simp_all only [Bool.false_eq_true];
  | _ => trivial;

lemma reducible_K4Henkin_K4H : (ğŠğŸ’(ğ‡) : DeductionParameter Î±) â‰¤â‚› ğŠğŸ’ğ‡ := by
  apply System.reducible_iff.mpr;
  intro p h;
  induction h using Deduction.inducition! with
  | hMaxm hp =>
    rcases hp with (hK | hFour)
    . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
    . obtain âŸ¨_, eâŸ© := hFour; subst_vars; exact axiomFour!;
  | hMdp ihpq ihp => exact ihpq â¨€ ihp;
  | hNec _ ihp => exact nec! ihp;
  | hHenkin _ ihp => exact henkin! ihp;
  | hLoeb => simp_all only [Bool.false_eq_true];
  | _ => trivial;

lemma reducible_K4Henkin_GL : (ğŠğŸ’ğ‡ : DeductionParameter Î±) â‰¤â‚› ğ†ğ‹ := by
  apply normal_reducible;
  intro p hp;
  rcases hp with (hK | hFour) | hH
  . obtain âŸ¨_, _, eâŸ© := hK; subst_vars; exact axiomK!;
  . obtain âŸ¨_, _, eâŸ© := hFour; subst_vars; exact axiomFour!;
  . obtain âŸ¨_, _, eâŸ© := hH; subst_vars; exact axiomH!;

lemma equivalent_GL_K4Loeb : (ğ†ğ‹ : DeductionParameter Î±) =â‚› ğŠğŸ’(ğ‹) := by
  apply Equiv.antisymm_iff.mpr;
  constructor;
  . exact reducible_GL_K4Loeb;
  . exact Reducible.trans (reducible_K4Loeb_K4Henkin) $ Reducible.trans reducible_K4Henkin_K4H reducible_K4Henkin_GL

end GL

end Reducible

-- Toolkit for completeness
section

variable {Î± : Type*} [DecidableEq Î±] [Inhabited Î±]

open System

def Theory.Consistent (ğ““ : DeductionParameter Î±) (T : Theory Î±) := âˆ€ {Î“ : List (Formula Î±)}, (âˆ€ p âˆˆ Î“, p âˆˆ T) â†’ ğ““ âŠ¬! Î“.conj' âŸ¶ âŠ¥
notation:max "(" ğ““ ")-Consistent " T:90 => Theory.Consistent ğ““ T

namespace Theory

variable {ğ““ : DeductionParameter Î±} {T : Theory Î±}

lemma self_Consistent [h : System.Consistent ğ““] : (ğ““)-Consistent Ax(ğ““) := by
  intro Î“ hÎ“;
  obtain âŸ¨q, hqâŸ© := h.exists_unprovable;
  by_contra hC;
  have : ğ““ âŠ¢! q := imp_trans''! hC efq! â¨€ (iff_provable_list_conj.mpr $ Î» _ h => âŸ¨Deduction.maxm $ hÎ“ _ hâŸ©);
  contradiction;

lemma def_not_Consistent : Â¬(ğ““)-Consistent T â†” âˆƒ Î“ : List (Formula Î±), (âˆ€ p âˆˆ Î“, p âˆˆ T) âˆ§ ğ““ âŠ¢! Î“.conj' âŸ¶ âŠ¥ := by simp [Consistent];

lemma union_Consistent : (ğ““)-Consistent (Tâ‚ âˆª Tâ‚‚) â†’ (ğ““)-Consistent Tâ‚ âˆ§ (ğ““)-Consistent Tâ‚‚ := by
  simp only [Consistent];
  intro h;
  constructor;
  . intro Î“ hÎ“; exact h (by intro p hp; simp; left; exact hÎ“ p hp);
  . intro Î“ hÎ“; exact h (by intro p hp; simp; right; exact hÎ“ p hp);

lemma union_not_Consistent : Â¬(ğ““)-Consistent Tâ‚ âˆ¨ Â¬(ğ““)-Consistent Tâ‚‚ â†’ Â¬(ğ““)-Consistent (Tâ‚ âˆª Tâ‚‚) := by
  contrapose;
  push_neg;
  exact union_Consistent;

lemma iff_insert_Consistent : (ğ““)-Consistent (insert p T) â†” âˆ€ {Î“ : List (Formula Î±)}, (âˆ€ q âˆˆ Î“, q âˆˆ T) â†’ ğ““ âŠ¬! p â‹ Î“.conj' âŸ¶ âŠ¥ := by
  constructor;
  . intro h Î“ hÎ“;
    by_contra hC;
    have : ğ““ âŠ¬! p â‹ List.conj' Î“ âŸ¶ âŠ¥ := implyLeft_cons_conj'!.not.mp $ @h (p :: Î“) (by
      rintro q hq;
      simp at hq;
      cases hq with
      | inl h => subst h; simp;
      | inr h => simp; right; exact hÎ“ q h;
    );
    contradiction;
  . intro h Î“ hÎ“;
    have := @h (Î“.remove p) (by
      intro q hq;
      have := by simpa using hÎ“ q $ List.mem_of_mem_remove hq;
      cases this with
      | inl h => simpa [h] using List.mem_remove_iff.mp hq;
      | inr h => assumption;
    );
    by_contra hC;
    have := imp_trans''! and_comm! $ imply_left_remove_conj'! (p := p) hC;
    contradiction;

lemma iff_insert_Inconsistent : Â¬(ğ““)-Consistent (insert p T) â†” âˆƒ Î“ : List (Formula Î±), (âˆ€ p âˆˆ Î“, p âˆˆ T) âˆ§ ğ““ âŠ¢! p â‹ Î“.conj' âŸ¶ âŠ¥ := by
  constructor;
  . contrapose; push_neg; apply iff_insert_Consistent.mpr;
  . contrapose; push_neg; apply iff_insert_Consistent.mp;

lemma provable_iff_insert_neg_not_Consistent : T *âŠ¢[ğ““]! p â†” Â¬(ğ““)-Consistent (insert (~p) T) := by
  constructor;
  . intro h;
    apply iff_insert_Inconsistent.mpr;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    existsi Î“;
    constructor;
    . exact hÎ“â‚;
    . apply and_imply_iff_imply_imply'!.mpr;
      apply imp_swap'!;
      exact imp_trans''! hÎ“â‚‚ dni!;
  . intro h;
    apply Context.provable_iff.mpr;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := iff_insert_Inconsistent.mp h;
    existsi Î“;
    constructor;
    . exact hÎ“â‚;
    . exact imp_trans''! (imp_swap'! $ and_imply_iff_imply_imply'!.mp hÎ“â‚‚) dne!;

lemma unprovable_iff_insert_neg_Consistent : T *âŠ¬[ğ““]! p â†” (ğ““)-Consistent (insert (~p) T) := by
  constructor;
  . contrapose; simp [not_not]; apply provable_iff_insert_neg_not_Consistent.mpr;
  . contrapose; simp [not_not]; apply provable_iff_insert_neg_not_Consistent.mp;

lemma neg_provable_iff_insert_not_Consistent : T *âŠ¢[ğ““]! ~p â†” Â¬(ğ““)-Consistent (insert (p) T) := by
  constructor;
  . intro h;
    apply iff_insert_Inconsistent.mpr;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    existsi Î“;
    constructor;
    . assumption;
    . apply and_imply_iff_imply_imply'!.mpr;
      apply imp_swap'!;
      exact hÎ“â‚‚;
  . intro h;
    apply Context.provable_iff.mpr;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := iff_insert_Inconsistent.mp h;
    existsi Î“;
    constructor;
    . exact hÎ“â‚;
    . exact imp_swap'! $ and_imply_iff_imply_imply'!.mp hÎ“â‚‚;

lemma neg_unprovable_iff_insert_Consistent : T *âŠ¬[ğ““]! ~p â†” (ğ““)-Consistent (insert (p) T) := by
  constructor;
  . contrapose; simp [not_not]; apply neg_provable_iff_insert_not_Consistent.mpr;
  . contrapose; simp [not_not]; apply neg_provable_iff_insert_not_Consistent.mp;

variable (hConsis : (ğ““)-Consistent T)

lemma unprovable_either : Â¬(T *âŠ¢[ğ““]! p âˆ§ T *âŠ¢[ğ““]! ~p) := by
  by_contra hC;
  have âŸ¨hCâ‚, hCâ‚‚âŸ© := hC;
  have : T *âŠ¢[ğ““]! âŠ¥ := hCâ‚‚ â¨€ hCâ‚;
  obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp this;
  have : ğ““ âŠ¬! List.conj' Î“ âŸ¶ âŠ¥ := hConsis hÎ“â‚;
  have : ğ““ âŠ¢! List.conj' Î“ âŸ¶ âŠ¥ := FiniteContext.toâ‚›! hÎ“â‚‚;
  contradiction;

lemma not_provable_falsum : T *âŠ¬[ğ““]! âŠ¥ := by
  by_contra hC;
  obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp $ hC;
  have : ğ““ âŠ¬! List.conj' Î“ âŸ¶ âŠ¥ := hConsis hÎ“â‚;
  have : ğ““ âŠ¢! List.conj' Î“ âŸ¶ âŠ¥ := hÎ“â‚‚;
  contradiction;

lemma not_mem_falsum_of_Consistent : âŠ¥ âˆ‰ T := by
  by_contra hC;
  have : ğ““ âŠ¬! âŠ¥ âŸ¶ âŠ¥ := hConsis (Î“ := [âŠ¥]) (by simpa);
  have : ğ““ âŠ¢! âŠ¥ âŸ¶ âŠ¥ := efq!;
  contradiction;

lemma either_consistent (p) : (ğ““)-Consistent (insert p T) âˆ¨ (ğ““)-Consistent (insert (~p) T) := by
  by_contra hC; push_neg at hC;
  obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := iff_insert_Inconsistent.mp hC.1;
  obtain âŸ¨Î”, hÎ”â‚, hÎ”â‚‚âŸ© := iff_insert_Inconsistent.mp hC.2;

  replace hÎ“â‚‚ := neg_equiv'!.mp hÎ“â‚‚;
  replace hÎ”â‚‚ := neg_equiv'!.mp hÎ”â‚‚;
  have : ğ““ âŠ¢! Î“.conj' â‹ Î”.conj' âŸ¶ âŠ¥ := demorganâ‚'! $ orâ‚ƒ'''! (imp_trans''! (imply_of_not_or'! $ demorganâ‚„'! hÎ“â‚‚) orâ‚!) (imp_trans''! (imply_of_not_or'! $ demorganâ‚„'! hÎ”â‚‚) orâ‚‚!) lem!;
  have : ğ““ âŠ¬! Î“.conj' â‹ Î”.conj' âŸ¶ âŠ¥ := unprovable_imp_trans''! imply_left_concat_conj'! (hConsis (by
    simp;
    intro q hq;
    rcases hq with hÎ“ | hÎ”
    . exact hÎ“â‚ _ hÎ“;
    . exact hÎ”â‚ _ hÎ”;
  ));
  contradiction;

lemma exists_maximal_Consistent_theory
  : âˆƒ Z, (ğ““)-Consistent Z âˆ§ T âŠ† Z âˆ§ âˆ€ U, (ğ““)-Consistent U â†’ Z âŠ† U â†’ U = Z
  := zorn_subset_nonempty { T : Theory Î± | (ğ““)-Consistent T } (by
    intro c hc chain hnc;
    existsi (â‹ƒâ‚€ c);
    simp only [Consistent, Set.mem_setOf_eq, Set.mem_sUnion];
    constructor;
    . intro Î“;
      by_contra hC;
      obtain âŸ¨hÎ“s, hÎ“dâŸ© := by simpa using hC;
      obtain âŸ¨U, hUc, hUsâŸ© :=
        Set.subset_mem_chain_of_finite c hnc chain
          (s := â†‘Î“.toFinset) (by simp)
          (by intro p hp; simp_all);
      simp [List.coe_toFinset] at hUs;
      have : (ğ““)-Consistent U := hc hUc;
      have : Â¬(ğ““)-Consistent U := by
        simp only [Consistent, not_forall, not_not, exists_prop];
        existsi Î“;
        constructor;
        . intro p hp; exact hUs hp;
        . assumption;
      contradiction;
    . intro s a;
      exact Set.subset_sUnion_of_mem a;
  ) T hConsis
protected alias lindenbaum := exists_maximal_Consistent_theory

open Classical in
lemma intro_union_Consistent
  (h : âˆ€ {Î“â‚ Î“â‚‚ : List (Formula Î±)}, (âˆ€ p âˆˆ Î“â‚, p âˆˆ Tâ‚) â†’ (âˆ€ p âˆˆ Î“â‚‚, p âˆˆ Tâ‚‚) â†’ ğ““ âŠ¬! Î“â‚.conj' â‹ Î“â‚‚.conj' âŸ¶ âŠ¥) : (ğ““)-Consistent (Tâ‚ âˆª Tâ‚‚) := by
  intro Î” hÎ”;
  simp at hÎ”;
  let Î”â‚ := (Î”.filter (Â· âˆˆ Tâ‚));
  let Î”â‚‚ := (Î”.filter (Â· âˆˆ Tâ‚‚));
  have : ğ““ âŠ¬! Î”â‚.conj' â‹ Î”â‚‚.conj' âŸ¶ âŠ¥ := @h Î”â‚ Î”â‚‚ (by intro _ h; simpa using List.of_mem_filter h) (by intro _ h; simpa using List.of_mem_filter h);
  exact unprovable_imp_trans''! (by
    apply FiniteContext.deduct'!;
    apply iff_provable_list_conj.mpr;
    intro q hq;
    cases (hÎ” q hq);
    . exact iff_provable_list_conj.mp (andâ‚'! FiniteContext.id!) q $ List.mem_filter_of_mem hq (by simpa);
    . exact iff_provable_list_conj.mp (andâ‚‚'! FiniteContext.id!) q $ List.mem_filter_of_mem hq (by simpa);
  ) this;

end Theory

structure MaximalConsistentTheory (ğ““ : DeductionParameter Î±) where
  theory : Theory Î±
  consistent : (ğ““)-Consistent theory
  maximal : âˆ€ {U}, theory âŠ‚ U â†’ Â¬(ğ““)-Consistent U

notation "(" ğ““ ")-MCT" => MaximalConsistentTheory ğ““

namespace MaximalConsistentTheory

open Theory

variable {ğ““ : DeductionParameter Î±}
variable {Î© Î©â‚ Î©â‚‚ : (ğ““)-MCT}
variable {p : Formula Î±}

lemma exists_maximal_Lconsistented_theory (consisT : (ğ““)-Consistent T) : âˆƒ Î© : (ğ““)-MCT, (T âŠ† Î©.theory) := by
  have âŸ¨Î©, hÎ©â‚, hÎ©â‚‚, hÎ©â‚ƒâŸ© := Theory.lindenbaum consisT;
  existsi âŸ¨
    Î©,
    hÎ©â‚,
    by
      rintro U âŸ¨hUâ‚, hUâ‚‚âŸ©;
      by_contra hC;
      have : U = Î© := hÎ©â‚ƒ U hC hUâ‚;
      subst_vars;
      simp at hUâ‚‚,
  âŸ©;
  exact hÎ©â‚‚;

alias lindenbaum := exists_maximal_Lconsistented_theory

noncomputable instance [System.Consistent ğ““] : Inhabited (ğ““)-MCT := âŸ¨lindenbaum self_Consistent |>.chooseâŸ©

lemma either_mem (Î© : (ğ““)-MCT) (p) : p âˆˆ Î©.theory âˆ¨ ~p âˆˆ Î©.theory := by
  by_contra hC; push_neg at hC;
  cases either_consistent Î©.consistent p with
  | inl h => have := Î©.maximal (Set.ssubset_insert hC.1); contradiction;
  | inr h => have := Î©.maximal (Set.ssubset_insert hC.2); contradiction;

lemma maximal' {p : Formula Î±} (hp : p âˆ‰ Î©.theory) : Â¬(ğ““)-Consistent (insert p Î©.theory) := Î©.maximal (Set.ssubset_insert hp)

lemma membership_iff : (p âˆˆ Î©.theory) â†” (Î©.theory *âŠ¢[ğ““]! p) := by
  constructor;
  . intro h; exact Context.by_axm! h;
  . intro hp;
    suffices ~p âˆ‰ Î©.theory by apply or_iff_not_imp_right.mp $ (either_mem Î© p); assumption;
    by_contra hC;
    have hnp : Î©.theory *âŠ¢[ğ““]! ~p := Context.by_axm! hC;
    have := hnp â¨€ hp;
    have := not_provable_falsum Î©.consistent;
    contradiction;

lemma subset_axiomset : Ax(ğ““) âŠ† Î©.theory := by
  intro p hp;
  apply membership_iff.mpr;
  apply Context.of!;
  exact âŸ¨Deduction.maxm (by aesop)âŸ©

@[simp] lemma not_mem_falsum : âŠ¥ âˆ‰ Î©.theory := not_mem_falsum_of_Consistent Î©.consistent

@[simp] lemma mem_verum : âŠ¤ âˆˆ Î©.theory := by apply membership_iff.mpr; apply verum!;

@[simp]
lemma unprovable_falsum : Î©.theory *âŠ¬[ğ““]! âŠ¥ := by apply membership_iff.not.mp; simp

lemma iff_mem_neg : (~p âˆˆ Î©.theory) â†” (p âˆ‰ Î©.theory) := by
  constructor;
  . intro hnp;
    by_contra hp;
    replace hp := membership_iff.mp hp;
    replace hnp := membership_iff.mp hnp;
    have : Î©.theory *âŠ¢[ğ““]! âŠ¥ := hnp â¨€ hp;
    have : Î©.theory *âŠ¬[ğ““]! âŠ¥ := unprovable_falsum;
    contradiction;
  . intro hp;
    have := provable_iff_insert_neg_not_Consistent.not.mp $ membership_iff.not.mp hp;
    have := (not_imp_not.mpr $ Î©.maximal (U := insert (~p) Î©.theory)) this;
    simp [Set.ssubset_def] at this;
    apply this;
    simp;

lemma iff_mem_negneg : (~~p âˆˆ Î©.theory) â†” (p âˆˆ Î©.theory) := by
  simp [membership_iff];
  constructor;
  . intro h; exact dne'! h;
  . intro h; exact dni'! h;

@[simp]
lemma iff_mem_imp : ((p âŸ¶ q) âˆˆ Î©.theory) â†” (p âˆˆ Î©.theory) â†’ (q âˆˆ Î©.theory) := by
  constructor;
  . intro hpq hp;
    replace dpq := membership_iff.mp hpq;
    replace dp  := membership_iff.mp hp;
    apply membership_iff.mpr;
    exact dpq â¨€ dp;
  . intro h;
    replace h : p âˆ‰ Î©.theory âˆ¨ q âˆˆ Î©.theory := or_iff_not_imp_left.mpr (by simpa using h);
    cases h with
    | inl h =>
      apply membership_iff.mpr;
      exact efq_of_neg! $ membership_iff.mp $ iff_mem_neg.mpr h;
    | inr h =>
      apply membership_iff.mpr;
      exact implyâ‚! â¨€ (membership_iff.mp h)

@[simp]
lemma iff_mem_and : ((p â‹ q) âˆˆ Î©.theory) â†” (p âˆˆ Î©.theory) âˆ§ (q âˆˆ Î©.theory) := by
  constructor;
  . intro hpq;
    replace hpq := membership_iff.mp hpq;
    constructor;
    . apply membership_iff.mpr; exact andâ‚'! hpq;
    . apply membership_iff.mpr; exact andâ‚‚'! hpq;
  . rintro âŸ¨hp, hqâŸ©;
    apply membership_iff.mpr;
    exact andâ‚ƒ'! (membership_iff.mp hp) (membership_iff.mp hq);

@[simp]
lemma iff_mem_or : ((p â‹ q) âˆˆ Î©.theory) â†” (p âˆˆ Î©.theory) âˆ¨ (q âˆˆ Î©.theory) := by
  constructor;
  . intro hpq;
    replace hpq := membership_iff.mp hpq;
    by_contra hC; simp [not_or] at hC;
    have âŸ¨hp, hqâŸ© := hC;
    replace hp := membership_iff.mp $ iff_mem_neg.mpr hp;
    replace hq := membership_iff.mp $ iff_mem_neg.mpr hq;
    have : Î©.theory *âŠ¢[ğ““]! âŠ¥ := orâ‚ƒ'''! hp hq hpq;
    have : Î©.theory *âŠ¬[ğ““]! âŠ¥ := unprovable_falsum;
    contradiction;
  . rintro (hp | hq);
    . apply membership_iff.mpr;
      exact orâ‚'! (membership_iff.mp hp);
    . apply membership_iff.mpr;
      exact orâ‚‚'! (membership_iff.mp hq);

lemma iff_congr : (Î©.theory *âŠ¢[ğ““]! (p âŸ· q)) â†’ ((p âˆˆ Î©.theory) â†” (q âˆˆ Î©.theory)) := by
  intro hpq;
  constructor;
  . intro hp; exact iff_mem_imp.mp (membership_iff.mpr $ andâ‚'! hpq) hp;
  . intro hq; exact iff_mem_imp.mp (membership_iff.mpr $ andâ‚‚'! hpq) hq;

lemma mem_dn_iff : (p âˆˆ Î©.theory) â†” (~~p âˆˆ Î©.theory) := iff_congr $ dn!

lemma equality_def : Î©â‚ = Î©â‚‚ â†” Î©â‚.theory = Î©â‚‚.theory := by
  constructor;
  . intro h; cases h; rfl;
  . intro h; cases Î©â‚; cases Î©â‚‚; simp_all;

lemma intro_equality {h : âˆ€ p, p âˆˆ Î©â‚.theory â†’ p âˆˆ Î©â‚‚.theory} : Î©â‚ = Î©â‚‚ := by
  exact equality_def.mpr $ Set.eq_of_subset_of_subset
    (by intro p hp; exact h p hp)
    (by
      intro p;
      contrapose;
      intro hp;
      apply iff_mem_neg.mp;
      apply h;
      apply iff_mem_neg.mpr hp;
    )

lemma neg_imp (h : q âˆˆ Î©â‚‚.theory â†’ p âˆˆ Î©â‚.theory) : (~p âˆˆ Î©â‚.theory) â†’ (~q âˆˆ Î©â‚‚.theory) := by
  contrapose;
  intro hq;
  apply iff_mem_neg.mp;
  apply mem_dn_iff.mp;
  apply h;
  exact mem_dn_iff.mpr $ iff_mem_neg.mpr hq;

lemma neg_iff (h : p âˆˆ Î©â‚.theory â†” q âˆˆ Î©â‚‚.theory) : (~p âˆˆ Î©â‚.theory) â†” (~q âˆˆ Î©â‚‚.theory) := âŸ¨neg_imp $ h.mpr, neg_imp $ h.mpâŸ©

-- These lemmata require ğ““ normality
section Normal

variable [ğ““.Normal]

lemma iff_mem_multibox : (â–¡^[n]p âˆˆ Î©.theory) â†” (âˆ€ {Î©' : (ğ““)-MCT}, (â–¡''â»Â¹^[n]Î©.theory âŠ† Î©'.theory) â†’ (p âˆˆ Î©'.theory)) := by
  constructor;
  . intro hp Î©' hÎ©'; apply hÎ©'; simpa;
  . contrapose;
    push_neg;
    intro hp;
    obtain âŸ¨Î©', hÎ©'âŸ© := lindenbaum (ğ““ := ğ““) (T := insert (~p) (â–¡''â»Â¹^[n]Î©.theory)) (by
      apply unprovable_iff_insert_neg_Consistent.mp;
      by_contra hC;
      obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp hC;
      have : ğ““ âŠ¢! â–¡^[n]Î“.conj' âŸ¶ â–¡^[n]p := imply_multibox_distribute'! hÎ“â‚‚;
      have : ğ““ âŠ¬! â–¡^[n]Î“.conj' âŸ¶ â–¡^[n]p := by
        have := Context.provable_iff.not.mp $ membership_iff.not.mp hp;
        push_neg at this;
        have : ğ““ âŠ¬! (â–¡'^[n]Î“ : List (Formula Î±)).conj' âŸ¶ â–¡^[n]p := FiniteContext.provable_iff.not.mp $ this (â–¡'^[n]Î“) (by
          intro q hq;
          obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© := by simpa using hq;
          subst hrâ‚‚;
          simpa using hÎ“â‚ r hrâ‚;
        );
        revert this;
        contrapose;
        simp [neg_neg];
        exact imp_trans''! collect_multibox_conj'!;
      contradiction;
    );
    existsi Î©';
    constructor;
    . exact Set.Subset.trans (by simp_all) hÎ©';
    . apply iff_mem_neg.mp;
      apply hÎ©';
      simp only [Set.mem_insert_iff, true_or]
lemma iff_mem_box : (â–¡p âˆˆ Î©.theory) â†” (âˆ€ {Î©' : (ğ““)-MCT}, (â–¡''â»Â¹Î©.theory âŠ† Î©'.theory) â†’ (p âˆˆ Î©'.theory)) := iff_mem_multibox (n := 1)

lemma multibox_dn_iff : (â–¡^[n](~~p) âˆˆ Î©.theory) â†” (â–¡^[n]p âˆˆ Î©.theory) := by
  simp only [iff_mem_multibox];
  constructor;
  . intro h Î© hÎ©; exact iff_mem_negneg.mp $ h hÎ©;
  . intro h Î© hÎ©; exact iff_mem_negneg.mpr $ h hÎ©;

lemma box_dn_iff : (â–¡~~p âˆˆ Î©.theory) â†” (â–¡p âˆˆ Î©.theory) := multibox_dn_iff (n := 1)

lemma mem_multibox_dual : â–¡^[n]p âˆˆ Î©.theory â†” ~(â—‡^[n](~p)) âˆˆ Î©.theory := by
  simp [membership_iff];
  constructor;
  . intro h;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    apply Context.provable_iff.mpr;
    existsi Î“;
    constructor;
    . assumption;
    . exact FiniteContext.provable_iff.mpr $ imp_trans''! (FiniteContext.provable_iff.mp hÎ“â‚‚) (andâ‚'! multibox_duality!);
  . intro h;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    apply Context.provable_iff.mpr;
    existsi Î“;
    constructor;
    . assumption;
    . exact FiniteContext.provable_iff.mpr $ imp_trans''! (FiniteContext.provable_iff.mp hÎ“â‚‚) (andâ‚‚'! multibox_duality!);
lemma mem_box_dual : â–¡p âˆˆ Î©.theory â†” (~(â—‡(~p)) âˆˆ Î©.theory) := mem_multibox_dual (n := 1)

-- lemma multidia_dn_iff : (â—‡^[n](~~p) âˆˆ Î©.theory) â†” (â—‡^[n]p âˆˆ Î©.theory) := by sorry

lemma dia_dn_iff : (â—‡~~p âˆˆ Î©.theory) â†” (â—‡p) âˆˆ Î©.theory := neg_iff box_dn_iff -- TODO: multidia_dn_iff (n := 1)

lemma mem_multidia_dual : â—‡^[n]p âˆˆ Î©.theory â†” ~(â–¡^[n](~p)) âˆˆ Î©.theory := by
  simp [membership_iff];
  constructor;
  . intro h;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    apply Context.provable_iff.mpr;
    existsi Î“;
    constructor;
    . assumption;
    . exact FiniteContext.provable_iff.mpr $ imp_trans''! (FiniteContext.provable_iff.mp hÎ“â‚‚) (andâ‚'! multidia_duality!);
  . intro h;
    obtain âŸ¨Î“, hÎ“â‚, hÎ“â‚‚âŸ© := Context.provable_iff.mp h;
    apply Context.provable_iff.mpr;
    existsi Î“;
    constructor;
    . assumption;
    . exact FiniteContext.provable_iff.mpr $ imp_trans''! (FiniteContext.provable_iff.mp hÎ“â‚‚) (andâ‚‚'! multidia_duality!);
lemma mem_dia_dual : â—‡p âˆˆ Î©.theory â†” (~(â–¡(~p)) âˆˆ Î©.theory) := mem_multidia_dual (n := 1)

lemma multibox_multidia : (âˆ€ {p : Formula Î±}, (â–¡^[n]p âˆˆ Î©â‚.theory â†’ p âˆˆ Î©â‚‚.theory)) â†” (âˆ€ {p : Formula Î±}, (p âˆˆ Î©â‚‚.theory â†’ â—‡^[n]p âˆˆ Î©â‚.theory)) := by
  constructor;
  . intro H p;
    contrapose;
    intro h;
    apply iff_mem_neg.mp;
    apply H;
    apply mem_dn_iff.mpr;
    apply (neg_iff $ mem_multidia_dual).mp;
    exact iff_mem_neg.mpr h;
  . intro H p;
    contrapose;
    intro h;
    apply iff_mem_neg.mp;
    apply (neg_iff $ mem_multibox_dual).mpr;
    apply iff_mem_negneg.mpr;
    apply H;
    exact iff_mem_neg.mpr h;

variable {Î“ : List (Formula Î±)}

lemma iff_mem_conj' : (Î“.conj' âˆˆ Î©.theory) â†” (âˆ€ p âˆˆ Î“, p âˆˆ Î©.theory) := by simp [membership_iff, iff_provable_list_conj];

lemma iff_mem_multibox_conj' : (â–¡^[n]Î“.conj' âˆˆ Î©.theory) â†” (âˆ€ p âˆˆ Î“, â–¡^[n]p âˆˆ Î©.theory) := by
  simp only [iff_mem_multibox, iff_mem_conj'];
  constructor;
  . intro h p hp Î©' hÎ©';
    exact (h hÎ©') p hp;
  . intro h Î©' hÎ©' p hp;
    exact @h p hp Î©' hÎ©';

lemma iff_mem_box_conj' : (â–¡Î“.conj' âˆˆ Î©.theory) â†” (âˆ€ p âˆˆ Î“, â–¡p âˆˆ Î©.theory) := iff_mem_multibox_conj' (n := 1)

end Normal

end MaximalConsistentTheory

end

end LO.Modal.Standard
