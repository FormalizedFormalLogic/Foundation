import Logic.Propositional.Basic.Completeness
import Logic.Modal.Normal.Formula
import Logic.Modal.Normal.HilbertStyle
import Logic.Modal.Normal.Semantics

/-!
# Completeness of Normal Modal Logic

## References
- <https://builds.openlogicproject.org/open-logic-complete.pdf>
-/

namespace LO.Modal.Normal

open Finset Set
open Formula Theory
open Deduction

attribute [simp] Finset.subset_union_right Finset.subset_union_left
attribute [simp] Set.insert_subset_iff

variable {Î± Î² : Type u} [Inhabited Î±] [DecidableEq Î²] [Inhabited Î²]

section

variable (Î› : AxiomSet Î²) (Î“ : Theory Î²)

def Maximal := âˆ€ p, (p âˆˆ Î“) âˆ¨ (~p âˆˆ Î“)

def Theory.Inconsistent (Î“ : Theory Î²) := Î“ âŠ¢á´¹[Î›]! âŠ¥

def Theory.Consistent (Î“ : Theory Î²) := Â¬(Inconsistent Î› Î“)

def Formula.Inconsistent (p : Formula Î²) := Theory.Inconsistent {p}

def Formula.Consistent (p : Formula Î²) := Theory.Consistent {p}

def WeakCompleteness := âˆ€ (p : Formula Î²), (âŠ§á´¹[(ð”½(Î›) : FrameClass Î±)] p) â†’ (âŠ¢á´¹[Î›] p)

def Completeness (ð”½ : FrameClass Î±) := âˆ€ (Î“ : Theory Î²) (p : Formula Î²), (Î“ âŠ¨á´¹[ð”½] p) â†’ (Î“ âŠ¢á´¹[Î›]! p)

end

variable {Î› : AxiomSet Î²}
variable {Î“ : Set (Formula Î²)} (hConsisÎ“ : Theory.Consistent Î› Î“)

@[simp]
lemma inconsistent_insert_falsum : Theory.Inconsistent Î› (insert âŠ¥ Î“) := by
  simp [Theory.Inconsistent];
  existsi {âŠ¥};
  exact âŸ¨(by simp), âŸ¨axm (by simp)âŸ©âŸ©;

@[simp]
lemma consistent_isempty_falsum (Î” : Context Î²) (hÎ” : â†‘Î” âŠ† Î“) : IsEmpty (Î” âŠ¢á´¹[Î›] âŠ¥) := by
  simp [Theory.Inconsistent, Theory.Consistent] at hConsisÎ“;
  exact hConsisÎ“ Î” (by assumption);

lemma consistent_no_falsum : âˆ€ (Î” : Context Î²), â†‘Î” âŠ† Î“ â†’ âŠ¥ âˆ‰ Î” := by
  intro Î” hÎ”;
  by_contra hC;
  have hâ‚ : Î” âŠ¢á´¹[Î›] âŠ¥ := axm hC;
  have hâ‚‚ : IsEmpty (Î” âŠ¢á´¹[Î›] âŠ¥) := consistent_isempty_falsum hConsisÎ“ Î” hÎ”;
  exact hâ‚‚.false hâ‚;

lemma consistent_no_falsum' : âŠ¥ âˆ‰ Î“ := by
  by_contra;
  apply consistent_no_falsum hConsisÎ“ {âŠ¥} (by aesop);
  simp;

@[simp]
lemma consistent_undeducible_falsum : (Î“ âŠ¬á´¹[Î›]! âŠ¥) := by
  by_contra hC;
  have âŸ¨Î”, hÎ”â‚, âŸ¨hÎ”â‚‚âŸ©âŸ© := hC;
  simp [Theory.Inconsistent, Theory.Consistent] at hConsisÎ“;
  exact hConsisÎ“ Î” hÎ”â‚ |>.false hÎ”â‚‚;

lemma consistent_neither_undeducible (p) : (Î“ âŠ¬á´¹[Î›]! p) âˆ¨ (Î“ âŠ¬á´¹[Î›]! ~p) := by
  by_contra hC; simp only [not_or] at hC;

  have hâ‚ := hC.1; simp at hâ‚;
  have hâ‚‚ := hC.2; simp at hâ‚‚;

  have âŸ¨x, âŸ¨hxâ‚, âŸ¨hxâ‚‚âŸ©âŸ©âŸ© := hâ‚;
  have âŸ¨y, âŸ¨hyâ‚, âŸ¨hyâ‚‚âŸ©âŸ©âŸ© := hâ‚‚;

  simp [Theory.Inconsistent, Theory.Consistent] at hConsisÎ“;
  exact hConsisÎ“ (x âˆª y) (by aesop) |>.false
    $ modus_ponens' (hyâ‚‚.weakening' (by simp)) (hxâ‚‚.weakening' (by simp));

lemma consistent_subset {Î“â‚ Î“â‚‚ : Theory Î²} : (Î“â‚ âŠ† Î“â‚‚) â†’ (Consistent Î› Î“â‚‚) â†’ (Consistent Î› Î“â‚) := by
  intro hs hConsisÎ“â‚‚ hInconsisÎ“â‚;
  simp [Theory.Consistent, Theory.Inconsistent] at hConsisÎ“â‚‚ hInconsisÎ“â‚;
  have âŸ¨Î”, hÎ”â‚, hÎ”â‚‚âŸ© := hInconsisÎ“â‚;
  exact hConsisÎ“â‚‚ Î” (Set.Subset.trans hÎ”â‚ hs) |>.false hÎ”â‚‚.some;

lemma consistent_insert {Î“ : Theory Î²} {p : Formula Î²} : (Consistent Î› (insert p Î“)) â†’ (Consistent Î› Î“) := consistent_subset (by simp)

lemma consistent_empty (hConsisÎ› : Theory.Consistent Î› Î›) : Theory.Consistent Î› âˆ… := consistent_subset (by simp) hConsisÎ›

lemma frameclass_unsatisfiable_insert_neg {ð”½ : FrameClass Î±} {Î“ : Theory Î²} : (Î“ âŠ­á´¹[ð”½] p) â†” (Theory.FrameClassSatisfiable ð”½ (insert (~p) Î“)) := by
  constructor;
  . intro hCon;
    simp [FrameClassConsequence, FrameConsequence] at hCon;
    simp [FrameClassSatisfiable, FrameSatisfiable];
    have âŸ¨F, hF, V, w, âŸ¨hâ‚, hâ‚‚âŸ©âŸ© := hCon;
    existsi F, hF, V, w;
    exact âŸ¨hâ‚‚, hâ‚âŸ©;
  . intro hSat hCon;
    simp [FrameClassConsequence, FrameConsequence] at hCon;
    simp [FrameClassSatisfiable, FrameSatisfiable] at hSat;
    have âŸ¨F, hF, V, w, âŸ¨hâ‚, hâ‚‚âŸ©âŸ© := hSat;
    exact hâ‚ $ hCon F hF V w hâ‚‚;

lemma frameclass_satisfiable_insert_neg {ð”½ : FrameClass Î±} {Î“ : Theory Î²} : (Î“ âŠ¨á´¹[ð”½] p) â†” Â¬(Theory.FrameClassSatisfiable ð”½ (insert (~p) Î“)) := by simpa using frameclass_unsatisfiable_insert_neg.not

lemma inconsistent_insert : (Inconsistent Î› (insert p Î“)) â†’ (âˆƒ (Î” : Context Î²), (â†‘Î” âŠ† Î“) âˆ§ (Deducible Î› (insert p Î”) âŠ¥)) := by
  intro hInconsisÎ“p;
  simp [Theory.Inconsistent, Theory.Consistent] at hInconsisÎ“p hConsisÎ“;
  have âŸ¨Î”, hÎ”, âŸ¨dÎ”âŸ©âŸ© := hInconsisÎ“p;
  existsi (erase Î” p);
  constructor;
  . aesop;
  . exact âŸ¨dÎ”.weakening' (by apply Finset.insert_erase_subset)âŸ©

lemma inconsistent_insert_neg : (Î“ âŠ¢á´¹[Î›]! p) â†” (Inconsistent Î› (insert (~p) Î“)) := by
  constructor;
  . intro h;
    simp only [Theory.Inconsistent];
    have âŸ¨Î”, hÎ”â‚, âŸ¨hÎ”â‚‚âŸ©âŸ© := h;
    existsi (insert (~p) Î”);
    constructor;
    . simp only [coe_insert];
      apply Set.insert_subset_insert;
      simpa;
    . exact âŸ¨(axm (by simp)).modus_ponens' (hÎ”â‚‚.weakening' (by simp))âŸ©;
  . intro h;
    have âŸ¨Î”, hÎ”, dÎ”âŸ© := inconsistent_insert h;
    existsi Î”;
    exact âŸ¨hÎ”, Deducible.dne' (by simpa using dÎ”.dtr)âŸ©;

lemma consistent_insert_neg {Î“ : Theory Î²} : (Î“ âŠ¬á´¹[Î›]! p) â†” (Consistent Î› (insert (~p) Î“)) := inconsistent_insert_neg.not

lemma completeness_def {ð”½ : FrameClass Î±} : (Completeness Î› ð”½) â†” (âˆ€ Î“, Consistent Î› Î“ â†’ FrameClassSatisfiable ð”½ Î“) := by
  constructor;
  . contrapose;
    simp [Completeness];
    intro Î” hâ‚ hâ‚‚;
    existsi Î”, âŠ¥;
    constructor;
    . intro F hF V w h;
      simp [FrameClassSatisfiable, FrameSatisfiable] at hâ‚‚;
      have âŸ¨p, âŸ¨hpâ‚, hpâ‚‚âŸ©âŸ© := hâ‚‚ F hF V w;
      have := h p hpâ‚;
      contradiction;
    . simpa [Theory.Consistent, Theory.Inconsistent] using hâ‚;
  . contrapose;
    simp [Completeness];
    intro Î” p hâ‚ hâ‚‚;
    existsi (insert (~p) Î”);
    constructor;
    . apply consistent_insert_neg.mp;
      simpa using hâ‚‚;
    . apply frameclass_satisfiable_insert_neg.mp;
      exact hâ‚;

lemma consistent_false (Î” : Context Î²) (h : â†‘Î” âŠ† Î“) : (Undeducible Î› Î” âŠ¥) := by
  simp [Theory.Consistent, Theory.Inconsistent] at hConsisÎ“;
  simpa using (hConsisÎ“ Î” h);

lemma consistent_either (hConsisÎ“ : Consistent Î› Î“) : âˆ€ p, (Consistent Î› (insert p Î“)) âˆ¨ (Consistent Î› (insert (~p) Î“)) := by
  intro p;
  by_contra hC; simp [not_or, Theory.Consistent] at hC;
  have âŸ¨Î”â‚, hÎ”â‚, dÎ”â‚âŸ© := inconsistent_insert hC.1;
  have âŸ¨Î”â‚‚, hÎ”â‚‚, dÎ”â‚‚âŸ© := inconsistent_insert hC.2;
  have : â†‘(Î”â‚‚ âˆª Î”â‚) âŠ† Î“ := by aesop;
  apply hConsisÎ“;
  existsi (Î”â‚‚ âˆª Î”â‚);
  constructor;
  . assumption;
  . exact dÎ”â‚‚.dtr.modus_ponens $ dÎ”â‚.dtr;

def Theory.MaximalConsistent (Î›) (Î“ : Theory Î²) := Theory.Consistent Î› Î“ âˆ§ Maximal Î“

section MaximalConsistent

variable (hMCÎ“ : MaximalConsistent Î› Î“)

lemma axiomset_include : Î› âŠ† Î“ := by
  intro p hp;
  by_contra hC;
  apply consistent_false hMCÎ“.1 {~p} (by have := hMCÎ“.2 p; aesop) âŸ¨(axm (by simp [NegDefinition.neg])).modus_ponens' (maxm hp)âŸ©;

lemma member_of_maximal_consistent : (p âˆˆ Î“) â†” (Î“ âŠ¢á´¹[Î›]! p) := by
  constructor;
  . intros; existsi {p}; aesop;
  . intro h;
    suffices ~p âˆ‰ Î“ by have := hMCÎ“.2 p; aesop;
    by_contra hC;
    have âŸ¨Î”, âŸ¨hÎ”â‚, âŸ¨hÎ”â‚‚âŸ©âŸ©âŸ© := h;
    have : (insert (~p) Î”) âŠ¢á´¹[Î›] âŠ¥ := (axm (by simp [NegDefinition.neg])).modus_ponens' (hÎ”â‚‚.weakening' (by simp));
    have : â†‘(insert (~p) Î”) âŠ† Î“ := by simp_all [coe_insert, Set.insert_subset_iff];
    apply consistent_false hMCÎ“.1 _ (by assumption) âŸ¨(by assumption)âŸ©;

lemma not_member_of_maximal_consistent : (p âˆ‰ Î“) â†” (Î“ âŠ¬á´¹[Î›]! p) := by
  simpa using (member_of_maximal_consistent hMCÎ“).not;

lemma maximal_consistent_modus_ponens' {p q : Formula Î²} : ((p âŸ¶ q) âˆˆ Î“) â†’ (p âˆˆ Î“) â†’ (q âˆˆ Î“) := by
  intro hp hpq;
  have dp  := (member_of_maximal_consistent hMCÎ“).mp hp;
  have dpq := (member_of_maximal_consistent hMCÎ“).mp hpq;
  exact (member_of_maximal_consistent hMCÎ“).mpr $ dp.modus_ponens' dpq;

lemma maximal_neg_include : (~p âˆˆ Î“) â†” (p âˆ‰ Î“) := by
  constructor;
  . intros;
    by_contra hC;
    have hp : ({p, ~p}) âŠ¢á´¹[Î›] p := axm (by simp);
    have hnp : ({p, ~p}) âŠ¢á´¹[Î›] ~p := axm (by simp);
    apply consistent_false hMCÎ“.1 {p, ~p} (by aesop;) âŸ¨hnp.modus_ponens' hpâŸ©;
  . have := hMCÎ“.2 p; aesop;

lemma maximal_imp_include : (p âŸ¶ q âˆˆ Î“) â†” (p âˆ‰ Î“) âˆ¨ (q âˆˆ Î“) := by
  constructor;
  . simp [or_iff_not_imp_left];
    intros;
    apply (member_of_maximal_consistent hMCÎ“).mpr;
    have hp : ({p, p âŸ¶ q}) âŠ¢á´¹[Î›] p := axm (by simp);
    have hpq : ({p, p âŸ¶ q}) âŠ¢á´¹[Î›] p âŸ¶ q := axm (by simp);
    have := hpq.modus_ponens' hp;
    existsi {p, p âŸ¶ q}, (by aesop)
    exact âŸ¨hpq.modus_ponens' hpâŸ©;
  . intros h;
    cases h;
    case inl h =>
      have := (maximal_neg_include hMCÎ“).mpr h;
      have dâ‚ : Î“ âŠ¢á´¹[Î›]! (~p âŸ¶ (p âŸ¶ q)) := by
        existsi âˆ…;
        constructor;
        . simp;
        . have dp : ({p, ~p}) âŠ¢á´¹[Î›] p := axm (by simp);
          have dnp : ({p, ~p}) âŠ¢á´¹[Î›] ~p := axm (by simp);
          exact âŸ¨(Deduction.efq _ _).modus_ponens' (modus_ponens' dnp dp) |>.dtr |>.dtrâŸ©;
      have dâ‚‚ : Î“ âŠ¢á´¹[Î›]! ~p := by existsi {~p}; aesop;
      apply (member_of_maximal_consistent hMCÎ“).mpr;
      exact dâ‚.modus_ponens' dâ‚‚;
    case inr h =>
      have dâ‚ : Î“ âŠ¢á´¹[Î›]! (q âŸ¶ (p âŸ¶ q)) := by
        existsi âˆ…, by simp;
        exact âŸ¨implyâ‚ _ _ _âŸ©;
      have dâ‚‚ : Î“ âŠ¢á´¹[Î›]! q := by existsi {q}; aesop;
      apply (member_of_maximal_consistent hMCÎ“).mpr;
      exact dâ‚.modus_ponens' dâ‚‚;

lemma maximal_imp_include' : (p âŸ¶ q âˆˆ Î“) â†” ((p âˆˆ Î“) â†’ (q âˆˆ Î“)) := by
  constructor;
  . intro hpq hp;
    have := (maximal_imp_include hMCÎ“).mp hpq;
    aesop;
  . intro hp;
    apply (maximal_imp_include hMCÎ“).mpr;
    simp [or_iff_not_imp_left];
    aesop;

lemma maximal_and_include : (p â‹ q âˆˆ Î“) â†” (p âˆˆ Î“) âˆ§ (q âˆˆ Î“) := by
  constructor;
  . intros h;
    simp_all only [(member_of_maximal_consistent hMCÎ“)];
    constructor;
    . exact h.conjâ‚';
    . exact h.conjâ‚‚';
  . rintro âŸ¨hp, hqâŸ©;
    simp_all only [(member_of_maximal_consistent hMCÎ“)];
    exact .conjâ‚ƒ' hp hq;

lemma maximal_or_include : (p â‹Ž q âˆˆ Î“) â†” (p âˆˆ Î“) âˆ¨ (q âˆˆ Î“) := by
  constructor;
  . intros h;
    by_contra hC; simp [not_or] at hC;
    have : Î“ âŠ¢á´¹[Î›]! âŠ¥ := .disjâ‚ƒ'
      (show Î“ âŠ¢á´¹[Î›]! (p âŸ¶ âŠ¥) by exact .axm (by apply maximal_neg_include hMCÎ“ |>.mpr; aesop;))
      (show Î“ âŠ¢á´¹[Î›]! (q âŸ¶ âŠ¥) by exact .axm (by apply maximal_neg_include hMCÎ“ |>.mpr; aesop;))
      (show Î“ âŠ¢á´¹[Î›]! (p â‹Ž q) by exact .axm h);
    exact consistent_undeducible_falsum hMCÎ“.1 this;
  . intro h;
    simp_all only [(member_of_maximal_consistent hMCÎ“)];
    cases h;
    case inl h => exact .disjâ‚' h;
    case inr h => exact .disjâ‚‚' h;

end MaximalConsistent

structure MaximalConsistentTheory (Î› : AxiomSet Î²) where
  theory : Theory Î²
  consistent : Consistent Î› theory
  maximal : Maximal theory

namespace MaximalConsistentTheory

variable (Î© Î©â‚ Î©â‚‚ : MaximalConsistentTheory Î›)

@[simp]
def membership (p : Formula Î²) (Î© : MaximalConsistentTheory Î›) := p âˆˆ Î©.theory
instance : Membership (Formula Î²) (MaximalConsistentTheory Î›) := âŸ¨membershipâŸ©

@[simp]
def subset := Î©â‚.theory âŠ† Î©â‚‚.theory
instance : HasSubset (MaximalConsistentTheory Î›) := âŸ¨subsetâŸ©

@[simp] def subset1 (Î“ : Theory Î²) (Î© : MaximalConsistentTheory Î›) := Î“ âŠ† Î©.theory
@[simp] def subset2 (Î© : MaximalConsistentTheory Î›) (Î“ : Theory Î²) := Î©.theory âŠ† Î“
infix:50 " âŠ† " => subset1
infix:50 " âŠ† " => subset2

lemma mc : MaximalConsistent Î› Î©.theory := by
  constructor;
  . exact Î©.consistent;
  . exact Î©.maximal;

@[simp] def box := â–¡Î©.theory
prefix:73  "â–¡" => box

@[simp] def dia := â—‡Î©.theory
prefix:73  "â—‡" => dia

@[simp] def prebox := â–¡â»Â¹Î©.theory
prefix:73  "â–¡â»Â¹" => prebox

@[simp] def predia := â—‡â»Â¹Î©.theory
prefix:73  "â—‡â»Â¹" => predia

variable  {Î© : MaximalConsistentTheory Î›}

lemma modus_ponens' {p q : Formula Î²} : ((p âŸ¶ q) âˆˆ Î©) â†’ (p âˆˆ Î©) â†’ (q âˆˆ Î©) := by
  apply maximal_consistent_modus_ponens' (Î©.mc);

lemma membership_iff {Î© : MaximalConsistentTheory Î›} {p : Formula Î²} : (p âˆˆ Î©) â†” (Î©.theory âŠ¢á´¹[Î›]! p) := by
  apply member_of_maximal_consistent (Î©.mc);

lemma iff_congr : (Î©.theory âŠ¢á´¹[Î›]! (p âŸ· q)) â†’ ((p âˆˆ Î©) â†” (q âˆˆ Î©)) := by
  intro hpq;
  simp only [LogicSymbol.iff] at hpq;
  constructor;
  . intro hp;
    exact membership_iff.mpr $ TheoryDeducible.conjâ‚' hpq |>.modus_ponens' (membership_iff.mp hp);
  . intro hq;
    exact membership_iff.mpr $ TheoryDeducible.conjâ‚‚' hpq |>.modus_ponens' (membership_iff.mp hq);

lemma dn_include : (p âˆˆ Î©) â†” (~~p âˆˆ Î©) := iff_congr (.iff_dn Î©.theory p)

lemma neg_include : (~p âˆˆ Î©) â†” (p âˆ‰ Î©) := maximal_neg_include (Î©.mc)

lemma imp_include' : (p âŸ¶ q âˆˆ Î©) â†” ((p âˆˆ Î©) â†’ (q âˆˆ Î©)) := maximal_imp_include' (Î©.mc)

lemma and_include : (p â‹ q âˆˆ Î©) â†” (p âˆˆ Î©) âˆ§ (q âˆˆ Î©) := maximal_and_include (Î©.mc)

lemma or_include : (p â‹Ž q âˆˆ Î©) â†” (p âˆˆ Î©) âˆ¨ (q âˆˆ Î©) := maximal_or_include (Î©.mc)

@[simp]
lemma no_falsum : âŠ¥ âˆ‰ Î© := consistent_no_falsum' Î©.consistent

@[simp]
lemma neither_mem : Â¬((p âˆˆ Î©) âˆ§ (~p âˆˆ Î©)) := by
  by_contra hC;
  exact Î©.no_falsum $ Î©.modus_ponens' hC.2 hC.1;

end MaximalConsistentTheory

section Lindenbaum

lemma exists_maximal_consistent_theory' :
  âˆƒ m âˆˆ {Î“ | Theory.Consistent Î› Î“}, Î“ âŠ† m âˆ§ âˆ€ a âˆˆ {Î“ | Theory.Consistent Î› Î“}, m âŠ† a â†’ a = m
  := zorn_subset_nonempty { Î“ : Theory Î² | Consistent Î› Î“ } (by
    intro c hc chain hnc;
    existsi (â‹ƒâ‚€ c);
    simp;
    constructor;
    . by_contra hC;
      simp [Theory.Consistent, Theory.Inconsistent] at hC;
      rcases hC with âŸ¨s, hs, âŸ¨s_consisâŸ©âŸ©;
      rcases Set.subset_mem_chain_of_finite c hnc chain (s := s) (Finset.finite_toSet s) hs with âŸ¨U, hUc, hsUâŸ©
      have : Theory.Consistent Î› U := by aesop;
      have := consistent_subset hsU (by aesop);
      simp [Theory.Consistent, Theory.Inconsistent] at this;
      exact this s Set.Subset.rfl |>.false s_consis;
    . intro s a;
      exact Set.subset_sUnion_of_mem a;
  ) Î“ hConsisÎ“

/--
  a.k.a. Lindenbaum Lemma
-/
lemma exists_maximal_consistent_theory : âˆƒ (Î© : MaximalConsistentTheory Î›), (Î“ âŠ† Î©) := by
  have âŸ¨Î©, âŸ¨hâ‚, âŸ¨hâ‚‚, hâ‚ƒâŸ©âŸ©âŸ© := exists_maximal_consistent_theory' hConsisÎ“;
  existsi âŸ¨Î©, hâ‚, (by
    intro p;
    cases (consistent_either hâ‚ p) with
    | inl h => have := hâ‚ƒ (insert p Î©) h (by simp); left; simp_all;
    | inr h => have := hâ‚ƒ (insert (~p) Î©) h (by simp); right; simp_all;
  )âŸ©;
  exact hâ‚‚;

end Lindenbaum

open MaximalConsistentTheory

variable (hK : ðŠ âŠ† Î›)

lemma boxed_context_deducible {Î“ : Theory Î²} (h : Î“ âŠ¢á´¹[Î›]! p) : (â–¡Î“ âŠ¢á´¹[Î›]! â–¡p) := by
  have âŸ¨Î”, hÎ”â‚, âŸ¨hÎ”â‚‚âŸ©âŸ© := h;
  existsi â–¡Î”;
  constructor
  . simpa using box_subset hÎ”â‚;
  . exact âŸ¨LogicK.Hilbert.deduction_by_boxed_context hK hÎ”â‚‚âŸ©;

lemma prebox_prov {Î“ : Theory Î²} (h : â–¡â»Â¹Î“ âŠ¢á´¹[Î›]! p) : (Î“ âŠ¢á´¹[Î›]! â–¡p) := by
  have := boxed_context_deducible hK h;
  simp [MaximalConsistent, Theory.Consistent, Theory.Inconsistent] at this;
  have âŸ¨Î”, hÎ”â‚, âŸ¨hÎ”â‚‚âŸ©âŸ© := this;
  existsi Î”;
  constructor;
  . exact Set.Subset.trans hÎ”â‚ (by aesop);
  . exact âŸ¨hÎ”â‚‚âŸ©;

lemma mct_mem_box_iff {Î© : MaximalConsistentTheory Î›} {p : Formula Î²} : (â–¡p âˆˆ Î©) â†” (âˆ€ (Î©' : MaximalConsistentTheory Î›), â–¡â»Â¹Î© âŠ† Î©' â†’ p âˆˆ Î©') := by
  constructor;
  . aesop;
  . contrapose;
    intro hC;
    have := (not_member_of_maximal_consistent Î©.mc).mp hC;
    have := consistent_insert_neg.mp $ not_imp_not.mpr (prebox_prov hK) this;
    have âŸ¨Î©', hÎ©'âŸ© := exists_maximal_consistent_theory this;
    simp;
    existsi Î©';
    constructor;
    . aesop;
    . exact neg_include.mp (by aesop);

def CanonicalModel (Î› : AxiomSet Î²) : Model (MaximalConsistentTheory Î›) Î² where
  frame (Î©â‚ Î©â‚‚) := (â–¡â»Â¹Î©â‚) âŠ† Î©â‚‚
  val (Î© a) := (atom a) âˆˆ Î©


namespace CanonicalModel

variable {Î› : AxiomSet Î²} (hK : ðŠ âŠ† Î›) {Î© Î©â‚ Î©â‚‚ : MaximalConsistentTheory Î›}

lemma frame_def: (CanonicalModel Î›).frame Î©â‚ Î©â‚‚ â†” (â–¡â»Â¹Î©â‚) âŠ† Î©â‚‚ := by rfl

lemma frame_def': (CanonicalModel Î›).frame Î©â‚ Î©â‚‚ â†” (â—‡Î©â‚‚ âŠ† Î©â‚) := by
  simp only [frame_def];
  constructor;
  . intro h p hp;
    have âŸ¨q, hqâ‚, hqâ‚‚âŸ© := dia_mem.mp hp;
    rw [â†hqâ‚‚, ModalDuality.dia];
    apply (Î©â‚.neg_include).mpr;
    by_contra hC;
    have : ~q âˆˆ Î©â‚‚ := by aesop;
    exact Î©â‚‚.neither_mem âŸ¨hqâ‚, (by simpa)âŸ©;
  . intro h p;
    contrapose;
    intro hnp;
    have : â—‡(~p) âˆˆ Î©â‚ := by
      simp [MaximalConsistentTheory.dia, Theory.dia, -Formula.dia, -Formula.neg] at h;
      simpa using h $ neg_include.mpr hnp
    have : ~(â–¡p) âˆˆ Î©â‚ := by
      suffices h : Î©â‚.theory âŠ¢á´¹[Î›]! ((â—‡~p) âŸ· ~(â–¡p)) by exact MaximalConsistentTheory.iff_congr h |>.mp this;
      existsi âˆ…, (by simp);
      exact LogicK.Hilbert.deducible_dianeg_negbox_iff hK âˆ… p;
    have := neg_include.mp this;
    aesop;

@[simp]
lemma val_def {a : Î²} :
  a âˆˆ (CanonicalModel Î›).val Î© â†” (atom a) âˆˆ Î©
  := by rfl

lemma axiomT (hT : ð“ âŠ† Î›) : Reflexive (CanonicalModel Î›).frame := by
  intro Î© p hp;
  have : â–¡p âŸ¶ p âˆˆ Î© := membership_iff.mpr $ .maxm (hT $ (by apply AxiomT.set.includes_AxiomT));
  apply Î©.modus_ponens' this hp;

lemma axiomD (hD : ðƒ âŠ† Î›) : Serial (CanonicalModel Î›).frame := by
  intro Î©;
  simp [frame_def];
  suffices h : Consistent Î› (â–¡â»Â¹Î©.theory) by exact exists_maximal_consistent_theory h;
  by_contra hC;
  simp [Theory.Consistent, Theory.Inconsistent] at hC;
  have dâ‚ : Î©.theory âŠ¢á´¹[Î›]! â–¡âŠ¥ := prebox_prov hK hC;
  have dâ‚‚ : Î©.theory âŠ¢á´¹[Î›]! (â–¡âŠ¥ âŸ¶ â—‡âŠ¥) := .maxm (hD $ (by apply AxiomD.set.includes_AxiomD));
  have dâ‚ƒ : Î©.theory âŠ¢á´¹[Î›]! ~(â—‡âŠ¥) := by simpa using (TheoryDeducible.boxverum Î©.theory).dni';
  exact consistent_undeducible_falsum Î©.consistent $
    dâ‚ƒ |>.modus_ponens' $
    dâ‚‚ |>.modus_ponens' dâ‚;

lemma axiomB (hB : ð âŠ† Î›) : Symmetric (CanonicalModel Î›).frame := by
  intro Î©â‚ Î©â‚‚ h;
  simp [frame_def] at h;
  simp [(frame_def' hK)];
  intro p hp;
  have âŸ¨q, hq, _âŸ© := dia_mem.mp hp;
  have dâ‚ : Î©â‚.theory âŠ¢á´¹[Î›]! q := membership_iff.mp hq;
  have dâ‚‚ : Î©â‚.theory âŠ¢á´¹[Î›]! (q âŸ¶ â–¡â—‡q) := .maxm (hB $ (by apply AxiomB.set.includes_AxiomB));
  have := membership_iff.mpr $ dâ‚‚.modus_ponens' dâ‚;
  aesop

lemma axiom4 (h4 : ðŸ’ âŠ† Î›) : Transitive (CanonicalModel Î›).frame := by
  intro Î©â‚ Î©â‚‚ Î©â‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ p hp;
  apply hâ‚‚â‚ƒ;
  apply hâ‚â‚‚;
  have : â–¡p âŸ¶ â–¡â–¡p âˆˆ Î©â‚ := membership_iff.mpr $ .maxm (h4 $ (by apply Axiom4.set.includes_Axiom4));
  exact Î©â‚.modus_ponens' this (by aesop);

lemma axiom5 (h5 : ðŸ“ âŠ† Î›) : Euclidean (CanonicalModel Î›).frame := by
  intro Î©â‚ Î©â‚‚ Î©â‚ƒ hâ‚â‚‚ hâ‚â‚ƒ;
  simp [frame_def] at hâ‚â‚‚;
  simp [(frame_def' hK)] at hâ‚â‚ƒ;
  simp [(frame_def' hK)];
  intro p hp;
  have âŸ¨q, _, _âŸ© := dia_mem.mp hp;
  have dâ‚ : Î©â‚.theory âŠ¢á´¹[Î›]! â—‡q := .axm (by aesop);
  have dâ‚‚ : Î©â‚.theory âŠ¢á´¹[Î›]! â—‡q âŸ¶ â–¡â—‡q := .maxm (h5 $ (by apply Axiom5.set.includes_Axiom5));
  have := membership_iff.mpr $ dâ‚‚.modus_ponens' dâ‚;
  aesop;

end CanonicalModel

lemma truthlemma {p : Formula Î²} : âˆ€ {Î©}, (âŠ§á´¹[CanonicalModel Î›, Î©] p) â†” (p âˆˆ Î©) := by
  induction p using rec' with
  | hatom => aesop;
  | hfalsum => simp;
  | himp =>
    intro Î©;
    constructor;
    . intros; apply imp_include'.mpr; aesop;
    . intro h; have := imp_include'.mp h; aesop;
  | hor =>
    intro Î©;
    constructor;
    . intros; apply or_include.mpr; aesop;
    . intro h; have := or_include.mp h; aesop;
  | hand =>
    intro Î©;
    constructor;
    . intros; apply and_include.mpr; aesop;
    . intro h; have := and_include.mp h; aesop;
  | hbox p ih =>
    intro Î©;
    constructor;
    . intro h;
      apply (mct_mem_box_iff hK).mpr;
      intro Î©' hÎ©';
      apply ih.mp;
      simp [Satisfies.box_def] at h;
      exact h Î©' hÎ©';
    . intro h Î©' hÎ©';
      apply ih.mpr;
      simp [Set.subset_def, CanonicalModel.frame_def] at hÎ©';
      exact hÎ©' p (by aesop);

lemma truthlemma' {Î“ : Theory Î²} : âˆ€ {Î©}, (âŠ§á´¹[CanonicalModel Î›, Î©] Î“) â†” (Î“ âŠ† Î©) := by
  intro Î©;
  constructor;
  . simp [Set.subset_def];
    intro h p hp;
    exact truthlemma hK |>.mp $ h p hp;
  . intro h p hp;
    apply truthlemma hK |>.mpr;
    aesop;

-- TODO: ã»ã¨ã‚“ã©åŒã˜è¨˜è¿°ãªã®ã§ã©ã†ã«ã‹ã—ã¦å…±é€šåŒ–ã—ãŸã„ï¼Ž

theorem LogicK.Hilbert.completes : Completeness (ðŠ : AxiomSet Î²) (ð”½((ðŠ : AxiomSet Î²)) : FrameClass (MaximalConsistentTheory (ðŠ : AxiomSet Î²))) := by
  apply completeness_def.mpr;
  intro Î“ hConsisÎ“;
  let âŸ¨Î©, hÎ©âŸ© := exists_maximal_consistent_theory hConsisÎ“;
  existsi (CanonicalModel ðŠ).frame;
  constructor;
  . apply LogicK.def_FrameClass;
  . existsi (CanonicalModel ðŠ).val, Î©;
    apply truthlemma' (by simp) |>.mpr;
    assumption;

theorem LogicS4.Hilbert.completes : Completeness (ð’ðŸ’ : AxiomSet Î²) (ð”½((ð’ðŸ’ : AxiomSet Î²)) : FrameClass (MaximalConsistentTheory (ð’ðŸ’ : AxiomSet Î²))) := by
  apply completeness_def.mpr;
  intro Î“ hConsisÎ“;
  let âŸ¨Î©, hÎ©âŸ© := exists_maximal_consistent_theory hConsisÎ“;
  existsi (CanonicalModel ð’ðŸ’).frame;
  constructor;
  . apply (LogicS4.def_FrameClass _).mp;
    constructor;
    . apply CanonicalModel.axiomT (subsets_T); -- TODO: ãªãœã‹`simp`ãŒåŠ¹ã‹ãªã„
    . apply CanonicalModel.axiom4 (by simp);
  . existsi (CanonicalModel ð’ðŸ’).val, Î©;
    apply truthlemma' (by exact subsets_K) |>.mpr;
    assumption;

theorem LogicS5.Hilbert.completes : Completeness (ð’ðŸ“ : AxiomSet Î²) (ð”½((ð’ðŸ“ : AxiomSet Î²)) : FrameClass (MaximalConsistentTheory (ð’ðŸ“ : AxiomSet Î²))) := by
  apply completeness_def.mpr;
  intro Î“ hConsisÎ“;
  let âŸ¨Î©, hÎ©âŸ© := exists_maximal_consistent_theory hConsisÎ“;
  existsi (CanonicalModel ð’ðŸ“).frame;
  constructor;
  . apply (LogicS5.def_FrameClass _).mp;
    constructor;
    . apply CanonicalModel.axiomT (by simp);
    . apply CanonicalModel.axiom5 (by simp) (by simp);
  . existsi (CanonicalModel ð’ðŸ“).val, Î©;
    apply truthlemma' (by exact subsets_K) |>.mpr;
    assumption;

end LO.Modal.Normal
