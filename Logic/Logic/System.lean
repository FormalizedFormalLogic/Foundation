import Logic.Logic.LogicSymbol
import Logic.Logic.Semantics
/-!
# Basic definitions and properties of proof system related notions

This file defines a characterization of the system/proof/provability/calculus of formulas.
Also defines soundness and completeness.

## Main Definitions
* `LO.System`: Proof system of logic.
* `LO.System.Inconsistent`
* `LO.System.Consistent`
* `LO.System.Translation`
* `LO.System.Compact`
* `LO.Sound`: Soundness of the proof system.
* `LO.Complete`: Completeness of the proof system.

-/

namespace LO

class System (S : Type*) (F : outParam Type*) where
  Prf : S â†’ F â†’ Type*

infix:45 " âŠ¢ " => System.Prf

namespace System

variable {S : Type*} {F : Type*} [System S F]

section

variable (ğ“¢ : S)

def Provable (f : F) : Prop := Nonempty (ğ“¢ âŠ¢ f)

abbrev Unprovable (f : F) : Prop := Â¬Provable ğ“¢ f

infix:45 " âŠ¢! " => Provable

infix:45 " âŠ¬! " => Unprovable

def PrfSet (s : Set F) : Type _ := {f : F} â†’ f âˆˆ s â†’ ğ“¢ âŠ¢ f

def ProvableSet (s : Set F) : Prop := âˆ€ f âˆˆ s, ğ“¢ âŠ¢! f

infix:45 " âŠ¢* " => PrfSet

infix:45 " âŠ¢*! " => ProvableSet

def theory : Set F := {f | ğ“¢ âŠ¢! f}

end

lemma unprovable_iff_isEmpty {ğ“¢ : S} {f : F} :
    ğ“¢ âŠ¬! f â†” IsEmpty (ğ“¢ âŠ¢ f) := by simp [Provable, Unprovable]

noncomputable def Provable.prf {ğ“¢ : S} {f : F} (h : ğ“¢ âŠ¢! f) : ğ“¢ âŠ¢ f :=
  Classical.choice h

lemma provableSet_iff {ğ“¢ : S} {s : Set F} :
    ğ“¢ âŠ¢*! s â†” Nonempty (ğ“¢ âŠ¢* s) := by
  simp [ProvableSet, PrfSet, Provable, Classical.nonempty_pi, â†imp_iff_not_or]

noncomputable def ProvableSet.prfSet {ğ“¢ : S} {s : Set F} (h : ğ“¢ âŠ¢*! s) : ğ“¢ âŠ¢* s :=
  Classical.choice (Î± := ğ“¢ âŠ¢* s) (provableSet_iff.mp h : Nonempty (ğ“¢ âŠ¢* s))

instance : Preorder S where
  le := fun ğ“¢ ğ“¢' â†¦ theory ğ“¢ âŠ† theory ğ“¢'
  le_refl := fun ğ“¢ â†¦ Set.Subset.refl _
  le_trans := fun _ _ _ hâ‚ hâ‚‚ â†¦ Set.Subset.trans hâ‚ hâ‚‚

lemma le_iff {ğ“¢ ğ“¢' : S} : ğ“¢ â‰¤ ğ“¢' â†” (âˆ€ {f}, ğ“¢ âŠ¢! f â†’ ğ“¢' âŠ¢! f) :=
  âŸ¨fun h _ hf â†¦ h hf, fun h _ hf â†¦ h hfâŸ©

lemma lt_iff {ğ“¢ ğ“¢' : S} : ğ“¢ < ğ“¢' â†” (âˆ€ {f}, ğ“¢ âŠ¢! f â†’ ğ“¢' âŠ¢! f) âˆ§ (âˆƒ f, ğ“¢ âŠ¬! f âˆ§ ğ“¢' âŠ¢! f) := by
  simp [lt_iff_le_not_le, le_iff]; intro _
  exact exists_congr (fun _ â†¦ by simp [and_comm])

lemma weakening {ğ“¢ ğ“¢' : S} (h : ğ“¢ â‰¤ ğ“¢') {f} : ğ“¢ âŠ¢! f â†’ ğ“¢' âŠ¢! f := le_iff.mp h

instance : Setoid S where
  r := fun ğ“¢ ğ“¢' â†¦ theory ğ“¢ = theory ğ“¢'
  iseqv := { refl := fun _ â†¦ rfl, symm := Eq.symm, trans := Eq.trans }

lemma equiv_def {ğ“¢ ğ“¢' : S} : ğ“¢ â‰ˆ ğ“¢' â†” theory ğ“¢ = theory ğ“¢' := iff_of_eq rfl

lemma equiv_iff {ğ“¢ ğ“¢' : S} : ğ“¢ â‰ˆ ğ“¢' â†” (âˆ€ f, ğ“¢ âŠ¢! f â†” ğ“¢' âŠ¢! f) := by simp [equiv_def, Set.ext_iff, theory]

lemma le_antisymm {ğ“¢ ğ“¢' : S} (h : ğ“¢ â‰¤ ğ“¢') (h' : ğ“¢' â‰¤ ğ“¢) : ğ“¢ â‰ˆ ğ“¢' :=
  equiv_iff.mpr (fun _ â†¦ âŸ¨fun hf â†¦ le_iff.mp h hf, fun hf â†¦ le_iff.mp h' hfâŸ©)

@[simp] lemma provableSet_theory (ğ“¢ : S) : ğ“¢ âŠ¢*! theory ğ“¢ := fun _ hf â†¦ hf

def Inconsistent (ğ“¢ : S) : Prop := âˆ€ f, ğ“¢ âŠ¢! f

class Consistent (ğ“¢ : S) : Prop where
  not_inconsistent : Â¬Inconsistent ğ“¢

lemma inconsistent_def {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” âˆ€ f, ğ“¢ âŠ¢! f := by simp [Inconsistent]

lemma not_inconsistent_iff_consistent {ğ“¢ : S} :
    Â¬Inconsistent ğ“¢ â†” Consistent ğ“¢ :=
  âŸ¨fun h â†¦ âŸ¨hâŸ©, by rintro âŸ¨hâŸ©; exact hâŸ©

alias âŸ¨_, Consistent.not_incâŸ© := not_inconsistent_iff_consistent

lemma not_consistent_iff_inconsistent {ğ“¢ : S} :
    Â¬Consistent ğ“¢ â†” Inconsistent ğ“¢ := by simp [â†not_inconsistent_iff_consistent]

alias âŸ¨_, Inconsistent.not_conâŸ© := not_consistent_iff_inconsistent

lemma consistent_iff_exists_unprovable {ğ“¢ : S} :
    Consistent ğ“¢ â†” âˆƒ f, ğ“¢ âŠ¬! f := by
  simp [â†not_inconsistent_iff_consistent, inconsistent_def]

alias âŸ¨Consistent.exists_unprovable, _âŸ© := consistent_iff_exists_unprovable

lemma Consistent.of_unprovable {ğ“¢ : S} {f} (h : ğ“¢ âŠ¬! f) : Consistent ğ“¢ :=
  âŸ¨fun hp â†¦ h (hp f)âŸ©

lemma inconsistent_iff_theory_eq_univ {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” theory ğ“¢ = Set.univ := by simp [inconsistent_def, theory, Set.ext_iff]

alias âŸ¨Inconsistent.theory_eq, _âŸ© := inconsistent_iff_theory_eq_univ

lemma Inconsistent.equiv {ğ“¢ ğ“¢' : S} (h : Inconsistent ğ“¢) (h' : Inconsistent ğ“¢') : ğ“¢ â‰ˆ ğ“¢' :=
  Set.ext fun f â†¦ by simp [h.theory_eq, h'.theory_eq]

lemma Inconsistent.of_ge {ğ“¢ ğ“¢' : S} (hğ“¢ : Inconsistent ğ“¢) (h : ğ“¢ â‰¤ ğ“¢') : Inconsistent ğ“¢' :=
  fun f â†¦ h (hğ“¢ f)

lemma Consistent.of_le {ğ“¢ ğ“¢' : S} (hğ“¢ : Consistent ğ“¢) (h : ğ“¢' â‰¤ ğ“¢) : Consistent ğ“¢' :=
  âŸ¨fun H â†¦ not_consistent_iff_inconsistent.mpr (H.of_ge h) hğ“¢âŸ©

structure Translation {S S' F F'} [System S F] [System S' F'] (ğ“¢ : S) (ğ“¢' : S') where
  toFun : F â†’ F'
  prf {f} : ğ“¢ âŠ¢ f â†’ ğ“¢' âŠ¢ toFun f

infix:40 " â† " => Translation

namespace Translation

variable {S S' S'' : Type*} {F F' F'' : Type*} [System S F] [System S' F'] [System S'' F'']

instance (ğ“¢ : S) (ğ“¢' : S') : CoeFun (Translation ğ“¢ ğ“¢') (fun _ â†¦ F â†’ F') := âŸ¨Translation.toFunâŸ©

protected def id (ğ“¢ : S) : ğ“¢ â† ğ“¢ where
  toFun := id
  prf := id

@[simp] lemma id_app (ğ“¢ : S) (f : F) : Translation.id ğ“¢ f = f := rfl

def comp {ğ“¢ : S} {ğ“¢' : S'} {ğ“¢'' : S''} (Ï† : ğ“¢' â† ğ“¢'') (Ïˆ : ğ“¢ â† ğ“¢') : ğ“¢ â† ğ“¢'' where
  toFun := Ï†.toFun âˆ˜ Ïˆ.toFun
  prf := Ï†.prf âˆ˜ Ïˆ.prf

@[simp] lemma comp_app {ğ“¢ : S} {ğ“¢' : S'} {ğ“¢'' : S''} (Ï† : ğ“¢' â† ğ“¢'') (Ïˆ : ğ“¢ â† ğ“¢') (f : F) :
    Ï†.comp Ïˆ f = Ï† (Ïˆ f) := rfl

end Translation

section

variable [LogicalConnective F]

variable (ğ“¢ : S)

def Complete : Prop := âˆ€ f, ğ“¢ âŠ¢! f âˆ¨ ğ“¢ âŠ¢! ~f

def Undecidable (f : F) : Prop := ğ“¢ âŠ¬! f âˆ§ ğ“¢ âŠ¬! ~f

end

variable (S)

class Axiomatized where
  axm : S â†’ Set F
  prfAxm (ğ“¢ : S) : ğ“¢ âŠ¢* axm ğ“¢
  weakening {ğ“¢ ğ“¢' : S} : axm ğ“¢ âŠ† axm ğ“¢' â†’ ğ“¢ âŠ¢ f â†’ ğ“¢' âŠ¢ f

class StrongCut [Axiomatized S] where
  cut {ğ“¢ ğ“£ : S} {p} : ğ“¢ âŠ¢* Axiomatized.axm ğ“£ â†’ ğ“£ âŠ¢ p â†’ ğ“¢ âŠ¢ p

variable {S}

section Axiomatized

namespace Axiomatized

variable [Axiomatized S] {ğ“¢ ğ“¢' : S}

@[simp] lemma provable_axm (ğ“¢ : S) : ğ“¢ âŠ¢*! axm ğ“¢ := fun _ hf â†¦ âŸ¨prfAxm ğ“¢ hfâŸ©

lemma axm_subset (ğ“¢ : S) : axm ğ“¢ âŠ† theory ğ“¢ := fun p hp â†¦ provable_axm ğ“¢ p hp

abbrev AxmSubset (ğ“¢ ğ“¢' : S) : Prop := axm ğ“¢ âŠ† axm ğ“¢'

infix:45 " âŠ†â‚â‚“ " => AxmSubset

lemma le_of_subset_axm (h : ğ“¢ âŠ†â‚â‚“ ğ“¢') : ğ“¢ â‰¤ ğ“¢' := by rintro f âŸ¨bâŸ©; exact âŸ¨weakening h bâŸ©

end Axiomatized

variable [Axiomatized S]

abbrev Finite (ğ“¢ : S) : Prop := (Axiomatized.axm ğ“¢).Finite

def FiniteAxiomatizable (ğ“¢ : S) : Prop := âˆƒ ğ“• : S, Finite ğ“• âˆ§ ğ“• â‰ˆ ğ“¢

lemma Consistent.of_subset {ğ“¢ ğ“¢' : S} (hğ“¢ : Consistent ğ“¢) (h : ğ“¢' âŠ†â‚â‚“ ğ“¢) : Consistent ğ“¢' :=
  hğ“¢.of_le (Axiomatized.le_of_subset_axm h)

lemma Inconsistent.of_supset {ğ“¢ ğ“¢' : S} (hğ“¢ : Inconsistent ğ“¢) (h : ğ“¢ âŠ†â‚â‚“ ğ“¢') : Inconsistent ğ“¢' :=
  hğ“¢.of_ge (Axiomatized.le_of_subset_axm h)

namespace StrongCut

variable [StrongCut S]

lemma cut! {ğ“¢ ğ“£ : S} {p : F} (H : ğ“¢ âŠ¢*! Axiomatized.axm ğ“£) (hp : ğ“£ âŠ¢! p) : ğ“¢ âŠ¢! p := by
  rcases hp with âŸ¨bâŸ©; exact âŸ¨StrongCut.cut H.prfSet bâŸ©

def translation {ğ“¢ ğ“£ : S} (B : ğ“¢ âŠ¢* Axiomatized.axm ğ“£) : ğ“£ â† ğ“¢ where
  toFun := id
  prf := StrongCut.cut B

end StrongCut

variable (S)

class Compact where
  Ï† {ğ“¢ : S} {f : F} : ğ“¢ âŠ¢ f â†’ S
  Ï†Prf {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Ï† b âŠ¢ f
  Ï†_subset {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Ï† b âŠ†â‚â‚“ ğ“¢
  Ï†_finite {ğ“¢ : S} {f : F} (b : ğ“¢ âŠ¢ f) : Finite (Ï† b)

variable {S}

namespace Compact

variable [Compact S]

lemma finite_provable {ğ“¢ : S} (h : ğ“¢ âŠ¢! f) : âˆƒ ğ“• : S, ğ“• âŠ†â‚â‚“ ğ“¢ âˆ§ Finite ğ“• âˆ§ ğ“• âŠ¢! f := by
  rcases h with âŸ¨bâŸ©
  exact âŸ¨Ï† b, Ï†_subset b, Ï†_finite b, âŸ¨Ï†Prf bâŸ©âŸ©

end Compact

end Axiomatized

end System

section

variable {S : Type*} {F : Type*} [LogicalConnective F] [System S F] {M : Type*} [Semantics M F]

class Sound (ğ“¢ : S) (ğ“œ : M) : Prop where
  sound : âˆ€ {f : F}, ğ“¢ âŠ¢! f â†’ ğ“œ âŠ§ f

class Complete (ğ“¢ : S) (ğ“œ : M) : Prop where
  complete : âˆ€ {f : F}, ğ“œ âŠ§ f â†’ ğ“¢ âŠ¢! f

namespace Sound

section

variable {ğ“¢ : S} {ğ“œ : M} [Sound ğ“¢ ğ“œ]

lemma not_provable_of_countermodel {p : F} (hp : Â¬ğ“œ âŠ§ p) : ğ“¢ âŠ¬! p :=
  fun b â†¦ hp (Sound.sound b)

lemma consistent_of_meaningful : Semantics.Meaningful ğ“œ â†’ System.Consistent ğ“¢ :=
  fun H â†¦ âŸ¨fun h â†¦ by rcases H with âŸ¨f, hfâŸ©; exact hf (Sound.sound (h f))âŸ©

lemma consistent_of_model [Semantics.Bot M] : System.Consistent ğ“¢ :=
  consistent_of_meaningful (ğ“œ := ğ“œ) inferInstance

lemma realizeSet_of_prfSet {T : Set F} (b : ğ“¢ âŠ¢*! T) : ğ“œ âŠ§* T :=
  âŸ¨fun _ hf => sound (b _ hf)âŸ©

end

section

variable [âˆ€ ğ“œ : M, Semantics.Meaningful ğ“œ] {ğ“¢ : S} {T : Set F} [Sound ğ“¢ (Semantics.models M T)]

lemma consequence_of_provable {f : F} : ğ“¢ âŠ¢! f â†’ T âŠ¨[M] f := sound

lemma consistent_of_satisfiable : Semantics.Satisfiable M T â†’ System.Consistent ğ“¢ :=
  fun H â†¦ consistent_of_meaningful (Semantics.meaningful_iff_satisfiableSet.mp H)

end

end Sound

namespace Complete

section

variable {ğ“¢ : S} {ğ“œ : M} [Complete ğ“¢ ğ“œ]

lemma meaningful_of_consistent : System.Consistent ğ“¢ â†’ Semantics.Meaningful ğ“œ := by
  contrapose; intro h
  simp [Semantics.not_meaningful_iff, System.not_consistent_iff_inconsistent] at h âŠ¢
  intro f; exact Complete.complete (h f)

end

section

variable [âˆ€ ğ“œ : M, Semantics.Meaningful ğ“œ] {ğ“¢ : S} {T : Set F} [Complete ğ“¢ (Semantics.models M T)]

lemma provable_of_consequence {f : F} : T âŠ¨[M] f â†’ ğ“¢ âŠ¢! f := complete

lemma satisfiable_of_consistent : System.Consistent ğ“¢ â†’ Semantics.Satisfiable M T :=
  fun H â†¦ Semantics.meaningful_iff_satisfiableSet.mpr (meaningful_of_consistent H)

variable [Sound ğ“¢ (Semantics.models M T)]

lemma provable_iff_consequence {f : F} : T âŠ¨[M] f â†” ğ“¢ âŠ¢! f := âŸ¨complete, Sound.soundâŸ©

lemma consistent_iff_satisfiable : System.Consistent ğ“¢ â†” Semantics.Satisfiable M T :=
  âŸ¨satisfiable_of_consistent, Sound.consistent_of_satisfiableâŸ©

end

end Complete

end

namespace System

variable {S : Type*} {F : Type*} [LogicalConnective F] [System S F]

variable (S)

class DeductiveExplosion where
  dexp {ğ“¢ : S} : ğ“¢ âŠ¢ âŠ¥ â†’ (p : F) â†’ ğ“¢ âŠ¢ p

variable {S}

section

variable [DeductiveExplosion S]

def DeductiveExplosion.dexp! {ğ“¢ : S} (h : ğ“¢ âŠ¢! âŠ¥) (f : F) : ğ“¢ âŠ¢! f := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨dexp b fâŸ©

lemma inconsistent_iff_provable_bot {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” ğ“¢ âŠ¢! âŠ¥ := âŸ¨fun h â†¦ h âŠ¥, fun h f â†¦ DeductiveExplosion.dexp! h fâŸ©

alias âŸ¨_, inconsistent_of_provableâŸ© := inconsistent_iff_provable_bot

lemma consistent_iff_unprovable_bot {ğ“¢ : S} :
    Consistent ğ“¢ â†” ğ“¢ âŠ¬! âŠ¥ := by
  simp [inconsistent_iff_provable_bot, â†not_inconsistent_iff_consistent]

alias âŸ¨Consistent.not_bot, _âŸ© := consistent_iff_unprovable_bot

variable [Axiomatized S] [Compact S]

lemma consistent_compact {ğ“¢ : S} :
    Consistent ğ“¢ â†” âˆ€ ğ“• : S, ğ“• âŠ†â‚â‚“ ğ“¢ â†’ Finite ğ“• â†’ Consistent ğ“• :=
  âŸ¨fun H ğ“• hğ“• _ â†¦ H.of_subset hğ“•,
   fun H â†¦ consistent_iff_unprovable_bot.mpr <| fun b â†¦ by
      rcases Compact.finite_provable b with âŸ¨ğ“•, fin, hğ“•, hâŸ©
      exact (H ğ“• fin hğ“•).not_bot hâŸ©

lemma inconsistent_compact {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” âˆƒ ğ“• : S, ğ“• âŠ†â‚â‚“ ğ“¢ âˆ§ Finite ğ“• âˆ§ Inconsistent ğ“• := by
  simp [â†not_consistent_iff_inconsistent, consistent_compact (ğ“¢ := ğ“¢)]
  tauto

end

variable (S)

class Deduction [Insert F S] where
  ofInsert {p q : F} {ğ“¢ : S} : insert p ğ“¢ âŠ¢ q â†’ ğ“¢ âŠ¢ p âŸ¶ q
  inv {p q : F} {ğ“¢ : S} : ğ“¢ âŠ¢ p âŸ¶ q â†’ insert p ğ“¢ âŠ¢ q

variable {S}

section

variable [Insert F S] [Deduction S]

alias deduction := Deduction.ofInsert

lemma Deduction.of_insert! {p q : F} {ğ“¢ : S} (h : insert p ğ“¢ âŠ¢! q) : ğ“¢ âŠ¢! p âŸ¶ q := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨Deduction.ofInsert bâŸ©

alias deduction! := Deduction.of_insert!

lemma Deduction.inv! {p q : F} {ğ“¢ : S} (h : ğ“¢ âŠ¢! p âŸ¶ q) : insert p ğ“¢ âŠ¢! q := by
  rcases h with âŸ¨bâŸ©; exact âŸ¨Deduction.inv bâŸ©

end

end System

end LO
