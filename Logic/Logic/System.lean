import Logic.Logic.LogicSymbol
import Logic.Logic.Semantics
/-!
# Basic definitions and properties of proof system related notions

This file defines a characterization of the system/proof/provability/calculus of formulas.
Also defines soundness and completeness.

## Main Definitions
* `LO.System`: Proof system of logic.
* `LO.Sound`: Soundness of the proof system.
* `LO.Complete`: Completeness of the proof system.

-/

namespace LO

class System (S : Type*) (F : outParam Type*) where
  Prf : S â†’ F â†’ Type*

infix:45 " âŠ« " => System.Prf

namespace System

variable {S : Type*} {F : Type*} [System S F]

section

variable (ğ“¢ : S)

def Provable (f : F) : Prop := Nonempty (ğ“¢ âŠ« f)

def Unprovable (f : F) : Prop := IsEmpty (ğ“¢ âŠ« f)

infix:45 " âŠ«! " => Provable

infix:45 " âŠ«Ì¸! " => Unprovable

def PrfSet (s : Set F) : Type _ := {f : F} â†’ f âˆˆ s â†’ ğ“¢ âŠ« f

def ProvableSet (s : Set F) : Prop := âˆ€ f âˆˆ s, ğ“¢ âŠ«! f

infix:45 " âŠ«* " => PrfSet

infix:45 " âŠ«*! " => ProvableSet

def theory : Set F := {f | ğ“¢ âŠ«! f}

end

lemma not_provable_iff_unprovable {ğ“¢ : S} {f : F} :
    Â¬ğ“¢ âŠ«! f â†” ğ“¢ âŠ«Ì¸! f := by simp [Provable, Unprovable]

lemma not_unprovable_iff_provable {ğ“¢ : S} {f : F} :
    Â¬ğ“¢ âŠ«Ì¸! f â†” ğ“¢ âŠ«! f := by simp [Provable, Unprovable]

instance : Preorder S where
  le := fun ğ“¢ ğ“¢' â†¦ theory ğ“¢ âŠ† theory ğ“¢'
  le_refl := fun ğ“¢ â†¦ Set.Subset.refl _
  le_trans := fun _ _ _ hâ‚ hâ‚‚ â†¦ Set.Subset.trans hâ‚ hâ‚‚

lemma le_iff {ğ“¢ ğ“¢' : S} : ğ“¢ â‰¤ ğ“¢' â†” (âˆ€ {f}, ğ“¢ âŠ«! f â†’ ğ“¢' âŠ«! f) :=
  âŸ¨fun h _ hf â†¦ h hf, fun h _ hf â†¦ h hfâŸ©

lemma lt_iff {ğ“¢ ğ“¢' : S} : ğ“¢ < ğ“¢' â†” (âˆ€ {f}, ğ“¢ âŠ«! f â†’ ğ“¢' âŠ«! f) âˆ§ (âˆƒ f, ğ“¢ âŠ«Ì¸! f âˆ§ ğ“¢' âŠ«! f) := by
  simp [lt_iff_le_not_le, le_iff]; intro _
  exact exists_congr (fun _ â†¦ by simp [and_comm, not_provable_iff_unprovable])

lemma weakening {ğ“¢ ğ“¢' : S} (h : ğ“¢ â‰¤ ğ“¢') {f} : ğ“¢ âŠ«! f â†’ ğ“¢' âŠ«! f := le_iff.mp h

def Equiv (ğ“¢ ğ“¢' : S) : Prop := theory ğ“¢ = theory ğ“¢'

@[simp, refl] protected lemma Equiv.refl (ğ“¢ : S) : Equiv ğ“¢ ğ“¢ := rfl

@[symm] lemma Equiv.symm {ğ“¢ ğ“¢' : S} : Equiv ğ“¢ ğ“¢' â†’ Equiv ğ“¢' ğ“¢ := Eq.symm

@[trans] lemma Equiv.trans {ğ“¢ ğ“¢' ğ“¢'' : S} : Equiv ğ“¢ ğ“¢' â†’ Equiv ğ“¢' ğ“¢'' â†’ Equiv ğ“¢ ğ“¢'' := Eq.trans

lemma equiv_iff {ğ“¢ ğ“¢' : S} : Equiv ğ“¢ ğ“¢' â†” (âˆ€ f, ğ“¢ âŠ«! f â†” ğ“¢' âŠ«! f) := by simp [Equiv, Set.ext_iff, theory]

lemma le_antisymm {ğ“¢ ğ“¢' : S} (h : ğ“¢ â‰¤ ğ“¢') (h' : ğ“¢' â‰¤ ğ“¢) : Equiv ğ“¢ ğ“¢' :=
  equiv_iff.mpr (fun _ â†¦ âŸ¨fun hf â†¦ le_iff.mp h hf, fun hf â†¦ le_iff.mp h' hfâŸ©)

def Inconsistent (ğ“¢ : S) : Prop := âˆ€ f, ğ“¢ âŠ«! f

class Consistent (ğ“¢ : S) : Prop where
  lt_top : Â¬Inconsistent ğ“¢

lemma inconsistent_def {ğ“¢ : S} :
    Inconsistent ğ“¢ â†” âˆ€ f, ğ“¢ âŠ«! f := by simp [Inconsistent]

lemma not_Inconsistent_iff_Consistent {ğ“¢ : S} :
    Â¬Inconsistent ğ“¢ â†” Consistent ğ“¢ :=
  âŸ¨fun h â†¦ âŸ¨hâŸ©, by rintro âŸ¨hâŸ©; exact hâŸ©

lemma not_Consistent_iff_Inconsistent {ğ“¢ : S} :
    Â¬Consistent ğ“¢ â†” Inconsistent ğ“¢ := by simp [â†not_Inconsistent_iff_Consistent]

structure Translation {S S' F F'} [System S F] [System S' F'] (ğ“¢ : S) (ğ“¢' : S') where
  toFun : F â†’ F'
  prf {f} : ğ“¢ âŠ« f â†’ ğ“¢' âŠ« toFun f

infix:40 " â† " => Translation

namespace Translation

protected def id (ğ“¢ : S) : ğ“¢ â† ğ“¢ where
  toFun := id
  prf := id

variable {S S' S'' : Type*} {F F' F'' : Type*} [System S F] [System S' F'] [System S'' F'']

def comp {ğ“¢ : S} {ğ“¢' : S'} {ğ“¢'' : S''} (t' : ğ“¢' â† ğ“¢'') (t : ğ“¢ â† ğ“¢') : ğ“¢ â† ğ“¢'' where
  toFun := t'.toFun âˆ˜ t.toFun
  prf := t'.prf âˆ˜ t.prf

end Translation

variable [LogicalConnective F]

variable (ğ“¢ : S)

def Complete : Prop := âˆ€ f, ğ“¢ âŠ«! f âˆ¨ ğ“¢ âŠ«! ~f

def Undecidable (f : F) : Prop := ğ“¢ âŠ«Ì¸! f âˆ§ ğ“¢ âŠ«Ì¸! ~f

class ModusPonens (ğ“¢ : S) where
  mdp {p q : F} : ğ“¢ âŠ« p âŸ¶ q â†’ ğ“¢ âŠ« p â†’ ğ“¢ âŠ« q

class Minimal (ğ“¢ : S) extends ModusPonens ğ“¢ where
  verum              : ğ“¢ âŠ« âŠ¤
  implyâ‚ (p q : F)   : ğ“¢ âŠ« p âŸ¶ q âŸ¶ p
  implyâ‚‚ (p q r : F) : ğ“¢ âŠ« (p âŸ¶ q âŸ¶ r) âŸ¶ (p âŸ¶ q) âŸ¶ p âŸ¶ r
  conjâ‚  (p q : F)   : ğ“¢ âŠ« p â‹ q âŸ¶ p
  conjâ‚‚  (p q : F)   : ğ“¢ âŠ« p â‹ q âŸ¶ q
  conjâ‚ƒ  (p q : F)   : ğ“¢ âŠ« p âŸ¶ q âŸ¶ p â‹ q
  disjâ‚  (p q : F)   : ğ“¢ âŠ« p âŸ¶ p â‹ q
  disjâ‚‚  (p q : F)   : ğ“¢ âŠ« q âŸ¶ p â‹ q
  disjâ‚ƒ  (p q r : F) : ğ“¢ âŠ« (p âŸ¶ r) âŸ¶ (q âŸ¶ r) âŸ¶ p â‹ q âŸ¶ r

/-- Supplymental -/
class HasEFQ (ğ“¢ : S) where
  efq (p : F) : ğ“¢ âŠ« âŠ¥ âŸ¶ p

class HasWeakLEM (ğ“¢ : S) where
  wlem (p : F) : ğ“¢ âŠ« ~p â‹ ~~p

class HasLEM (ğ“¢ : S) where
  lem (p : F) : ğ“¢ âŠ« p â‹ ~p

class DNE (ğ“¢ : S) where
  dne (p : F) : ğ“¢ âŠ« ~~p âŸ¶ p

class Dummett (ğ“¢ : S) where
  dummett (p q : F) : ğ“¢ âŠ« (p âŸ¶ q) â‹ (q âŸ¶ p)

class Peirce (ğ“¢ : S) where
  peirce (p q : F) : ğ“¢ âŠ« ((p âŸ¶ q) âŸ¶ p) âŸ¶ p

/--
  Intuitionistic Propositional Logic.

  Modal companion of `ğ’ğŸ’`
-/
class Intuitionistic (ğ“¢ : S) extends Minimal ğ“¢, HasEFQ ğ“¢

/--
  Propositional Logic for Weak Law of Excluded Middle.

  Modal companion of `ğ’ğŸ’.ğŸ`
-/
class WeakLEM (ğ“¢ : S) extends Intuitionistic ğ“¢, HasWeakLEM ğ“¢

/--
  GÃ¶del-Dummett Propositional Logic.

  Modal companion of `ğ’ğŸ’.ğŸ‘`
-/
class GD (ğ“¢ : S) extends Intuitionistic ğ“¢, Dummett ğ“¢

/--
  Classical Propositional Logic.

  Modal companion of `ğ’ğŸ“`
-/
class Classical (ğ“¢ : S) extends Minimal ğ“¢, DNE ğ“¢

end System

end LO
