import Logic.Logic.System

/-!
# Sequent calculus and variants

This file defines a characterization of Tait style calculus and Gentzen style calculus.

## Main Definitions
* `LO.Tait`
* `LO.Gentzen`

-/

namespace LO

class OneSided (F : Type u) where
  Derivation : List F â†’ Type u

class TwoSided (F : Type u) where
  Derivation : List F â†’ List F â†’ Type u

prefix: 45 " âŠ¢Â¹ " => OneSided.Derivation

infix: 45 " âŠ¢Â² " => TwoSided.Derivation

abbrev OneSided.Derivable [OneSided F] (Î” : List F) : Prop := Nonempty (âŠ¢Â¹ Î”)

abbrev TwoSided.Derivable [TwoSided F] (Î“ Î” : List F) : Prop := Nonempty (Î“ âŠ¢Â² Î”)

prefix: 45 " âŠ¢Â¹! " => OneSided.Derivable

infix: 45 " âŠ¢Â²! " => TwoSided.Derivable

class Tait (F : Type u) [LogicalConnective F] extends OneSided F where
  verum (Î” : List F)         : âŠ¢Â¹ âŠ¤ :: Î”
  and {p q : F} {Î” : List F} : âŠ¢Â¹ p :: Î” â†’ âŠ¢Â¹ q :: Î” â†’ âŠ¢Â¹ p â‹ q :: Î”
  or {p q : F} {Î” : List F}  : âŠ¢Â¹ p :: q :: Î” â†’ âŠ¢Â¹ p â‹ q :: Î”
  wk {Î” Î”' : List F}         : âŠ¢Â¹ Î” â†’ Î” âŠ† Î”' â†’ âŠ¢Â¹ Î”'
  em {p} {Î” : List F}        : p âˆˆ Î” â†’ ~p âˆˆ Î” â†’ âŠ¢Â¹ Î”

class Tait.Cut (F : Type u) [LogicalConnective F] [Tait F] where
  cut {Î” : List F} {p} : âŠ¢Â¹ p :: Î” â†’ âŠ¢Â¹ ~p :: Î” â†’ âŠ¢Â¹ Î”

class Gentzen (F : Type u) [LogicalConnective F] extends TwoSided F where
  verum (Î“ Î” : List F)                : Î“ âŠ¢Â² âŠ¤ :: Î”
  falsum (Î“ Î” : List F)               : âŠ¥ :: Î“ âŠ¢Â² Î”
  negLeft {p : F} {Î“ Î” : List F}      : Î“ âŠ¢Â² p :: Î” â†’ ~p :: Î“ âŠ¢Â² Î”
  negRight {p : F} {Î“ Î” : List F}     : p :: Î“ âŠ¢Â² Î” â†’ Î“ âŠ¢Â² ~p :: Î”
  andLeft {p q : F} {Î“ Î” : List F}    : p :: q :: Î“ âŠ¢Â² Î” â†’ p â‹ q :: Î“ âŠ¢Â² Î”
  andRight {p q : F} {Î“ Î” : List F}   : Î“ âŠ¢Â² p :: Î” â†’ Î“ âŠ¢Â² q :: Î” â†’ Î“ âŠ¢Â² p â‹ q :: Î”
  orLeft {p q : F} {Î“ Î” : List F}     : p :: Î“ âŠ¢Â² Î” â†’ q :: Î“ âŠ¢Â² Î” â†’ p â‹ q :: Î“ âŠ¢Â² Î”
  orRight {p q : F} {Î“ Î” : List F}    : Î“ âŠ¢Â² p :: q :: Î” â†’ Î“ âŠ¢Â² p â‹ q :: Î”
  implyLeft {p q : F} {Î“ Î” : List F}  : Î“ âŠ¢Â² p :: Î” â†’ q :: Î“ âŠ¢Â² Î” â†’ (p âŸ¶ q) :: Î“ âŠ¢Â² Î”
  implyRight {p q : F} {Î“ Î” : List F} : p :: Î“ âŠ¢Â² q :: Î” â†’ Î“ âŠ¢Â² (p âŸ¶ q) :: Î”
  wk {Î“ Î“' Î” Î”' : List F}             : Î“ âŠ¢Â² Î” â†’ Î“ âŠ† Î“' â†’ Î” âŠ† Î”' â†’ Î“' âŠ¢Â² Î”'
  closed (p) {Î“ Î” : List F}               : p âˆˆ Î“ â†’ p âˆˆ Î” â†’ Î“ âŠ¢Â² Î”

class Gentzen.Cut (F : Type u) [LogicalConnective F] [Gentzen F] where
  cut {Î“ Î” : List F} {p} : Î“ âŠ¢Â² p :: Î” â†’ p :: Î“ âŠ¢Â² Î” â†’ Î“ âŠ¢Â² Î”

class LawfulTwoSided (S : Type*) {F : Type*} [LogicalConnective F] [System F S] [TwoSided F] where
  toProofâ‚ {Î“} {ğ“¢ : S} {p : F} : Î“ âŠ¢Â² [p] â†’ (âˆ€ q âˆˆ Î“, ğ“¢ âŠ¢ q) â†’ ğ“¢ âŠ¢ p

variable {F : Type*} [LogicalConnective F]

namespace LawfulTwoSided

variable [System F S] [TwoSided F] [LawfulTwoSided S]

def toProofOfNil {p : F} (b : [] âŠ¢Â² [p]) (ğ“¢ : S) : ğ“¢ âŠ¢ p :=
  toProofâ‚ b (by intro q h; exact False.elim ((List.mem_nil_iff q).mp h))

lemma toProofâ‚! {Î“} {ğ“¢ : S} {p : F} (b : Î“ âŠ¢Â² [p]) (H : âˆ€ q âˆˆ Î“, ğ“¢ âŠ¢! q) : ğ“¢ âŠ¢! p :=
  âŸ¨toProofâ‚ b (fun q hq => (H q hq).get)âŸ©

end LawfulTwoSided

namespace OneSided

variable [OneSided F] {Î“ Î” : List F}

protected abbrev cast (d : âŠ¢Â¹ Î”) (e : Î” = Î“) : âŠ¢Â¹ Î“ := cast (congrArg _ e) d

end OneSided

namespace Tait

variable [DeMorgan F] [Tait F]

variable {Î“ Î” : List F}

instance : TwoSided F where
  Derivation := fun Î“ Î” => âŠ¢Â¹ Î“.map (~Â·) ++ Î”

def ofConsLeft {p : F} {Î“ Î” : List F} (b : p :: Î“ âŠ¢Â² Î”) :
    âŠ¢Â¹ ~p :: (Î“.map (~Â·) ++ Î”) := wk b (by simp)

def ofConsRight {p : F} {Î“ Î” : List F} (b : Î“ âŠ¢Â² p :: Î”) :
    âŠ¢Â¹ p :: (Î“.map (~Â·) ++ Î”) :=
  wk b (by
    simp
    exact âŸ¨List.subset_cons_of_subset _ (List.subset_append_left _ _),
      List.subset_cons_of_subset _ (List.subset_append_right _ _)âŸ©)

def ofConsRightâ‚‚ {p q : F} {Î“ Î” : List F} (b : Î“ âŠ¢Â² p :: q :: Î”) :
    âŠ¢Â¹ p :: q :: (Î“.map (~Â·) ++ Î”) :=
  wk b (by
    simp
    exact âŸ¨List.subset_cons_of_subset _ $ List.subset_cons_of_subset _ $ List.subset_append_left _ _,
      List.subset_cons_of_subset _ $ List.subset_cons_of_subset _ $ List.subset_append_right _ _âŸ©)

def ofConsLeftRight {p q : F} {Î“ Î” : List F} (b : p :: Î“ âŠ¢Â² q :: Î”) :
    âŠ¢Â¹ ~p :: q :: (Î“.map (~Â·) ++ Î”) :=
  wk b (by
    simp
    exact âŸ¨List.subset_cons_of_subset _ $ List.subset_cons_of_subset _ $ List.subset_append_left _ _,
      List.subset_cons_of_subset _ $ List.subset_cons_of_subset _ $ List.subset_append_right _ _âŸ©)

def toConsLeft {p : F} {Î“ Î” : List F}
    (b : âŠ¢Â¹ ~p :: (Î“.map (~Â·) ++ Î”)) :
    p :: Î“ âŠ¢Â² Î” := wk b (by simp)

def toConsRight {p : F} {Î“ Î” : List F}
    (b : âŠ¢Â¹ p :: (Î“.map (~Â·) ++ Î”)) :
    Î“ âŠ¢Â² p :: Î” :=
  wk b (by
    simp
    exact List.subset_append_of_subset_right _ (List.subset_cons _ _))

instance : Gentzen F where
  verum := fun _ _ => toConsRight (verum _)
  falsum := fun _ _ => toConsLeft (by simpa using verum _)
  negLeft := fun b => toConsLeft (OneSided.cast (ofConsRight b) (by simp))
  negRight := fun b => toConsRight (OneSided.cast (ofConsLeft b) (by simp))
  andLeft := fun b => OneSided.cast (or b) (by simp)
  andRight := fun bp bq =>
    toConsRight (OneSided.cast (and (ofConsRight bp) (ofConsRight bq)) (by simp))
  orLeft := fun bp bq =>
    toConsLeft (OneSided.cast (and (ofConsLeft bp) (ofConsLeft bq)) (by simp))
  orRight := fun b => toConsRight (OneSided.cast (or $ ofConsRightâ‚‚ b) (by simp))
  implyLeft := fun bp bq =>
    toConsLeft (OneSided.cast (and (ofConsRight bp) (ofConsLeft bq)) (by simp[DeMorgan.imply]))
  implyRight := fun b =>
    toConsRight (OneSided.cast (or $ ofConsLeftRight b) (by simp[DeMorgan.imply]))
  wk := fun b hÎ“ hÎ” => wk b (by
    simp
    exact âŸ¨List.subset_append_of_subset_left _ $ List.map_subset _ hÎ“,
      List.subset_append_of_subset_right _ $ hÎ”âŸ©)
  closed := fun p _ _ hÎ“ hÎ” => em (p := p)
    (List.mem_append.mpr $ .inr $ hÎ”)
    (List.mem_append.mpr $ .inl $ List.mem_map_of_mem (~Â·) hÎ“)

variable [Tait.Cut F]

instance : Gentzen.Cut F := âŸ¨fun d d' => Cut.cut (ofConsRight d) (ofConsLeft d')âŸ©

def equiv : Î“ âŠ¢Â² Î” â‰ƒ âŠ¢Â¹ Î“.map (~Â·) ++ Î” := Equiv.refl _

def tauto (b : âŠ¢Â¹ Î”) : Î“ âŠ¢Â² Î” := wk b (by simp)

end Tait

namespace Gentzen

variable [Gentzen F] [Gentzen.Cut F] {Î“ Î” E : List F}

def wkLeft {Î“ Î“' Î” : List F} (d : Î“ âŠ¢Â² Î”) (ss : Î“ âŠ† Î“') : Î“' âŠ¢Â² Î” := wk d ss (by simp)

def wkRight {Î“ Î” Î”' : List F} (d : Î“ âŠ¢Â² Î”) (ss : Î” âŠ† Î”') : Î“ âŠ¢Â² Î”' := wk d (by simp) ss

def rotateLeft {Î“ Î” : List F} {p} (d : Î“ ++ [p] âŠ¢Â² Î”) : p :: Î“ âŠ¢Â² Î” := Gentzen.wkLeft d (by simp)

def rotateRight {Î“ Î” : List F} {p} (d : Î“ âŠ¢Â² Î” ++ [p]) : Î“ âŠ¢Â² p :: Î” := Gentzen.wkRight d (by simp)

def wkL {Î“' Î” : List F} (Î“) (ss : Î“ âŠ† Î“') (d : Î“ âŠ¢Â² Î”) : Î“' âŠ¢Â² Î” := wk d ss (by simp)

def wkR {Î“ Î”' : List F} (Î”) (ss : Î” âŠ† Î”') (d : Î“ âŠ¢Â² Î”) : Î“ âŠ¢Â² Î”' := wk d (by simp) ss

def verum' (h : âŠ¤ âˆˆ Î”) : Î“ âŠ¢Â² Î” := wkRight (verum Î“ Î”) (by simp[h])

def Cut.cut' {Î“â‚ Î“â‚‚ Î”â‚ Î”â‚‚ : List F} (dâ‚ : Î“â‚ âŠ¢Â² p :: Î”â‚) (dâ‚‚ : p :: Î“â‚‚ âŠ¢Â² Î”â‚‚) : Î“â‚ ++ Î“â‚‚ âŠ¢Â² Î”â‚ ++ Î”â‚‚ :=
  let dâ‚ : Î“â‚ ++ Î“â‚‚ âŠ¢Â² p :: (Î”â‚ ++ Î”â‚‚) := wk dâ‚ (by simp) (List.cons_subset_cons _ $ by simp)
  let dâ‚‚ : p :: (Î“â‚ ++ Î“â‚‚) âŠ¢Â² Î”â‚ ++ Î”â‚‚ := wk dâ‚‚ (List.cons_subset_cons _ $ by simp) (by simp)
  Cut.cut dâ‚ dâ‚‚

def ofNegLeft {p} (b : ~p :: Î“ âŠ¢Â² Î”) : Î“ âŠ¢Â² p :: Î” :=
  let d : p :: Î“ âŠ¢Â² p :: Î” :=
    Gentzen.wk (show [p] âŠ¢Â² [p] from closed _ (List.mem_singleton.mpr rfl) (List.mem_singleton.mpr rfl))
      (by simp) (by simp)
  Cut.cut (negRight d) (wkRight b (by simp))

def ofNegRight {p} (b : Î“ âŠ¢Â² ~p :: Î”) : p :: Î“ âŠ¢Â² Î” :=
  let d : p :: Î“ âŠ¢Â² p :: Î” :=
    Gentzen.wk (show [p] âŠ¢Â² [p] from closed _ (List.mem_singleton.mpr rfl) (List.mem_singleton.mpr rfl))
      (by simp) (by simp)
  Cut.cut (wkLeft b (by simp)) (negLeft d)

def ofImplyRight {p q} (b : Î“ âŠ¢Â² (p âŸ¶ q) :: Î”) : p :: Î“ âŠ¢Â² q :: Î” :=
  let d : (p âŸ¶ q) :: p :: Î“ âŠ¢Â² q :: Î” :=
    implyLeft (closed p (by simp) (by simp)) (closed q (by simp) (by simp))
  wk (Cut.cut' b d) (by simp) (by simp)

def modusPonens {p q} (bâ‚ : Î“ âŠ¢Â² (p âŸ¶ q) :: Î”) (bâ‚‚ : Î“ âŠ¢Â² p :: Î”) : Î“ âŠ¢Â² q :: Î” :=
  wk (Cut.cut' bâ‚‚ (ofImplyRight bâ‚)) (by simp) (by simp)

variable {S : Type*} [Collection F S]

structure Disjconseq (ğ“£ : S) (Î“ : List F) where
  antecedent : List F
  subset : âˆ€ p âˆˆ antecedent, p âˆˆ ğ“£
  derivation : antecedent âŠ¢Â² Î“

infix: 45 " âŠ¢' " => Disjconseq

def DisjconseqEquivDerivation {ğ“£ : S} :
    ğ“£ âŠ¢' Î“ â‰ƒ (Î” : {Î” : List F // âˆ€ Ï€ âˆˆ Î”, Ï€ âˆˆ ğ“£}) Ã— Î” âŠ¢Â² Î“ where
  toFun := fun b => âŸ¨âŸ¨b.antecedent, b.subsetâŸ©, b.derivationâŸ©
  invFun := fun p => âŸ¨p.1, p.1.prop, p.2âŸ©
  left_inv := fun b => by simp
  right_inv := fun b => by simp

def Disjconseq.weakening {ğ“£ U : S} {Î“ : List F} (b : ğ“£ âŠ¢' Î“) (h : ğ“£ âŠ† U) : U âŠ¢' Î“ where
  antecedent := b.antecedent
  subset := fun p hp => Collection.subset_iff.mp h _ (b.subset p hp)
  derivation := b.derivation

def toDisjconseq {ğ“£ : S} {Î“ Î”} (d : Î“ âŠ¢Â² Î”) (ss : âˆ€ p âˆˆ Î“, p âˆˆ ğ“£) : ğ“£ âŠ¢' Î” where
  antecedent := Î“
  subset := ss
  derivation := d

namespace Disjconseq

variable {ğ“£ : S}

def wk' {S S'} [Collection F S] [Collection F S'] {ğ“£ : S} {ğ“£' : S'}
    (H : Collection.set ğ“£ âŠ† Collection.set ğ“£') {Î“} : ğ“£ âŠ¢' Î“ â†’ ğ“£' âŠ¢' Î“ := fun d â†¦
  âŸ¨d.antecedent, fun p hp â†¦ H (d.subset p hp), d.derivationâŸ©

def tauto {Î”} (d : [] âŠ¢Â² Î”) : ğ“£ âŠ¢' Î” := toDisjconseq d (by simp)

def wk (b : ğ“£ âŠ¢' Î“) (ss : Î“ âŠ† Î“') : ğ“£ âŠ¢' Î“' where
  antecedent := b.antecedent
  subset := b.subset
  derivation := wkRight b.derivation ss

def cut (b : ğ“£ âŠ¢' p :: Î“) (b' : ğ“£ âŠ¢' ~p :: Î“) : ğ“£ âŠ¢' Î“ where
  antecedent := b.antecedent ++ b'.antecedent
  subset := by
    simp
    rintro p (hp | hp)
    Â· exact b.subset _ hp
    Â· exact b'.subset _ hp
  derivation :=
    let d : b.antecedent ++ b'.antecedent âŠ¢Â² p :: Î“ := wkLeft b.derivation (by simp)
    let d' : b.antecedent ++ b'.antecedent âŠ¢Â² ~p :: Î“ := wkLeft b'.derivation (by simp)
    Cut.cut d' (negLeft d)

def cut' (b : ğ“£ âŠ¢' p :: Î“) (b' : ğ“£ âŠ¢' ~p :: Î”) : ğ“£ âŠ¢' Î“ ++ Î” where
  antecedent := b.antecedent ++ b'.antecedent
  subset := by
    simp
    rintro p (hp | hp)
    Â· exact b.subset _ hp
    Â· exact b'.subset _ hp
  derivation := by
    let d : b.antecedent ++ b'.antecedent âŠ¢Â² p :: Î“ := wkLeft b.derivation (by simp)
    let d' : b.antecedent ++ b'.antecedent âŠ¢Â² ~p :: Î” := wkLeft b'.derivation (by simp)
    exact Gentzen.wk (Cut.cut' d' (negLeft d)) (by simp) (by simp)

def verum (Î“ : List F) : ğ“£ âŠ¢' âŠ¤ :: Î“ := âŸ¨[], by simp, Gentzen.verum _ _âŸ©

def verum' (h : âŠ¤ âˆˆ Î“) : ğ“£ âŠ¢' Î“ := wk (verum Î“) (by simp[h])

def and (bp : ğ“£ âŠ¢' p :: Î”) (bq : ğ“£ âŠ¢' q :: Î”) : ğ“£ âŠ¢' p â‹ q :: Î” where
  antecedent := bp.antecedent ++ bq.antecedent
  subset := by
    simp
    rintro p (hp | hp)
    Â· exact bp.subset _ hp
    Â· exact bq.subset _ hp
  derivation := Gentzen.andRight
      (Gentzen.wkLeft bp.derivation (List.subset_append_left _ _))
      (Gentzen.wkLeft bq.derivation (List.subset_append_right _ _))

def or (b : ğ“£ âŠ¢' p :: q :: Î”) : ğ“£ âŠ¢' p â‹ q :: Î” where
  antecedent := b.antecedent
  subset := b.subset
  derivation := Gentzen.orRight b.derivation

def deduction [DecidableEq F] {p} (b : cons p ğ“£ âŠ¢' Î”) : ğ“£ âŠ¢' ~p :: Î” where
  antecedent := b.antecedent.filter (Â· â‰  p)
  subset := by
    simp[List.mem_filter]
    intro q hq ne
    simpa[ne] using b.subset q hq
  derivation := negRight (wkLeft b.derivation $ by
    intro q hq
    by_cases e : q = p <;> simp[List.mem_filter, hq, e])

def deductionNeg [DecidableEq F] {p} (b : cons (~p) ğ“£ âŠ¢' Î”) : ğ“£ âŠ¢' p :: Î” where
  antecedent := b.antecedent.filter (Â· â‰  ~p)
  subset := by
    simp[List.mem_filter]
    intro q hq ne
    simpa[ne] using b.subset q hq
  derivation := ofNegLeft (wkLeft b.derivation $ by
    intro q hq
    by_cases e : q = ~p <;> simp[List.mem_filter, hq, e])

end Disjconseq

variable (F S)

instance : System F S := âŸ¨(Â· âŠ¢' [Â·])âŸ©

variable {F S}

instance : System.Axiomatized S where
  prfAxm := fun {ğ“£ f} hf â†¦
    âŸ¨[f], by simpa, closed _ (List.mem_singleton.mpr rfl) (List.mem_singleton.mpr rfl)âŸ©
  weakening := fun ss b => b.weakening ss

def toProof {ğ“£ : S} : {Î“ Î” : List F} â†’ Î“ âŠ¢Â² Î” â†’ (âˆ€ q âˆˆ Î“, ğ“£ âŠ¢ q) â†’ ğ“£ âŠ¢' Î”
  | [],     _, d, _ => toDisjconseq d (by simp)
  | q :: Î“, Î”, d, h =>
    let bn : ğ“£ âŠ¢' ~q :: Î” := toProof (negRight d) (fun q hq => h q (by simp[hq]))
    let b : ğ“£ âŠ¢' [q] := h q (by simp)
    b.cut' bn

instance : LawfulTwoSided S := âŸ¨toProofâŸ©

def of {p : F} (b : [] âŠ¢Â² [p]) {ğ“£ : S} : ğ“£ âŠ¢ p := âŸ¨[], by simp, bâŸ©

instance strongCut (S T) [Collection F S] [Collection F T] :
    System.StrongCut S T := âŸ¨fun dU dp â†¦ toProof dp.derivation (fun q hq => dU <| dp.subset q hq)âŸ©

def proofEquivDerivation {ğ“£ : S} {p : F} :
    ğ“£ âŠ¢ p â‰ƒ (Î” : {Î” : List F // âˆ€ Ï€ âˆˆ Î”, Ï€ âˆˆ ğ“£}) Ã— Î” âŠ¢Â² [p] :=
  DisjconseqEquivDerivation

lemma provable_iff {ğ“£ : S} {p : F} :
    ğ“£ âŠ¢! p â†” âˆƒ Î” : List F, (âˆ€ Ï€ âˆˆ Î”, Ï€ âˆˆ ğ“£) âˆ§ Î” âŠ¢Â²! [p] :=
  âŸ¨by rintro âŸ¨bâŸ©; rcases proofEquivDerivation b with âŸ¨Î”, dâŸ©; exact âŸ¨Î”, Î”.prop, âŸ¨dâŸ©âŸ©,
   by rintro âŸ¨Î”, h, âŸ¨dâŸ©âŸ©; exact âŸ¨proofEquivDerivation.symm âŸ¨âŸ¨Î”, hâŸ©, dâŸ©âŸ©âŸ©

instance deductiveExplosion : System.DeductiveExplosion S := âŸ¨fun {ğ“¢} b p â†¦
  let t : ğ“¢ âŠ¢ ~âŠ¥ := âŸ¨[], by simp, Gentzen.negRight (Gentzen.falsum _ _)âŸ©
  let b : ğ“¢ âŠ¢' [] := Disjconseq.cut' b t
  Disjconseq.wk b (by simp)âŸ©

instance compact : System.Compact S where
  Ï† := fun b â†¦ b.antecedent.toCollection
  Ï†Prf := fun b â†¦ âŸ¨b.antecedent, by intro p; simp, b.derivationâŸ©
  Ï†_subset := by intro ğ“£ p b; simpa [Collection.subset_iff] using b.subset
  Ï†_finite := by intro ğ“£ p b; simp

variable {ğ“£ : S}

lemma consistent_iff_isEmpty :
    System.Consistent ğ“£ â†” IsEmpty (ğ“£ âŠ¢' []) :=
  âŸ¨by contrapose
      simp [System.Consistent, System.not_consistent_iff_inconsistent, System.inconsistent_iff_provable_bot]
      intro b; exact âŸ¨b.wk (by simp)âŸ©,
   by contrapose
      simp [System.Consistent, System.not_consistent_iff_inconsistent, System.inconsistent_iff_provable_bot]
      rintro âŸ¨Î”, h, dâŸ©
      have : Î” âŠ¢Â² [] := Cut.cut d (falsum _ _)
      exact âŸ¨toDisjconseq this hâŸ©âŸ©

lemma inconsistent_iff_nonempty :
    System.Inconsistent ğ“£ â†” Nonempty (ğ“£ âŠ¢' []) := by
  simp [â†System.not_consistent_iff_inconsistent, consistent_iff_isEmpty]

lemma provable_iff_inconsistent {p} :
    ğ“£ âŠ¢! p â†” System.Inconsistent (cons (~p) ğ“£) :=
  âŸ¨by rintro âŸ¨âŸ¨Î”, h, dâŸ©âŸ©
      simp [inconsistent_iff_nonempty]
      exact âŸ¨âŸ¨~p :: Î”, by simp; intro q hq; right; exact h q hq, negLeft dâŸ©âŸ©,
   by letI := Classical.typeDecidableEq F
      simp [inconsistent_iff_nonempty]
      exact fun b â†¦ âŸ¨b.deductionNegâŸ©âŸ©

lemma refutable_iff_inconsistent {p} :
    ğ“£ âŠ¢! ~p â†” System.Inconsistent (cons p ğ“£) :=
  âŸ¨by rintro âŸ¨âŸ¨Î”, h, dâŸ©âŸ©
      simp [inconsistent_iff_nonempty]
      exact âŸ¨âŸ¨p :: Î”, by simp; intro q hq; right; exact h q hq, ofNegRight dâŸ©âŸ©,
   by letI := Classical.typeDecidableEq F
      simp [inconsistent_iff_nonempty]
      exact fun b â†¦ âŸ¨b.deductionâŸ©âŸ©

lemma consistent_insert_iff_not_refutable {p}  :
    System.Consistent (cons p ğ“£) â†” ğ“£ âŠ¬! ~p := by
  simp [System.Unprovable, refutable_iff_inconsistent, System.not_inconsistent_iff_consistent]

lemma inconsistent_of_provable_and_refutable {p}
    (bp : ğ“£ âŠ¢ p) (br : ğ“£ âŠ¢ ~p) : System.Inconsistent ğ“£ :=
  System.not_consistent_iff_inconsistent.mp <| fun A => by
    have : ğ“£ âŠ¢' [] := Disjconseq.cut bp br
    exact (consistent_iff_isEmpty.mp A).false this

lemma inconsistent_of_provable_and_refutable! {p}
    (bp : ğ“£ âŠ¢! p) (br : ğ“£ âŠ¢! ~p) : System.Inconsistent ğ“£ := by
  rcases bp with âŸ¨bpâŸ©; rcases br with âŸ¨brâŸ©
  exact inconsistent_of_provable_and_refutable bp br

section

variable {S S' : Type*} [Collection F S] [Collection F S']

def wk' {ğ“£ : S} {ğ“£' : S'} (H : Collection.set ğ“£ âŠ† Collection.set ğ“£') {p} : ğ“£ âŠ¢ p â†’ ğ“£' âŠ¢ p := Disjconseq.wk' H

def wk'! {ğ“£ : S} {ğ“£' : S'} (H : Collection.set ğ“£ âŠ† Collection.set ğ“£') {p} : ğ“£ âŠ¢! p â†’ ğ“£' âŠ¢! p := by
  rintro âŸ¨bâŸ©; exact âŸ¨wk' H bâŸ©

def le_of_subset {ğ“£ : S} {ğ“£' : S'} (H : Collection.set ğ“£ âŠ† Collection.set ğ“£') : ğ“£ â‰¤â‚› ğ“£' := fun _ â†¦ wk'! H

end

@[simp] lemma consistent_theory_iff_consistent :
    System.Consistent (System.theory ğ“£) â†” System.Consistent ğ“£ :=
  âŸ¨fun h â†¦ h.of_le (le_of_subset <| by simp [Set.subset_def]; intro p hp; exact System.Axiomatized.provable_axm  ğ“£ hp),
   fun consis â†¦ System.consistent_iff_unprovable_bot.mpr <| by
      rintro h
      have : System.Inconsistent ğ“£ := System.inconsistent_iff_provable_bot.mpr <| System.StrongCut.cut! (by simp) h
      exact System.not_inconsistent_iff_consistent.mpr consis thisâŸ©

end Gentzen

end LO
