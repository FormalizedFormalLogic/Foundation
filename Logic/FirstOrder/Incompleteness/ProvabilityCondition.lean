import Logic.FirstOrder.Arith.Theory
import Logic.Logic.HilbertStyle.Gentzen
import Logic.Logic.HilbertStyle.Supplemental

namespace LO.FirstOrder

structure ProvabilityPredicate (Lâ‚€ L : Language) where
  prov : Semisentence Lâ‚€ 1

namespace ProvabilityPredicate

section

variable [Semiterm.Operator.GoedelNumber Lâ‚€ (Sentence L)]

def pr (Î² : ProvabilityPredicate Lâ‚€ L) (Ïƒ : Sentence L) : Semisentence Lâ‚€ n := Î².prov/[â¸¢Ïƒâ¸£]

notation "â¦" Î² "â¦" Ïƒ:80 => pr Î² Ïƒ

class Conservative (Î² : ProvabilityPredicate Lâ‚€ L) (Tâ‚€ : Theory Lâ‚€) (T : outParam (Theory L)) where
  iff (Ïƒ : Sentence L) : T âŠ¢! Ïƒ â†” Tâ‚€ âŠ¢! â¦Î²â¦ Ïƒ

def consistency (Î² : ProvabilityPredicate Lâ‚€ L) : Sentence Lâ‚€ := ~â¦Î²â¦âŠ¥
notation "Conâ¦" Î² "â¦" => consistency Î²

end

section Conditions

variable [Semiterm.Operator.GoedelNumber L (Sentence L)]

class HilbertBernaysâ‚ (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) where
  D1 {Ïƒ : Sentence L} : T âŠ¢! Ïƒ â†’ Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ


class HilbertBernaysâ‚‚ (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) where
  D2 {Ïƒ Ï„ : Sentence L} : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ âŸ¶ Ï„) âŸ¶ â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦Ï„


class HilbertBernaysâ‚ƒ (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) where
  D3 {Ïƒ : Sentence L} : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦â¦Î²â¦Ïƒ

-- MEMO: ãŸã¨ãˆã°`P(Ïƒ)`ã‚’ã€Œ`Ïƒ`ãŒ`ğšºâ‚`æ–‡ã§ã‚ã‚‹ã€ãªã©ã‚’å–ã‚Œã°ï¼Œæ¨™æº–çš„ãªæ§‹æˆã‚’ã™ã‚Œã°`â¦Î²â¦Ïƒ`ã¯`ğšºâ‚`æ–‡ãªã®ã§ç›´ã¡ã«`HilbertBernaysâ‚ƒ`ãŒå¾—ã‚‰ã‚Œã‚‹ï¼
class FormalizedCompleteness (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) (P : Sentence L â†’ Prop) where
  FC {Ïƒ : Sentence L} : P Ïƒ â†’ Tâ‚€ âŠ¢! Ïƒ âŸ¶ â¦Î²â¦Ïƒ

def HilbertBernaysâ‚ƒ_of_FormalizedCompleteness {Î² : ProvabilityPredicate L L} {Tâ‚€ T}
  [Î².FormalizedCompleteness Tâ‚€ T P] (hP : âˆ€ Ïƒ, P (â¦Î²â¦Ïƒ))
  : Î².HilbertBernaysâ‚ƒ Tâ‚€ T := âŸ¨Î» {Ïƒ} => FormalizedCompleteness.FC (hP Ïƒ)âŸ©

class HilbertBernays (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L))
  extends Î².HilbertBernaysâ‚ Tâ‚€ T, Î².HilbertBernaysâ‚‚ Tâ‚€ T, Î².HilbertBernaysâ‚ƒ Tâ‚€ T

class Diagonalization (T : Theory L) where
  fixpoint : Semisentence L 1 â†’ Sentence L
  diag (Î¸) : T âŠ¢! fixpoint Î¸ âŸ· Î¸/[â¸¢fixpoint Î¸â¸£]


class Loeb (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) where
  LT {Ïƒ : Sentence L} : T âŠ¢! â¦Î²â¦Ïƒ âŸ¶ Ïƒ â†’ T âŠ¢! Ïƒ

class FormalizedLoeb (Î² : ProvabilityPredicate L L) (Tâ‚€ : Theory L) (T : outParam (Theory L)) where
  FLT {Ïƒ : Sentence L} : Tâ‚€ âŠ¢! â¦Î²â¦(â¦Î²â¦Ïƒ âŸ¶ Ïƒ) âŸ¶ â¦Î²â¦Ïƒ


section

variable {Tâ‚€ T : Theory L}
variable [Tâ‚€ â‰¼ T] {Ïƒ Ï„ : Sentence L}

def HilbertBernaysâ‚.D1s [HilbertBernaysâ‚ Î² Tâ‚€ T]: T âŠ¢! Ïƒ â†’ T âŠ¢! â¦Î²â¦Ïƒ := by
  intro h;
  apply System.Subtheory.prf! (ğ“¢ := Tâ‚€);
  apply HilbertBernaysâ‚.D1 h;

def HilbertBernaysâ‚‚.D2s [HilbertBernaysâ‚‚ Î² Tâ‚€ T] : T âŠ¢! â¦Î²â¦(Ïƒ âŸ¶ Ï„) âŸ¶ â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦Ï„ := by
  apply System.Subtheory.prf! (ğ“¢ := Tâ‚€);
  apply HilbertBernaysâ‚‚.D2;

def HilbertBernaysâ‚‚.D2' [HilbertBernays Î² Tâ‚€ T] [System.ModusPonens T] : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ âŸ¶ Ï„) â†’ Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦Ï„ := by
  intro h;
  exact HilbertBernaysâ‚‚.D2 â¨€ h;

def HilbertBernaysâ‚ƒ.D3s [HilbertBernaysâ‚ƒ Î² Tâ‚€ T] : T âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦â¦Î²â¦Ïƒ := by
  apply System.Subtheory.prf! (ğ“¢ := Tâ‚€);
  apply HilbertBernaysâ‚ƒ.D3;

namespace HilbertBernays

open LO.System

variable [DecidableEq (Sentence L)]
         [HilbertBernays Î² Tâ‚€ T]

def prov_distribute_imply (h : T âŠ¢! Ïƒ âŸ¶ Ï„) : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦Ï„ := HilbertBernaysâ‚‚.D2' $ HilbertBernaysâ‚.D1 h

def prov_distribute_iff (h : T âŠ¢! Ïƒ âŸ· Ï„) : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ âŸ· â¦Î²â¦Ï„ := by
  apply iff_intro!;
  . exact prov_distribute_imply $ conjâ‚'! h;
  . exact prov_distribute_imply $ conjâ‚‚'! h;

def prov_distribute_and : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ â‹ Ï„) âŸ¶ â¦Î²â¦Ïƒ â‹ â¦Î²â¦Ï„ := by
  have hâ‚ : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ â‹ Ï„) âŸ¶ â¦Î²â¦Ïƒ := HilbertBernaysâ‚‚.D2' <| HilbertBernaysâ‚.D1 conjâ‚!;
  have hâ‚‚ : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ â‹ Ï„) âŸ¶ â¦Î²â¦Ï„ := HilbertBernaysâ‚‚.D2' <| HilbertBernaysâ‚.D1 conjâ‚‚!;
  exact implyRightAnd! hâ‚ hâ‚‚;

def prov_distribute_and! : Tâ‚€ âŠ¢! â¦Î²â¦(Ïƒ â‹ Ï„) â†’ Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ â‹ â¦Î²â¦Ï„ := Î» h => prov_distribute_and â¨€ h

def prov_collect_and : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ â‹ â¦Î²â¦Ï„ âŸ¶ â¦Î²â¦(Ïƒ â‹ Ï„) := by
  have hâ‚ : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦(Ï„ âŸ¶ Ïƒ â‹ Ï„) := prov_distribute_imply $ conjâ‚ƒ!;
  have hâ‚‚ : Tâ‚€ âŠ¢! â¦Î²â¦(Ï„ âŸ¶ Ïƒ â‹ Ï„) âŸ¶ â¦Î²â¦Ï„ âŸ¶ â¦Î²â¦(Ïƒ â‹ Ï„) := HilbertBernaysâ‚‚.D2;
  apply andImplyIffImplyImply'!.mpr;
  exact imp_trans! hâ‚ hâ‚‚;

end HilbertBernays

end

end Conditions


section

variable [DecidableEq (Sentence L)]
         [Semiterm.Operator.GoedelNumber L (Sentence L)]
         {Tâ‚€ T : Theory L} [Tâ‚€ â‰¼ T] [Diagonalization Tâ‚€]
         {Î² : ProvabilityPredicate L L}
open LO.System LO.System.NegationEquiv
open HilbertBernaysâ‚ HilbertBernaysâ‚‚ HilbertBernaysâ‚ƒ HilbertBernays FormalizedCompleteness
open Diagonalization

abbrev goedel
  (Tâ‚€ T : Theory L) [Diagonalization Tâ‚€]
  (Î² : ProvabilityPredicate L L) [Î².HilbertBernaysâ‚ Tâ‚€ T] : Sentence L
  := fixpoint Tâ‚€ â€œx | Â¬!Î².prov xâ€
local notation "Î³" => goedel Tâ‚€ T Î²


section

variable [Î².HilbertBernaysâ‚ Tâ‚€ T]

lemma goedel_spec : Tâ‚€ âŠ¢! Î³ âŸ· ~â¦Î²â¦Î³ := by
  convert (diag (T := Tâ‚€) â€œx | Â¬!Î².prov xâ€);
  simp [goedel, â†Rew.hom_comp_app, Rew.substs_comp_substs];
  rfl;

private lemma goedel_specAuxâ‚ : T âŠ¢! Î³ âŸ· ~â¦Î²â¦Î³ := Subtheory.prf! (ğ“¢ := Tâ‚€) goedel_spec

private lemma goedel_specAuxâ‚‚ : T âŠ¢! ~Î³ âŸ¶ â¦Î²â¦Î³ := contraâ‚‚'! $ conjâ‚‚'! goedel_specAuxâ‚

-- MEMO: ç‰¹ã«GÃ¶delæ–‡`Î³`ã§å¥å…¨æ€§ãŒæˆã‚Šç«‹ã¤ã¨ã„ã†ä»®å®šï¼å®Ÿéš›ã«ã¯æ¨™æº–çš„ãªæ§‹æˆã‚’ã™ã‚Œã°`â¦Î²â¦Î³`ã¯`ğš·â‚`æ–‡ã§ã‚ã‚‹ã‹ã‚‰`ğš·â‚`å¥å…¨æ€§ã‚’ç¤ºã›ã°è‰¯ã„ï¼
class GoedelSound (Î² : ProvabilityPredicate L L) (Tâ‚€ T) [Diagonalization Tâ‚€] [Î².HilbertBernaysâ‚ Tâ‚€ T] where
  Î³_sound : T âŠ¢! â¦Î²â¦(goedel Tâ‚€ T Î²) â†’ T âŠ¢! (goedel Tâ‚€ T Î²)


-- MEMO: ç‰¹ã«GÃ¶delæ–‡ã®å¦å®š`~Î³`ã§å½¢å¼åŒ–ã•ã‚ŒãŸå®Œå…¨æ€§ãŒæˆã‚Šç«‹ã¤ã¨ã„ã†ä»®å®šï¼`~â¦Î²â¦Î³`ã¯`ğšºâ‚`æ–‡ã§ã‚ã‚‹ã‹ã‚‰å½¢å¼åŒ–ã•ã‚ŒãŸ`ğšºâ‚`å®Œå…¨æ€§ã‚’ç¤ºã›ã°è‰¯ã„ï¼
class NotGoedelFormalizedCompleted (Î² : ProvabilityPredicate L L) (Tâ‚€ T) [Diagonalization Tâ‚€] [Î².HilbertBernaysâ‚ Tâ‚€ T] (P : outParam (Sentence L â†’ Prop)) extends FormalizedCompleteness Î² Tâ‚€ T P where
  P_nÎ³ : P (~(goedel Tâ‚€ T Î²))

lemma NotGoedelFormalizedCompleted.Î³_fcomplete [Î².NotGoedelFormalizedCompleted Tâ‚€ T P] : Tâ‚€ âŠ¢! ~Î³ âŸ¶ â¦Î²â¦(~Î³) := by
  exact FC (T := T) (P := P) (NotGoedelFormalizedCompleted.P_nÎ³);

end


open GoedelSound NotGoedelFormalizedCompleted


section First

variable [Î².HilbertBernaysâ‚ Tâ‚€ T]

theorem unprovable_goedel [System.Consistent T] : T âŠ¬! Î³ := by
  intro h;
  have hâ‚ : T âŠ¢! â¦Î²â¦Î³ := D1s (Tâ‚€ := Tâ‚€) h;
  have hâ‚‚ : T âŠ¢! ~â¦Î²â¦Î³ := (conjâ‚'! goedel_specAuxâ‚) â¨€ h;
  have : T âŠ¢! âŠ¥ := (neg_equiv'!.mp hâ‚‚) â¨€ hâ‚;

  have := not_consistent_iff_inconsistent.mpr $ inconsistent_iff_provable_bot.mpr this;
  contradiction;

theorem unrefutable_goedel [System.Consistent T] [Î².GoedelSound Tâ‚€ T] : T âŠ¬! ~Î³ := by
  intro hâ‚‚;
  have hâ‚ : T âŠ¢! Î³ := Î³_sound $ goedel_specAuxâ‚‚ â¨€ hâ‚‚;
  have : T âŠ¢! âŠ¥ := (neg_equiv'!.mp hâ‚‚) â¨€ hâ‚;

  have := not_consistent_iff_inconsistent.mpr $ inconsistent_iff_provable_bot.mpr this;
  contradiction;

theorem goedel_independent [System.Consistent T] [Î².GoedelSound Tâ‚€ T] : System.Undecidable T Î³ := by
  suffices T âŠ¬! Î³ âˆ§ T âŠ¬! ~Î³ by simpa [System.Undecidable, not_or] using this;
  constructor;
  . apply unprovable_goedel;
  . apply unrefutable_goedel;

theorem first_incompleteness [System.Consistent T] [Î².GoedelSound Tâ‚€ T]
  : Â¬System.Complete T := System.incomplete_iff_exists_undecidable.mpr âŸ¨Î³, goedel_independentâŸ©

end First


section Second

lemma formalized_consistent_of_existance_unprovable [Î².HilbertBernays Tâ‚€ T] : Tâ‚€ âŠ¢! ~â¦Î²â¦Ïƒ âŸ¶ Conâ¦Î²â¦ := contraâ‚€'! $ D2 â¨€ (D1 efq!)

private lemma consistency_lemma_1 [Tâ‚€ â‰¼ U] [Î².HilbertBernays Tâ‚€ U] : (U âŠ¢! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦Ïƒ) â†” (U âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦(~Ïƒ)) := by
  constructor;
  . intro H;
    exact contraâ‚ƒ'! $ imp_trans! (Subtheory.prf! (ğ“¢ := Tâ‚€) formalized_consistent_of_existance_unprovable) H;
  . intro H;
    apply contraâ‚€'!;
    have : Tâ‚€ âŠ¢! â¦Î²â¦Ïƒ â‹ â¦Î²â¦(~Ïƒ) âŸ¶ â¦Î²â¦âŠ¥ := imp_trans! prov_collect_and $ prov_distribute_imply no_both!;
    have : U âŠ¢! â¦Î²â¦Ïƒ âŸ¶ â¦Î²â¦(~Ïƒ) âŸ¶ â¦Î²â¦âŠ¥ := Subtheory.prf! $ andImplyIffImplyImply'!.mp $ this;
    exact this â¨€â‚ H;

variable [Diagonalization T] [Î².HilbertBernays Tâ‚€ T]

/-- Formalized First theorem of unprovability -/
lemma formalized_unprovable_goedel [Î².NotGoedelFormalizedCompleted Tâ‚€ T P] : T âŠ¢! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦Î³ := by
  have hâ‚ : T âŠ¢! â¦Î²â¦Î³ âŸ¶ ~Î³ := contraâ‚'! $ conjâ‚'! $ goedel_specAuxâ‚;
  have hâ‚‚ : T âŠ¢! ~Î³ âŸ¶ â¦Î²â¦(~Î³) := Subtheory.prf! (ğ“¢ := Tâ‚€) $ Î³_fcomplete;
  exact consistency_lemma_1 (Tâ‚€ := Tâ‚€) |>.mpr $ imp_trans! hâ‚ hâ‚‚;

lemma iff_goedel_consistency [Î².NotGoedelFormalizedCompleted Tâ‚€ T P] : T âŠ¢! Î³ âŸ· Conâ¦Î²â¦
  := iff_trans! goedel_specAuxâ‚ $ iff_intro! (Subtheory.prf! (ğ“¢ := Tâ‚€) formalized_consistent_of_existance_unprovable) (formalized_unprovable_goedel)

theorem unprovable_consistency [Î².NotGoedelFormalizedCompleted Tâ‚€ T P] [System.Consistent T] : T âŠ¬! Conâ¦Î²â¦
  := unprovable_iff! iff_goedel_consistency |>.mp $ unprovable_goedel (Tâ‚€ := Tâ‚€)

theorem unrefutable_consistency [Î².NotGoedelFormalizedCompleted Tâ‚€ T P] [System.Consistent T] [Î².GoedelSound Tâ‚€ T] : T âŠ¬! ~Conâ¦Î²â¦
  := unprovable_iff! (neg_iff'! $ iff_goedel_consistency) |>.mp $ unrefutable_goedel (Tâ‚€ := Tâ‚€)

end Second


def kreisel
  (Tâ‚€ T : Theory L) [Diagonalization Tâ‚€]
  (Î² : ProvabilityPredicate L L) [Î².HilbertBernays Tâ‚€ T]
  (Ïƒ : Sentence L) : Sentence L := fixpoint Tâ‚€ â€œx | !Î².prov x â†’ !Ïƒâ€

local notation "Îº(" Ïƒ ")" => kreisel Tâ‚€ T Î² Ïƒ

section

variable [Î².HilbertBernays Tâ‚€ T]

lemma kreisel_spec (Ïƒ : Sentence L) : Tâ‚€ âŠ¢! Îº(Ïƒ) âŸ· (â¦Î²â¦(Îº(Ïƒ)) âŸ¶ Ïƒ) := by
  convert (diag (T := Tâ‚€) â€œx | !Î².prov x â†’ !Ïƒâ€);
  simp [kreisel, â†Rew.hom_comp_app, Rew.substs_comp_substs];
  rfl;

lemma kreisel_specAuxâ‚ (Ïƒ : Sentence L) : Tâ‚€ âŠ¢! â¦Î²â¦Îº(Ïƒ) âŸ¶ â¦Î²â¦Ïƒ := (imp_trans! (D2 â¨€ (D1 (Subtheory.prf! $ conjâ‚'! (kreisel_spec Ïƒ)))) D2) â¨€â‚ D3

lemma kreisel_specAuxâ‚‚ (Ïƒ : Sentence L) : Tâ‚€ âŠ¢! (â¦Î²â¦Îº(Ïƒ) âŸ¶ Ïƒ) âŸ¶ Îº(Ïƒ) := conjâ‚‚'! (kreisel_spec Ïƒ)

end


section LoebTheorem

variable [Î².HilbertBernays Tâ‚€ T]

theorem loeb_theorm (H : T âŠ¢! â¦Î²â¦Ïƒ âŸ¶ Ïƒ) : T âŠ¢! Ïƒ := by
  have dâ‚ : T âŠ¢! â¦Î²â¦Îº(Ïƒ) âŸ¶ Ïƒ := imp_trans! (Subtheory.prf! (kreisel_specAuxâ‚ Ïƒ)) H;
  have dâ‚‚ : T âŠ¢! â¦Î²â¦Îº(Ïƒ)      := Subtheory.prf! (ğ“¢ := Tâ‚€) (D1 $ Subtheory.prf! (kreisel_specAuxâ‚‚ Ïƒ) â¨€ dâ‚);
  exact dâ‚ â¨€ dâ‚‚;

instance : Loeb Î² Tâ‚€ T := âŸ¨loeb_theorm (Tâ‚€ := Tâ‚€) (T := T)âŸ©

theorem formalized_loeb_theorem : Tâ‚€ âŠ¢! â¦Î²â¦(â¦Î²â¦Ïƒ âŸ¶ Ïƒ) âŸ¶ â¦Î²â¦Ïƒ := by
  have hÎºâ‚ : Tâ‚€ âŠ¢! â¦Î²â¦(Îº(Ïƒ)) âŸ¶ â¦Î²â¦Ïƒ := kreisel_specAuxâ‚ Ïƒ;
  have : Tâ‚€ âŠ¢! (â¦Î²â¦Ïƒ âŸ¶ Ïƒ) âŸ¶ (â¦Î²â¦Îº(Ïƒ) âŸ¶ Ïƒ) := imply_left_replace! hÎºâ‚;
  have : T âŠ¢! (â¦Î²â¦Ïƒ âŸ¶ Ïƒ) âŸ¶ Îº(Ïƒ) := Subtheory.prf! (ğ“¢ := Tâ‚€) $ imp_trans! this (kreisel_specAuxâ‚‚ Ïƒ);
  exact imp_trans! (D2 â¨€ (D1 this)) hÎºâ‚;

instance : FormalizedLoeb Î² Tâ‚€ T := âŸ¨formalized_loeb_theorem (Tâ‚€ := Tâ‚€) (T := T)âŸ©

end LoebTheorem


section

-- another proof of the 2nd incompleteness theorem via LÃ¶b
lemma unprovable_consistency_via_loeb
  [Î².Loeb Tâ‚€ T]
  : System.Consistent T â†’ T âŠ¬! Conâ¦Î²â¦ := by
  contrapose;
  intro hC; simp at hC;
  have : T âŠ¢! âŠ¥ := Loeb.LT Tâ‚€ $ neg_equiv'!.mp hC;
  apply System.not_consistent_iff_inconsistent.mpr;
  apply System.inconsistent_of_provable this;

/-- Formalized Second theorem of unprovability -/
lemma formalized_unprovable_consistency
  [Î².HilbertBernays Tâ‚€ T] [Î².NotGoedelFormalizedCompleted Tâ‚€ T P]
  [System.Consistent T] [Î².GoedelSound Tâ‚€ T]
  : T âŠ¬! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦(~Conâ¦Î²â¦) := by
  by_contra hC;
  have : T âŠ¢! ~Conâ¦Î²â¦ := Loeb.LT Tâ‚€ $ contraâ‚'! hC;
  have : T âŠ¬! ~Conâ¦Î²â¦ := unrefutable_consistency (Tâ‚€ := Tâ‚€);
  contradiction;

/-- Formalized First theorem of unrefutability -/
lemma formalized_unrefutable_goedel
  [Î².HilbertBernays Tâ‚€ T] [Î².NotGoedelFormalizedCompleted Tâ‚€ T P]
  [System.Consistent T] [Î².GoedelSound Tâ‚€ T]
  : T âŠ¬! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦(~Î³) := by
  by_contra hC;
  have : T âŠ¬! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦(~Conâ¦Î²â¦)  := formalized_unprovable_consistency (Tâ‚€ := Tâ‚€);
  have : T âŠ¢! Conâ¦Î²â¦ âŸ¶ ~â¦Î²â¦(~Conâ¦Î²â¦) := imp_trans! hC $ Subtheory.prf! $ conjâ‚'! $ neg_iff'! $ prov_distribute_iff (Tâ‚€ := Tâ‚€) $ neg_iff'! $ iff_goedel_consistency;
  contradiction;

end

end

end ProvabilityPredicate

end LO.FirstOrder
