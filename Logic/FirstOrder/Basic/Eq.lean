import Logic.FirstOrder.Basic.Elab
import Logic.FirstOrder.Basic.Semantics.Elementary
import Logic.FirstOrder.Basic.Soundness

namespace LO

namespace FirstOrder

variable {L : Language} {Î¼ : Type*} [Semiformula.Operator.Eq L]
namespace Semiterm

def varSumInL {k} : Fin k â†’ Semiterm L Î¼ (k + k) := fun i => #(Fin.castLE (by simp) i)

def varSumInR {k} : Fin k â†’ Semiterm L Î¼ (k + k) := fun i => #(Fin.natAdd k i)

@[simp] lemma substs_varSumInL (wâ‚ wâ‚‚ : Fin k â†’ Semiterm L Î¼ n) (i) :
  Rew.substs (Matrix.vecAppend rfl wâ‚ wâ‚‚) (varSumInL i) = wâ‚ i := by simp [varSumInL, Matrix.vecAppend_eq_ite]

@[simp] lemma substs_varSumInR (wâ‚ wâ‚‚ : Fin k â†’ Semiterm L Î¼ n) (i) :
  Rew.substs (Matrix.vecAppend rfl wâ‚ wâ‚‚) (varSumInR i) = wâ‚‚ i := by simp [varSumInR, Matrix.vecAppend_eq_ite]

@[simp] lemma emb_varSumInL {o} [IsEmpty o] (i : Fin k) :
  (Rew.emb (varSumInL (Î¼ := o) i) : Semiterm L Î¼ (k + k)) = varSumInL i := by simp [varSumInL]

@[simp] lemma emb_varSumInR {o} [IsEmpty o] (i : Fin k) :
  (Rew.emb (varSumInR (Î¼ := o) i) : Semiterm L Î¼ (k + k)) = varSumInR i := by simp [varSumInR]

end Semiterm

namespace Semiformula

def vecEq {k} (v w : Fin k â†’ Semiterm L Î¼ n) : Semiformula L Î¼ n := Matrix.conj (fun i => â€œ!!(v i) = !!(w i)â€)

end Semiformula

namespace Theory
open Semiterm

class Sub (T U : Theory L) where
  sub : T âŠ† U

section Eq

inductive eqAxiom : Theory L
  | refl : eqAxiom â€œâˆ€ #0 = #0â€
  | symm : eqAxiom â€œâˆ€ âˆ€ (#1 = #0 â†’ #0 = #1)â€
  | trans : eqAxiom â€œâˆ€ âˆ€ âˆ€ (#2 = #1 â†’ #1 = #0 â†’ #2 = #0)â€
  | funcExt {k} (f : L.Func k) :
    eqAxiom â€œâˆ€* (!(Semiformula.vecEq varSumInL varSumInR) â†’ !!(Semiterm.func f varSumInL) = !!(Semiterm.func f varSumInR))â€
  | relExt {k} (r : L.Rel k) :
    eqAxiom â€œâˆ€* (!(Semiformula.vecEq varSumInL varSumInR) â†’ !(Semiformula.rel r varSumInL) â†’ !(Semiformula.rel r varSumInR))â€

notation "ğ„ğ" => eqAxiom

end Eq

end Theory

abbrev Theory.addEqAxiom (T : Theory L) : Theory L := T + ğ„ğ

postfix:max "â¼" => Theory.addEqAxiom

abbrev ConsequenceWithEq (M : Type*) [Semantics (Sentence L) M] (T : Theory L) (Ïƒ : Sentence L) : Prop := Tâ¼ âŠ¨[M] Ïƒ

abbrev Consequenceâ‚€WithEq (T : Theory L) (Ïƒ : Sentence L) : Prop := Tâ¼ âŠ¨ Ïƒ

notation T:45 " âŠ¨â‚Œ[" M "] " Ïƒ:46 => ConsequenceWithEq M T Ïƒ

notation T:45 " âŠ¨â‚Œ " Ïƒ:46 => Consequenceâ‚€WithEq T Ïƒ

abbrev ProofWithEq (T : Theory L) (Ïƒ : Sentence L) : Type _ := Tâ¼ âŠ¢ Ïƒ

abbrev ProvableWithEq (T : Theory L) (Ïƒ : Sentence L) : Prop := Tâ¼ âŠ¢! Ïƒ

infix:45 " âŠ¢â‚Œ " => ProofWithEq

infix:45 " âŠ¢â‚Œ! " => ProvableWithEq

namespace Structure

namespace Eq

@[simp] lemma models_eqAxiom {M : Type u} [Nonempty M] [Structure L M] [Structure.Eq L M] : M âŠ§â‚˜* (ğ„ğ : Theory L) := âŸ¨by
  intro Ïƒ h
  cases h <;> simp [models_def, Semiformula.vecEq, Semiterm.val_func]
  Â· intro e h; congr; funext i; exact h i
  case relExt r =>
    simp [Semiformula.eval_rel]; intro e h; simp [congr_arg (rel r) (funext h)]âŸ©

variable (L)

instance models_eqAxiom' (M : Type u) [Nonempty M] [Structure L M] [Structure.Eq L M] : M âŠ§â‚˜* (ğ„ğ : Theory L) := Structure.Eq.models_eqAxiom

variable {M : Type u} [Nonempty M] [Structure L M]

def eqv (a b : M) : Prop := (@Semiformula.Operator.Eq.eq L _).val ![a, b]

variable {L}

variable (H : M âŠ§â‚˜* (ğ„ğ : Theory L))

open Semiterm Theory Semiformula

lemma eqv_refl (a : M) : eqv L a a := by
  have : M âŠ§â‚˜ â€œâˆ€ #0 = #0â€ := H.realize (Theory.eqAxiom.refl (L := L))
  simp [models_def] at this
  exact this a

lemma eqv_symm {a b : M} : eqv L a b â†’ eqv L b a := by
  have : M âŠ§â‚˜ â€œâˆ€ âˆ€ (#1 = #0 â†’ #0 = #1)â€ := H.realize (Theory.eqAxiom.symm (L := L))
  simp [models_def] at this
  exact this a b

lemma eqv_trans {a b c : M} : eqv L a b â†’ eqv L b c â†’ eqv L a c := by
  have : M âŠ§â‚˜ â€œâˆ€ âˆ€ âˆ€ (#2 = #1 â†’ #1 = #0 â†’ #2 = #0)â€ := H.realize (Theory.eqAxiom.trans (L := L))
  simp [models_def] at this
  exact this a b c

lemma eqv_funcExt {k} (f : L.Func k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    eqv L (func f v) (func f w) := by
  have : M âŠ§â‚˜ â€œâˆ€* (!(vecEq varSumInL varSumInR) â†’ !!(Semiterm.func f varSumInL) = !!(Semiterm.func f varSumInR))â€ :=
    H.realize (eqAxiom.funcExt f (L := L))
  simp [varSumInL, varSumInR, models_def, vecEq, Semiterm.val_func] at this
  simpa [Matrix.vecAppend_eq_ite] using this (Matrix.vecAppend rfl v w) (fun i => by simpa [Matrix.vecAppend_eq_ite] using h i)

lemma eqv_relExt_aux {k} (r : L.Rel k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    rel r v â†’ rel r w := by
  have : M âŠ§â‚˜ â€œâˆ€* (!(vecEq varSumInL varSumInR) â†’ !(Semiformula.rel r varSumInL) â†’ !(Semiformula.rel r varSumInR))â€ :=
    H.realize (eqAxiom.relExt r (L := L))
  simp [varSumInL, varSumInR, models_def, vecEq, Semiterm.val_func, eval_rel (r := r)] at this
  simpa [eval_rel, Matrix.vecAppend_eq_ite] using this (Matrix.vecAppend rfl v w) (fun i => by simpa [Matrix.vecAppend_eq_ite] using h i)

lemma eqv_relExt {k} (r : L.Rel k) {v w : Fin k â†’ M} (h : âˆ€ i, eqv L (v i) (w i)) :
    rel r v = rel r w := by
  simp; constructor
  Â· exact eqv_relExt_aux H r h
  Â· exact eqv_relExt_aux H r (fun i => eqv_symm H (h i))

lemma eqv_equivalence : Equivalence (eqv L (M := M)) where
  refl := eqv_refl H
  symm := eqv_symm H
  trans := eqv_trans H

def eqvSetoid (H : M âŠ§â‚˜* (ğ„ğ : Theory L)) : Setoid M := Setoid.mk (eqv L) (eqv_equivalence H)

def QuotEq := Quotient (eqvSetoid H)

instance QuotEq.inhabited : Nonempty (QuotEq H) := Nonempty.map (âŸ¦Â·âŸ§) inferInstance

lemma of_eq_of {a b : M} : (âŸ¦aâŸ§ : QuotEq H) = âŸ¦bâŸ§ â†” eqv L a b := Quotient.eq (r := eqvSetoid H)

namespace QuotEq

def func â¦ƒkâ¦„ (f : L.Func k) (v : Fin k â†’ QuotEq H) : QuotEq H :=
  Quotient.liftVec (s := eqvSetoid H) (âŸ¦Structure.func f Â·âŸ§) (fun _ _ hvw => (of_eq_of H).mpr (eqv_funcExt H f hvw)) v

def rel â¦ƒkâ¦„ (r : L.Rel k) (v : Fin k â†’ QuotEq H) : Prop :=
  Quotient.liftVec (s := eqvSetoid H) (Structure.rel r) (fun _ _ hvw => eqv_relExt H r hvw) v

variable {H}

instance struc : Structure L (QuotEq H) where
  func := QuotEq.func H
  rel := QuotEq.rel H

lemma funk_mk {k} (f : L.Func k) (v : Fin k â†’ M) : Structure.func (M := QuotEq H) f (fun i => âŸ¦v iâŸ§) = âŸ¦Structure.func f vâŸ§ :=
  Quotient.liftVec_mk (s := eqvSetoid H) _ _ _

lemma rel_mk {k} (r : L.Rel k) (v : Fin k â†’ M) : Structure.rel (M := QuotEq H) r (fun i => âŸ¦v iâŸ§) â†” Structure.rel r v :=
  of_eq <| Quotient.liftVec_mk (s := eqvSetoid H) _ _ _

lemma val_mk {e} {Îµ} (t : Semiterm L Î¼ n) : Semiterm.valm (QuotEq H) (fun i => âŸ¦e iâŸ§) (fun i => âŸ¦Îµ iâŸ§) t = âŸ¦Semiterm.valm M e Îµ tâŸ§ :=
  by induction t <;> simp [*, funk_mk, Semiterm.val_func]

lemma eval_mk {e} {Îµ} {p : Semiformula L Î¼ n} :
    Semiformula.Evalm (QuotEq H) (fun i => âŸ¦e iâŸ§) (fun i => âŸ¦Îµ iâŸ§) p â†” Semiformula.Evalm M e Îµ p := by
  induction p using Semiformula.rec' <;> simp [*, Semiformula.eval_rel, Semiformula.eval_nrel, val_mk, rel_mk]
  case hall n p ih =>
    constructor
    Â· intro h a; exact (ih (e := a :> e)).mp (by simpa [Matrix.comp_vecCons] using h âŸ¦aâŸ§)
    Â· intro h a;
      induction' a using Quotient.ind with a
      simpa [Matrix.comp_vecCons] using ih.mpr (h a)
  case hex n p ih =>
    constructor
    Â· intro âŸ¨a, hâŸ©
      induction' a using Quotient.ind with a
      exact âŸ¨a, (ih (e := a :> e)).mp (by simpa [Matrix.comp_vecCons] using h)âŸ©
    Â· intro âŸ¨a, hâŸ©; exact âŸ¨âŸ¦aâŸ§, by simpa [Matrix.comp_vecCons] using ih.mpr hâŸ©

lemma models_iff {Ïƒ : Sentence L} : (QuotEq H) âŠ§â‚˜ Ïƒ â†” M âŠ§â‚˜ Ïƒ := by
  simpa [models_def, Semiformula.Evalf, eq_finZeroElim, Empty.eq_elim] using
    eval_mk (H := H) (e := finZeroElim) (Îµ := Empty.elim) (p := Ïƒ)

variable (H)

lemma elementaryEquiv : QuotEq H â‰¡â‚‘[L] M := fun _ => models_iff

variable {H}

lemma rel_eq (a b : QuotEq H) : (@Semiformula.Operator.Eq.eq L _).val (M := QuotEq H) ![a, b] â†” a = b := by
  induction' a using Quotient.ind with a
  induction' b using Quotient.ind with b
  rw[of_eq_of]; simp [eqv, Semiformula.Operator.val];
  simpa [Evalm, Matrix.fun_eq_vecâ‚‚, Empty.eq_elim] using
    eval_mk (H := H) (e := ![a, b]) (Îµ := Empty.elim) (p := Semiformula.Operator.Eq.eq.sentence)

instance structureEq : Structure.Eq L (QuotEq H) := âŸ¨rel_eqâŸ©

end QuotEq

end Eq

end Structure

lemma consequence_iff_eq {T : Theory L} [ğ„ğ â‰¼ T] {Ïƒ : Sentence L} :
    T âŠ¨[Struc.{v, u} L] Ïƒ â†” (âˆ€ (M : Type v) [Nonempty M] [Structure L M] [Structure.Eq L M], M âŠ§â‚˜* T â†’ M âŠ§â‚˜ Ïƒ) := by
  simp [consequence_iff]; constructor
  Â· intro h M x s _ hM; exact h M x hM
  Â· intro h M x s hM
    haveI : Nonempty M := âŸ¨xâŸ©
    have H : M âŠ§â‚˜* (ğ„ğ : Theory L) := models_of_subtheory hM
    have e : Structure.Eq.QuotEq H â‰¡â‚‘[L] M := Structure.Eq.QuotEq.elementaryEquiv H
    exact e.models.mp $ h (Structure.Eq.QuotEq H) âŸ¦xâŸ§ (e.modelsTheory.mpr hM)

lemma consequence_iff_eq' {T : Theory L} [ğ„ğ â‰¼ T] {Ïƒ : Sentence L} :
    T âŠ¨[Struc.{v, u} L] Ïƒ â†” (âˆ€ (M : Type v) [Nonempty M] [Structure L M] [Structure.Eq L M] [M âŠ§â‚˜* T], M âŠ§â‚˜ Ïƒ) := by
  rw [consequence_iff_eq]

lemma consequence_iff_add_eq {T : Theory L} {Ïƒ : Sentence L} :
    T âŠ¨â‚Œ[Struc.{v, u} L] Ïƒ â†” (âˆ€ (M : Type v) [Nonempty M] [Structure L M] [Structure.Eq L M], M âŠ§â‚˜* T â†’ M âŠ§â‚˜ Ïƒ) :=
  Iff.trans consequence_iff_eq (forallâ‚„_congr <| fun M _ _ _ â†¦ by simp)

lemma satisfiable_iff_eq {T : Theory L} [ğ„ğ â‰¼ T] :
    Semantics.Satisfiable (Struc.{v, u} L) T â†” (âˆƒ (M : Type v) (_ : Nonempty M) (_ : Structure L M) (_ : Structure.Eq L M), M âŠ§â‚˜* T) := by
  simp [satisfiable_iff]; constructor
  Â· intro âŸ¨M, x, s, hMâŸ©;
    haveI : Nonempty M := âŸ¨xâŸ©
    have H : M âŠ§â‚˜* (ğ„ğ : Theory L) := models_of_subtheory hM
    have e : Structure.Eq.QuotEq H â‰¡â‚‘[L] M := Structure.Eq.QuotEq.elementaryEquiv H
    exact âŸ¨Structure.Eq.QuotEq H, âŸ¦xâŸ§, inferInstance, inferInstance, e.modelsTheory.mpr hMâŸ©
  Â· intro âŸ¨M, i, s, _, hMâŸ©; exact âŸ¨M, i, s, hMâŸ©

def ModelOfSatEq {T : Theory L} [ğ„ğ â‰¼ T] (sat : Semantics.Satisfiable (Struc.{v, u} L) T) : Type _ :=
  have H : ModelOfSat sat âŠ§â‚˜* (ğ„ğ : Theory L) := models_of_subtheory (ModelOfSat.models sat)
  Structure.Eq.QuotEq H

namespace ModelOfSatEq

variable {T : Theory L} [ğ„ğ â‰¼ T] (sat : Semantics.Satisfiable (Struc.{v, u} L) T)

noncomputable instance : Nonempty (ModelOfSatEq sat) := Structure.Eq.QuotEq.inhabited _

noncomputable instance struc : Structure L (ModelOfSatEq sat) := Structure.Eq.QuotEq.struc

noncomputable instance : Structure.Eq L (ModelOfSatEq sat) := Structure.Eq.QuotEq.structureEq

lemma models : ModelOfSatEq sat âŠ§â‚˜* T :=
  have e : ModelOfSatEq sat â‰¡â‚‘[L] ModelOfSat sat :=
    Structure.Eq.QuotEq.elementaryEquiv (models_of_subtheory (ModelOfSat.models sat))
  e.modelsTheory.mpr (ModelOfSat.models _)

instance mod : ModelOfSatEq sat âŠ§â‚˜* T := models sat

open Semiterm Semiformula

noncomputable instance [Operator.Zero L] : Zero (ModelOfSatEq sat) := âŸ¨(@Operator.Zero.zero L _).val ![]âŸ©

instance strucZero [Operator.Zero L] : Structure.Zero L (ModelOfSatEq sat) := âŸ¨rflâŸ©

noncomputable instance [Operator.One L] : One (ModelOfSatEq sat) := âŸ¨(@Operator.One.one L _).val ![]âŸ©

instance [Operator.One L] : Structure.One L (ModelOfSatEq sat) := âŸ¨rflâŸ©

noncomputable instance [Operator.Add L] : Add (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Add.add L _).val ![x, y]âŸ©

instance [Operator.Add L] : Structure.Add L (ModelOfSatEq sat) := âŸ¨fun _ _ => rflâŸ©

noncomputable instance [Operator.Mul L] : Mul (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Mul.mul L _).val ![x, y]âŸ©

instance [Operator.Mul L] : Structure.Mul L (ModelOfSatEq sat) := âŸ¨fun _ _ => rflâŸ©

instance [Operator.LT L] : LT (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.LT.lt L _).val ![x, y]âŸ©

instance [Operator.LT L] : Structure.LT L (ModelOfSatEq sat) := âŸ¨fun _ _ => iff_of_eq rflâŸ©

instance [Operator.Mem L] : Membership (ModelOfSatEq sat) (ModelOfSatEq sat) :=
  âŸ¨fun x y => (@Operator.Mem.mem L _).val ![x, y]âŸ©

instance [Operator.Mem L] : Structure.Mem L (ModelOfSatEq sat) := âŸ¨fun _ _ => iff_of_eq rflâŸ©

end ModelOfSatEq

namespace Semiformula

def existsUnique (p : Semiformula L Î¼ (n + 1)) : Semiformula L Î¼ n :=
  âˆƒ' (p â‹ (âˆ€' ((Rew.substs (#0 :> (#Â·.succ.succ))).hom p âŸ¶ â€œ#0 = #1â€)))

prefix:64 "âˆƒ'! " => existsUnique

variable {M : Type*} [s : Structure L M] [Structure.Eq L M]

@[simp] lemma eval_existsUnique {e Îµ} {p : Semiformula L Î¼ (n + 1)} :
    Eval s e Îµ (âˆƒ'! p) â†” âˆƒ! x, Eval s (x :> e) Îµ p := by
  simp [existsUnique, Semiformula.eval_substs, Matrix.comp_vecCons', ExistsUnique]

end Semiformula

end FirstOrder

end LO
