import Logic.FirstOrder.Arith.Model
import Logic.Vorspiel.ExistsUnique
import Mathlib.Algebra.Order.Monoid.Canonical.Defs
import Mathlib.Algebra.Associated
--import Logic.FirstOrder.Principia.Meta

namespace LO

namespace FirstOrder

namespace Arith

noncomputable section

variable {M : Type} [Zero M] [One M] [Add M] [Mul M] [LT M] [ùêèùêÄ‚Åª.Mod M]

open Language

namespace Model

instance : LE M := ‚ü®fun x y => x = y ‚à® x < y‚ü©

lemma le_def {x y : M} : x ‚â§ y ‚Üî x = y ‚à® x < y := iff_of_eq rfl

protected lemma add_zero : ‚àÄ x : M, x + 0 = x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addZero oRing _)

protected lemma add_assoc : ‚àÄ x y z : M, (x + y) + z = x + (y + z) := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addAssoc oRing _)

protected lemma add_comm : ‚àÄ x y : M, x + y = y + x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addComm oRing _)

lemma add_eq_of_lt : ‚àÄ x y : M, x < y ‚Üí ‚àÉ z, x + z = y := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addEqOfLt oRing _)

@[simp] lemma zero_le : ‚àÄ x : M, 0 ‚â§ x := by
  simpa[models_iff, Structure.le_iff_of_eq_of_lt] using Theory.Mod.models M (@Theory.PAminus.zeroLe oRing _)

lemma zero_lt_one : (0 : M) < 1 := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.zeroLtOne oRing _)

lemma one_le_of_zero_lt : ‚àÄ x : M, 0 < x ‚Üí 1 ‚â§ x := by
  simpa[models_iff, Structure.le_iff_of_eq_of_lt] using Theory.Mod.models M (@Theory.PAminus.oneLeOfZeroLt oRing _)

lemma add_lt_add : ‚àÄ x y z : M, x < y ‚Üí x + z < y + z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addLtAdd oRing _)

protected lemma mul_zero : ‚àÄ x : M, x * 0 = 0 := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulZero oRing _)

protected lemma mul_one : ‚àÄ x : M, x * 1 = x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulOne oRing _)

protected lemma mul_assoc : ‚àÄ x y z : M, (x * y) * z = x * (y * z) := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulAssoc oRing _)

protected lemma mul_comm : ‚àÄ x y : M, x * y = y * x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulComm oRing _)

lemma mul_lt_mul : ‚àÄ x y z : M, x < y ‚Üí 0 < z ‚Üí x * z < y * z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulLtMul oRing _)

lemma distr : ‚àÄ x y z : M, x * (y + z) = x * y + x * z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.distr oRing _)

lemma lt_irrefl : ‚àÄ x : M, ¬¨x < x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltIrrefl oRing _)

lemma lt_trans : ‚àÄ x y z : M, x < y ‚Üí y < z ‚Üí x < z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltTrans oRing _)

lemma lt_tri : ‚àÄ x y : M, x < y ‚à® x = y ‚à® y < x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltTri oRing _)

instance : AddCommMonoid M where
  add_assoc := Model.add_assoc
  zero_add  := fun x => Model.add_comm x 0 ‚ñ∏ Model.add_zero x
  add_zero  := Model.add_zero
  add_comm  := Model.add_comm

instance : CommMonoid M where
  mul_assoc := Model.mul_assoc
  one_mul   := fun x => Model.mul_comm x 1 ‚ñ∏ Model.mul_one x
  mul_one   :=  Model.mul_one
  mul_comm  := Model.mul_comm

instance : LinearOrder M where
  le_refl := fun x => Or.inl (by simp)
  le_trans := by
    rintro x y z (rfl | hx) (rfl | hy) <;> simp[*, le_def]
    ¬∑ exact Or.inr (Model.lt_trans _ _ _ hx hy)
  le_antisymm := by
    rintro x y (rfl | hx) <;> simp
    rintro (rfl | hy) <;> try simp
    exact False.elim $ Model.lt_irrefl _ (Model.lt_trans _ _ _ hx hy)
  le_total := by
    intro x y
    rcases Model.lt_tri x y with (h | rfl | h) <;> simp[*, le_def]
  lt_iff_le_not_le := fun x y =>
    ‚ü®fun h => ‚ü®Or.inr h, by
      simp[le_def]; rintro (rfl | h'); { exact lt_irrefl y h }; { exact lt_irrefl _ (lt_trans _ _ _ h h') }‚ü©,
     by simp[not_or, le_def]; rintro (rfl | h) <;> simp[*] ‚ü©
  decidableLE := fun _ _ => Classical.dec _

protected lemma zero_mul : ‚àÄ x : M, 0 * x = 0 := fun x => by simpa[mul_comm] using Model.mul_zero x

instance : LinearOrderedCommSemiring M where
  left_distrib := distr
  right_distrib := fun x y z => by simp[mul_comm _ z]; exact distr z x y
  zero_mul := Model.zero_mul
  mul_zero := Model.mul_zero
  mul_assoc := Model.mul_assoc
  mul_comm := mul_comm
  one_mul   := fun x => Model.mul_comm x 1 ‚ñ∏ Model.mul_one x
  mul_one   :=  Model.mul_one
  add_le_add_left := by rintro x y (rfl | h) z <;> simp[add_comm z]; exact Or.inr (add_lt_add x y z h)
  zero_le_one := Or.inr zero_lt_one
  le_of_add_le_add_left := by
    rintro x y z h
    have : y ‚â§ z ‚à® z < y := le_or_lt y z
    rcases this with (hyz | hyz)
    ¬∑ exact hyz
    ¬∑ have : x + z < x + y := by simpa[add_comm] using add_lt_add z y x hyz
      exact False.elim ((lt_iff_not_ge _ _).mp this h)
  exists_pair_ne := ‚ü®0, 1, ne_of_lt zero_lt_one‚ü©
  mul_lt_mul_of_pos_left := by
    rintro x y z h hz; simp[mul_zero]; { simpa[mul_comm z] using mul_lt_mul x y z h hz }
  mul_lt_mul_of_pos_right := by
    rintro x y z h hz; simp[mul_zero]; { simpa using mul_lt_mul x y z h hz }
  le_total := le_total
  decidableLE := fun _ _ => Classical.dec _

instance : CanonicallyOrderedAddCommMonoid M where
  bot := 0
  bot_le := by simp
  exists_add_of_le := by
    rintro x y (rfl | h)
    ¬∑ exact ‚ü®0, by simp‚ü©
    ¬∑ simpa[eq_comm] using add_eq_of_lt x y h
  le_self_add := by intro x y; simp

@[simp] lemma numeral_eq_natCast : (n : ‚Ñï) ‚Üí (ORingSymbol.numeral n : M) = n
  | 0     => rfl
  | 1     => by simp
  | n + 2 => by simp[ORingSymbol.numeral, numeral_eq_natCast (n + 1), add_assoc, one_add_one_eq_two]

lemma not_neg (x : M) : ¬¨x < 0 := by simp

lemma eq_succ_of_pos {x : M} (h : 0 < x) : ‚àÉ y, x = y + 1 := by
  rcases le_iff_exists_add.mp (one_le_of_zero_lt x h) with ‚ü®y, rfl‚ü©
  exact ‚ü®y, add_comm 1 y‚ü©

lemma le_iff_lt_succ {x y : M} : x ‚â§ y ‚Üî x < y + 1 :=
  ‚ü®by intro h; exact lt_of_le_of_lt h (lt_add_one y),
   fun h => by
    rcases lt_iff_exists_add.mp h with ‚ü®z, hz, h‚ü©
    rcases eq_succ_of_pos hz with ‚ü®z', rfl‚ü©
    have : y = x + z' := by simpa[‚Üêadd_assoc] using h
    simp[this]‚ü©

lemma eq_nat_of_lt_nat : ‚àÄ {n : ‚Ñï} {x : M}, x < n ‚Üí ‚àÉ m : ‚Ñï, x = m
  | 0,     x, hx => by simp[not_neg] at hx
  | n + 1, x, hx => by
    have : x ‚â§ n := by simpa[le_iff_lt_succ] using hx
    rcases this with (rfl | hx)
    ¬∑ exact ‚ü®n, rfl‚ü©
    ¬∑ exact eq_nat_of_lt_nat hx

open Hierarchy

lemma val_numeral {n} : ‚àÄ (t : Semiterm ‚Ñí‚Çí·µ£ Empty n),
    ‚àÄ v, Semiterm.val! M (v ¬∑) Empty.elim t = (Semiterm.val! ‚Ñï v Empty.elim t)
  | #_,                                _ => by simp
  | Semiterm.func Language.Zero.zero _, e => by simp
  | Semiterm.func Language.One.one _,   e => by simp
  | Semiterm.func Language.Add.add v,   e => by simp[Semiterm.val_func, val_numeral (v 0), val_numeral (v 1)]
  | Semiterm.func Language.Mul.mul v,   e => by simp[Semiterm.val_func, val_numeral (v 0), val_numeral (v 1)]

lemma pval_of_pval_nat_of_sigma_one : ‚àÄ {n} {œÉ : Semisentence ‚Ñí‚Çí·µ£ n},
    Hierarchy Œ£ 1 œÉ ‚Üí ‚àÄ {e}, Semiformula.PVal! ‚Ñï e œÉ ‚Üí Semiformula.PVal! M (e ¬∑) œÉ
  | _, _, Hierarchy.verum _ _ _,               _ => by simp
  | _, _, Hierarchy.falsum _ _ _,              _ => by simp
  | _, _, Hierarchy.rel _ _ Language.Eq.eq v,  e => by simp[Semiformula.eval_rel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.nrel _ _ Language.Eq.eq v, e => by simp[Semiformula.eval_nrel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.rel _ _ Language.LT.lt v,  e => by simp[Semiformula.eval_rel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.nrel _ _ Language.LT.lt v, e => by simp[Semiformula.eval_nrel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.and hp hq,                 e => by
    simp; intro ep eq; exact ‚ü®pval_of_pval_nat_of_sigma_one hp ep, pval_of_pval_nat_of_sigma_one hq eq‚ü©
  | _, _, Hierarchy.or hp hq,                  e => by
    simp; rintro (h | h)
    ¬∑ left; exact pval_of_pval_nat_of_sigma_one hp h
    ¬∑ right; exact pval_of_pval_nat_of_sigma_one hq h
  | _, _, Hierarchy.ball pt hp,                e => by
    rcases Rew.positive_iff.mp pt with ‚ü®t, rfl‚ü©
    simp[val_numeral]; intro h x hx
    rcases eq_nat_of_lt_nat hx with ‚ü®x, rfl‚ü©
    simpa[Matrix.comp_vecCons'] using pval_of_pval_nat_of_sigma_one hp (h x (by simpa using hx))
  | _, _, Hierarchy.bex pt hp,                 e => by
    rcases Rew.positive_iff.mp pt with ‚ü®t, rfl‚ü©
    simp[val_numeral]; intro x hx h
    exact ‚ü®x, by simpa using hx, by simpa[Matrix.comp_vecCons'] using pval_of_pval_nat_of_sigma_one hp h‚ü©
  | _, _, Hierarchy.sigma (p := p) hp,         e => by
    simp; intro x h
    have : Hierarchy Œ£ 1 p := hp.accum _
    exact ‚ü®x, by simpa[Matrix.comp_vecCons'] using pval_of_pval_nat_of_sigma_one this h‚ü©
  | _, _, Hierarchy.ex hp,                     e => by
    simp; intro x hx; exact ‚ü®x, by simpa[Matrix.comp_vecCons'] using pval_of_pval_nat_of_sigma_one hp hx‚ü©

end Model

variable {T : Theory ‚Ñí‚Çí·µ£} [ùêÑùê™ ‚âæ T] [ùêèùêÄ‚Åª ‚âæ T]

theorem sigma_one_completeness {œÉ : Sentence ‚Ñí‚Çí·µ£} (hœÉ : Hierarchy Œ£ 1 œÉ) :
    ‚Ñï ‚äß‚Çò œÉ ‚Üí T ‚ä¢ œÉ := fun H =>
  Complete.complete (oRing_consequence_of _ _ (fun M _ _ _ _ _ _ => by
    haveI : ùêèùêÄ‚Åª.Mod M := Theory.Mod.of_subtheory (T‚ÇÅ := T) M (Semantics.ofSystemSubtheory _ _)
    simpa [Matrix.empty_eq] using Model.pval_of_pval_nat_of_sigma_one (M := M) hœÉ H))

end

end Arith

end FirstOrder

end LO
