import Logic.FirstOrder.Arith.Model
import Logic.Vorspiel.ExistsUnique
import Mathlib.Algebra.Order.Monoid.Canonical.Defs
import Mathlib.Algebra.Associated
--import Logic.FirstOrder.Principia.Meta

namespace LO

namespace FirstOrder

namespace Arith

namespace PAminus

noncomputable section

variable {M : Type} [Inhabited M] [DecidableEq M] [ORingSymbol M]
  [Structure â„’â‚’áµ£ M] [Structure.ORing â„’â‚’áµ£ M]
  [ğğ€â».Mod M]

namespace Model

open Language

instance : LE M := âŸ¨fun x y => x = y âˆ¨ x < yâŸ©

lemma le_def {x y : M} : x â‰¤ y â†” x = y âˆ¨ x < y := iff_of_eq rfl

lemma add_zero : âˆ€ x : M, x + 0 = x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addZero oRing _)

lemma add_assoc : âˆ€ x y z : M, (x + y) + z = x + (y + z) := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addAssoc oRing _)

lemma add_comm : âˆ€ x y : M, x + y = y + x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addComm oRing _)

lemma add_eq_of_lt : âˆ€ x y : M, x < y â†’ âˆƒ z, x + z = y := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addEqOfLt oRing _)

@[simp] lemma zero_le : âˆ€ x : M, 0 â‰¤ x := by
  simpa[models_iff, Structure.le_iff_of_eq_of_lt] using Theory.Mod.models M (@Theory.PAminus.zeroLe oRing _)

lemma zero_lt_one : (0 : M) < 1 := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.zeroLtOne oRing _)

lemma one_le_of_zero_lt : âˆ€ x : M, 0 < x â†’ 1 â‰¤ x := by
  simpa[models_iff, Structure.le_iff_of_eq_of_lt] using Theory.Mod.models M (@Theory.PAminus.oneLeOfZeroLt oRing _)

lemma add_lt_add : âˆ€ x y z : M, x < y â†’ x + z < y + z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.addLtAdd oRing _)

lemma mul_zero : âˆ€ x : M, x * 0 = 0 := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulZero oRing _)

lemma mul_one : âˆ€ x : M, x * 1 = x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulOne oRing _)

lemma mul_assoc : âˆ€ x y z : M, (x * y) * z = x * (y * z) := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulAssoc oRing _)

lemma mul_comm : âˆ€ x y : M, x * y = y * x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulComm oRing _)

lemma mul_lt_mul : âˆ€ x y z : M, x < y â†’ 0 < z â†’ x * z < y * z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.mulLtMul oRing _)

lemma distr : âˆ€ x y z : M, x * (y + z) = x * y + x * z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.distr oRing _)

lemma lt_irrefl : âˆ€ x : M, Â¬x < x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltIrrefl oRing _)

lemma lt_trans : âˆ€ x y z : M, x < y â†’ y < z â†’ x < z := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltTrans oRing _)

lemma lt_tri : âˆ€ x y : M, x < y âˆ¨ x = y âˆ¨ y < x := by
  simpa[models_iff] using Theory.Mod.models M (@Theory.PAminus.ltTri oRing _)

instance : AddCommMonoid M where
  add_assoc := Model.add_assoc
  zero_add  := fun x => Model.add_comm x 0 â–¸ Model.add_zero x
  add_zero  := Model.add_zero
  add_comm  := Model.add_comm

instance : CommMonoid M where
  mul_assoc := Model.mul_assoc
  one_mul   := fun x => Model.mul_comm x 1 â–¸ Model.mul_one x
  mul_one   :=  Model.mul_one
  mul_comm  := Model.mul_comm

instance : LinearOrder M where
  le_refl := fun x => Or.inl (by simp)
  le_trans := by
    rintro x y z (rfl | hx) (rfl | hy) <;> simp[*, le_def]
    Â· exact Or.inr (Model.lt_trans _ _ _ hx hy)
  le_antisymm := by
    rintro x y (rfl | hx) <;> simp
    rintro (rfl | hy) <;> try simp
    exact False.elim $ Model.lt_irrefl _ (Model.lt_trans _ _ _ hx hy)
  le_total := by
    intro x y
    rcases Model.lt_tri x y with (h | rfl | h) <;> simp[*, le_def]
  lt_iff_le_not_le := fun x y =>
    âŸ¨fun h => âŸ¨Or.inr h, by
      simp[le_def]; rintro (rfl | h'); { exact lt_irrefl y h }; { exact lt_irrefl _ (lt_trans _ _ _ h h') }âŸ©,
     by simp[not_or, le_def]; rintro (rfl | h) <;> simp[*] âŸ©
  decidableLE := fun _ _ => Classical.dec _

lemma zero_mul : âˆ€ x : M, 0 * x = 0 := fun x => by simpa[mul_comm] using mul_zero x

instance : LinearOrderedCommSemiring M where
  left_distrib := distr
  right_distrib := fun x y z => by simp[mul_comm _ z]; exact distr z x y
  zero_mul := zero_mul
  mul_zero := mul_zero
  mul_assoc := Model.mul_assoc
  mul_comm := mul_comm
  one_mul   := fun x => Model.mul_comm x 1 â–¸ Model.mul_one x
  mul_one   :=  Model.mul_one
  add_le_add_left := by rintro x y (rfl | h) z <;> simp[add_comm z]; exact Or.inr (add_lt_add x y z h)
  zero_le_one := Or.inr zero_lt_one
  le_of_add_le_add_left := by
    rintro x y z h
    have : y â‰¤ z âˆ¨ z < y := le_or_lt y z
    rcases this with (hyz | hyz)
    Â· exact hyz
    Â· have : x + z < x + y := by simpa[add_comm] using add_lt_add z y x hyz
      exact False.elim ((lt_iff_not_ge _ _).mp this h)
  exists_pair_ne := âŸ¨0, 1, ne_of_lt zero_lt_oneâŸ©
  mul_lt_mul_of_pos_left := by
    rintro x y z h hz; simp[mul_zero]; { simpa[mul_comm z] using mul_lt_mul x y z h hz }
  mul_lt_mul_of_pos_right := by
    rintro x y z h hz; simp[mul_zero]; { simpa using mul_lt_mul x y z h hz }
  le_total := le_total
  decidableLE := fun _ _ => Classical.dec _

instance : CanonicallyOrderedAddCommMonoid M where
  bot := 0
  bot_le := by simp
  exists_add_of_le := by
    rintro x y (rfl | h)
    Â· exact âŸ¨0, by simpâŸ©
    Â· simpa[eq_comm] using add_eq_of_lt x y h
  le_self_add := by intro x y; simp

@[simp] lemma numeral_eq_natCast : (n : â„•) â†’ (ORingSymbol.numeral n : M) = n
  | 0     => rfl
  | 1     => by simp
  | n + 2 => by simp[ORingSymbol.numeral, numeral_eq_natCast (n + 1), add_assoc, one_add_one_eq_two]

lemma not_neg (x : M) : Â¬x < 0 := by simp

lemma eq_succ_of_pos {x : M} (h : 0 < x) : âˆƒ y, x = y + 1 := by
  rcases le_iff_exists_add.mp (one_le_of_zero_lt x h) with âŸ¨y, rflâŸ©
  exact âŸ¨y, add_comm 1 yâŸ©

lemma le_of_lt_succ {x y : M} : x < y + 1 â†” x â‰¤ y :=
  âŸ¨fun h => by
    rcases lt_iff_exists_add.mp h with âŸ¨z, hz, hâŸ©
    rcases eq_succ_of_pos hz with âŸ¨z', rflâŸ©
    have : y = x + z' := by simpa[â†add_assoc] using h
    simp[this],
   by intro h; exact lt_of_le_of_lt h (lt_add_one y)âŸ©

lemma eq_nat_of_lt_nat : âˆ€ {n : â„•} {x : M}, x < n â†’ âˆƒ m : â„•, x = m
  | 0,     x, hx => by simp[not_neg] at hx
  | n + 1, x, hx => by
    have : x â‰¤ n := by simpa[le_of_lt_succ] using hx
    rcases this with (rfl | hx)
    Â· exact âŸ¨n, rflâŸ©
    Â· exact eq_nat_of_lt_nat hx

open Hierarchy

lemma val_numeral {n} : âˆ€ (t : Semiterm â„’â‚’áµ£ Empty n),
    âˆ€ v, Semiterm.val! M (v Â·) Empty.elim t = (Semiterm.val! â„• v Empty.elim t)
  | #_,                                _ => by simp
  | Semiterm.func Language.Zero.zero _, e => by simp
  | Semiterm.func Language.One.one _,   e => by simp
  | Semiterm.func Language.Add.add v,   e => by simp[Semiterm.val_func, val_numeral (v 0), val_numeral (v 1)]
  | Semiterm.func Language.Mul.mul v,   e => by simp[Semiterm.val_func, val_numeral (v 0), val_numeral (v 1)]

lemma sigma_zero_completeness : âˆ€ {n} {Ïƒ : Semisentence â„’â‚’áµ£ n},
    Hierarchy Î£ 0 Ïƒ â†’ âˆ€ {e}, Semiformula.PVal! â„• e Ïƒ â†’ Semiformula.PVal! M (e Â·) Ïƒ
  | _, _, Hierarchy.verum _ _ _,               _ => by simp
  | _, _, Hierarchy.falsum _ _ _,              _ => by simp
  | _, _, Hierarchy.rel _ _ Language.Eq.eq v,  e => by simp[Semiformula.eval_rel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.nrel _ _ Language.Eq.eq v, e => by simp[Semiformula.eval_nrel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.rel _ _ Language.LT.lt v,  e => by simp[Semiformula.eval_rel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.nrel _ _ Language.LT.lt v, e => by simp[Semiformula.eval_nrel, Matrix.comp_vecCons', val_numeral]
  | _, _, Hierarchy.and hp hq,                 e => by
    simp; intro ep eq; exact âŸ¨sigma_zero_completeness hp ep, sigma_zero_completeness hq eqâŸ©
  | _, _, Hierarchy.or hp hq,                  e => by
    simp; rintro (h | h)
    Â· left; exact sigma_zero_completeness hp h
    Â· right; exact sigma_zero_completeness hq h
  | _, _, Hierarchy.ball pt hp,                 e => by
    rcases Rew.positive_iff.mp pt with âŸ¨t, rflâŸ©
    simp[val_numeral]; intro h x hx
    rcases eq_nat_of_lt_nat hx with âŸ¨x, rflâŸ©
    simpa[Matrix.comp_vecCons'] using sigma_zero_completeness hp (h x (by simpa using hx))
  | _, _, Hierarchy.bex pt hp,                  e => by
    rcases Rew.positive_iff.mp pt with âŸ¨t, rflâŸ©
    simp[val_numeral]; intro x hx h
    exact âŸ¨x, by simpa using hx, by simpa[Matrix.comp_vecCons'] using sigma_zero_completeness hp hâŸ©

lemma sigma_one_completeness : âˆ€ {n} {Ïƒ : Semisentence â„’â‚’áµ£ n},
    Hierarchy Î£ 1 Ïƒ â†’ âˆ€ {e}, Semiformula.PVal! â„• e Ïƒ â†’ Semiformula.PVal! M (e Â·) Ïƒ
  | _, _, Hierarchy.verum _ _ _,               _ => by simp
  | _, _, Hierarchy.falsum _ _ _,              _ => by simp
  | _, _, Hierarchy.rel _ _ r v,               e => sigma_zero_completeness (by simp)
  | _, _, Hierarchy.nrel _ _ r v,              e => sigma_zero_completeness (by simp)
  | _, _, Hierarchy.and hp hq,                 e => by
    simp; intro ep eq; exact âŸ¨sigma_one_completeness hp ep, sigma_one_completeness hq eqâŸ©
  | _, _, Hierarchy.or hp hq,                  e => by
    simp; rintro (h | h)
    Â· left; exact sigma_one_completeness hp h
    Â· right; exact sigma_one_completeness hq h
  | _, _, Hierarchy.ball pt hp,                 e => by
    rcases Rew.positive_iff.mp pt with âŸ¨t, rflâŸ©
    simp[val_numeral]; intro h x hx
    rcases eq_nat_of_lt_nat hx with âŸ¨x, rflâŸ©
    simpa[Matrix.comp_vecCons'] using sigma_one_completeness hp (h x (by simpa using hx))
  | _, _, Hierarchy.bex pt hp,                  e => by
    rcases Rew.positive_iff.mp pt with âŸ¨t, rflâŸ©
    simp[val_numeral]; intro x hx h
    exact âŸ¨x, by simpa using hx, by simpa[Matrix.comp_vecCons'] using sigma_one_completeness hp hâŸ©
  | _, _, Hierarchy.sigma (p := p) hp,         e => by
    simp; intro x h
    have : Hierarchy Î£ 1 p := hp.accum _
    exact âŸ¨x, by simpa[Matrix.comp_vecCons'] using sigma_one_completeness this hâŸ©
  | _, _, Hierarchy.ex hp,                     e => by
    simp; intro x hx; exact âŸ¨x, by simpa[Matrix.comp_vecCons'] using sigma_one_completeness hp hxâŸ©
  | _, _, Hierarchy.accum _ hp,                e => sigma_zero_completeness (by simpa [Hierarchy.zero_iff_sigma_zero] using hp)

end Model

section sigma_one_completeness

variable {T : Theory â„’â‚’áµ£} [ğ„ğª â‰¾ T] [ğğ€â» â‰¾ T]

theorem sigma_one_completeness {Ïƒ : Sentence â„’â‚’áµ£} (hÏƒ : Hierarchy Î£ 1 Ïƒ) :
    â„• âŠ§â‚˜ Ïƒ â†’ T âŠ¢ Ïƒ := fun H =>
  Complete.complete (consequence_of _ _ (fun M _ _ _ _ _ => by
    haveI : ğğ€â».Mod M := Theory.Mod.of_subtheory (Tâ‚ := T) M (Semantics.ofSystemSubtheory _ _)
    simpa[Matrix.empty_eq] using @Model.sigma_one_completeness M _ _ _ _ _ _ _ hÏƒ ![] (by simpa[models_iff] using H)))

end sigma_one_completeness

namespace Model

variable {x y z : M}

section msub

lemma msub_existsUnique (x y : M) : âˆƒ! z, (x â‰¥ y â†’ x = y + z) âˆ§ (x < y â†’ z = 0) := by
  have : y â‰¤ x âˆ¨ x < y := le_or_lt y x
  rcases this with (hxy | hxy) <;> simp[hxy]
  Â· simp [show Â¬x < y from not_lt.mpr hxy]
    have : âˆƒ z, x = y + z := exists_add_of_le hxy
    rcases this with âŸ¨z, rflâŸ©
    exact ExistsUnique.intro z rfl (fun x h => (add_left_cancel h).symm)
  Â· simp [show Â¬y â‰¤ x from not_le.mpr hxy]

def msub (x y : M) : M := Classical.choose! (msub_existsUnique x y)

infix:65 " -Ì‡ " => msub

lemma msub_spec_of_ge (h : x â‰¥ y) : x = y + (x -Ì‡ y) := (Classical.choose!_spec (msub_existsUnique x y)).1 h

lemma msub_spec_of_lt (h : x < y) : x -Ì‡ y = 0 := (Classical.choose!_spec (msub_existsUnique x y)).2 h

lemma msub_eq_iff : z = x -Ì‡ y â†” ((x â‰¥ y â†’ x = y + z) âˆ§ (x < y â†’ z = 0)) := Classical.choose!_eq_iff _

lemma msub_definable : Î£á´¬[0]-Functionâ‚‚ (Î» x y : M â†¦ x -Ì‡ y) :=
  âŸ¨â€œ(#2 â‰¤ #1 â†’ #1 = #2 + #0) âˆ§ (#1 < #2 â†’ #0 = 0)â€,
    by simp[Hierarchy.zero_iff_sigma_zero], by intro v; simp[msub_eq_iff]; rflâŸ©

@[simp] lemma msub_le_self (x y : M) : x -Ì‡ y â‰¤ x := by
  have : y â‰¤ x âˆ¨ x < y := le_or_lt y x
  rcases this with (hxy | hxy) <;> simp[hxy]
  Â· simpa [â† msub_spec_of_ge hxy] using show x -Ì‡ y â‰¤ y + (x -Ì‡ y) from le_add_self
  Â· simp[msub_spec_of_lt hxy]

lemma msub_polybounded : PolyBoundedâ‚‚ (Î» x y : M â†¦ x -Ì‡ y) := âŸ¨#0, Î» _ â†¦ by simpâŸ©

end msub

section Dvd

lemma le_mul_self_of_pos_left (hy : 0 < y) : x â‰¤ y * x := by
  have : 1 * x â‰¤ y * x := mul_le_mul_of_nonneg_right (one_le_of_zero_lt y hy) (by simp)
  simpa using this

lemma le_mul_self_of_pos_right (hy : 0 < y) : x â‰¤ x * y := by
  simpa [mul_comm x y] using le_mul_self_of_pos_left hy

lemma dvd_iff_bounded {x y : M} : x âˆ£ y â†” âˆƒ z â‰¤ y, y = x * z := by
  by_cases hx : x = 0
  Â· simp[hx]; rintro rfl; exact âŸ¨0, by simpâŸ©
  Â· constructor
    Â· rintro âŸ¨z, rflâŸ©; exact âŸ¨z, le_mul_self_of_pos_left (pos_iff_ne_zero.mpr hx), rflâŸ©
    Â· rintro âŸ¨z, hz, rflâŸ©; exact dvd_mul_right x z

lemma dvd_definable : Î£á´¬[0]-Relation (Î» x y : M â†¦ x âˆ£ y) :=
  âŸ¨âˆƒ[â€œ#0 < #2 + 1â€] â€œ#2 = #1 * #0â€, by simp,
  Î» v â†¦ by simp[dvd_iff_bounded, Matrix.vecHead, Matrix.vecTail, le_of_lt_succ]âŸ©

end Dvd

@[simp] lemma lt_one_iff_eq_zero : x < 1 â†” x = 0 := âŸ¨by
  intro hx
  have : x â‰¤ 0 := by exact le_of_lt_succ.mp (show x < 0 + 1 from by simpa using hx)
  exact nonpos_iff_eq_zero.mp this,
  by rintro rfl; exact zero_lt_oneâŸ©

lemma le_one_iff_eq_zero_or_one : x â‰¤ 1 â†” x = 0 âˆ¨ x = 1 :=
  âŸ¨by intro h; rcases h with (rfl | ltx)
      Â· simp
      Â· simp [show x = 0 from by simpa using ltx],
   by rintro (rfl | rfl) <;> simpâŸ©

lemma le_of_dvd (h : 0 < y) : x âˆ£ y â†’ x â‰¤ y := by
  rintro âŸ¨z, rflâŸ©
  exact le_mul_self_of_pos_right
    (pos_iff_ne_zero.mpr (show z â‰  0 from by rintro rfl; simp at h))

lemma dvd_antisymm : x âˆ£ y â†’ y âˆ£ x â†’ x = y := by
  intro hx hy
  rcases show x = 0 âˆ¨ 0 < x from eq_zero_or_pos x with (rfl | ltx)
  Â· simp [show y = 0 from by simpa using hx]
  Â· rcases show y = 0 âˆ¨ 0 < y from eq_zero_or_pos y with (rfl | lty)
    Â· simp [show x = 0 from by simpa using hy]
    Â· exact le_antisymm (le_of_dvd lty hx) (le_of_dvd ltx hy)

lemma dvd_one : x âˆ£ 1 â†” x = 1 := âŸ¨by { intro hx; exact dvd_antisymm hx (by simp) }, by rintro rfl; simpâŸ©

section Prime

lemma eq_one_or_eq_of_dvd_of_prime {p x : M} (pp : Prime p) (hxp : x âˆ£ p) : x = 1 âˆ¨ x = p := by
  have : p âˆ£ x âˆ¨ x âˆ£ 1 := pp.left_dvd_or_dvd_right_of_dvd_mul (show x âˆ£ p * 1 from by simpa using hxp)
  rcases this with (hx | hx)
  Â· right; exact dvd_antisymm hxp hx
  Â· left; exact dvd_one.mp hx

/-
lemma prime_iff_bounded {x : M} : Prime x â†” 1 < x âˆ§ âˆ€ y â‰¤ x, (y âˆ£ x â†’ y = 1 âˆ¨ y = x) := by
  constructor
  Â· intro prim
    have : 1 < x := by
      by_contra A; simp at A
      rcases le_one_iff_eq_zero_or_one.mp A with (rfl | rfl)
      Â· exact not_prime_zero prim
      Â· exact not_prime_one prim
    exact âŸ¨this, fun y hy hyx â†¦ eq_one_or_eq_of_dvd_of_prime prim hyxâŸ©
  Â· intro H; constructor
    Â· sorry
    Â· constructor
      Â· sorry
      Â· intro y z h
-/

def IsPrime (x : M) : Prop := 1 < x âˆ§ âˆ€ y â‰¤ x, (y âˆ£ x â†’ y = 1 âˆ¨ y = x)
-- TODO: prove IsPrime x â†” Prime x

lemma isPrime_definable : Î£á´¬[0]-Predicate (Î» x : M â†¦ IsPrime x) := by
  have : Î£á´¬[0]-Relation (Î» x y : M â†¦ x âˆ£ y) := dvd_definable
  rcases this with âŸ¨dvd, hdvd, sdvdâŸ©
  let prime : Semisentence â„’â‚’áµ£ 1 := â€œ1 < #0â€ â‹ (âˆ€[â€œ#0 < #1 + 1â€] dvd âŸ¶ â€œ#0 = 1 âˆ¨ #0 = #1â€)
  exact âŸ¨prime, by simp[prime, hdvd, Hierarchy.zero_iff_sigma_zero],
    fun v â†¦ by
      simp [Semiformula.eval_substs, Matrix.comp_vecCons', Matrix.vecHead, Matrix.constant_eq_singleton,
        IsPrime, â† sdvd, le_of_lt_succ]âŸ©

end Prime

end Model

end

end PAminus

end Arith
